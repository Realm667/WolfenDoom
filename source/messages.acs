int messagestart; // Used to track layer of hud messages that we should be OK to start on

function int GetMessageStart (void)
{
	messagestart -= 4;
	if (messagestart < 0) { messagestart = 64; } // Allow overlapping messages that don't interfere with each other.  This lets 16 dialogue messages play at once, seamlessly.

	return messagestart;
}

script "BoADialogue"(int headtalky,int headsilent,int message)
{
	int messagelayer = GetMessageStart();

	// Fade in HUD BGs
	SetHudSize(320, 200, true);
	SetFont("HEADBAR");
	hudmessagebold(s:"A"; HUDMSG_FADEINOUT, messagelayer + 4, CR_UNTRANSLATED, 160.0, 24.0, 999.0, 1.0, 1.0);
	SetFont("HEADBOVL"); //mxd. Frame overlay
	hudmessagebold(s:"A"; HUDMSG_FADEINOUT, messagelayer + 1, CR_UNTRANSLATED, 160.0, 24.0, 999.0, 1.0, 1.0); //mxd

	// Fade in the portrait
	SetFont(headsilent);
	hudmessagebold(s:"A"; HUDMSG_FADEINOUT, messagelayer + 3, CR_UNTRANSLATED, 21.0, 16.0, 999.0, 1.0, 1.0);

	// Start talk animation
	Delay(35);
	SetFont(headtalky);
	hudmessagebold(s:"A"; HUDMSG_PLAIN, messagelayer + 3, CR_UNTRANSLATED, 21.0, 16.0, 0.0);

	// Play the audio (if it's there)
	PlayMessageSound(0, message);

	// Show message
	AmbientSound("RADIONOS",127);
	SetHudSize(640, 400, 1);
	SetFont("SMALLFONT");
	SetHudWrapWidth(540);
	hudmessagebold(l:message; HUDMSG_TYPEON | HUDMSG_LOG, messagelayer + 2, CR_GRAY, 100.1, 8.1, 999.0, 0.03, 1.0);

	// Stop talk animation
	delay(5*35);
	SetHudSize(320, 200, true);
	SetFont(headsilent);
	hudmessagebold(s:"A"; HUDMSG_PLAIN, messagelayer + 3, CR_UNTRANSLATED, 21.0, 16.0, 0.0);

	delay(5*35);

	// Fade out the portrait
	hudmessagebold(s:"A"; HUDMSG_FADEINOUT, messagelayer + 3, CR_UNTRANSLATED, 21.0, 16.0, 0.0, 0.0, 1.0);

	//Fade out HUD BGs
	SetHudSize(320, 200, true);
	SetFont("HEADBAR");
	hudmessagebold(s:"A"; HUDMSG_FADEINOUT, messagelayer + 4, CR_UNTRANSLATED, 160.0, 24.0, 0.0, 0.0, 1.0);
	SetFont("HEADBOVL"); //mxd. Frame overlay
	hudmessagebold(s:"A"; HUDMSG_FADEINOUT, messagelayer + 1, CR_UNTRANSLATED, 160.0, 24.0, 0.0, 0.0, 1.0); //mxd

	//Fade out the message
	SetHudSize(640, 400, 1);
	SetFont("SMALLFONT");
	SetHudWrapWidth(540);
	hudmessagebold(l:message; HUDMSG_FADEINOUT, messagelayer + 2, CR_GRAY, 100.1, 8.1, 0.0, 0.0, 1.0);
}

script "HintMessage" (int message)
{
	if (message == "") { Terminate; }

	message = StrParam(l:message);

	SetHudSize(640, 480, true);
	hudmessagebold(s:message; HUDMSG_FADEINOUT,80,CR_GRAY,320.0,470.0,3.0,1.0,1.0);
	delay(10);
	AmbientSound("menu/change", 96);
}

script "TankExitMessage" (void)
{
	SetHudSize(640, 480, true);
	hudmessagebold(l:"TANKEXITHOLD"; HUDMSG_FADEINOUT,80,CR_GRAY,320.0,470.0,1.0,1.0,0.5);
	delay(10);
	AmbientSound("menu/change", 96);
}

script "FollowerMessage" (int type, int count)
{
	int messagelayer = GetMessageStart();
	int messagetype = "";
	int message = "";
	int messagestr = "";

	switch (type)
	{
		case 0:
			messagetype = StrParam(s:"STAY", i:count);
		break;
		case 1:
			messagetype = StrParam(s:"STATIC", i:count);
		break;
		case 2:
			messagetype = StrParam(s:"COME", i:count);
		break;
		case 3:
			messagetype = StrParam(s:"ALWAYS", i:count);
		break;
		default:
			messagetype = "";
		break;
	}

	// Try to get the message string, prepending the actor's class name
	message = StrParam(s:GetActorClass(0), l:messagetype);
	messagestr = StrParam(l:message);
	if (messagestr == message)
	{
		if (GetCVar("developer")) { Log(s:"\cgMissing voiceover: \cjCheck SNDINFO entry and audio file for \cf", s:message, s:"\cj! Falling back to \cf", s:StrParam(s:"FOLLOWER", l:messagetype), s:"\cj."); }
		// If not found, fall back to the generic messages
		message = StrParam(s:"FOLLOWER", l:messagetype);
		messagestr = StrParam(l:message);
		if (messagestr == message) { Terminate; }
	}

	str head = GetUserVariable(0, "head");
	if (head == "") { Terminate; }

	// Quick message display...  Variant of the BoADialogue script...

	str headsilent = StrParam(s:head, i:0);
	str headtalky = StrParam(s:head, i:1);

	// Fade in HUD BGs
	SetHudSize(320, 200, true);
	SetFont("HEADBAR");
	hudmessagebold(s:"A"; HUDMSG_FADEINOUT, messagelayer + 4, CR_UNTRANSLATED, 160.0, 24.0, 999.0, 1.0, 0.5);
	SetFont("HEADBOVL"); //mxd. Frame overlay
	hudmessagebold(s:"A"; HUDMSG_FADEINOUT, messagelayer + 1, CR_UNTRANSLATED, 160.0, 24.0, 999.0, 1.0, 0.5); //mxd

	// Fade in the portrait
	SetFont(headsilent);
	hudmessagebold(s:"A"; HUDMSG_FADEINOUT, messagelayer + 3, CR_UNTRANSLATED, 21.0, 16.0, 999.0, 1.0, 0.5);

	// Start talk animation
	Delay(35);
	SetFont(headtalky);
	hudmessagebold(s:"A"; HUDMSG_PLAIN, messagelayer + 3, CR_UNTRANSLATED, 21.0, 16.0, 0.0);

	// Play the audio (if it's there)
	PlayMessageSound(0, message);

	// Show message
	AmbientSound("RADIONOS",127);
	SetHudSize(640, 400, 1);
	SetFont("SMALLFONT");
	SetHudWrapWidth(540);
	hudmessagebold(s:messagestr; HUDMSG_TYPEON | HUDMSG_LOG, messagelayer + 2, CR_GRAY, 100.1, 8.1, 999.0, 0.03, 0.5);

	// Stop talk animation
	delay(2*35);
	SetHudSize(320, 200, true);
	SetFont(headsilent);
	hudmessagebold(s:"A"; HUDMSG_PLAIN, messagelayer + 3, CR_UNTRANSLATED, 21.0, 16.0, 0.0);

	// Fade out the portrait
	hudmessagebold(s:"A"; HUDMSG_FADEINOUT, messagelayer + 3, CR_UNTRANSLATED, 21.0, 16.0, 0.0, 0.0, 0.5);

	//Fade out HUD BGs
	SetHudSize(320, 200, true);
	SetFont("HEADBAR");
	hudmessagebold(s:"A"; HUDMSG_FADEINOUT, messagelayer + 4, CR_UNTRANSLATED, 160.0, 24.0, 0.0, 0.0, 0.5);
	SetFont("HEADBOVL"); //mxd. Frame overlay
	hudmessagebold(s:"A"; HUDMSG_FADEINOUT, messagelayer + 1, CR_UNTRANSLATED, 160.0, 24.0, 0.0, 0.0, 0.5); //mxd

	//Fade out the message
	SetHudSize(640, 400, 1);
	SetFont("SMALLFONT");
	SetHudWrapWidth(540);
	hudmessagebold(s:messagestr; HUDMSG_FADEINOUT, messagelayer + 2, CR_GRAY, 100.1, 8.1, 0.0, 0.0, 0.5);
}

script "ShowStats" (void)
{
	ScriptCall("MapStatsHandler", "Toggle");
}