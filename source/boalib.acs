#library "boalib"

#libdefine MAX_PLAYERS 8
#libdefine MAX_VOLUME 127
#libdefine MAX_OBJECTIVES 6
#libdefine MAX_PLAYER_HEALTH 150
#libdefine PLAYER_TAG_OFFSET 1000
#libdefine BRIDGECOUNT 256 // Track sector tags up to 256

global int 1:disable_mutants; //mxd. When set to true, Mutant and MutantMelee will immediately despawn after spawning.
global int 2:disable_big_mutants; //mxd. When set to true, BigMutant1 and BigMutant2 will immediately despawn after spawning.
global int 3:disable_supermutants; //mxd. When set to true, UberMutant and SuperMutant will immediately despawn after spawning.
global int 4:astrostein; //[Ed] When set to true, grenades will change to the Astrostein variant
global int 5:HealthScriptDeactivate;
global int 6:mission_number; //The variable for the mission number
global int 7:zombiemap_available; //The variable for RE secret maps
global int 8:hints_collected; //The variable for RE clues on c3m1
global int 60:time; // Used for day/night transition display on status bar

int playerID;

// Repair action status variable
int repairstate[65536]; // Arbitrarily large to account for most possible line ids

// Wave spawner configuration variables
int maxzones = 10; // How many waves can be set up
int zonestarttid = 100; // Base TID for wave spawn points - wave 1 will add 1, wave 2 will add 2, etc.
int currentzone = 0; // The currently activated wave
int lastzone = 0; // The previous wave (used for fallback spawnpoints if it's night again and no other wave has been triggered)

// CK bridge tracking
bool bridgestatus[BRIDGECOUNT];

#include "zcommon.acs"
#include "functions.acs"
#include "colorgrade.acs"
#include "compass.acs"
#include "droplets.acs"
#include "flinch.acs"
#include "gameplay.acs"
#include "interactive.acs"
#include "messages.acs"
#include "objectives.acs"
#include "sprint.acs"
#include "footsteps.acs"
#include "tilt.acs"
#include "wavespawn.acs"
#include "boptions.acs"

#include "utils_cheats.acs"

// Consolidated gameplay loop: moved to ZScript
// Script "BoA_PlayerLoop" ENTER ...

// Uses ZScript to check that a sound exists
function bool PlayMessageSound(int tid, int message)
{
	// Get localized SNDINFO entry for localized voice acting
	str messageKey = StrParam(s:"SND_", s:message);
	str messageEntry = StrParam(l:messageKey);
	int hasSound = StrCmp(messageKey, messageEntry);
	int soundLength = 0;
	if (hasSound != 0)
	{
		soundLength = ScriptCall("Object", "S_GetLength", messageEntry);
	}

	if (!hasSound || soundLength == 0)
	{
		if (GetCVar("developer")) { Log(s:"\cgMissing voiceover: \cjCheck SNDINFO entry and audio file for \cf", s:message, s:"\cj!"); }

		return false;
	}

	if (!tid) { tid = playerID; }

	PlaySound(tid, messageEntry, CHAN_BODY, 1.0, false, ATTN_NONE, false);

	return true;
}

// Attempt to localize a string, but if it isn't available in the current
// language, return an empty string.
function str OptionalLocalizedString (str identifier)
{
	str value = StrParam(l:identifier);
	if (StrCmp(identifier, value) == 0)
	{
		return "";
	}
	return value;
}


function void AddLabel(int tid, str label)
{
	ScriptCall("OverlayHandler", "Add", tid, StrParam(l:label));
}

function void AddIconLabel(int tid, str label, str icon)
{
	ScriptCall("OverlayHandler", "Add", tid, StrParam(l:label), icon);
}

function void AddKeyLabel(int tid, str key, str name)
{
	ScriptCall("OverlayHandler", "Add", tid, KeyInfo(key, name));
}

function void AddKeyIconLabel(int tid, str key, str name, str icon)
{
	ScriptCall("OverlayHandler", "Add", tid, KeyInfo(key, name), icon);
}

function str KeyInfo(str key, str name)
{
	if (!StrLen(name))
	{
		name = StrParam(s:"CNTRLMNU_", s:key);
	}
	name = StrParam(l:name);

	str keybind = StrParam(s:"+", s:key);
	keybind = StrParam(k:keybind);

	return StrParam(s:name, s:"\n\c[Dark Red]", s:keybind);
}

function str GetKeyMessage(str message, str key)
{
	message = StrParam(l:message);

	if (key != 0)
	{
		key = StrParam(s:key);
		str bind = StrParam(k:key);

		str clr = "\cU";

		if (!StrCmp(bind, "???", 3)) { clr = "\c[Dark Red]"; }

		message = StrParam(s:message, s:"  ", s:clr, s:"<", s:bind, s:">");
	}

	return message;
}