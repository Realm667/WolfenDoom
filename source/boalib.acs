#library "boalib"

#libdefine MAX_PLAYERS 8
#libdefine MAX_VOLUME 127
#libdefine MAX_OBJECTIVES 6
#libdefine MAX_PLAYER_HEALTH 150
#libdefine PLAYER_TAG_OFFSET 1000

global int 1:disable_mutants; //mxd. When set to true, Mutant and MutantMelee will immediately despawn after spawning.
global int 2:disable_big_mutants; //mxd. When set to true, BigMutant1 and BigMutant2 will immediately despawn after spawning.
global int 3:disable_supermutants; //mxd. When set to true, UberMutant and SuperMutant will immediately despawn after spawning.
global int 4:astrostein; //[Ed] When set to true, grenades will change to the Astrostein variant
global int 5:HealthScriptDeactivate;
global int 6:mission_number; //The variable for the mission number
global int 60:time; // Used for day/night transition display on status bar

int playerID;

// Repair action status variable
int repairstate[65536]; // Arbitrarily large to account for most possible line ids

// Wave spawner configuration variables
int maxwaves = 10; // How many waves can be set up
int wavestarttid = 100; // Base TID for wave spawn points - wave 1 will add 1, wave 2 will add 2, etc.
int currentwave = 0; // The currently activated wave
int lastwave = 0; // The previous wave (used for fallback spawnpoints if it's night again and no other wave has been triggered)

#include "zcommon.acs"
#include "functions.acs"
#include "colorgrade.acs"
#include "compass.acs"
#include "droplets.acs"
#include "flinch.acs"
#include "gameplay.acs"
#include "interactive.acs"
#include "messages.acs"
#include "objectives.acs"
#include "sprint.acs"
#include "footsteps.acs"
#include "tilt.acs"
#include "wavespawn.acs"
#include "boptions.acs"

#include "zturret.acs"

// Consolidated gameplay loop.
// ALL restarting/perpetual functions should be called from here, not in their own ENTER scripts.
// The more ENTER scripts are constantly looping, the worse performance will be!
Script "BoA_PlayerLoop" ENTER
{
	playerID = PLAYER_TAG_OFFSET + PlayerNumber();
	Thing_ChangeTID(0, playerID);

	if (GameType() == GAME_TITLE_MAP) { Terminate; }

	While (True)
	{
		// Do screen drawing actions
		DrawObjectives();

		if (CheckInventory("Sherman") == 0) // Only do these if you're not driving a tank
		{
			DrawScreenOverlays();

			// Do inventory checks
			DoSprinting();
			DoStaminaCheck();

			// Do visual effects
			DoFlinchRecovery();
			DoTiltEffect();

			// Do sound effects
			DoFootstepSounds();
		}

		DoUnderwaterEffects();

		Delay(1);
	}
}

// Uses ZScript to check that a sound exists
function bool PlayMessageSound(int tid, int message)
{
	// Get localized SNDINFO entry for localized voice acting
	str messageKey = StrParam(s:"SND_", s:message);
	str messageEntry = StrParam(l:messageKey);
	int hasSound = StrCmp(messageKey, messageEntry);
	int soundLength = 0;
	if (hasSound != 0)
	{
		soundLength = ScriptCall("Object", "S_GetLength", messageEntry);
	}

	if (!hasSound || soundLength == 0)
	{
		if (GetCVar("developer")) { Log(s:"\cgMissing voiceover: \cjCheck SNDINFO entry and audio file for \cf", s:message, s:"\cj!"); }

		return false;
	}

	if (!tid) { tid = playerID; }

	PlaySound(tid, messageEntry, CHAN_BODY, 1.0, false, ATTN_NONE, false);

	return true;
}