ACTOR HydrantShootable
{
	//$Category Props (Wolf3D)/Street
	//$Title Hydrant (shootable)
	//$Color 3
	Health 1
	Radius 10
	Height 42
	Mass 999999
	+DONTGIB
	+NOBLOOD
	+NOICEDEATH
	+NOTAUTOAIMED
	+SHOOTABLE
	+SOLID
	DeathSound "hydrant/death"
	States
	{
	Spawn:
		HYDR A -1
		Stop
	Death:
		HYDB A 4 A_Scream
		"####" BC 4
		TNT1 A 0 A_PlaySound ("hydrant/broken", 7, frandom(0.8, 1.0), TRUE, ATTN_NORM)
	Broken:
		HYDR BCDE 4
		Loop
	}
}

ACTOR Tires
{
	//$Category Props (Wolf3D)/Street
	//$Title Tires (grey)
	//$Color 3
	Radius 16
	Height 8
	+SOLID
	States
	{
	Spawn:
		TIRE A -1
		Stop
	}
}

ACTOR RoadBarrier //3d actor
{
	//$Category Props (Wolf3D)/Street
	//$Title Barrier
	//$Color 3
	Radius 32
	Height 64
	+SOLID
	States
	{
	Spawn:
		MDLA A -1
		Stop
	}
}

ACTOR StreetBin
{
	//$Category Props (Wolf3D)/Street
	//$Title Recycle Bin (Street)
	//$Color 3
	Scale 0.85
	Radius 16
	Height 56
	Health 20
	Mass 100
	+NOBLOOD
	+NOTAUTOAIMED
	+PUSHABLE
	+SHOOTABLE
	+SOLID
	States
	{
	Spawn:
		SRCB A -1
		Stop
	Death:
		TNT1 A 0 A_UnSetSolid
		"####" A 0 A_PlaySound("METALBRK") //here you need a sound that is more fitting
		"####" AAAAAA 0 A_SpawnItemEx ("Debris_Trash", random(0,16), random(0,16), random(0,56), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE) //make sure to always adjust XYZ position depending on the size of the object and the amount of AAA 
		SRCB E -1
		Stop
	}
}

ACTOR Denkmal
{
	//$Category Props (Wolf3D)/Street
	//$Title Statue Horse (Street)
	//$Color 3
	Scale 0.85
	Radius 64
	Height 128
	+SOLID
	States
	{
	Spawn:
		DNML A -1
		Stop
	}
}

ACTOR CafeStuff //3d actor
{
	//$Category Props (Wolf3D)/Street
	//$Title Table with Umbrella and Chairs (Cafe)
	//$Color 3
	Radius 28
	Height 32
	Scale 0.65
	+SOLID
	States
	{
	Spawn:
		MDLA A 0 NoDelay A_SpawnItemEx("Cup1", Random(-24, -16), Random(-24, -16), 32, 0, 0, 0, 0, 0, 128)
		"####" A 0 A_SpawnItemEx("Cup1", Random(16, 24), Random(-24, -16), 32, 0, 0, 0, 0, 0, 128)
		"####" A 0 A_SpawnItemEx("Cup1", Random(-24, -16), Random(16, 24), 32, 0, 0, 0, 0, 0, 128)
		"####" A 0 A_SpawnItemEx("Cup1", Random(16, 24), Random(16, 24), 32, 0, 0, 0, 0, 0, 128)
		"####" A -1
		Stop
	}
}

ACTOR JunkPile1
{
	//$Category Props (Wolf3D)/Street
	//$Title Junk Pile (middle)
	//$Color 3
	Radius 25
	Height 20
	+SOLID
	States
	{
	Spawn:
		JPIL A -1
		Stop
	}
}

ACTOR JunkPile2
{
	//$Category Props (Wolf3D)/Street
	//$Title Junk Pile (large)
	//$Color 3
	Radius 28
	Height 28
	+SOLID
	States
	{
	Spawn:
		JPIL B -1
		Stop
	}
}

ACTOR NTomb1 //3d actor
{
	//$Category Props (Wolf3D)/Street
	//$Title Tombstone 1
	//$Color 3
	Radius 15
	Height 68
	+SOLID
	States
	{
	Spawn:
		MDLA A -1
		Stop
	}
}

ACTOR NTomb2 : NTomb1 //3d actor
{
	//$Title Tombstone 2
	Radius 15
	Height 62
}

ACTOR NTomb3 : NTomb1 //3d actor
{
	//$Title Tombstone 3
	Radius 15
	Height 48
}

ACTOR NTomb4 : NTomb1 //3d actor
{
	//$Title Tombstone 4
	Radius 15
	Height 43
}

ACTOR NTomb5 : NTomb1 //3d actor
{
	//$Title Tombstone 5
	Radius 12
	Height 35
}

ACTOR Haycart //3d actor
{
	//$Category Props (Wolf3D)/Street
	//$Title Haycart
	//$Color 3
	
	//$Arg0 "Type"
	//$Arg0Tooltip "Pickup the desired type\nNormal: 0\nDestroyed: 1"
	
	Radius 24 //reduced for map-wise placement - ozy
	Height 32
	+SOLID
	DistanceCheck "scenelod"
	States
	{
	Spawn:
		MDLA A 0 NoDelay A_JumpIf(Args[0]==1, "Destroyed")
		MDLA A -1
		Stop
		Destroyed:
		MDLA B -1
		Stop
	}
}

ACTOR BurningBarrelBoA
{
	//$Category Props (Wolf3D)/Street
	//$Title Burning Barrel (Destroyable)
	//$Color 3
	Radius 16
	Height 64
	Health 35
	Mass 0x7ffffff
	+NOBLOOD
	+NOTAUTOAIMED
	+PUSHABLE
	+SHOOTABLE
	+SOLID
	States
	{
	Spawn:
		BBAR A 0 NoDelay A_PlaySound("SFX/FireLoop1", CHAN_BODY, frandom(0.4,0.8), true, ATTN_STATIC)
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("FloatingCinder", 0, 0, random(48,54), 1, 0, random (1, 3), random (0, 360), SXF_TRANSFERPITCH | SXF_CLIENTSIDE) //Ozy81 from Gore
		BBAR ABCD 3
		Loop
	Death:
		TNT1 A 0 A_UnSetSolid
		"####" A 0 A_StopSound(CHAN_BODY)
		"####" A 0 A_PlaySound("METALBRK")
		"####" AAAAAA 0 A_SpawnItemEx ("Debris_Metal3Dark", random(0,16), random(0,16), random(0,56), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" A 0 A_SpawnItemEx("KD_HL2SmokeGenerator", 0, 0, 0, 0, 0, 0, 0,SXF_CLIENTSIDE | SXF_TRANSFERSCALE)
		"####" AAAAAA 0 A_SpawnItemEx ("BarrelFireSpawner", random(8,12), random(8,12), random(0,56), 0, 0, 0, random(0,360), SXF_TRANSFERPITCH | SXF_CLIENTSIDE)
		BBAR E -1
		Stop
	}
}