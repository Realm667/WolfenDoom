//base actors
ACTOR LightBase : SwitchableDecoration
{
	//$Category Lights (BoA)/ACS Switchable
	//$Color 11
	DistanceCheck "scenelod"
	+NOGRAVITY
}

ACTOR LightBase2 : LightBase
{
	+NOBLOCKMAP
	+NOTAUTOAIMED
	+SOLID
}

ACTOR LightBaseArgs : LightBase
{
	//$Category Lights (BoA)/ARGs
	//$Arg0 Sound
	//$Arg0Type 11
	//$Arg0Enum { 0 = "Yes"; 1 = "No"; }
	+NOBLOCKMAP
	+SOLID
}

ACTOR LightBaseInt : LightBase
{
	//$Category Lights (BoA)/ACS Switchable
	Health -1
	-FLOORCLIP
	-NOBLOCKMAP
	+DONTSPLASH
	+DONTTHRUST
	+NOBLOOD
	+NOBLOODDECALS
	+NODAMAGE
	+NOTAUTOAIMED
	+SHOOTABLE
}

ACTOR 3dGlass : ModelBase
{
	RenderStyle "Shaded"
	StencilColor "CC CC CC"
}

//3d lights
ACTOR ChanGold : LightBase
{
	//$Title 3d Gold Chandelier (SWITCHABLE)
	Radius 16
	Height 52
	+SPAWNCEILING
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;} //mxd. DORMANT flag must be updated manually
	Spawn:
		MDLA A -1 LIGHT("3DCHAN")
		Stop
	Inactive:
		MDLA B -1 {bDormant = TRUE;} //mxd. DORMANT flag must be updated manually
		Stop
	}
}

ACTOR WallLight1 : ModelBase
{
	//$Category Lights (BoA)/Static
	//$Title 3d Wall Light (OFF, STATIC)
	//$Color 11
	DistanceCheck "scenelod"
	Radius 4
	Height 16
	-SOLID
	+NOBLOCKMAP
	+NOGRAVITY
}

ACTOR WallLight1Lit : WallLight1
{
	//$Title 3d Wall Light (ON, STATIC)
	States
	{
	Spawn:
		MDLA A 0 NODELAY A_SpawnItemEx("VolumetricLight_ConeDown", ScaleX*16, 0, 24, 0, 0, 0, 0, SXF_ISTRACER, 0, tid)
		MDLA A -1 LIGHT(OZYWALLT)
		Stop
	}
}

ACTOR WallLight1NC : WallLight1
{
	//$Category Lights (BoA)/No Cones
	//$Title 3d Wall Light, no cone (ON, STATIC)
	States
	{
	Spawn:
		MDLA A -1 LIGHT(OZYWALT3)
		Stop
	}
}

ACTOR WallLight2 : WallLight1
{
	//$Title 3d Wall Lantern (OFF, STATIC)
	Height 24
}

ACTOR WallLight2Lit : WallLight2
{
	//$Title 3d Wall Lantern (ON, STATIC)
	States
	{
	Spawn:
		MDLA A -1 LIGHT(OZYWALLT)
		Stop
	}
}

ACTOR SimpleLight1 : LightBaseInt
{
	//$Title 3d Simple Light (OFF, SET DORMANT & ACTIVATE IT VIA ACS)
	Radius 8
	Height 12
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A 0 NODELAY
	Activate:
		MDLA A -1
		Loop
	Inactive:
		MDLA A 0 A_SpawnItemEx("VolumetricLight_ConeDown", 0, 0, ScaleX-ScaleY-Height/24, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid)
	InactiveLights:
		MDLA A 0 {bDormant = TRUE;}
		MDLA A 0 A_RemoveChildren(TRUE, RMVF_MISC)
		MDLA B -1 LIGHT(OZYSIMLT)
		Stop
	}
}

ACTOR SimpleLight1Lit : SimpleLight1
{
	//$Title 3d Simple Light (ON, SET DORMANT & ACTIVATE IT VIA ACS)
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A 0 NODELAY
	ActiveLights:
		MDLA A 0 A_SpawnItemEx("VolumetricLight_ConeDown", 0, 0, ScaleX-ScaleY-Height/24, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid)
	Activate:
		MDLA A 1 LIGHT(OZYSIMLT)
		Loop
	Inactive:
		MDLA A 0 {bDormant = TRUE;}
		MDLA A 0 A_RemoveChildren(TRUE, RMVF_MISC)
		MDLA B -1
		Stop
	}
}

ACTOR SimpleLight1NC : SimpleLight1
{
	//$Category Lights (BoA)/No Cones
	//$Title 3d Simple Light, no cone (ON, SET DORMANT & ACTIVATE IT VIA ACS)
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A 0 NODELAY
	Activate:
		MDLA A 1 LIGHT(OZYSIMLT)
		Loop
	Inactive:
		MDLA A 0 {bDormant = TRUE;}
		MDLA B -1
		Stop
	}
}

ACTOR WallLight2Switch : SimpleLight1
{
	//$Category Lights (BoA)/No Cones
	//$Title 3d Wall Lantern, no cone (ON, SWITCHABLE)
	Radius 4
	Height 24
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A 0 NODELAY
	Activate:
		MDLA A 1 LIGHT(OZYSIMLT)
		Loop
	Inactive:
		MDLA A 0 {bDormant = TRUE;}
		MDLA B -1
		Stop
	}
}

ACTOR Chandelier1Switchable : LightBaseARGs
{
	//$Title 3d Chandelier with Tall Candles (RUSTY, ARGS)
	Radius 10
	Height 56
	+SPAWNCEILING
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A 0 NODELAY A_PlaySound("FIRE_SMALL", CHAN_BODY, frandom(0.2,0.4) - args[0], TRUE)
	ActiveFlames: //ozy. let's apply these nice flames on candles first - coords be damned
		MDLA A 0 {
		A_SpawnItemEx("3dFlame_Normal", ScaleX*1, 	ScaleY*33, 	ScaleX+ScaleY*14, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid);
		A_SpawnItemEx("3dFlame_Normal", ScaleX*1, 	ScaleY*-31, ScaleX+ScaleY*5, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid);
		A_SpawnItemEx("3dFlame_Normal", ScaleX*24, 	ScaleY*23, 	ScaleX+ScaleY*7, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid);
		A_SpawnItemEx("3dFlame_Normal", ScaleX*-22, ScaleY*-22, ScaleX+ScaleY*7, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid);
		A_SpawnItemEx("3dFlame_Normal", ScaleX*34, 	ScaleY*1, 	ScaleX+ScaleY*14, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid);
		A_SpawnItemEx("3dFlame_Normal", ScaleX*-31, ScaleY*1, 	ScaleX+ScaleY*12, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid);
		A_SpawnItemEx("3dFlame_Normal", ScaleX*-22, ScaleY*23, 	ScaleX+ScaleY*5, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid);
		A_SpawnItemEx("3dFlame_Normal", ScaleX*24, 	ScaleY*-22, ScaleX+ScaleY*8, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid); }
	Activate:
		MDLA A 1
		Loop
	Inactive:
		MDLA A 0 {bDormant = TRUE;}
		MDLA A 0 A_RemoveChildren(TRUE, RMVF_MISC) //ozy81 - remove flames if deactivated
		"####" A -1 A_StopSound(CHAN_BODY)
		Stop
	}
}

ACTOR Chandelier2Switchable : Chandelier1Switchable
{
	//$Title 3d Chandelier with Short Candles (RUSTY, ARGS)
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A 0 NODELAY A_PlaySound("FIRE_SMALL", CHAN_BODY, frandom(0.2,0.4) - args[0], TRUE)
	ActiveFlames:
		MDLA A 0 {
		A_SpawnItemEx("3dFlame_Normal", ScaleX*0, ScaleY*26, 	ScaleX+ScaleY*10, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid);
		A_SpawnItemEx("3dFlame_Normal", ScaleX*0, ScaleY*-26.5, ScaleX+ScaleY*6, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid);
		A_SpawnItemEx("3dFlame_Normal", ScaleX*18.7, ScaleY*18.2, ScaleX+ScaleY*7, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid);
		A_SpawnItemEx("3dFlame_Normal", ScaleX*-18.8, ScaleY*-18.8, ScaleX+ScaleY*7, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid);
		A_SpawnItemEx("3dFlame_Normal", ScaleX*26.5, ScaleY*-0.2, ScaleX+ScaleY*10, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid);
		A_SpawnItemEx("3dFlame_Normal", ScaleX*-26.4, ScaleY*0, ScaleX+ScaleY*8.8,0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid);
		A_SpawnItemEx("3dFlame_Normal", ScaleX*-18.8, ScaleY*18.7, 	ScaleX+ScaleY*5, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid);
		A_SpawnItemEx("3dFlame_Normal", ScaleX*19.2, ScaleY*-18.9, ScaleX+ScaleY*8, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid); }
	Activate:
		MDLA A 1
		Loop
	Inactive:
		MDLA A 0 {bDormant = TRUE;}
		MDLA A 0 A_RemoveChildren(TRUE, RMVF_MISC)
		"####" A -1 A_StopSound(CHAN_BODY)
		Stop
	}
}

ACTOR Chandelier3Switchable : Chandelier1Switchable
{
	//$Title 3d Chandelier with Tall Candles (BLACK, ARGS)
}

ACTOR Chandelier4Switchable : Chandelier2Switchable
{
	//$Title 3d Chandelier with Short Candles (BLACK, ARGS)
}

ACTOR ArtDeco_WLight1 : SwitchableDecoration
{
	//$Category Lights (BoA)/ACS Switchable
	//$Title 3d Art-Deco Wall Light 1 (SET DORMANT & ACTIVATE IT VIA ACS)
	//$Color 11
	DistanceCheck "scenelod"
	Radius 1
	Height 1
	-FLOORCLIP
	+NOBLOCKMAP
	+DONTTHRUST
	+NOGRAVITY
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;} //update manually on GZDB
	Spawn:
		MDLA A -1 LIGHT(OZYWALT2)
		Stop
	Inactive:
		MDLA B -1 {bDormant = TRUE;} //update manually on GZDB
		Stop
	}
}

ACTOR CODNeoLights1 : LightBaseInt
{
	//$Category Lights (BoA)/ACS Switchable
	//$Title 3d Neoclassical Wall Light (SET DORMANT & ACTIVATE IT VIA ACS)
	Radius 8
	Height 24
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;} //update manually on GZDB
	Spawn:
		MDLA A -1 LIGHT(OZYWALLT)
		Stop
	Inactive:
		MDLA B -1 {bDormant = TRUE;} //update manually on GZDB
		Stop
	}
}

ACTOR CODNeoLights2 : CODNeoLights1
{
	//$Title 3d Neoclassical Wartorn Wall Light (SET DORMANT & ACTIVATE IT VIA ACS)
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;} //update manually on GZDB
	Spawn:
		MDLA A -1 LIGHT(OZYWALT4)
		Stop
	Inactive:
		MDLA B -1 {bDormant = TRUE;} //update manually on GZDB
		Stop
	}
}

ACTOR COD_ArtDecoLight1A : SwitchableDecoration
{
	//$Category Lights (BoA)/ACS Switchable
	//$Title 3d Art Deco Ceiling Light (OFF, SET DORMANT & ACTIVATE IT VIA ACS)
	//$Color 11
	DistanceCheck "scenelod"
	Radius 8
	Height 0
	+NOBLOCKMAP
	+NOGRAVITY
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A 0 NODELAY
	Activate:
		MDLA A -1
		Loop
	Inactive:
		MDLA A 0 {bDormant = TRUE;}
		MDLA B -1 LIGHT(OZYSIMLT)
		Stop
	}
}

ACTOR COD_ArtDecoLight1B : COD_ArtDecoLight1A
{
	//$Title 3d Art Deco Ceiling Light (ON, SET DORMANT & ACTIVATE IT VIA ACS)
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A 0 NODELAY
	Activate:
		MDLA A -1 LIGHT(OZYSIMLT)
		Loop
	Inactive:
		MDLA A 0 {bDormant = TRUE;}
		MDLA B -1
		Stop
	}
}

ACTOR COD_StationLight1A : COD_ArtDecoLight1A
{
	//$Title 3d Train Station Ceiling Light (OFF, SET DORMANT & ACTIVATE IT VIA ACS)
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A 0 NODELAY
	Activate:
		MDLA A -1
		Loop
	Inactive:
		MDLA A 0 A_SpawnItemEx("VolumetricLight_ConeDown", 0, 0, ScaleX+ScaleY*1, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid)
	InactiveLights:
		MDLA A 0 {bDormant = TRUE;}
		MDLA A 0 A_RemoveChildren(TRUE, RMVF_MISC)
		MDLA B -1 LIGHT(OZYSIMLT)
		Stop
	}
}

ACTOR COD_StationLight1B : COD_ArtDecoLight1A
{
	//$Title 3d Train Station Ceiling Light (ON, SET DORMANT & ACTIVATE IT VIA ACS)
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A 0 NODELAY
	ActiveLights:
		MDLA A 0 A_SpawnItemEx("VolumetricLight_ConeDown", 0, 0, ScaleX+ScaleY*1, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid)
	Activate:
		MDLA A 1 LIGHT(OZYSIMLT)
		Loop
	Inactive:
		MDLA A 0 {bDormant = TRUE;}
		MDLA A 0 A_RemoveChildren(TRUE, RMVF_MISC)
		MDLA B -1
		Stop
	}
}

ACTOR COD_StationLight1BNC : COD_ArtDecoLight1A
{
	//$Category Lights (BoA)/No Cones
	//$Title 3d Train Station Ceiling Light, no cone (ON, SET DORMANT & ACTIVATE IT VIA ACS)
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A 0 NODELAY
	Activate:
		MDLA A 1 LIGHT(OZYSIMLT)
		Loop
	Inactive:
		MDLA A 0 {bDormant = TRUE;}
		MDLA B -1
		Stop
	}
}

ACTOR COD_RustyChandelier1A : COD_ArtDecoLight1A
{
	//$Title 3d Rusty Chandelier (OFF, SET DORMANT & ACTIVATE IT VIA ACS)
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A 0 NODELAY
	Activate:
		MDLA A -1
		Loop
	Inactive:
		MDLA A 0 {bDormant = TRUE;}
		MDLA B -1 LIGHT(3DCHAN2)
		Stop
	}
}

ACTOR COD_RustyChandelier1B : COD_ArtDecoLight1A
{
	//$Title 3d Rusty Chandelier (ON, SET DORMANT & ACTIVATE IT VIA ACS)
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A 0 NODELAY
	Activate:
		MDLA A -1 LIGHT(3DCHAN2)
		Loop
	Inactive:
		MDLA A 0 {bDormant = TRUE;}
		MDLA B -1
		Stop
	}
}

ACTOR COD_LampGlassOn : 3dGlass { DistanceCheck "scenelod" }
ACTOR COD_LampGlassOff : 3dGlass { DistanceCheck "scenelod" }

ACTOR COD_OilLamp : LightBaseARGs
{
	//$Title 3d Oil Lamp (RUSTY, SWITCHABLE)
	Radius 4
	Height 4
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A 0 NODELAY A_PlaySound("FIRE_SMALL", CHAN_BODY, 0.4 - args[0], TRUE)
	ActiveFlames:
		MDLA A 0 {A_SpawnItemEx("3dFlame_Lamp", 0, 0, ScaleX+ScaleY*6, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid);
				A_SpawnItemEx("COD_LampGlassOn", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPITCH | SXF_TRANSFERROLL | SXF_TRANSFERSCALE, 0, tid);}
	Activate:
		MDLA A 1
		Loop
	Inactive:
		MDLA A 0 {bDormant = TRUE;}
		MDLA A 0 A_RemoveChildren(TRUE, RMVF_MISC) //ozy. remove flames & lit glass if deactivated
		MDLA B 0 A_SpawnItemEx("COD_LampGlassOff", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPITCH | SXF_TRANSFERROLL | SXF_TRANSFERSCALE) //add unlit glass
		"####" B -1 A_StopSound(CHAN_BODY)
		Stop
	}
}

//needed for 3d chandeliers
ACTOR 3dFlame_Normal
{
	DistanceCheck "sfxlod"
	Scale 0.06
	+FORCEYBILLBOARD
	+NOBLOCKMAP
	+NOGRAVITY
	+NOINTERACTION
	+RANDOMIZE
	RenderStyle "Add"
	States
	{
	Spawn:
		TNT1 A 0 A_Jump(235,2) //cindeeeeeeeeeeeeeerz! Yes, I love them - ozy
		TNT1 A 0 A_SpawnItemEx("FloatingCinder", 0, 0, random(0,2), 1, 0, random (1, 3), random (0, 360), SXF_TRANSFERPITCH | SXF_CLIENTSIDE)
		TNT1 A 0 A_Jump(128,"Flames2","Flames3","Flames4")
		3DFR ABCDEFGHIJ 2 LIGHT ("3DFLAME_N")
		Loop
	Flames2:
		3DFR JFHGEDCABI 2 LIGHT ("3DFLAME_N")
		Goto Spawn
	Flames3:
		3DFR BDCEGIJHFA 2 LIGHT ("3DFLAME_N")
		Goto Spawn
	Flames4:
		3DFR FADCEGIHJB 2 LIGHT ("3DFLAME_N")
		Goto Spawn
	}
}

ACTOR 3dFlame_Short : 3dFlame_Normal
{
	Scale 0.03
	States
	{
	Spawn:
		TNT1 A 0 A_Jump(250,2)
		TNT1 A 0 A_SpawnItemEx("FloatingCinder", 0, 0, random(0,2), 1, 0, random (1, 3), random (0, 360), SXF_TRANSFERPITCH | SXF_CLIENTSIDE)
		TNT1 A 0 A_Jump(128,"Flames2","Flames3","Flames4")
		3DFR ABCDEFGHIJ 2 LIGHT ("3DFLAME_S")
		Loop
	Flames2:
		3DFR JFHGEDCABI 2 LIGHT ("3DFLAME_S")
		Goto Spawn
	Flames3:
		3DFR BDCEGIJHFA 2 LIGHT ("3DFLAME_S")
		Goto Spawn
	Flames4:
		3DFR FADCEGIHJB 2 LIGHT ("3DFLAME_S")
		Goto Spawn
	}
}

ACTOR 3dFlame_Tall : 3dFlame_Normal
{
	Scale 0.09
	States
	{
	Spawn:
		TNT1 A 0 A_Jump(215,2)
		TNT1 A 0 A_SpawnItemEx("FloatingCinder", 0, 0, random(0,2), 1, 0, random (1, 3), random (0, 360), SXF_TRANSFERPITCH | SXF_CLIENTSIDE)
		TNT1 A 0 A_Jump(128,"Flames2","Flames3","Flames4")
		3DFR ABCDEFGHIJ 2 LIGHT ("3DFLAME_T")
		Loop
	Flames2:
		3DFR JFHGEDCABI 2 LIGHT ("3DFLAME_T")
		Goto Spawn
	Flames3:
		3DFR BDCEGIJHFA 2 LIGHT ("3DFLAME_T")
		Goto Spawn
	Flames4:
		3DFR FADCEGIHJB 2 LIGHT ("3DFLAME_T")
		Goto Spawn
	}
}

ACTOR 3dFlame_Lamp : 3dFlame_Normal
{
	Scale 0.02
	States
	{
	Spawn:
		TNT1 A 0 A_Jump(250,2)
		TNT1 A 0 A_SpawnItemEx("FloatingCinder", 0, 0, random(0,2), 1, 0, random (1, 3), random (0, 360), SXF_TRANSFERPITCH | SXF_CLIENTSIDE)
		TNT1 A 0 A_Jump(128,"Flames2","Flames3","Flames4")
		3DFR ABCDEFGHIJ 2 LIGHT ("3DFLAME_L")
		Loop
	Flames2:
		3DFR JFHGEDCABI 2 LIGHT ("3DFLAME_L")
		Goto Spawn
	Flames3:
		3DFR BDCEGIJHFA 2 LIGHT ("3DFLAME_L")
		Goto Spawn
	Flames4:
		3DFR FADCEGIHJB 2 LIGHT ("3DFLAME_L")
		Goto Spawn
	}
}

ACTOR 3dFlame_Oven : 3dFlame_Normal
{
	Scale 0.08
	States
	{
	Spawn:
		TNT1 A 0 A_Jump(144,2)
		TNT1 A 0 A_SpawnItemEx("FloatingCinder2", ScaleX*48, 0, 4, 1, 0, random (1, 3), random (0, 360), SXF_TRANSFERPITCH | SXF_CLIENTSIDE)
		TNT1 A 0 A_Jump(128,"Flames2","Flames3","Flames4")
		3DFR ABCDEFGHIJ 2 LIGHT ("WolfStove")
		Loop
	Flames2:
		3DFR JFHGEDCABI 2 LIGHT ("WolfStove")
		Goto Spawn
	Flames3:
		3DFR BDCEGIJHFA 2 LIGHT ("WolfStove")
		Goto Spawn
	Flames4:
		3DFR FADCEGIHJB 2 LIGHT ("WolfStove")
		Goto Spawn
	}
}

ACTOR CeilingLightM : LightBase2
{
	//$Title 3d Ceiling Light (MIDDLE, SWITCHABLE)
	Radius 16
	Height 23
	+SPAWNCEILING
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A 0 NODELAY
	ActiveLights:
		MDLA A 0 A_SpawnItemEx("VolumetricLight_ConeDown", 0, 0, ScaleX+ScaleY*12, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid)
	Activate:
		MDLA A 1
		Loop
	Inactive:
		MDLA A 0 {bDormant = TRUE;}
		MDLA A 0 A_RemoveChildren(TRUE, RMVF_MISC)
		MDLA B -1
		Stop
	}
}

ACTOR CeilingLightM2 : CeilingLightM
{
	//$Title 3d Ceiling Light (SMALL, SWITCHABLE)
	Radius 8
	Height 10
	-SOLID
	States
	{
	ActiveLights:
		MDLA A 0 A_SpawnItemEx("VolumetricLight_ConeDown", 0, 0, ScaleX+ScaleY*8, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid)
	Activate:
		MDLA A 1
		Loop
	}
}

ACTOR CeilingLightMNC : CeilingLightM
{
	//$Category Lights (BoA)/No Cones
	//$Title 3d Ceiling Light, no cone (MIDDLE, SWITCHABLE)
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A 0 NODELAY
	Activate:
		MDLA A 1
		Loop
	Inactive:
		MDLA A 0 {bDormant = TRUE;}
		MDLA A 0 A_RemoveChildren(TRUE, RMVF_MISC)
		MDLA B -1
		Stop
	}
}

ACTOR CeilingLightM2NC : CeilingLightM
{
	//$Category Lights (BoA)/No Cones
	//$Title 3d Ceiling Light, no cone (SMALL, SWITCHABLE)
	Radius 8
	Height 10
	-SOLID
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A 0 NODELAY
	Activate:
		MDLA A 1
		Loop
	Inactive:
		MDLA A 0 {bDormant = TRUE;}
		MDLA A 0 A_RemoveChildren(TRUE, RMVF_MISC)
		MDLA B -1
		Stop
	}
}

ACTOR StreetLight1 : SwitchableDecoration
{
	//$Category Lights (BoA)/ACS Switchable
	//$Title 3d Street Light (OFF, SWITCHABLE)
	//$Color 11
	DistanceCheck "scenelod"
	Radius 4
	Height 128
	+SOLID
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
		MDLA A 0 A_RemoveChildren(TRUE, RMVF_MISC)
	Spawn:
		MDLA B -1
		Stop
	Inactive:
		MDLA A 0 {bDormant = TRUE;}
	InactiveLights:
		MDLA A 0 A_SpawnItemEx("VolumetricLight_ConeDown", ScaleX*50, 0, ScaleX+ScaleY*128, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid)
	InactiveStop:
		MDLA A 1 LIGHT("LIGHTPOSTLIGHT")
		Loop
	}
}

ACTOR StreetLight1Lit : StreetLight1
{
	//$Title 3d Street Light (ON, SWITCHABLE)
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A 0 NODELAY
	ActiveLights:
		MDLA A 0 A_SpawnItemEx("VolumetricLight_ConeDown", ScaleX*50, 0, ScaleX+ScaleY*128, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid)
	Activate:
		MDLA A 1 LIGHT("LIGHTPOSTLIGHT")
		Loop
	Inactive:
		MDLA A 0 {bDormant = TRUE;}
		MDLA A 0 A_RemoveChildren(TRUE, RMVF_MISC)
		MDLA B -1
		Stop
	}
}

ACTOR StreetLight2 : SwitchableDecoration
{
	//$Category Lights (BoA)/ACS Switchable
	//$Title 3d Street Light (TRIPLE, SWITCHABLE)
	//$Color 11
	DistanceCheck "scenelod"
	Radius 4
	Height 128
	+SOLID
	States
	{
	Active:
		MDLA C 0 {bDormant = FALSE;}
	Spawn:
		MDLA C -1 LIGHT("BOASTLT2")
		Stop
	Inactive:
		MDLA A -1 {bDormant = TRUE;}
		Stop
	}
}

ACTOR StreetLight3 : StreetLight2
{
	//$Title 3d Street Light (SINGLE, SWITCHABLE)
	States
	{
	Active:
		MDLA D 0 {bDormant = FALSE;}
	Spawn:
		MDLA D -1 LIGHT("BOASTLT3")
		Stop
	Inactive:
		MDLA B -1 {bDormant = TRUE;}
		Stop
	}
}

ACTOR CastleLight1 : SwitchableDecoration
{
	//$Category Lights (BoA)/ACS Switchable
	//$Title 3d Castle Light 1 (SWITCHABLE)
	//$Color 11
	DistanceCheck "scenelod"
	Radius 16
	Height 32
	+NOBLOCKMAP
	+NOGRAVITY
	+SOLID
	+SPAWNCEILING
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A 0 NODELAY A_SpawnItemEx("VolumetricLight_ConeDown", 0, 0, ScaleX+ScaleY*28, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid)
		MDLA A -1 LIGHT("BOACEIL3")
		Stop
	Inactive:
		MDLA A 1 {
			A_KillChildren;
			A_RemoveChildren(TRUE, RMVF_MISC);
		}
		MDLA B -1 {bDormant = TRUE;}
		Stop
	}
}

ACTOR CastleLight2 : CastleLight1
{
	//$Title 3d Castle Light 2 (SWITCHABLE)
	Height 56
	States
	{
	Spawn:
		MDLA A 0 NODELAY A_SpawnItemEx("VolumetricLight_ConeDown", 0, 0, ScaleX+ScaleY*52, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid)
		MDLA A -1 LIGHT("BOACEIL3")
		Stop
	}
}

ACTOR CastleLight1NC : CastleLight1
{
	//$Category Lights (BoA)/No Cones
	//$Title 3d Castle Light 1, no cone (SWITCHABLE)
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A -1 LIGHT("BOACEIL3")
		Stop
	Inactive:
		MDLA B -1 {bDormant = TRUE;}
		Stop
	}
}

ACTOR CastleLight2NC : CastleLight2
{
	//$Category Lights (BoA)/No Cones
	//$Title 3d Castle Light 2, no cone (SWITCHABLE)
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A -1 LIGHT("BOACEIL3")
		Stop
	Inactive:
		MDLA B -1 {bDormant = TRUE;}
		Stop
	}
}

ACTOR Cell_Light : CastleLight1
{
	//$Title 3d Cell Light (SWITCHABLE)
	Radius 8
	Height 1
	States
	{
	Spawn:
		MDLA A 0 NODELAY A_SpawnItemEx("VolumetricLight_ConeDown", 0, 0, ScaleX+ScaleY*-30, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid)
		MDLA A -1 LIGHT("BOACEIL2")
		Stop
	}
}

ACTOR Cell_LightNC : CastleLight1
{
	//$Category Lights (BoA)/No Cones
	//$Title 3d Cell Light, no cone (SWITCHABLE)
	Radius 8
	Height 1
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A -1 LIGHT("BOACEIL2")
		Stop
	Inactive:
		MDLA B -1 {bDormant = TRUE;}
		Stop
	}
}

ACTOR ColumnLight
{
	//$Category Lights (BoA)/Static
	//$Title 3d Column Light (ON)
	//$Color 11
	DistanceCheck "scenelod"
	Radius 8
	Height 136
	+SOLID
	States
	{
	Spawn:
		MDLA A -1
		Stop
	}
}

ACTOR ColumnLight2 : ColumnLight
{
	//$Title 3d Column Light (OFF)
	States
	{
	Spawn:
		MDLA B -1
		Stop
	}
}

ACTOR StageLight : ColumnLight
{
	//$Title 3d Theatre Stage Light
	Height 8
	-SOLID
	+NOGRAVITY
	States
	{
	Spawn:
		MDLA A -1
		Stop
	}
}

ACTOR FutureCeilingLight : LightBase
{
	//$Title 3d Modern Ceiling Light (SWITCHABLE)
	Radius 16
	Height 0
	+SPAWNCEILING
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A 0 NODELAY
	ActiveLights:
		MDLA A 0 A_SpawnItemEx("VolumetricLight_ConeDown", 0, 0, ScaleX+ScaleY*8, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid)
	Activate:
		MDLA A 1
		Loop
	Inactive:
		MDLA A 0 {bDormant = TRUE;}
		MDLA A 0 A_RemoveChildren(TRUE, RMVF_MISC)
		MDLA B -1
		Stop
	}
}

ACTOR FutureCeilingLightNC : FutureCeilingLight
{
	//$Category Lights (BoA)/No Cones
	//$Title 3d Modern Ceiling Light, no cone (SWITCHABLE)
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A 0 NODELAY
	Activate:
		MDLA A 1
		Loop
	Inactive:
		MDLA A 0 {bDormant = TRUE;}
		MDLA B -1
		Stop
	}
}

ACTOR FutureCeilingLightNC2 : LightBase2
{
	//$Category Lights (BoA)/No Cones
	//$Title 3d Modern Ceiling Light, no cone & no hitbox (SWITCHABLE)
	-SOLID
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A 0 NODELAY
	Activate:
		MDLA A 1
		Loop
	Inactive:
		MDLA A 0 {bDormant = TRUE;}
		MDLA B -1
		Stop
	}
}

ACTOR MedicalLight : LightBase2
{
	//$Category Lights (BoA)/No Cones
	//$Title Medical Operating Light, no hitbox (SWITCHABLE)
	Radius 32
	Height 0
	-SOLID
	+SPAWNCEILING
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A 0 NODELAY
	Activate:
		MDLA A 1
		Loop
	Inactive:
		MDLA A 0 {bDormant = TRUE;}
		MDLA B -1
		Stop
	}
}

ACTOR LightBulb : SwitchableDecoration
{
	//$Category Lights (BoA)/ARGs
	//$Title 3d Light Bulb (OFF, BREAKABLE)
	//$Color 11
	//$Arg0 "Type"
	//$Arg0Tooltip "Pickup the desired type\nBreakable: 0\nUnBreakable: 1"
	DistanceCheck "scenelod"
	Radius 4
	Height 1
	Health 1
	+DONTFALL
	+DONTTHRUST
	+NOBLOOD
	+NOGRAVITY
	+NOTAUTOAIMED
	+SHOOTABLE
	+SOLID
	+SPAWNCEILING
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A -1 NODELAY A_JumpIf(Args[0]==1, "UnBreakable")
		Stop
	UnBreakable:
		"####" "#" 0 A_ChangeLinkFlags(1)
		"####" "#" -1
		Stop
	Inactive:
		MDLA B -1 BRIGHT LIGHT(BOACEIL5) {bDormant = TRUE;}
		Stop
	Death:
		TNT1 A 0 A_UnSetSolid
		"####" AAAA 0 A_SpawnItemEx("Debris_GlassShard_Small", 0, 0, 0, random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" A 0 A_PlaySound("GLASS5")
		MDLA CCCCCC 3 A_SpawnItemEx("Spark_B1", 0, 0, 0, 0, frandom(-1.0,1.0), frandom(-1.0,1.0), random(0,360), SXF_CLIENTSIDE)
		MDLA C -1
		Stop
	}
}

ACTOR LightBulbOn : LightBulb
{
	//$Title 3d Light Bulb (ON, BREAKABLE)
	States
	{
	Active:
		MDLA B 0 {bDormant = FALSE;}
	Spawn:
		MDLA B -1 NODELAY BRIGHT LIGHT(BOACEIL5) A_JumpIf(Args[0]==1, "UnBreakable")
		Stop
	UnBreakable:
		"####" "#" 0 A_ChangeLinkFlags(1)
		"####" "#" -1 BRIGHT LIGHT(BOACEIL5)
		Stop
	Inactive:
		MDLA A -1 {bDormant = TRUE;}
		Stop
	}
}

ACTOR LightBulb_Red : LightBulb
{
	//$Title 3d Light Bulb, Red (OFF, BREAKABLE)
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A -1 NODELAY A_JumpIf(Args[0]==1, "UnBreakable")
		Stop
	UnBreakable:
		"####" "#" 0 A_ChangeLinkFlags(1)
		"####" "#" -1
		Stop
	Inactive:
		MDLA B -1 BRIGHT LIGHT(BOACEIL6) {bDormant = TRUE;}
		Stop
	}
}

ACTOR LightBulbOn_Red : LightBulb
{
	//$Title 3d Light Bulb, Red (ON, BREAKABLE)
	States
	{
	Active:
		MDLA B 0 {bDormant = FALSE;}
	Spawn:
		MDLA B -1 NODELAY BRIGHT LIGHT(BOACEIL6) A_JumpIf(Args[0]==1, "UnBreakable")
		Stop
	UnBreakable:
		"####" "#" 0 A_ChangeLinkFlags(1)
		"####" "#" -1 BRIGHT LIGHT(BOACEIL6)
		Stop
	Inactive:
		MDLA A -1 {bDormant = TRUE;}
		Stop
	}
}

ACTOR LightBulb_Grn : LightBulb
{
	//$Title 3d Light Bulb, Green (OFF, BREAKABLE)
	States
	{
	Active:
		MDLA A 0 {bDormant = FALSE;}
	Spawn:
		MDLA A -1 NODELAY A_JumpIf(Args[0]==1, "UnBreakable")
		Stop
	UnBreakable:
		"####" "#" 0 A_ChangeLinkFlags(1)
		"####" "#" -1
		Stop
	Inactive:
		MDLA B -1 BRIGHT LIGHT(BOACEIL7) {bDormant = TRUE;}
		Stop
	}
}

ACTOR LightBulbOn_Grn : LightBulb
{
	//$Title 3d Light Bulb, Green (ON, BREAKABLE)
	States
	{
	Active:
		MDLA B 0 {bDormant = FALSE;}
	Spawn:
		MDLA B -1 NODELAY BRIGHT LIGHT(BOACEIL7) A_JumpIf(Args[0]==1, "UnBreakable")
		Stop
	UnBreakable:
		"####" "#" 0 A_ChangeLinkFlags(1)
		"####" "#" -1 BRIGHT LIGHT(BOACEIL7)
		Stop
	Inactive:
		MDLA A -1 {bDormant = TRUE;}
		Stop
	}
}

//Non-Interactive variants
ACTOR CeilingLightM_NoInt : CeilingLightM
{
	//$Category Lights (BoA)/Non-Interactive
	//$Title 3d Non-Interactive Ceiling Light (MIDDLE, SWITCHABLE)
	+NOINTERACTION
}

ACTOR CeilingLightM2_NoInt : CeilingLightM2
{
	//$Category Lights (BoA)/Non-Interactive
	//$Title 3d Non-Interactive Ceiling Light (SMALL, SWITCHABLE)
	+NOINTERACTION
}

ACTOR CastleLight1_NoInt : CastleLight1
{
	//$Category Lights (BoA)/Non-Interactive
	//$Title 3d Non-Interactive Castle Light 1 (SWITCHABLE)
	+NOINTERACTION
}

ACTOR CastleLight2_NoInt : CastleLight1
{
	//$Category Lights (BoA)/Non-Interactive
	//$Title 3d Non-Interactive Castle Light 2 (SWITCHABLE)
	+NOINTERACTION
}

ACTOR ChanGold_NoInt : ChanGold
{
	//$Category Lights (BoA)/Non-Interactive
	//$Title 3d Non-Interactive Gold Chandelier (SWITCHABLE)
	+NOINTERACTION
}

ACTOR Cell_Light_NoInt : Cell_Light
{
	//$Category Lights (BoA)/Non-Interactive
	//$Title 3d Non-Interactive Cell Light (SWITCHABLE)
	+NOINTERACTION
}

//2d Lights
ACTOR TableLightM : SwitchableDecoration
{
	//$Category Lights (BoA)/ACS Switchable
	//$Title 2d Bureau Light 1 (MIDDLE, SWITCHABLE)
	//$Color 11
	Radius 4
	Height 32
	Scale 0.50
	ProjectilePassHeight 4
	+NOTAUTOAIMED
	+SOLID
	+USESPECIAL
	Activation THINGSPEC_Switch
	States
	{
	Spawn:
		TLIT A 0
		Goto CheckDormant
	CheckDormant:
		"####" "#" 0  {
			if (bDormant) {
				return ResolveState("Off");
			}
			return ResolveState("On");
		}
	On:
		"####" "#" -1 BRIGHT LIGHT("BOATABL1")
		Stop
	Off:
		"####" "#" -1
		Stop
	Active: // These are just used to toggle the dormant state whenever 'used' by the player, regardless of starting value
	Inactive:
		"####" "#"0 {
			A_PlaySound("SMSWITCH");
			bDormant = !bDormant;
			return ResolveState("CheckDormant");
		}
	}
}

ACTOR TableLightM2 : TableLightM
{
	//$Title 2d Bureau Light 2 (MIDDLE, SWITCHABLE)
	Scale 0.7
	States
	{
	Spawn:
		TLIT C 0
		Goto CheckDormant
	}
}

ACTOR TableLightM3Y : TableLightM
{
	//$Title 2d NeoClassic Yellow Lamp (MIDDLE, SWITCHABLE)
	Scale 0.53
	States
	{
	Spawn:
		TLIT G 0
		Goto CheckDormant
	}
}

ACTOR TableLightM3B : TableLightM
{
	//$Title 2d NeoClassic Blue Lamp (MIDDLE, SWITCHABLE)
	Scale 0.53
	States
	{
	Spawn:
		TLIT H 0
		Goto CheckDormant
	}
}

ACTOR TableLightM3R : TableLightM
{
	//$Title 2d NeoClassic Red Lamp (MIDDLE, SWITCHABLE)
	Scale 0.53
	States
	{
	Spawn:
		TLIT I 0
		Goto CheckDormant
	}
}

ACTOR TableLightM3G : TableLightM
{
	//$Title 2d NeoClassic Grey Lamp (MIDDLE, SWITCHABLE)
	Scale 0.53
	States
	{
	Spawn:
		TLIT J 0
		Goto CheckDormant
	}
}

ACTOR TableLightS : TableLightM
{
	//$Title 2d Table Light, Args (SMALL, SWITCHABLE)
	//$Arg0 "Type"
	//$Arg0Tooltip "Pickup the desired type\nRandom: 0\nClassic: 1\nBrown: 2\nFat: 3\nTall: 4"
	Height 16
	-SOLID
	States
	{
	Spawn:
		TLIT B 0 NODELAY {
		if (args[0] ==0 | args[0] >=6)
			{ frame = RandomPick(1, 3, 4, 5);} // For frame numbers, 0 = A, 1 = B, 2 = C, etc...  So this picks frame B, D, E, or F.
		if (args[0] ==1)
			{ frame = 1; }
		if (args[0] ==2)
			{ frame = 3; }
		if (args[0] ==3)
			{ frame = 4; }
		if (args[0] ==4)
			{ frame = 5; }
			A_Jump(256, "CheckDormant");}
	On:
		"####" "#" -1 BRIGHT LIGHT("BOATABL2")
		Stop
	}
}

ACTOR TableLightS2 : TableLightM
{
	//$Title 2d NeoClassic Table Light, Args (SMALL, SWITCHABLE)
	//$Arg0 "Type"
	//$Arg0Tooltip "Pickup the desired type\nRandom: 0\nGrey: 1\nYellow: 2\nBlue: 3\nRed: 4"
	Height 16
	Scale 0.20
	-SOLID
	States
	{
	Spawn:
		TLIT K 0 NODELAY {
		if (args[0] ==0 | args[0] >=5)
			{ frame = RandomPick(10, 11, 12, 13);} // This picks frame K, L, M, or N.
		if (args[0] ==1)
			{ frame = 10; }
		if (args[0] ==2)
			{ frame = 11; }
		if (args[0] ==3)
			{ frame = 12; }
		if (args[0] ==4)
			{ frame = 13; }
			A_Jump(256, "CheckDormant");}
	On:
		"####" "#" -1 BRIGHT LIGHT("BOATABL2")
		Stop
	}
}

ACTOR TableLightSA : TableLightM
{
	//$Title 2d Small Table Light 1 (SWITCHABLE)
	Height 16
	-SOLID
	States
	{
	Spawn:
		TLIT B 0
		Goto CheckDormant
	On:
		"####" "#" -1 BRIGHT LIGHT("BOATABL2")
		Stop
	}
}

ACTOR TableLightSB : TableLightM
{
	//$Title 2d Small Table Light 2 (SWITCHABLE)
	Height 16
	-SOLID
	States
	{
	Spawn:
		TLIT D 0
		Goto CheckDormant
	On:
		"####" "#" -1 BRIGHT LIGHT("BOATABL2")
		Stop
	}
}

ACTOR TableLightSC : TableLightM
{
	//$Title 2d Small Table Light 3 (SWITCHABLE)
	Height 16
	-SOLID
	States
	{
	Spawn:
		TLIT E 0
		Goto CheckDormant
	On:
		"####" "#" -1 BRIGHT LIGHT("BOATABL2")
		Stop
	}
}

ACTOR TableLightSD : TableLightM
{
	//$Title 2d Small Table Light 4 (SWITCHABLE)
	Height 16
	-SOLID
	States
	{
	Spawn:
		TLIT F 0
		Goto CheckDormant
	On:
		"####" "#" -1 BRIGHT LIGHT("BOATABL2")
		Stop
	}
}

Actor RedFire //straight from Demonic Uprising
{
	RenderStyle Add
	Scale 0.175
	+BRIGHT
	+FORCEXYBILLBOARD
	+NOINTERACTION
	States
	{
	Spawn:
		FIR1 A 0
		FIR1 A 0 A_Jump(128, "Spawn2")
		FIR1 ABCDEFG 3
		FIR1 HIJKL 2 A_FadeOut(0.15)
		Stop
	Spawn2:
		FIR2 ABCDEFG 3
		FIR2 HIJKL 2 A_FadeOut(0.15)
		Stop
	}
}

Actor ZykFire : RedFire
{
	States
	{
	Spawn:
		FIR3 A 0
		FIR3 A 0 A_Jump(128, "Spawn2")
		FIR3 ABCDEFG 3
		FIR3 HIJKL 2 A_FadeOut(0.15)
		Stop
	Spawn2:
		FIR4 ABCDEFG 3
		FIR4 HIJKL 2 A_FadeOut(0.15)
		Stop
	}
}

Actor MutFire : RedFire
{
	States
	{
	Spawn:
		FIR5 A 0
		FIR5 A 0 A_Jump(128, "Spawn2")
		FIR5 ABCDEFG 3
		FIR5 HIJKL 2 A_FadeOut(0.15)
		Stop
	Spawn2:
		FIR6 ABCDEFG 3
		FIR6 HIJKL 2 A_FadeOut(0.15)
		Stop
	}
}

ACTOR BoAWallTorch1 : SwitchableDecoration
{
	//$Category Lights (BoA)/ACS Switchable
	//$Title 2d Wall Torch (SWITCHABLE)
	//$Color 11
	Radius 10
	Scale 0.50
	+FIXMAPTHINGPOS
	+NOBLOCKMAP
	+NOGRAVITY
	-SOLID
	States
	{
	Active:
		WLTR I 0 A_PlaySound("Ignite")
		"####" I 0 {bDormant = FALSE;}
	Spawn:
		WLTR I 3 BRIGHT Light("WALLTORCH") A_SpawnItemEx("RedFire", Random(-2, 2), Random(-2, 2), Random(34, 36), 0.001 * Random(10, 200), 0.001 * Random(10, 200), 0.001 * Random(1000, 2000), 0, SXF_SETMASTER | SXF_CLIENTSIDE)
		Loop
	Inactive:
		"####" I 0 A_RemoveChildren(TRUE, RMVF_MISC)
		WLTR I -1 {bDormant = TRUE;}
		Stop
	}
}

ACTOR BoAWallTorch2 : BoAWallTorch1
{
	//$Title 2d Wall Torch, Zyklon (SWITCHABLE)
	States
	{
	Active:
		WLTR I 0 A_PlaySound("Ignite")
		"####" I 0 {bDormant = FALSE;}
	Spawn:
		WLTR I 3 BRIGHT Light("WALLTORC2") A_SpawnItemEx("ZykFire", Random(-2, 2), Random(-2, 2), Random(34, 36), 0.001 * Random(10, 200), 0.001 * Random(10, 200), 0.001 * Random(1000, 2000), 0, SXF_SETMASTER | SXF_CLIENTSIDE)
		Loop
	Inactive:
		"####" I 0 A_RemoveChildren(TRUE, RMVF_MISC)
		WLTR I -1 {bDormant = TRUE;}
		Stop
	}
}

ACTOR BoAWallTorch3 : BoAWallTorch1
{
	//$Title 2d Wall Torch, Mutant (SWITCHABLE)
	States
	{
	Active:
		WLTR I 0 A_PlaySound("Ignite")
		"####" I 0 {bDormant = FALSE;}
	Spawn:
		WLTR I 3 BRIGHT Light("WALLTORC3") A_SpawnItemEx("MutFire", Random(-2, 2), Random(-2, 2), Random(34, 36), 0.001 * Random(10, 200), 0.001 * Random(10, 200), 0.001 * Random(1000, 2000), 0, SXF_SETMASTER | SXF_CLIENTSIDE)
		Loop
	Inactive:
		"####" I 0 A_RemoveChildren(TRUE, RMVF_MISC)
		WLTR I -1 {bDormant = TRUE;}
		Stop
	}
}

ACTOR CreepyCandle : TableLightM
{
	//$Category Lights (BoA)/ACS Switchable
	//$Title 2d Creepy Candle (ON)
	//$Color 11
	Radius 4
	Height 8
	Scale 0.8
	-SOLID
	States
	{
	Spawn:
		CDLE B 0
		Goto CheckDormant
	CheckDormant:
		"####" "#" 0  {
			if (bDormant) {
				return ResolveState("Off");
			}
			return ResolveState("On");
		}
	On:
		"####" BCDA 4 LIGHT(CREPLIT2)
		Loop
	Off:
		"####" E -1
		Stop
	Active:
	Inactive:
		"####" E 0 {
			bDormant = !bDormant;
			return ResolveState("CheckDormant");}
	}
}

ACTOR CreepyCandlestick : CreepyCandle
{
	//$Title 2d Creepy Candlestick (ON)
	Radius 16
	Height 56
	Scale 1.0
	+SOLID
	States
	{
	Spawn:
		CNHD B 0
		Goto CheckDormant
	On:
		"####" BCDA 4 LIGHT(CREPLIT1)
		Loop
	}
}

ACTOR FBowl1 : SwitchableDecoration
{
	//$Category Lights (BoA)/ACS Switchable
	//$Title 2d Fire Bowl (ON)
	//$Color 11
	//$Sprite FBWLA0
	Radius 32
	Height 26
	Scale 0.78 //mxd
	+SOLID
	States
	{
	Spawn:
		FBWL A 0
		Goto CheckDormant
	CheckDormant:
		"####" "#" 0  {
			if (bDormant) {
				return ResolveState("Off");
			}
			return ResolveState("On");
		}
	On:
		"####" A 0 A_SpawnItemEx("FBowlFire", 0, 0, Height, 0, 0, 0, 0, SXF_NOCHECKPOSITION | SXF_SETMASTER | SXF_CLIENTSIDE, 0, tid)
		"####" A 0 A_PlaySound("SFX/FireLoop1", CHAN_BODY, 0.7, TRUE, ATTN_STATIC)
	FireLoop:
		FBWL ABCDEFGHIJKLM 2 LIGHT(BOAEBWL1)
		Loop
	Off:
		"####" N -1
		Stop
	Active:
	Inactive:
		FBWL N 0 {
			A_StopSound(CHAN_BODY);
			A_RemoveChildren(TRUE, RMVF_MISC);
			bDormant = !bDormant;
			return ResolveState("CheckDormant");}
	}
}

ACTOR FBowlFire
{
	Radius 32
	Height 30
	DamageType "Fire"
	ProjectilePassHeight 0
	+DONTSPLASH
	+NOGRAVITY
	States
	{
	Spawn:
		TNT1 A 0 A_SpawnItemEx("FloatingCinder", 0, 0, random(0,2), 1, 0, random (1, 3), random (0, 360), SXF_TRANSFERPITCH | SXF_CLIENTSIDE)
		"####" A 16 A_Explode(8, Radius, 0, FALSE, Radius)
		Loop
	}
}

ACTOR CBowl1 : SwitchableDecoration
{
	//$Category Lights (BoA)/ACS Switchable
	//$Title 2d Ceiling Fire Bowl (ON)
	//$Color 11
	Radius 8
	Height 80
	+NOGRAVITY
	+SOLID
	+SPAWNCEILING
	States
	{
	Spawn:
		CFIR A 0
		Goto CheckDormant
	CheckDormant:
		"####" "#" 0  {
			if (bDormant) {
				return ResolveState("Off");
			}
			return ResolveState("On");
		}
	On:
		"####" ABC 5
		Loop
	Off:
		"####" D -1
		Stop
	Active:
	Inactive:
		"####" D 0 {
			bDormant = !bDormant;
			return ResolveState("CheckDormant");}
	}
}

ACTOR FBarrel1 : SwitchableDecoration
{
	//$Category Lights (BoA)/ACS Switchable
	//$Title 2d Fire Barrel (ON)
	//$Color 11
	Radius 16
	Height 48
	ProjectilePassHeight 0
	+SOLID
	States
	{
	Spawn:
		FCNE A 0
		Goto CheckDormant
	CheckDormant:
		"####" "#" 0  {
			if (bDormant) {
				return ResolveState("Off");
			}
			return ResolveState("On");
		}
	On:
		"####" A 0 A_SpawnItemEx("FloatingCinder", 0, 0, random(48,54), 1, 0, random (1, 3), random (0, 360), SXF_SETMASTER | SXF_TRANSFERPITCH | SXF_CLIENTSIDE, 0, tid)
	FireLoop:
		"####" ABCD 3 LIGHT(BOAEBRL1)
		Loop
	Off:
		"####" E -1
		Stop
	Active:
	Inactive:
		"####" E 0 {
			A_RemoveChildren(TRUE, RMVF_MISC);
			bDormant = !bDormant;
			return ResolveState("CheckDormant");}
	}
}

ACTOR Red_Light : SwitchableDecoration
{
	//$Category Lights (BoA)
	//$Title 2d Light, Red (ON, SWITCHABLE)
	//$Color 11
	Radius 16
	Height 16
	+DONTSPLASH
	+NOBLOCKMAP
	+NOGRAVITY
	+SPAWNCEILING
	States
	{
	Active:
		"####" A 0 {bDormant = FALSE;}
	Spawn:
		STRR A -1 LIGHT(FIXEDRED)
		Stop
	Inactive:
		"####" A -1 {bDormant = TRUE;}
		Stop
	}
}

ACTOR Blue_Light : Red_Light
{
	//$Title 2d Light, Blue (ON, SWITCHABLE)
	States
	{
	Spawn:
		STRB A -1 LIGHT(FIXEDBLU)
		Stop
	}
}

ACTOR Green_Light : Red_Light
{
	//$Title 2d Light, Green (ON, SWITCHABLE)
	States
	{
	Spawn:
		STRG A -1 LIGHT(FIXEDGRN)
		Stop
	}
}

ACTOR Yellow_Light : Red_Light
{
	//$Title 2d Light, Yellow (ON, SWITCHABLE)
	States
	{
	Spawn:
		STRY A -1 LIGHT(FIXEDYEL)
		Stop
	}
}

///////////
// FLARE //
///////////
ACTOR ActiveFlare
{
	//$Category Lights (BoA)
	//$Title Active Signal Flare
	//$Color 11
	Radius 22
	Height 11
	States
	{
	Spawn:
		FLRI A 0
		"####" A 0 A_PlaySound("Flare/Light")
		"####" A 0 A_PlaySound("Flare/Loop", CHAN_BODY, 1, TRUE, ATTN_STATIC)
	Spawn.Loop:
		FLRI AB 1
		Loop
	}
}