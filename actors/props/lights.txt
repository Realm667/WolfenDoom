ACTOR AlertLight
{
	//$Category Misc (Wolf3D)
	//$Title Alert Light (stealth)
	//$Color 1
	+INVISIBLE
	+NOGRAVITY
	States
	{
		Spawn:
			UNKN A 1 NoDelay { if (args[0] == 0) { A_SetArg(0, 64); }}
		Static:
			UNKN A 1 A_LookEx(LOF_NOSOUNDCHECK | LOF_DONTCHASEGOAL | LOF_NOSEESOUND, 0, args[0] * 2.0, 0, 360, "Illuminate")
			Loop
		Illuminate:
			UNKN A 1 ACS_NamedExecuteAlways("AddExtraVisibility", 0, (args[0] * 2.0 - GetDistance(0)) / 1.0 + 8, 1)
			Goto Static
	}
}

//3d lights
ACTOR Chandelier1Switchable : SwitchableDecoration
{
	//$Category Light Sources (Wolf3D)
	//$Title 3d Chandelier with Tall Candles (rusty, switchable)
	//$Color 11
	
	//$Arg0 Sound
	//$Arg0Type 11
	//$Arg0Enum { 0 = "Yes"; 1 = "No"; }
	
	Radius 10
	Height 56
	+NOBLOCKMAP
	+NOGRAVITY
	+SOLID
	+SPAWNCEILING
	
	States
	{
	Active:
		MDLA A 0 A_ChangeFlag("DORMANT", FALSE) //mxd. DORMANT flag must be updated manually
	Spawn:
		MDLA A 0 NoDelay A_PlaySound("FIRE_SMALL", CHAN_BODY, 0.6 - args[0], TRUE)
	ActiveFlames: //ozy. let's apply these nice flames on candles first - coords be damned
		MDLA A 0 {
			A_SpawnItemEx("3dFlame_Normal", ScaleX*1, 	ScaleY*33, 	ScaleX+ScaleY*14, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE);
			A_SpawnItemEx("3dFlame_Normal", ScaleX*1, 	ScaleY*-31, ScaleX+ScaleY*5, 	0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE);
			A_SpawnItemEx("3dFlame_Normal", ScaleX*24, 	ScaleY*23, 	ScaleX+ScaleY*7, 	0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE);
			A_SpawnItemEx("3dFlame_Normal", ScaleX*-22, ScaleY*-22, ScaleX+ScaleY*7, 	0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE);
			A_SpawnItemEx("3dFlame_Normal", ScaleX*34, 	ScaleY*1, 	ScaleX+ScaleY*14, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE);
			A_SpawnItemEx("3dFlame_Normal", ScaleX*-31, ScaleY*1, 	ScaleX+ScaleY*12, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE);
			A_SpawnItemEx("3dFlame_Normal", ScaleX*-22, ScaleY*23, 	ScaleX+ScaleY*5, 	0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE);
			A_SpawnItemEx("3dFlame_Normal", ScaleX*24, 	ScaleY*-22, ScaleX+ScaleY*8, 	0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE);}
	Activate:
		MDLA A 1 //LIGHT ("FireSpawnerSmall")
		Loop
	Inactive:
		MDLA A 0 A_ChangeFlag("DORMANT", TRUE) //mxd. DORMANT flag must be updated manually
		MDLA A 0 A_RemoveChildren(TRUE, RMVF_MISC) //ozy. remove flames if deactivated
		"####" A -1 A_StopSound(CHAN_BODY)
		Stop
	}
}

ACTOR Chandelier2Switchable : SwitchableDecoration
{
	//$Category Light Sources (Wolf3D)
	//$Title 3d Chandelier with Short Candles (rusty, switchable)
	//$Color 11
	
	//$Arg0 Sound
	//$Arg0Type 11
	//$Arg0Enum { 0 = "Yes"; 1 = "No"; }
	
	Radius 10
	Height 56
	+NOBLOCKMAP
	+NOGRAVITY
	+SOLID
	+SPAWNCEILING
	
	States
	{
	Active:
		MDLA A 0 A_ChangeFlag("DORMANT", FALSE) //mxd. DORMANT flag must be updated manually
	Spawn:
		MDLA A 0 NoDelay A_PlaySound("FIRE_SMALL", CHAN_BODY, 0.6 - args[0], TRUE)
	ActiveFlames:
		MDLA A 0 {
			A_SpawnItemEx("3dFlame_Normal", ScaleX*0,			ScaleY*26, 		ScaleX+ScaleY*10, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE);
			A_SpawnItemEx("3dFlame_Normal", ScaleX*0,			ScaleY*-26.5, ScaleX+ScaleY*6, 	0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE);
			A_SpawnItemEx("3dFlame_Normal", ScaleX*18.7, 	ScaleY*18.2, 	ScaleX+ScaleY*7, 	0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE);
			A_SpawnItemEx("3dFlame_Normal", ScaleX*-18.8, ScaleY*-18.8, ScaleX+ScaleY*7, 	0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE);
			A_SpawnItemEx("3dFlame_Normal", ScaleX*26.5, 	ScaleY*-0.2, 	ScaleX+ScaleY*10, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE);
			A_SpawnItemEx("3dFlame_Normal", ScaleX*-26.4, ScaleY*0, 		ScaleX+ScaleY*8.8,0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE);
			A_SpawnItemEx("3dFlame_Normal", ScaleX*-18.8, ScaleY*18.7, 	ScaleX+ScaleY*5, 	0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE);
			A_SpawnItemEx("3dFlame_Normal", ScaleX*19.2, 	ScaleY*-18.9, ScaleX+ScaleY*8, 	0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE);}
	Activate:
		MDLA A 1
		Loop
	Inactive:
		MDLA A 0 A_ChangeFlag("DORMANT", TRUE) //mxd. DORMANT flag must be updated manually
		MDLA A 0 A_RemoveChildren(TRUE, RMVF_MISC) //ozy. remove flames if deactivated
		"####" A -1 A_StopSound(CHAN_BODY)
		Stop
	}
}

ACTOR Chandelier3Switchable : Chandelier1Switchable
{
	//$Title 3d Chandelier with Tall Candles (black, switchable)
}

ACTOR Chandelier4Switchable : Chandelier2Switchable
{
	//$Title 3d Chandelier with Short Candles (black, switchable)
}

ACTOR COD_OilLamp : SwitchableDecoration
{
	//$Category Light Sources (Wolf3D)
	//$Title 3d Oil Lamp (rusty, switchable)
	//$Color 11
	
	//$Arg0 Sound
	//$Arg0Type 11
	//$Arg0Enum { 0 = "Yes"; 1 = "No"; }
	
	Radius 4
	Height 4
	+NOBLOCKMAP
	+NOGRAVITY
	+SOLID
	
	States
	{
	Active:
		MDLA A 0 A_ChangeFlag("DORMANT", FALSE) //mxd. DORMANT flag must be updated manually
	Spawn:
		MDLA A 0 NoDelay A_PlaySound("FIRE_SMALL", CHAN_BODY, 0.4 - args[0], TRUE)
	ActiveFlames:
		MDLA A 0 {A_SpawnItemEx("3dFlame_Lamp", 0, 0, ScaleX+ScaleY*6, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE);
							A_SpawnItemEx("COD_LampGlassOn", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPITCH | SXF_TRANSFERROLL | SXF_TRANSFERSCALE);}
	Activate:
		MDLA A 1
		Loop
	Inactive:
		MDLA A 0 A_ChangeFlag("DORMANT", TRUE) //mxd. DORMANT flag must be updated manually
		MDLA A 0 A_RemoveChildren(TRUE, RMVF_MISC) //ozy. remove flames & lit glass if deactivated
		MDLA B 0 A_SpawnItemEx("COD_LampGlassOff", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPITCH | SXF_TRANSFERROLL | SXF_TRANSFERSCALE) //add unlit glass
		"####" B -1 A_StopSound(CHAN_BODY)
		Stop
	}
}

//needed for 3d chandeliers
ACTOR 3dFlame_Normal
{
	Scale 0.06
	+FORCEYBILLBOARD
	+NOBLOCKMAP
	+NOGRAVITY
	+NOINTERACTION
	+RANDOMIZE
	RenderStyle "Add"
	States
	{
	Spawn:
		TNT1 A 0 A_Jump(235,2) //cindeeeeeeeeeeeeeerz! Yes, I love them - ozy
		TNT1 A 0 A_SpawnItemEx("FloatingCinder", 0, 0, random(0,2), 1, 0, random (1, 3), random (0, 360), SXF_TRANSFERPITCH | SXF_CLIENTSIDE)
		3DFR ABCDEFGHIJ 2 LIGHT ("3DFLAME_N")
		Loop
	}
}

ACTOR 3dFlame_Short : 3dFlame_Normal
{
	Scale 0.03
	States
	{
	Spawn:
		TNT1 A 0 A_Jump(250,2)
		TNT1 A 0 A_SpawnItemEx("FloatingCinder", 0, 0, random(0,2), 1, 0, random (1, 3), random (0, 360), SXF_TRANSFERPITCH | SXF_CLIENTSIDE)
		3DFR ABCDEFGHIJ 2 LIGHT ("3DFLAME_S")
		Loop
	}
}

ACTOR 3dFlame_Tall : 3dFlame_Normal
{
	Scale 0.09
	States
	{
	Spawn:
		TNT1 A 0 A_Jump(215,2)
		TNT1 A 0 A_SpawnItemEx("FloatingCinder", 0, 0, random(0,2), 1, 0, random (1, 3), random (0, 360), SXF_TRANSFERPITCH | SXF_CLIENTSIDE)
		3DFR ABCDEFGHIJ 2 LIGHT ("3DFLAME_T")
		Loop
	}
}

ACTOR 3dFlame_Lamp : 3dFlame_Normal
{
	Scale 0.02
	States
	{
	Spawn:
		TNT1 A 0 A_Jump(250,2)
		TNT1 A 0 A_SpawnItemEx("FloatingCinder", 0, 0, random(0,2), 1, 0, random (1, 3), random (0, 360), SXF_TRANSFERPITCH | SXF_CLIENTSIDE)
		3DFR ABCDEFGHIJ 2 LIGHT ("3DFLAME_L")
		Loop
	}
}

ACTOR CeilingLightM : SwitchableDecoration
{
	//$Category Light Sources (Wolf3D)
	//$Title Ceiling Light (middle, switchable)
	//$Color 11
	Radius 16
	Height 23
	+NOBLOCKMAP
	+NOGRAVITY
	+NOTAUTOAIMED
	+SOLID
	+SPAWNCEILING
	States
	{
	Active:
		MDLA A 0 A_ChangeFlag("DORMANT", FALSE)
	Spawn:
	  MDLA A 0 NoDelay
	ActiveLights:
		MDLA A 0 A_SpawnItemEx("VolumetricLight_ConeDown", 0, 0, ScaleX+ScaleY*12, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE)
	Activate:
		MDLA A 1
		Loop
	Inactive:
		MDLA A 0 A_ChangeFlag("DORMANT", TRUE)
		MDLA A 0 A_RemoveChildren(TRUE, RMVF_MISC)
		MDLA B -1
		Stop
	}
}

ACTOR CeilingLightM2 : CeilingLightM
{
	//$Title Ceiling Light (small, switchable)
	Radius 8
	Height 10
	-SOLID
	States
	{
	ActiveLights:
		MDLA A 0 A_SpawnItemEx("VolumetricLight_ConeDown", 0, 0, ScaleX+ScaleY*8, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE)
	Activate:
		MDLA A 1
		Loop
	}
}

ACTOR StreetLight1 : SwitchableDecoration 
{
	//$Category Light Sources (Wolf3D)
	//$Title Street Light (off, switchable)
	//$Color 11
	Radius 4
	Height 128
	+SOLID
	States
	{
	Active:
		MDLA A 0 A_ChangeFlag("DORMANT", FALSE)
		MDLA A 0 A_RemoveChildren(TRUE, RMVF_MISC)
	Spawn:
		MDLA B -1
		Stop
	Inactive:
		MDLA A 0 A_ChangeFlag("DORMANT", TRUE)
	InactiveLights:
		MDLA A 0 A_SpawnItemEx("VolumetricLight_ConeDown", ScaleX*50, 0, ScaleX+ScaleY*128, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE)
	InactiveStop:
		MDLA A 1 Light("LIGHTPOSTLIGHT")
		Loop
	}
}

ACTOR StreetLight1Lit : StreetLight1
{
	//$Title Street Light (on, switchable)
	States
	{
	Active:
		MDLA A 0 A_ChangeFlag("DORMANT", FALSE)
	Spawn:
		MDLA A 0 NoDelay
	ActiveLights:
		MDLA A 0 A_SpawnItemEx("VolumetricLight_ConeDown", ScaleX*50, 0, ScaleX+ScaleY*128, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE)
	Activate:
		MDLA A 1 Light("LIGHTPOSTLIGHT")
		Loop
	Inactive:
		MDLA A 0 A_ChangeFlag("DORMANT", TRUE)
		MDLA A 0 A_RemoveChildren(TRUE, RMVF_MISC)
		MDLA B -1
		Stop
	}
}

ACTOR StreetLight2 : SwitchableDecoration
{
	//$Category Light Sources (Wolf3D)
	//$Title Street Light (triple, switchable)
	//$Color 11
	Radius 4
	Height 128
	+SOLID
	States
	{
	Active:
		MDLA C 0 A_ChangeFlag("DORMANT", FALSE) //mxd. DORMANT flag must be updated manually
	Spawn:
		MDLA C -1 Light("BOASTLT2")
		Stop
	Inactive:
		MDLA A -1 A_ChangeFlag("DORMANT", TRUE) //mxd. DORMANT flag must be updated manually
		Stop
	}
}

ACTOR StreetLight3 : SwitchableDecoration
{
	//$Category Light Sources (Wolf3D)
	//$Title Street Light (single, switchable)
	//$Color 11
	Radius 4
	Height 128
	+SOLID
	States
	{
	Active:
		MDLA D 0 A_ChangeFlag("DORMANT", FALSE) //mxd. DORMANT flag must be updated manually
	Spawn:
		MDLA D -1 Light("BOASTLT3")
		Stop
	Inactive:
		MDLA B -1 A_ChangeFlag("DORMANT", TRUE) //mxd. DORMANT flag must be updated manually
		Stop
	}
}

ACTOR CastleLight1 : SwitchableDecoration
{
	//$Category Light Sources (Wolf3D)
	//$Title Castle Light 1 (switchable)
	//$Color 11
	Radius 16
	Height 32
	+NOBLOCKMAP
	+NOGRAVITY
	+SOLID
	+SPAWNCEILING
	States
	{
	Active:
		MDLA A 0 A_ChangeFlag("DORMANT", FALSE) //mxd. DORMANT flag must be updated manually
	Spawn:
		MDLA A 0 NoDelay {A_SpawnItemEx("AlertLight", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER);
											A_SpawnItemEx("VolumetricLight_ConeDown", 0, 0, ScaleX+ScaleY*28, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE);}
		MDLA A -1 Light("BOACEIL3")
		Stop
	Inactive:
		MDLA A 1 {A_KillChildren;
							A_RemoveChildren(TRUE, RMVF_MISC);}
		MDLA B -1 A_ChangeFlag("DORMANT", TRUE) //mxd. DORMANT flag must be updated manually
		Stop
	}
}

ACTOR CastleLight2 : CastleLight1
{
	//$Title Castle Light 2 (switchable)
	Height 56
	States
	{
	Spawn:
		MDLA A 0 NoDelay {A_SpawnItemEx("AlertLight", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER);
											A_SpawnItemEx("VolumetricLight_ConeDown", 0, 0, ScaleX+ScaleY*52, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE);}
		MDLA A -1 Light("BOACEIL3")
		Stop
	}
}

ACTOR Cell_Light : CastleLight1
{
	//$Title Cell Light (switchable)
	Radius 8
	Height 38
	States
	{
	Spawn:
		MDLA A 0 NoDelay {A_SpawnItemEx("AlertLight", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER);
											A_SpawnItemEx("VolumetricLight_ConeDown", 0, 0, ScaleX+ScaleY*34, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE);}
		MDLA A -1 Light("BOACEIL2")
		Stop
	}
}

ACTOR ColumnLight
{
	//$Category Light Sources (Wolf3D)/Static
	//$Title Column Light (on)
	//$Color 11
	Radius 8
	Height 136
	+SOLID
	States
	{
	Spawn:
		MDLA A -1
		Stop
	}
}

ACTOR ColumnLight2 : ColumnLight
{
	//$Title Column Light (off)
	States
	{
	Spawn:
		MDLA B -1
		Stop
	}
}

ACTOR LightBulb : SwitchableDecoration
{
	//$Category Light Sources (Wolf3D)/Breakable
	//$Title Light Bulb (off, breakable)
	//$Color 11
	
	//$Arg0 "Type"
	//$Arg0Tooltip "Pickup the desired type\nBreakable: 0\nUnBreakable: 1"
	
	Radius 4
	Height 1
	Health 1
	Mass 0x7ffffff
	+DONTFALL
	+NOBLOOD
	+NOGRAVITY
	+NOTAUTOAIMED
	+SHOOTABLE
	+SOLID
	+SPAWNCEILING
	States
	{
	Active:
		MDLA A 0 A_ChangeFlag("DORMANT", FALSE)
	Spawn:
		MDLA A -1 NoDelay A_JumpIf(Args[0]==1, "UnBreakable")
		Stop
	UnBreakable:
		"####" "#" 0 A_ChangeFlag("NOBLOCKMAP", TRUE)
		"####" "#" -1
		Stop
	Inactive:
		MDLA B -1 BRIGHT LIGHT(BOACEIL5) A_ChangeFlag("DORMANT", TRUE)
		Stop
	Death:
		TNT1 A 0 A_UnSetSolid
		"####" AAAA 0 A_SpawnItemEx("Debris_GlassShard_Small", 0, 0, 0, random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" A 0 A_PlaySound("GLASS5")
		MDLA CCCCCC 3 A_SpawnItemEx("Spark_B1", 0, 0, 0, 0, frandom(-1.0,1.0), frandom(-1.0,1.0), random(0,360), SXF_CLIENTSIDE)
		MDLA C -1
		Stop
	}
}

ACTOR LightBulbOn : LightBulb
{
	//$Title Light Bulb (on, breakable)
	States
	{
	Active:
		MDLA B 0 A_ChangeFlag("DORMANT", FALSE)
	Spawn:
		MDLA B -1 NoDelay BRIGHT LIGHT(BOACEIL5) A_JumpIf(Args[0]==1, "UnBreakable")
		Stop
	UnBreakable:
		"####" "#" 0 A_ChangeFlag("NOBLOCKMAP", TRUE)
		"####" "#" -1 BRIGHT LIGHT(BOACEIL5)
		Stop
	Inactive:
		MDLA A -1 A_ChangeFlag("DORMANT", TRUE)
		Stop
	}
}

ACTOR LightBulb_Red : LightBulb
{
	//$Title Light Bulb, Red (off, breakable)
	States
	{
	Active:
		MDLA A 0 A_ChangeFlag("DORMANT", FALSE)
	Spawn:
		MDLA A -1 NoDelay A_JumpIf(Args[0]==1, "UnBreakable")
		Stop
	UnBreakable:
		"####" "#" 0 A_ChangeFlag("NOBLOCKMAP", TRUE)
		"####" "#" -1
		Stop
	Inactive:
		MDLA B -1 BRIGHT LIGHT(BOACEIL6) A_ChangeFlag("DORMANT", TRUE)
		Stop
	}
}

ACTOR LightBulbOn_Red : LightBulb
{
	//$Title Light Bulb, Red (on, breakable)
	States
	{
	Active:
		MDLA B 0 A_ChangeFlag("DORMANT", FALSE)
	Spawn:
		MDLA B -1 NoDelay BRIGHT LIGHT(BOACEIL6) A_JumpIf(Args[0]==1, "UnBreakable") 
		Stop
	UnBreakable:
		"####" "#" 0 A_ChangeFlag("NOBLOCKMAP", TRUE)
		"####" "#" -1 BRIGHT LIGHT(BOACEIL6)
		Stop
	Inactive:
		MDLA A -1 A_ChangeFlag("DORMANT", TRUE)
		Stop
	}
}

ACTOR LightBulb_Grn : LightBulb
{
	//$Title Light Bulb, Green (off, breakable)
	States
	{
	Active:
		MDLA A 0 A_ChangeFlag("DORMANT", FALSE)
	Spawn:
		MDLA A -1 NoDelay A_JumpIf(Args[0]==1, "UnBreakable")
		Stop
	UnBreakable:
		"####" "#" 0 A_ChangeFlag("NOBLOCKMAP", TRUE)
		"####" "#" -1
		Stop
	Inactive:
		MDLA B -1 BRIGHT LIGHT(BOACEIL7) A_ChangeFlag("DORMANT", TRUE)
		Stop
	}
}

ACTOR LightBulbOn_Grn : LightBulb
{
	//$Title Light Bulb, Green (on, breakable)
	States
	{
	Active:
		MDLA B 0 A_ChangeFlag("DORMANT", FALSE)
	Spawn:
		MDLA B -1 NoDelay BRIGHT LIGHT(BOACEIL7) A_JumpIf(Args[0]==1, "UnBreakable") 
		Stop
	UnBreakable:
		"####" "#" 0 A_ChangeFlag("NOBLOCKMAP", TRUE)
		"####" "#" -1 BRIGHT LIGHT(BOACEIL7)
		Stop
	Inactive:
		MDLA A -1 A_ChangeFlag("DORMANT", TRUE)
		Stop
	}
}

//2d Lights
ACTOR TableLightM : SwitchableDecoration
{
	//$Category Light Sources (Wolf3D)
	//$Title Bureau Light 1 (middle, switchable)
	//$Color 11
	Radius 4
	Height 32
	ProjectilePassHeight 4
	+NOTAUTOAIMED
	+SOLID
	+USESPECIAL
	Activation THINGSPEC_Switch
	States
	{
	Active:
		TLIT A 0 A_ChangeFlag("DORMANT", FALSE) //mxd. DORMANT flag must be updated manually
		"####" A 0 A_PlaySound("SMSWITCH")
	Spawn:
		TLIT A -1 BRIGHT Light("BOATABL1")
		Stop
	Inactive:
		TLIT A 0 A_ChangeFlag("DORMANT", TRUE) //mxd. DORMANT flag must be updated manually
		"####" A -1 A_PlaySound("SMSWITCH")
		Stop
	}
}

ACTOR TableLightM2 : TableLightM
{
	//$Title Bureau Light 2 (middle, switchable)
	States
	{
	Active:
		TLIT C 0 A_ChangeFlag("DORMANT", FALSE) //mxd. DORMANT flag must be updated manually
		"####" C 0 A_PlaySound("SMSWITCH")
	Spawn:
		TLIT C -1 BRIGHT Light("BOATABL1")
		Stop
	Inactive:
		TLIT C 0 A_ChangeFlag("DORMANT", TRUE) //mxd. DORMANT flag must be updated manually
		"####" C -1 A_PlaySound("SMSWITCH")
		Stop
	}
}

ACTOR TableLightS : TableLightM
{
	//$Title Table Light (small, switchable)
	Height 16
	States
	{
	Active:
		TLIT B 0 A_ChangeFlag("DORMANT", FALSE) //mxd. DORMANT flag must be updated manually
		"####" B 0 A_PlaySound("SMSWITCH")
	Spawn:
		TLIT B -1 BRIGHT Light("BOATABL2")
		Stop
	Inactive:
		TLIT B 0 A_ChangeFlag("DORMANT", TRUE) //mxd. DORMANT flag must be updated manually
		"####" B -1 A_PlaySound("SMSWITCH")
		Stop
	}
}

ACTOR BoAWallTorch1 : SwitchableDecoration
{
	//$Category Light Sources (Wolf3D)
	//$Title Wall Torch (switchable)
	//$Color 11
	+FIXMAPTHINGPOS
	+NOBLOCKMAP
	+NOGRAVITY
	Radius 10
	States
	{
	Active:
		WLTR A 0 A_PlaySound("Ignite")
		"####" A 0 A_ChangeFlag("DORMANT", FALSE) //mxd. DORMANT flag must be updated manually
	Spawn:
		WLTR ABCDEFGH 4
		Loop
	Inactive:
		WLTR I -1 A_ChangeFlag("DORMANT", TRUE) //mxd. DORMANT flag must be updated manually
		Stop
	}
}

ACTOR CreepyCandle
{
	//$Category Light Sources (Wolf3D)/Static
	//$Title Creepy Candle (on)
	//$Color 11
	+NOGRAVITY
	Radius 4
	Height 8
	Scale 0.8
	States
	{
	Spawn:
		CDLE BCDA 4 LIGHT(CREPLIT2)
		Loop
	}
}

ACTOR CreepyCandlestick
{
	//$Category Light Sources (Wolf3D)/Static
	//$Title Creepy Candlestick (on)
	//$Color 11
	+NOGRAVITY
	+SOLID
	Radius 16
	Height 56
	States
	{
	Spawn:
		CNHD BCDA 4 LIGHT(CREPLIT1)
		Loop
	}
}

ACTOR FBowl1
{
	//$Category Light Sources (Wolf3D)/Static
	//$Title Fire Bowl (egypt)
	//$Color 11
	Radius 32
	Height 26
	Scale 0.78 //mxd
	+SOLID
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SpawnItemEx("FBowlFire", 0, 0, Height, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
	FireLoop:
		FBWL ABCDEFGHIJKLM 2
		Loop
	}
}

ACTOR FBowlFire
{
	Radius 32
	Height 30
	DamageType Fire
	ProjectilePassHeight 0
	+DONTSPLASH
	+NOGRAVITY
	States
	{
	Spawn:
		TNT1 A 0 A_SpawnItemEx("FloatingCinder", 0, 0, random(0,2), 1, 0, random (1, 3), random (0, 360), SXF_TRANSFERPITCH | SXF_CLIENTSIDE) //Ozy81 from Gore
		"####" A 16 A_Explode(8, Radius, 0, FALSE, Radius)
		Loop
	}
}

ACTOR CBowl1
{
	//$Category Light Sources (Wolf3D)/Static
	//$Title Ceiling Fire Bowl (egypt)
	//$Color 11
	Radius 8
	Height 80
	+NOGRAVITY
	+SOLID
	+SPAWNCEILING
	States
	{
	Spawn:
		CFIR ABC 5
		Loop
	}
}

ACTOR FBarrel1
{
	//$Category Light Sources (Wolf3D)/Static
	//$Title Fire Barrel (egypt)
	//$Color 11
	Radius 16
	Height 64
	ProjectilePassHeight 0
	+SOLID
	States
	{
	Spawn:
		TNT1 A 0 A_SpawnItemEx("FloatingCinder", 0, 0, random(48,54), 1, 0, random (1, 3), random (0, 360), SXF_TRANSFERPITCH | SXF_CLIENTSIDE) //Ozy81 from Gore
		FCNE ABCD 3
		Loop
	}
}

//Strobelights - fixed by ozy81
ACTOR StrobeLightBlue : SwitchableDecoration 
{
	//$Category Light Sources (Wolf3D)
	//$Title Strobe Light (blue, switchable)
	//$Color 11
	Radius 16
	Height 16
	+DONTSPLASH
	+NOBLOCKMAP
	+NOGRAVITY
	+SPAWNCEILING
	States
	{
	Active:
		"####" A 0 A_ChangeFlag("DORMANT", FALSE) //mxd. DORMANT flag must be updated manually
		Emitter:
			"####" A 0 A_SpawnItemEx("StrobeEmitterBlue", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER)
			Spin:
				STRB DCBA 4 BRIGHT
				Loop
	Spawn:
		STRB A -1
		Stop
	Inactive:
		STRB A 0 A_RemoveChildren(TRUE, RMVF_MISC)
		STRB A -1 A_ChangeFlag("DORMANT", TRUE) //mxd. DORMANT flag must be updated manually
		Stop
	}
}

ACTOR StrobeLightGreen : StrobeLightBlue
{
	//$Title Strobe Light (green, switchable)
	States
	{
	Active:
		"####" A 0 A_ChangeFlag("DORMANT", FALSE) //mxd. DORMANT flag must be updated manually
		Emitter:
			"####" A 0 A_SpawnItemEx("StrobeEmitterGreen", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER)
			Spin:
				STRG DCBA 4 BRIGHT
				Loop
	Spawn:
		STRG A -1
		Stop
	Inactive:
		STRG A 0 A_RemoveChildren(TRUE, RMVF_MISC)
		STRG A -1 A_ChangeFlag("DORMANT", TRUE) //mxd. DORMANT flag must be updated manually
		Stop
	}
}

ACTOR StrobeLightRed : StrobeLightBlue
{
	//$Title Strobe Light (red, switchable)
	States
	{
	Active:
		"####" A 0 A_ChangeFlag("DORMANT", FALSE) //mxd. DORMANT flag must be updated manually
		Emitter:
			"####" A 0 A_SpawnItemEx("StrobeEmitterRed", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER)
			Spin:
				STRR DCBA 4 BRIGHT
				Loop
	Spawn:
		STRR A -1
		Stop
	Inactive:
		STRR A 0 A_RemoveChildren(TRUE, RMVF_MISC)
		STRR A -1 A_ChangeFlag("DORMANT", TRUE) //mxd. DORMANT flag must be updated manually
		Stop
	}
}

ACTOR StrobeLightYellow : StrobeLightBlue
{
	//$Title Strobe Light (yellow, switchable)
	States
	{
	Active:
		"####" A 0 A_ChangeFlag("DORMANT", FALSE) //mxd. DORMANT flag must be updated manually
		Emitter:
			"####" A 0 A_SpawnItemEx("StrobeEmitterYellow", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER)
			Spin:
				STRY DCBA 4 BRIGHT
				Loop
	Spawn:
		STRY A -1
		Stop
	Inactive:
		STRY A 0 A_RemoveChildren(TRUE, RMVF_MISC)
		STRY A -1 A_ChangeFlag("DORMANT", TRUE) //mxd. DORMANT flag must be updated manually
		Stop
	}
}

ACTOR Red_Light
{
	//$Category Light Sources (Wolf3D)
	//$Title Light, Red (on)
	//$Color 11
	Radius 16
	Height 16
	+DONTSPLASH
	+NOBLOCKMAP
	+NOGRAVITY
	+SPAWNCEILING
	States
	{
	Spawn:
		STRR A -1 LIGHT(FIXEDRED)
		Stop
	}
}

ACTOR Blue_Light : Red_Light
{
	//$Title Light, Blue (on)
	States
	{
	Spawn:
		STRB A -1 LIGHT(FIXEDBLU)
		Stop
	}
}

ACTOR Green_Light : Red_Light
{
	//$Title Light, Green (on)
	States
	{
	Spawn:
		STRG A -1 LIGHT(FIXEDGRN)
		Stop
	}
}

ACTOR Yellow_Light : Red_Light
{
	//$Title Light, Yellow (on)
	States
	{
	Spawn:
		STRY A -1 LIGHT(FIXEDYEL)
		Stop
	}
}

//ROTATING EMITTERS
ACTOR StrobeEmitterBlue
{
	Radius 16
	Height 16
	+DONTSPLASH
	+NOGRAVITY
	+SPAWNCEILING
	States
	{
	Spawn:
		TNT1 A 1
		"####" A 0 A_CustomMissile ("StrobeRayBlue", 0, 0, 0, 2, -35)
		"####" A 0 A_CustomMissile ("StrobeRayBlue", 0, 0, 180, 2, -35)
		"####" A 0 A_SetAngle(10 + angle)
		Loop
	}
}

ACTOR StrobeEmitterGreen : StrobeEmitterBlue
{
	States
	{
	Spawn:
		TNT1 A 1
		"####" A 0 A_CustomMissile ("StrobeRayGreen", 0, 0, 0, 2, -35)
		"####" A 0 A_CustomMissile ("StrobeRayGreen", 0, 0, 180, 2, -35)
		"####" A 0 A_SetAngle(10 + angle)
		Loop
	}
}

ACTOR StrobeEmitterRed : StrobeEmitterBlue
{
	States
	{
	Spawn:
		TNT1 A 1
		"####" A 0 A_CustomMissile ("StrobeRayRed", 0, 0, 0, 2, -35)
		"####" A 0 A_CustomMissile ("StrobeRayRed", 0, 0, 180, 2, -35)
		"####" A 0 A_SetAngle(10 + angle)
		Loop
	}
}

ACTOR StrobeEmitterYellow : StrobeEmitterBlue
{
	States
	{
	Spawn:
		TNT1 A 1
		"####" A 0 A_CustomMissile ("StrobeRayYellow", 0, 0, 0, 2, -35)
		"####" A 0 A_CustomMissile ("StrobeRayYellow", 0, 0, 180, 2, -35)
		"####" A 0 A_SetAngle(10 + angle)
		Loop
	}
}

//LIGHT RAYS
ACTOR StrobeRayBlue
{
	Radius 2
	Height 2
	Speed 100
	Projectile
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	+DONTSPLASH
	+PAINLESS
	+THRUACTORS
	States
	{
	Death:
		TNT1 A 2 LIGHT(BLUESTROBE)
		Stop
	}
}

ACTOR StrobeRayGreen : StrobeRayBlue
{
	States
	{
	Death:
		TNT1 A 2 LIGHT(GREENSTROBE)
		Stop
	}
}

ACTOR StrobeRayRed : StrobeRayBlue
{
	States
	{
	Death:
		TNT1 A 2 LIGHT(REDSTROBE)
		Stop
	}
}

ACTOR StrobeRayYellow : StrobeRayBlue
{
	States
	{
	Death:
		TNT1 A 2 LIGHT(YELLOWSTROBE)
		Stop
	}
}