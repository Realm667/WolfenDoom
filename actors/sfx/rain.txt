//Rain Spawner by Tormentor667
ACTOR RainSpawner
{
	//$Category Special Effects (Wolf3D)
	//$Title Rain Spawner
	//$Color 12

	//$Arg0 "Radius"
	//$Arg0Tooltip "Radius in map units\nDefault: 0"
	//$Arg1 "Frequency"
	//$Arg1Tooltip "Frequency, the lower the number, the heavier the rainfall\nRange: 0 - 255"
	//$Arg2 "Sound"
	//$Arg2Type 11
	//$Arg2Enum { 0 = "Yes"; 1 = "No"; }
	//$Arg3 "Drop Length"
	//$Arg3Type 11
	//$Arg3Enum { 0 = "Long"; 1 = "Short"; }
	//$Arg3Tooltip "Drop length, use long (0) for heavy rainfall and short (1) for a mizzle"
	//$Arg4 "Pitch"
	//$Arg4Tooltip "The rainfall's pitch, for maps with wind and storm, use angle for direction.\nThe higher the number, the heavier the wind.\nRange: 0 - 75, where 75 is almost horizontal rain"

	Radius 1
	Height 1
	YScale 0.1
	
	+NOCLIP
	+CLIENTSIDEONLY
	+SPAWNCEILING
	+NOGRAVITY
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf(!CallACS("rainswitch"), "Vanish")
		TNT1 A 0 A_SetScale(scalex, scaley)
		TNT1 A 0 A_JumpIf(!Args[2], "StartSound")
		goto RainLoop
		
	StartSound:
		TNT1 A 0 A_PlaySound("Ambient/Rain", 7, 1.0, true)
		goto RainLoop
		
	RainLoop:
		TNT1 A 0 A_CheckRange(1200.0, "RainWait", true)
		TNT1 A 0 A_JumpIf(!Args[3], "RainLong")
		
	RainShort:
		TNT1 A 1 A_SpawnItemEx("RainDropShort", Random(-Args[0], Args[0]), Random(-Args[0], Args[0]), -2, Args[4], 0, -20+(Args[4]/2), 0, SXF_TRANSFERPITCH, Args[1])
		Goto RainLoop
	RainLong:
		TNT1 A 1 A_SpawnItemEx("RainDropLong", Random(-Args[0], Args[0]), Random(-Args[0], Args[0]), -2, Args[4], 0, -40+(Args[4]/2), 0, SXF_TRANSFERPITCH, Args[1])
		Goto RainLoop
		
	RainWait:
		TNT1 A 10
		goto RainLoop
		
	Vanish:
		TNT1 A 1
		Stop
	}
}

ACTOR RainDropShort
{
	Height 1
	Radius 4
	RenderStyle "Translucent"
	Alpha 0.7
	
	+NOGRAVITY
	+MISSILE
	+CLIENTSIDEONLY
	+FORCEYBILLBOARD
	+NOBLOCKMAP
	+DONTSPLASH
	
	States
	{
	Spawn:
		RNDR A 0
		RNDR A 0 A_SetScale(scalex+(frandom(-0.05,0.05)), scaley+(frandom(-0.05,0.05)))
		RNDR A 0 A_SetTranslucent(frandom(0.7,0.4))
	Rainfall:
		"####" A 0 A_CheckRange(1536.0, "Vanish", true) //removes the rain as soon as it is out of sight
		"####" A 4 A_JumpIf(WaterLevel > 0, "Death")
		Loop
	Death:
		"####" B 0 A_SetScale(0.10,0.10)
		"####" B 0 A_SetTranslucent(frandom(0.8,0.2))
	RainDeath:
		"####" B 0 A_FadeOut(0.1)
		"####" B 1 A_SetScale(scalex+0.05, scaley+0.05)
		Loop
	Vanish:
		TNT1 A 1
		Stop
	}
}

ACTOR RainDropLong : RainDropShort {}
