//////////////////////
// SNOW SPAWNERS    //
// by Tormentor667  //
// improved by Ceeb //
//////////////////////

Actor SnowParticle
{
	Radius 1
	Height 1
	Projectile
	RenderStyle Translucent
	Alpha 0
	Scale 0.6
	+MISSILE
	+NOBLOCKMAP
	+DONTSPLASH
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SetScale(frandom(0.3, 0.6))
		SNOW AAAAAAAAAAAAAA 1 A_FadeIn(0.05)
		SNOW A -1
		Stop
	Death:
		SNOW A 1 A_FadeOut(0.05)
		Loop
	}
}

Actor SnowSpawner
{
	//$Category Special Effects (Wolf3D)
	//$Title Snow Spawner (arg1 rad, arg2 freq, arg3 point)
	//$Color 12

	//$Arg0 "Radius"
	//$Arg0Tooltip "Radius in map units"
	//$Arg1 "Frequency"
	//$Arg1Tooltip "The lower the number, the heavier the snowfall"
	//$Arg2 "Area"
	//$Arg2Type 11
	//$Arg2Enum { 0 = "Square"; 1 = "Circle"; }

	Radius 1
	Height 1
	+NOBLOCKMAP
	+NOGRAVITY
	+NOSECTOR
	+NOINTERACTION
	+NOCLIP
	+CLIENTSIDEONLY
	+SPAWNCEILING
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_CheckSight("Unsighted")
		TNT1 A 0 A_JumpIf(CallACS("snowswitch")==0,"Unsighted")
		TNT1 A 0 A_JumpIf(Args[2] > 0,2)
		TNT1 A 8 A_SpawnItemEx ("SnowParticle",Random(-Args[0],Args[0]),Random(-Args[0],Args[0]),0,frandom(-1.0,1.0),frandom(-1.0,1.0),frandom(-1.0,-3.0),0,0,Args[1])
		Loop
		TNT1 A 8 A_SpawnItemEx ("SnowParticle",Random(-Args[0],Args[0]),0,0,frandom(-1.0,1.0),frandom(-1.0,1.0),frandom(-1.0,-3.0),Random(0,360),0,Args[1])
		Loop
	Unsighted:
		TNT1 A 8
		Goto Spawn
	}
}