///////////////////////////////////
// SNOW SPAWNERS by Tormentor667 //
//		improved by Ceeb & MaxED	 //
///////////////////////////////////

ACTOR SnowSpawner
{
	//$Category Special Effects (Wolf3D)
	//$Title Snow Spawner
	//$Color 12

	//$Arg0 "Radius"
	//$Arg0Tooltip "Radius in map units\nDefault: 0"
	//$Arg1 "Frequency"
	//$Arg1Tooltip "The lower the number, the heavier the snowfall\nRange: 0 - 255"
	//$Arg2 "Area"
	//$Arg2Type 11
	//$Arg2Enum { 0 = "Square"; 1 = "Circle"; }

	Radius 1
	Height 3
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOCLIP
	+NOGRAVITY
	+NOINTERACTION
	+NOSECTOR
	+SPAWNCEILING
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf(!CallACS("snowswitch"), "Vanish")
		Goto PickShape
	PickShape:
		TNT1 A 0 A_JumpIf(Args[2], "CircleLoop")
		Goto SquareLoop
	CircleLoop:
		TNT1 A 0 A_CheckRange(1024.0, "CircleWait", TRUE)
		TNT1 A 6 A_SpawnItemEx("SnowParticle", random(-Args[0], Args[0]), 0, 0, frandom(-1.0, 1.0), frandom(-1.0, 1.0),frandom(-1.0, -3.0), random(0, 359), 0, Args[1])
		Loop
	CircleWait:
		TNT1 A 10
		Goto CircleLoop	
	SquareLoop:
		TNT1 A 0 A_CheckRange(1024.0, "SquareWait", TRUE)
		TNT1 A 6 A_SpawnItemEx("SnowParticle", random(-Args[0], Args[0]), random(-Args[0], Args[0]), 0, frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, -3.0), 0, 0, Args[1])
		Loop
	SquareWait:
		TNT1 A 10
		Goto SquareLoop
	Vanish:
		TNT1 A 1
		Stop
	}
}

ACTOR SnowParticle
{
	Radius 1
	Height 3
	Projectile
	RenderStyle "Translucent"
	Alpha 0
	Scale 0.6
	+CLIENTSIDEONLY
	+DONTSPLASH
	+FORCEXYBILLBOARD
	+MISSILE
	+NOBLOCKMAP
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SetScale(frandom(0.3, 0.6))
		SNOW AAAAAAAAAAAAAA 1 A_FadeIn(0.05)
		SNOW A -1
		Stop
	Death:
		SNOW A 1 A_FadeOut(0.05)
		Loop
	}
}