/////////////////////////////
// ACTORS FOR REGULAR MAPS // by GuardSoul, optimized and improved by Tormentor667
/////////////////////////////

ACTOR PuffSmoke
{
	DistanceCheck "sfxlod"
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+NOINTERACTION
	+WINDTHRUST
	RenderStyle "Translucent"
	Alpha 0.1
	Scale 0.04
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SetScale(ScaleX*(frandom(0.7,1.0)), ScaleY*(frandom(0.7,1.0)))
		TNT1 A 0 A_Jump(256,1,2)
		GRM2 A 0 A_Jump(256,"End")
		GRM1 A 0 A_Jump(256,"End")
	End:
		"####" AAAAAAAAA 1 A_FadeIn(0.04)
		"####" AAAAAAAAAAAAAAAA 2 {
						A_FadeOut(0.025);
						A_SetScale(ScaleX*1.02, ScaleY*1.02);
						}
		Stop
	}
}

ACTOR WaterSmoke : PuffSmoke //needed for 3d toilet & 3d hydrants - ozy81
{
	DistanceCheck "sfxlod"
	Alpha 0.2
	Scale 0.03
	States
		{
		Spawn:
			TNT1 A 0 NODELAY A_SetScale(ScaleX*(frandom(0.4,0.7)), ScaleY*(frandom(0.4,0.7)))
			TNT1 A 0 A_Jump(256,1,2)
			WTSA A 0 A_Jump(256,"End")
			WTSC A 0 A_Jump(256,"End")
		End:
			"####" AAAAAAAA 1 A_FadeIn(0.03)
			"####" AAAAAAAAAAAAAA 2 A_FadeOut(0.015)
			Stop
		}
}

ACTOR PowerPlantSmokePuff : PuffSmoke {DistanceCheck "sfxlod" Scale 0.64}
ACTOR PowerPlantSmokePuffSmall : PuffSmoke {DistanceCheck "sfxlod" Scale 0.32}
ACTOR WaterSmokePuff : WaterSmoke {DistanceCheck "sfxlod" Scale 0.18}
ACTOR WaterSmokePuffSmall : WaterSmoke {DistanceCheck "sfxlod" Scale 0.10}

ACTOR PowerPlantSmokeGenerator : SwitchableDecoration
{
	//$Category Special Effects (Wolf3D)
	//$Title Smoke Generator (Normal)
	//$Color 12
	+CLIENTSIDEONLY
	+INVULNERABLE
	+NOGRAVITY
	+NOINTERACTION
	+SHOOTABLE
	States
	{
	Spawn:
	Active:
		TNT1 A 0 NODELAY A_JumpIf(CallACS("SmokeSwitch")==0,"EndSpawn")
	ActiveLoop:
		TNT1 A 0 A_CheckSightOrRange(1200.0, "LoopWait", TRUE)
		TNT1 A 4 A_SpawnItemEx("PowerPlantSmokePuff", random(-3,3), random(-3,3), 0, 0, 0, 3)
		Loop
	Inactive:
		TNT1 A -1
		Stop
	EndSpawn:
		TNT1 A 1
		Stop
	LoopWait:
		TNT1 A 10
		Goto ActiveLoop
	}
}

ACTOR PowerPlantSmokeGeneratorSmall : SwitchableDecoration
{
	//$Category Special Effects (Wolf3D)
	//$Title Smoke Generator (Small)
	//$Color 12
	+CLIENTSIDEONLY
	+NOINTERACTION
	States
	{
	Spawn:
	Active:
		TNT1 A 0 NODELAY A_JumpIf(CallACS("SmokeSwitch")==0,"EndSpawn")
	ActiveLoop:
		TNT1 A 0 A_CheckSightOrRange(1200.0, "LoopWait", TRUE)
		TNT1 A 4 A_SpawnItemEx("PowerPlantSmokePuffSmall", random(-8,8), random(-8,8), 0, 0, 0, 3)
		Loop
	Inactive:
		TNT1 A -1
		Stop
	EndSpawn:
		TNT1 A 1
		Stop
	LoopWait:
		TNT1 A 10
		Goto ActiveLoop
	}
}

ACTOR PowerPlantSmokeGenerator2 : PowerPlantSmokeGenerator
{
	//$Title Smoke Generator (Special)
	//$Color 12
	States
	{
	Spawn:
	Active:
		TNT1 A 0 NODELAY A_JumpIf(CallACS("SmokeSwitch")==0,"EndSpawn")
	ActiveLoop:
		TNT1 A 0 A_CheckSightOrRange(1200.0, "LoopWait", TRUE)
		TNT1 A 4 A_SpawnItemEx("PowerPlantSmokePuff", random(-32,32), random(-32,32), 0, 0, 0, -3)
		Loop
	Inactive:
		TNT1 A -1
		Stop
	EndSpawn:
		TNT1 A 1
		Stop
	LoopWait:
		TNT1 A 10
		Goto ActiveLoop
	}
}

/////////////////////////////
// ACTORS FOR VOLCANO MAPS // by Tormentor667
/////////////////////////////

ACTOR VolcanoPuffSmoke
{
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+NOINTERACTION
	+WINDTHRUST
	RenderStyle "Translucent"
	Alpha 1.0
	Scale 8
	States
	{
	Spawn:
		TNT1 A 0 NODELAY
		GRM3 A 0 A_Jump(256,"End")
	End:
		"####" AAAAAAAAAA 5 A_FadeIn(0.1)
		"####" AAAAAAAAAAAAAAAA 8 A_FadeOut(0.0625)
		Stop
	}
}

ACTOR VolcanoSmokeGenerator : SwitchableDecoration
{
	//$Category Special Effects (Wolf3D)
	//$Title Volcano Smoke Generator (Huge)
	//$Color 12
	+INVULNERABLE
	+NOGRAVITY
	+NOINTERACTION
	+SHOOTABLE
	States
	{
	Spawn:
	Active:
		TNT1 A 0 A_JumpIf(CallACS("SmokeSwitch")==0,"EndSpawn")
		TNT1 A 30 A_SpawnItemEx("VolcanoPuffSmoke", random(-128,128), random(-128,128), 0, random(1,5), random(1,5), random(10,20), random(1,359))
		Loop
	Inactive:
		TNT1 A -1
		Stop
	EndSpawn:
		TNT1 A 1
		Stop
	}
}

///////////////////
// AMBIENT SMOKE //
///////////////////

ACTOR AmbientSmoke
{
	DistanceCheck "sfxlod"
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+NOINTERACTION
	+WINDTHRUST
	RenderStyle "Translucent"
	Alpha 0.001
	Scale 0.3
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2)
		GRM2 A 0 A_Jump(256,"End")
		GRM1 A 0 A_Jump(256,"End")
	End:
		"####" AAAAAAAAAAAAAAAAAA 1 A_FadeIn(0.005)
		"####" AAAAAAAAAAAAAAAAAAA 3 A_FadeOut(0.005)
		Stop
	}
}

ACTOR AmbientSmokeGenerator : SwitchableDecoration
{
	//$Category Special Effects (Wolf3D)
	//$Title Ambient Smoke Generator
	//$Color 12
	//$Arg0 "Radius"
	//$Arg0Tooltip "Radius in map units\nDefault: 0"
	//$Arg1 "Frequency"
	//$Arg1Tooltip "Frequency, the lower the number, the heavier the smoke\nRange: 0 - 255"
	+CLIENTSIDEONLY
	+NOINTERACTION
	States
	{
	Spawn:
	Active:
		TNT1 A 0 NODELAY A_JumpIf(CallACS("SmokeSwitch")==0,"EndSpawn")
	ActiveLoop:
		TNT1 A 0 A_CheckSightOrRange(1200.0, "LoopWait", TRUE)
		TNT1 A 8 A_SpawnItemEx("AmbientSmoke", random(-Args[0], Args[0]), random(-Args[0], Args[0]), random(0,48), 0, 0, 0.5, 0, 0, Args[1])
		Loop
	Inactive:
		TNT1 A -1
		Stop
	EndSpawn:
		TNT1 A 1
		Stop
	LoopWait:
		TNT1 A 10
		Goto ActiveLoop
	}
}