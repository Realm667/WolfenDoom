ACTOR CinderSpawner : SwitchableDecoration
{
	//$Category Special Effects (Wolf3D)
	//$Title Cinder Spawner (floor)
	//$Color 12

	//$Arg0 "Radius"
	//$Arg0Tooltip "Radius in map units"
	//$Arg1 "Height"
	//$Arg1Tooltip "Z-dimension (bottom is the z-level the spawner is on)"
	//$Arg2 "Min. Speed"
	//$Arg2Tooltip "Minimum speed the particles have, randomized between min. and max."
	//$Arg3 "Max. Speed"
	//$Arg3Tooltip "Maximum speed the particles have, randomized between min. and max."
	//$Arg4 "Frequency"
	//$Arg4Tooltip "0 is always, 255 is never at a per-tic basis"
	Radius 1
	Height 1
	+NOCLIP
	+NOGRAVITY
	+CLIENTSIDEONLY
	States
	{
	Active:
	  TNT1 A 0 A_ChangeFlag ("DORMANT", false) //mxd. DORMANT flag must be updated manually
	Spawn:
		TNT1 A 5 A_SpawnItemEx("FloatingCinder", Random(-Args[0],Args[0]), Random(-Args[0],Args[0]), Random(0,Args[1]), Random(Args[2],Args[3]), 0, Random(-Args[2],Args[2]), Random(-4, 4), 128, Args[4])
	  loop
	Inactive:
		TNT1 A -1 A_ChangeFlag ("DORMANT", true) //mxd. DORMANT flag must be updated manually
		stop
	}
}

ACTOR FloatingCinder
{
	+MISSILE
	+NOBLOCKMAP
	+NOGRAVITY
	+DONTSPLASH
	+FORCEXYBILLBOARD
	Radius 0
	Height 0
	RenderStyle "Add"
	Alpha 0.8
	Scale 0.04
	States
	{
	Spawn:
		EMBR A 0 NODELAY A_SetScale(FRandom(0.03, 0.06))
		TNT1 A 0 A_JumpIf(CallACS("CinderSwitch")==0,"EndSpawn")
		TNT1 A 0 A_Jump(80,8)
		TNT1 A 0 A_Jump(80,6)
		TNT1 A 0 A_Jump(80,4)
		TNT1 A 0 A_Jump(80,2)
		EMBR AAAAAAAAAAAAAAAAAAAAAAAA 8 BRIGHT
	Death:
		EMBR A 1 BRIGHT A_FadeOut(0.06)
		Loop
	EndSpawn:
		TNT1 A -1
		Goto Spawn
	}
}

////////////////
// SKY CINDER //
////////////////

ACTOR CinderSpawnerSky
{
	//$Category Special Effects (Wolf3D)
	//$Title Cinder Spawner (ceiling)
	//$Color 12

	//$Arg0 "Radius"
	//$Arg0Tooltip "Radius in map units\nDefault: 0"
	//$Arg1 "Frequency"
	//$Arg1Tooltip "The lower the number, the heavier the amount of cinders\nRange: 0 - 255"
	//$Arg2 "Area"
	//$Arg2Type 11
	//$Arg2Enum { 0 = "Square"; 1 = "Circle"; }

	Radius 1
	Height 3
	
	+NOBLOCKMAP
	+NOGRAVITY
	+NOSECTOR
	+NOINTERACTION
	+NOCLIP
	+CLIENTSIDEONLY
	+SPAWNCEILING
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf(!CallACS("snowswitch"), "Vanish")
		goto PickShape
		
	PickShape:
		TNT1 A 0 A_JumpIf(Args[2], "CircleLoop")
		goto SquareLoop
		
	CircleLoop:
		TNT1 A 0 A_CheckRange(1024.0, "CircleWait", true)
		TNT1 A 6 A_SpawnItemEx("FloatingCinder", random(-Args[0], Args[0]), 0, 0, frandom(-1.0, 1.0), frandom(-1.0, 1.0),frandom(-1.0, -3.0), random(0, 359), 0, Args[1])
		loop
		
	CircleWait:
		TNT1 A 10
		goto CircleLoop	
		
	SquareLoop:
		TNT1 A 0 A_CheckRange(1024.0, "SquareWait", true)
		TNT1 A 6 A_SpawnItemEx("FloatingCinder", random(-Args[0], Args[0]), random(-Args[0], Args[0]), 0, frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, -3.0), 0, 0, Args[1])
		Loop
		
	SquareWait:
		TNT1 A 10
		goto SquareLoop
		
	Vanish:
		TNT1 A 1
		Stop
	}
}