ACTOR CinderSpawner : SwitchableDecoration
{
	//$Category Special Effects (Wolf3D)
	//$Title Cinder Spawner (floor)
	//$Color 12
	//$Arg0 "Radius"
	//$Arg0Tooltip "Radius in map units"
	//$Arg1 "Height"
	//$Arg1Tooltip "Z-dimension (bottom is the z-level the spawner is on)"
	//$Arg2 "Min. Speed"
	//$Arg2Tooltip "Minimum speed the particles have, randomized between min. and max."
	//$Arg3 "Max. Speed"
	//$Arg3Tooltip "Maximum speed the particles have, randomized between min. and max."
	//$Arg4 "Frequency"
	//$Arg4Tooltip "0 is always, 255 is never at a per-tic basis"
	Radius 1
	Height 1
	+CLIENTSIDEONLY
	+NOCLIP
	+NOGRAVITY
	States
	{
	Active:
		TNT1 A 0 A_ChangeFlag ("DORMANT", FALSE) //mxd. DORMANT flag must be updated manually
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(CallACS("CinderSwitch")==0,"EndSpawn")
	SpawnLoop:
		TNT1 A 0 A_CheckRange(1024.0, "SpawnWait", TRUE)
		TNT1 A 5 A_SpawnItemEx("FloatingCinder", random(-Args[0],Args[0]), random(-Args[0],Args[0]), random(0,Args[1]), random(Args[2],Args[3]), 0, random(-Args[2],Args[2]), random(-4, 4), 128, Args[4])
		Loop
	SpawnWait:
		TNT1 A 35
		Goto SpawnLoop
	Inactive:
		TNT1 A -1 A_ChangeFlag ("DORMANT", TRUE) //mxd. DORMANT flag must be updated manually
		Stop
	EndSpawn:
		TNT1 A 1
		Stop
	}
}

ACTOR FloatingCinder
{
	DistanceCheck "sfxlod"
	Radius 0
	Height 0
	+DONTSPLASH
	+FORCEXYBILLBOARD
	+MISSILE
	+NOBLOCKMAP
	+NOGRAVITY
	RenderStyle "Add"
	Alpha 0.8
	Scale 0.04
	States
	{
	Spawn:
		EMBR A 0 NODELAY A_SetScale(frandom(0.03, 0.06))
		TNT1 A 0 A_Jump(80,8)
		TNT1 A 0 A_Jump(80,6)
		TNT1 A 0 A_Jump(80,4)
		TNT1 A 0 A_Jump(80,2)
		EMBR AAAAAAAAAAAAAAAAAAAAAAAA 8 BRIGHT
	Death:
		EMBR A 1 BRIGHT A_FadeOut(0.06)
		Loop
	}
}

////////////////
// SKY CINDER //
////////////////
ACTOR CinderSpawnerSky
{
	//$Category Special Effects (Wolf3D)
	//$Title Cinder Spawner (ceiling)
	//$Color 12
	//$Arg0 "Radius"
	//$Arg0Tooltip "Radius in map units\nDefault: 0"
	//$Arg1 "Frequency"
	//$Arg1Tooltip "The lower the number, the heavier the amount of cinders\nRange: 0 - 255"
	//$Arg2 "Area"
	//$Arg2Type 11
	//$Arg2Enum { 0 = "Square"; 1 = "Circle"; }
	Radius 1
	Height 3
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOCLIP
	+NOGRAVITY
	+NOINTERACTION
	+NOSECTOR
	+SPAWNCEILING
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf(!CallACS("snowswitch"), "Vanish")
		Goto PickShape
	PickShape:
		TNT1 A 0 A_JumpIf(Args[2], "CircleLoop")
		Goto SquareLoop
	CircleLoop:
		TNT1 A 0 A_CheckRange(1024.0, "CircleWait", TRUE)
		TNT1 A 6 A_SpawnItemEx("FloatingCinder", random(-Args[0], Args[0]), 0, 0, frandom(-1.0, 1.0), frandom(-1.0, 1.0),frandom(-1.0, -3.0), random(0, 359), 0, Args[1])
		Loop
	CircleWait:
		TNT1 A 10
		Goto CircleLoop
	SquareLoop:
		TNT1 A 0 A_CheckRange(1024.0, "SquareWait", TRUE)
		TNT1 A 6 A_SpawnItemEx("FloatingCinder", random(-Args[0], Args[0]), random(-Args[0], Args[0]), 0, frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, -3.0), 0, 0, Args[1])
		Loop
	SquareWait:
		TNT1 A 10
		Goto SquareLoop
	Vanish:
		TNT1 A 1
		Stop
	}
}