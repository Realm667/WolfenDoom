ACTOR CloudSpawner
{
	//$Category Special Effects (Wolf3D)
	//$Title Cloudstorm Spawner (arg1 xRadius, arg2 zRadius, arg3 Chance, arg4 Size, arg5 Color)
	//$Color 12
	//$Arg0 "Radius(x/y)"
	//$Arg0Tooltip "Radius of spawn area in map units"
	//$Arg1 "Height(z)"
	//$Arg1Tooltip "Height of spawn area in map units"
	//$Arg2 "Frequency"
	//$Arg2Tooltip "0 is always, 255 is never at a 30-tic basis"
	//$Arg3 "Size"
	//$Arg3Type 11
	//$Arg3Enum { 0 = "Small"; 1 = "Medium"; 2 = "Large"; }
	//$Arg4 "Color"
	//$Arg4Type 11
	//$Arg4Enum { 0 = "Grey"; 1 = "Tan"; }
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOCLIP
	+NOGRAVITY
	+NOINTERACTION
	+NOSECTOR
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(CallACS("CloudSwitch")==0,"EndSpawn")
		TNT1 A 0 A_JumpIf(Args[4] == 1, "Tan")
	Grey:
		TNT1 A 0 A_JumpIf(Args[3] == 0, "SmallGrey")
		TNT1 A 0 A_JumpIf(Args[3] == 1, "MediumGrey")
		TNT1 A 0 A_JumpIf(Args[3] > 1, "LargeGrey")
		Goto SmallGrey
	Tan:
		TNT1 A 0 A_JumpIf(Args[3] == 0, "SmallTan")
		TNT1 A 0 A_JumpIf(Args[3] == 1, "MediumTan")
		TNT1 A 0 A_JumpIf(Args[3] > 1, "LargeTan")
		Goto SmallTan
	SmallGrey:
		TNT1 A 30 A_SpawnItemEx("SmallCloudGrey", random(Args[0]*2, -Args[0]*2), random(Args[0]*2, -Args[0]*2), random(0, Args[1]), random(5, 15), 0, 0, random(6, -6), 128, Args[2])
		Loop
	MediumGrey:
		TNT1 A 30 A_SpawnItemEx("MediumCloudGrey", random(Args[0]*2, -Args[0]*2), random(Args[0]*2, -Args[0]*2), random(0, Args[1]), random(5, 15), 0, 0, random(6, -6), 128, Args[2])
		Loop
	LargeGrey:
		TNT1 A 30 A_SpawnItemEx("LargeCloudGrey", random(Args[0]*2, -Args[0]*2), random(Args[0]*2, -Args[0]*2), random(0, Args[1]), random(5, 15), 0, 0, random(6, -6), 128, Args[2])
		Loop
	SmallTan:
		TNT1 A 30 A_SpawnItemEx("SmallCloudTan", random(Args[0]*2, -Args[0]*2), random(Args[0]*2, -Args[0]*2), random(0, Args[1]), random(5, 15), 0, 0, random(6, -6), 128, Args[2])
		Loop
	MediumTan:
		TNT1 A 30 A_SpawnItemEx("MediumCloudTan", random(Args[0]*2, -Args[0]*2), random(Args[0]*2, -Args[0]*2), random(0, Args[1]), random(5, 15), 0, 0, random(6, -6), 128, Args[2])
		Loop
	LargeTan:
		TNT1 A 30 A_SpawnItemEx("LargeCloudTan", random(Args[0]*2, -Args[0]*2), random(Args[0]*2, -Args[0]*2), random(0, Args[1]), random(5, 15), 0, 0, random(6, -6), 128, Args[2])
		Loop
	EndSpawn:
		TNT1 A 1
		Stop
	}
}

//Base Cloud Grey
ACTOR CloudBaseGrey
{
	+MISSILE
	+NOBLOCKMAP
	+NOGRAVITY
	Radius 1
	Height 1
	RenderStyle "Translucent"
	Alpha 0.1
	Scale 0.5
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256, "Cloud1", "Cloud2")
	Cloud1:
		CLXG A 0
		Goto Movement
	Cloud2:
		CLXG B 0
		Goto Movement
	Movement:
		"####""###########" 3 A_FadeIn(0.05)
		"####" "#" 1000 
		Wait
	Death:
		"####" "#" 5 A_FadeOut(0.05)
		Loop
	}
}

ACTOR SmallCloudGrey : CloudBaseGrey  {	Scale 1.0 }
ACTOR MediumCloudGrey : CloudBaseGrey {	Scale 2.0 }
ACTOR LargeCloudGrey : CloudBaseGrey  {	Scale 4.0 }


//Base Cloud Tan
ACTOR CloudBaseTan : CloudBaseGrey
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256, "Cloud1", "Cloud2")
	Cloud1:
		CLXT A 0
		Goto Movement
	Cloud2:
		CLXT B 0
		Goto Movement
	}
}

//Grey Clouds
ACTOR SmallCloudTan : CloudBaseTan  {	Scale 1.0 }
ACTOR MediumCloudTan : CloudBaseTan {	Scale 2.0 }
ACTOR LargeCloudTan : CloudBaseTan  {	Scale 4.0 }