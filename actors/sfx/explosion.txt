// -----------------------------------------------------------------------------
// Half-Life 2 styled explosions
//
// Made by KeksDose, December 1 2013
//
// These are smoky explosions with a quick flash. You can directly spawn them
// from either ACS or DECORATE, or place them in a map with the "Dormant" flag
// and simply activate them with a line special.
// -----------------------------------------------------------------------------

ACTOR GeneralExplosion_Medium : SwitchableDecoration
{
	//$Category Special Effects (Wolf3D)
	//$Title Explosion Effect (activatable)
	//$Color 12

	//$Arg0 "Quake"
	//$Arg0Type 11
	//$Arg0Enum { 0 = "Yes"; 1 = "No"; }
	//$Arg1 "Sound"
	//$Arg1Type 11
	//$Arg1Enum { 0 = "Yes"; 1 = "No"; }
	+NOINTERACTION
	States
	{
	Spawn:
	Active:
		TNT1 A 0 A_JumpIf(Args[1], 2)
		TNT1 A 0 A_PlaySound("panzer/explode", CHAN_AUTO, 0.75, FALSE, ATTN_IDLE)
		TNT1 A 0 A_JumpIf(!Args[0], "Quake")
		Goto Explosion
	Quake:
		TNT1 A 0 Radius_Quake(10,10,0,16,0)
		Goto Explosion
	Explosion:
		TNT1 A 0 A_Explode(100, 192)
		TNT1 A 0 A_JumpIf(CallACS("BoomSwitch") == 0, "Inactive")
		TNT1 A 0 A_SpawnItemEx("KD_HL2Flash", 0, 0, 0, 0, 0, 0, 0,SXF_CLIENTSIDE | SXF_TRANSFERSCALE)
		TNT1 A 0 A_SpawnItemEx("KD_HL2SmokeGenerator", 0, 0, 0, 0, 0, 0, 0,SXF_CLIENTSIDE | SXF_TRANSFERSCALE)
		TNT1 A 0 A_SpawnItemEx("KD_HL2SparkGenerator", 0, 0, 0, 0, 0, 0, 0,SXF_CLIENTSIDE | SXF_TRANSFERSCALE)
	Inactive:
		TNT1 A 350
		Wait
	}
}

ACTOR KD_HL2GeneratorBase {+NOINTERACTION}

ACTOR KD_HL2ExplosionBase
{
	+FORCEXYBILLBOARD
	+NOINTERACTION
	RenderStyle "Add"
	Alpha 0.99
}

ACTOR KD_HL2Flash : KD_HL2ExplosionBase
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SetScale(frandom(0.5, 0.8) * ScaleX)
		TNT1 A 0 A_Jump(256,1,2)
		EXN1 AB 0 Light("FireSpawnerSmall") A_Jump(256,"Fade")
	Fade:
		"####" "#" 1 BRIGHT Light("FireSpawnerLarge") A_FadeOut(frandom(0.05, 0.10) * (6 - CallACS("BoomSwitch")))
		Loop
	}
}

ACTOR KD_HL2Smoke : KD_HL2ExplosionBase
{
	RenderStyle "Translucent"
	Alpha 0.8
	States
	{
	Spawn:
		TSMK A 1 A_SetScale(ScaleX + frandom(0.005, 0.015))
		TSMK A 0 A_FadeOut(frandom(0.006, 0.012) * (6 - CallACS("BoomSwitch")))
		Loop
	}
}

ACTOR KD_HL2SmokeGenerator : KD_HL2GeneratorBase
{
	States
	{
	Spawn:
		TNT1 A 0 A_Countdown
		TNT1 A 0 A_SpawnItemEx("KD_HL2Smoke", 0, 0, 0, 0.0,frandom(0.0, 2.5) * ScaleX,frandom(-2.0, 2.0) * ScaleX, random(0, 359),SXF_CLIENTSIDE | SXF_TRANSFERSCALE, 30)
		Loop
	}
}

ACTOR KD_HL2Spark : KD_HL2ExplosionBase
{
	+BRIGHT
	States
	{
	Spawn:
		PAO1 A 5 NoDelay Light("BPUFF1") A_SetScale(frandom(0.07, 0.1) * ScaleX)
	Drop:
		PAO1 A 0 A_ChangeVelocity(frandom(0.98, 0.99) * VelX,frandom(0.98, 0.99) * VelY,VelZ - frandom(0.2, 0.5), CVF_REPLACE)
		PAO1 A 1 A_SetScale(ScaleX - 0.002)
		PAO1 A 0 A_FadeOut(0.01 * (6 - CallACS("BoomSwitch")))
		Loop
	}
}

ACTOR KD_HL2SparkGenerator : KD_HL2GeneratorBase
{
	States
	{
	Spawn:
		TNT1 A 0 A_Countdown
		TNT1 AAA 0 A_SpawnItemEx("KD_HL2Spark", 0, 0, 0, 0.0,frandom(0.0, 13.5) * ScaleX,frandom(-1.0, 9.5) * ScaleX, random(0, 359),SXF_CLIENTSIDE | SXF_TRANSFERSCALE, 60)
		Loop
	}
}

//////////////////////////////////////
// Variants in Size by Tormentor667 //
//////////////////////////////////////
ACTOR GeneralExplosion_Large : GeneralExplosion_Medium {Scale 2.0}
ACTOR GeneralExplosion_Small : GeneralExplosion_Medium {Scale 0.5}

//////////////////////////////////////
// Variant for Astrostein ////////////
//////////////////////////////////////
ACTOR KD_HL2FlashAstrostein : KD_HL2ExplosionBase
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SetScale(frandom(0.5, 0.8) * ScaleX)
		TNT1 A 0 A_Jump(256,1,2)
		EXN2 AB 0 Light("FireSpawnerSmall") A_Jump(256,"Fade")
	Fade:
		"####" "#" 1 BRIGHT Light("AstrosteinExplosionSpawnerLarge") A_FadeOut(frandom(0.05, 0.10))
		Wait
	}
}

ACTOR AstrosteinExplosion_Medium : SwitchableDecoration
{
	//$Category Special Effects (Wolf3D)
	//$Title Astrostein Explosion Effect (activatable)
	//$Color 12
	+NOINTERACTION
	States
	{
	Spawn:
	Active:
		TNT1 A 0 A_PlaySound("astrostein/explosion")
		TNT1 A 0 Radius_Quake(10,10,0,16,0)
		TNT1 A 0 A_Explode(100, 192)
		TNT1 A 0 A_JumpIf(CallACS("BoomSwitch")==0,"EndSpawn")
		TNT1 A 0 A_SpawnItemEx("KD_HL2FlashAstrostein", 0, 0, 0, 0, 0, 0, 0,SXF_CLIENTSIDE | SXF_TRANSFERSCALE)
		TNT1 A 0 A_SpawnItemEx("KD_HL2SmokeGenerator", 0, 0, 0, 0, 0, 0, 0,SXF_CLIENTSIDE | SXF_TRANSFERSCALE)
		TNT1 A 0 A_SpawnItemEx("KD_HL2SparkGenerator", 0, 0, 0, 0, 0, 0, 0,SXF_CLIENTSIDE | SXF_TRANSFERSCALE)
	Inactive:
		TNT1 A 350
		Wait
	EndSpawn:
		TNT1 A 1
		Stop
	}
}

ACTOR AstrosteinExplosion_Large : AstrosteinExplosion_Medium {Scale 2.0}
ACTOR AstrosteinExplosion_Small : AstrosteinExplosion_Medium {Scale 0.5}

//////////////////////////////////////
// Variant for Prototypes ////////////
//////////////////////////////////////
ACTOR KD_HL2FlashProto : KD_HL2ExplosionBase
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SetScale(frandom(0.5, 0.8) * ScaleX)
		TNT1 A 0 A_Jump(256,1,2)
		EXN3 AB 0 Light("FireSpawnerSmall") A_Jump(256,"Fade")
	Fade:
		"####" "#" 1 BRIGHT Light("ProtoExplosionSpawnerLarge") A_FadeOut(frandom(0.05, 0.10))
		Wait
	}
}

ACTOR ProtoExplosion_Medium : SwitchableDecoration
{
	+NOINTERACTION
	States
	{
	Spawn:
	Active:
		TNT1 A 0 A_PlaySound("astrostein/explosion")
		TNT1 A 0 Radius_Quake(10,10,0,16,0)
		TNT1 A 0 A_Explode(100, 192)
		TNT1 A 0 A_JumpIf(CallACS("BoomSwitch")==0,"EndSpawn")
		TNT1 A 0 A_SpawnItemEx("KD_HL2FlashProto", 0, 0, 0, 0, 0, 0, 0,SXF_CLIENTSIDE | SXF_TRANSFERSCALE)
		TNT1 A 0 A_SpawnItemEx("KD_HL2SmokeGenerator", 0, 0, 0, 0, 0, 0, 0,SXF_CLIENTSIDE | SXF_TRANSFERSCALE)
		TNT1 A 0 A_SpawnItemEx("KD_HL2SparkGenerator", 0, 0, 0, 0, 0, 0, 0,SXF_CLIENTSIDE | SXF_TRANSFERSCALE)
	Inactive:
		TNT1 A 350
		Wait
	EndSpawn:
		TNT1 A 1
		Stop
	}
}

ACTOR ProtoExplosion_Large : ProtoExplosion_Medium {Scale 2.0}
ACTOR ProtoExplosion_Small : ProtoExplosion_Medium {Scale 0.5}

//////////////////////////////
// Variant for lags - Ozy81 //
//////////////////////////////
//This use CVARs only for the explosion check, all other values are forced to 1

ACTOR SceneryExplosion_Medium : GeneralExplosion_Medium
{
	//$Title Scenery Explosion Effect (activatable)
	States
	{
	Spawn:
	Active:
		TNT1 A 0 A_CheckSightOrRange(1280, "Unseen")
		TNT1 A 0 A_PlaySound("panzer/explode", CHAN_AUTO, 0.75, FALSE, ATTN_IDLE)
		TNT1 A 0 A_JumpIf(!Args[0], "Quake")
		Goto Explosion
	Quake:
		TNT1 A 0 Radius_Quake(10,10,0,16,0)
		Goto Explosion
	Explosion:
		TNT1 A 0 A_Explode(100, 192)
		TNT1 A 0 A_JumpIf(CallACS("BoomSwitch") == 0, "Inactive")
		TNT1 A 0 A_SpawnItemEx("KD_LagsFlash", 0, 0, 0, 0, 0, 0, 0,SXF_CLIENTSIDE | SXF_TRANSFERSCALE)
		TNT1 A 0 A_SpawnItemEx("KD_LagsSmokeGenerator", 0, 0, 0, 0, 0, 0, 0,SXF_CLIENTSIDE | SXF_TRANSFERSCALE)
		TNT1 A 0 A_SpawnItemEx("KD_LagsSparkGenerator", 0, 0, 0, 0, 0, 0, 0,SXF_CLIENTSIDE | SXF_TRANSFERSCALE)
	Inactive:
		TNT1 A 350
		Wait
	Unseen:
		"####" "#" 25
		Goto Spawn
	}
}

ACTOR SceneryExplosion_Large : SceneryExplosion_Medium {Scale 2.0}
ACTOR SceneryExplosion_Small : SceneryExplosion_Medium {Scale 0.5}

ACTOR KD_LagsFlash : KD_HL2Flash
{
	States
	{
	Fade:
		"####" "#" 1 BRIGHT Light("FireSpawnerLarge") A_FadeOut(frandom(0.05, 0.10) * 1)
		Loop
	}
}

ACTOR KD_LagsSmoke : KD_HL2Smoke
{
	States
	{
	Spawn:
		TSMK A 1 A_SetScale(ScaleX + frandom(0.005, 0.015))
		TSMK A 0 A_FadeOut(frandom(0.006, 0.012) * 1)
		Loop
	}
}

ACTOR KD_LagsSmokeGenerator : KD_HL2SmokeGenerator
{
	States
	{
	Spawn:
		TNT1 A 0 A_Countdown
		TNT1 A 0 A_SpawnItemEx("KD_LagsSmoke", 0, 0, 0, 0.0,frandom(0.0, 2.5) * ScaleX,frandom(-2.0, 2.0) * ScaleX, random(0, 359),SXF_CLIENTSIDE | SXF_TRANSFERSCALE, 30)
		Loop
	}
}

ACTOR KD_LagsSpark : KD_HL2Spark
{
	States
	{
	Drop:
		PAO1 A 0 A_ChangeVelocity(frandom(0.98, 0.99) * VelX,frandom(0.98, 0.99) * VelY,VelZ - frandom(0.2, 0.5), CVF_REPLACE)
		PAO1 A 1 A_SetScale(ScaleX - 0.002)
		PAO1 A 0 A_FadeOut(0.01 * 1)
		Loop
	}
}

ACTOR KD_LagsSparkGenerator :  KD_HL2SparkGenerator
{
	States
	{
	Spawn:
		TNT1 A 0 A_Countdown
		TNT1 A 0 A_SpawnItemEx("KD_LagsSpark", 0, 0, 0, 0.0,frandom(0.0, 13.5) * ScaleX,frandom(-1.0, 9.5) * ScaleX, random(0, 359),SXF_CLIENTSIDE | SXF_TRANSFERSCALE, 60)
		Loop
	}
}