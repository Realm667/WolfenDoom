const int SPLASHES_FLAGS = SXF_CLIENTSIDE | SXF_TRANSFERRENDERSTYLE | SXF_TRANSFERSTENCILCOL;

ACTOR WaterSplashGeneratorNormal : EffectSpawner
{
	//$Category Special Effects (BoA)
	//$Title Water Splash Generator (normal; Renderstyle, Stencilcolor are taken into account)
	//$Color 12
	//$Sprite WTSAA0
	//$Arg3 "Sound"
	//$Arg3Type 11
	//$Arg3Enum { 0 = "Yes"; 1 = "No"; }
	+CLIENTSIDEONLY
	+DONTSPLASH
	+NOBLOCKMAP
	+NOCLIP
	+NOGRAVITY
	+NOINTERACTION
	States
	{
	Spawn:
	Active:
		TNT1 A 0 NODELAY A_JumpIf(CallACS("SplashSwitch")==0,"EndSpawn")
		TNT1 A 0 A_JumpIf(Args[3] > 0, 2)
		TNT1 A 0 A_StartSound("water/lap", CHAN_7, 0, 1.0)
		TNT1 AAAAAAA 2 A_SpawnItemEx("WaterSplashSpawner", 0, 0, 0, 0, 0, 0, 0, SPLASHES_FLAGS)
	Inactive:
		TNT1 A -1
		Loop
	EndSpawn:
		TNT1 A 1
		Stop
	}
}

ACTOR WaterSplashSpawner
{
	+DONTSPLASH
	States
	{
	Spawn:
		TNT1 A 0 NODELAY
		TNT1 A 0 A_Jump(256, "Splash1", "Splash2", "Splash3", "Splash4")
	Splash1:
		TNT1 A 0 A_SpawnItemEx("WaterSplashObject1", 0, 0, 0, 0, 0, 0, 0, SPLASHES_FLAGS)
		Stop
	Splash2:
		TNT1 A 0 A_SpawnItemEx("WaterSplashObject2", 0, 0, 0, 0, 0, 0, 0, SPLASHES_FLAGS)
		Stop
	Splash3:
		TNT1 A 0 A_SpawnItemEx("WaterSplashObject3", 0, 0, 0, 0, 0, 0, 0, SPLASHES_FLAGS)
		Stop
	Splash4:
		TNT1 A 0 A_SpawnItemEx("WaterSplashObject4", 0, 0, 0, 0, 0, 0, 0, SPLASHES_FLAGS)
		Stop
	}
}

ACTOR WaterSplashObject1
{
	Mass 100
	Gravity 6.0
	Alpha 0.8
	Scale 0.3
	-NOGRAVITY
	+CLIENTSIDEONLY
	+DONTSPLASH
	+NOINTERACTION
	States
	{
	Spawn:
		WTSA A 0 NODELAY
		"####" A 0 ThrustThingZ(0,random(4,8),0,0)
		"####" A 0 ThrustThing(random(0,255),random(0,1),0,0)
		"####" A 1 A_SetScale(ScaleX+0.01, ScaleY+0.01)
		"####" A 0 A_FadeOut(.025,FTF_REMOVE)
		Goto Spawn+3
	}
}

ACTOR WaterSplashObject2 : WaterSplashObject1
{
	States
	{
		Spawn:
			WTSB A 0 NODELAY
			Goto Super::Spawn+1
	}
}

ACTOR WaterSplashObject3 : WaterSplashObject1
{
	States
	{
		Spawn:
			WTSB A 0 NODELAY
			Goto Super::Spawn+1
	}
}
ACTOR WaterSplashObject4 : WaterSplashObject1
{
	States
	{
		Spawn:
			WTSC A 0 NODELAY
			Goto Super::Spawn+1
	}
}

ACTOR WaterSplashCloud : ParticleBase
{
	Mass 100
	Gravity 6.0
	Alpha 0.0
	Scale 0.7
	-NOGRAVITY
	+CLIENTSIDEONLY
	+DONTSPLASH
	+NOINTERACTION
	States
	{
	Spawn:
		WTFG A 0 NODELAY
		"####" A 0 ThrustThingZ(0,random(1,4),0,0)
		"####" A 0 ThrustThing(random(0,255),random(0,1),0,0)
		"####" AAAAAAAAA 2 A_FadeIn(.03)
		"####" A 2 A_SetScale(ScaleX+0.01, ScaleY+0.01)
		"####" A 0 A_FadeOut(.03,FTF_REMOVE)
		Goto Spawn+12
	}
}

ACTOR WaterSplashGeneratorNormalLooping : WaterSplashGeneratorNormal
{
	//$Title Water Splash Generator (normal, looping; Renderstyle, Stencilcolor are taken into account)
	States
	{
	Spawn:
	Active:
		TNT1 A 0 NODELAY A_JumpIf(CallACS("SplashSwitch")==0,"EndSpawn")
		TNT1 A 4 A_SpawnItemEx("WaterSplashSpawner", 0, 0, 0, 0, 0, 0, 0, SPLASHES_FLAGS)
		TNT1 A 4 A_SpawnItemEx("WaterSplashCloud", random(-8,8), random(-8,8), random(0,16), 0, 0, 0, 0, SPLASHES_FLAGS)
		Loop
	Inactive:
		TNT1 A -1
		Loop
	}
}