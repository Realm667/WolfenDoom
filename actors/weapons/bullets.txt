ACTOR Tracer : FastProjectile
{
	Radius 2
	Height 2
	Speed 75
	Decal BulletChip
	+BLOODSPLATTER
	+NOEXTREMEDEATH
	+SEEKERMISSILE
	+WINDTHRUST
	States
	{
	Spawn:
		TNT1 A 1 BRIGHT A_JumpIfTracerCloser(96,"Whiz")
		TNT1 A 0 A_SpawnItemEx("TracerTrail", 0, 0, 0, velx, vely, velz, 0, SXF_ABSOLUTEVELOCITY) //mxd
		Loop
	Whiz:
		TNT1 A -1 BRIGHT A_SpawnItemEx("Whizzer")
		Stop
	Death:
	Crash:
		TNT1 AAA 0 A_SpawnItemEx("TracerSpark", 0, 0, 0, random(-2,2), random(-2,2), random(-2,2), random(0,359)) //T667 improvements
		TNT1 A 0 A_ChangeFlag("WINDTHRUST", 0)
		PUFF B 3 BRIGHT LIGHT(BPUFF1) A_PlaySound("ricochet")
		PUFF CD 3 BRIGHT LIGHT(BPUFF1)
		Stop
	XDeath:
		TNT1 A 0 A_ChangeFlag("WINDTHRUST", 0)
		TNT1 A 1 A_PlaySound("hitflesh")
		Stop
	}
}

ACTOR Whizzer
{
	States
	{
	Spawn:
		TNT1 A 1 A_PlaySound("whiz")
		Stop
	}
}

ACTOR TracerTrail
{
	Alpha .75
	Scale .4
	RenderStyle "Add"
	+CANNOTPUSH
	+DONTSPLASH
	+FORCEXYBILLBOARD
	+MISSILE //mxd
	+NOBLOCKMAP
	+NODAMAGETHRUST
	+NOGRAVITY
	+NOTELEPORT
	States
	{
	Spawn:
		PUFF A 1 BRIGHT A_FadeOut(0.3)
		Loop
	}
}

ACTOR TracerSpark
{
	Health 4
	Radius 3
	Height 6
	Gravity .1
	Speed .1
	RenderStyle "Add"
	Scale .3
	Mass 0
	BounceType Doom
	+DONTSPLASH
	+FLOORCLIP
	+FORCEXYBILLBOARD
	+MISSILE
	+NOTELEPORT
	+WINDTHRUST
	States
	{
	Spawn:
		PUFF A 1
		"####" AEFGHI 2 BRIGHT A_SetTranslucent(.8,1)
		"####" J 1 BRIGHT A_SetTranslucent(.8,1)
		"####" K 1 BRIGHT A_SetTranslucent(.7,1)
		"####" K 1 BRIGHT A_SetTranslucent(.6,1)
		"####" K 1 BRIGHT A_SetTranslucent(.5,1)
		"####" K 1 BRIGHT A_SetTranslucent(.4,1)
		"####" K 1 BRIGHT A_SetTranslucent(.3,1)
		"####" K 1 BRIGHT A_SetTranslucent(.2,1)
		"####" K 0 A_ChangeFlag("WINDTHRUST", 0)
		Stop
	}
}

ACTOR TankSpark : TracerSpark //use this as blood for vehicles et similae
{
	Scale .4
	Mass 450
	States
	{
	Spawn:
		PUFF A 4
		"####" AEFGHI 8 BRIGHT A_SetTranslucent(.8,1)
		"####" J 4 BRIGHT A_SetTranslucent(.8,1)
		"####" K 4 BRIGHT A_SetTranslucent(.7,1)
		"####" K 4 BRIGHT A_SetTranslucent(.6,1)
		"####" K 4 BRIGHT A_SetTranslucent(.5,1)
		"####" K 4 BRIGHT A_SetTranslucent(.4,1)
		"####" K 4 BRIGHT A_SetTranslucent(.3,1)
		"####" K 4 BRIGHT A_SetTranslucent(.2,1)
		"####" K 0 A_ChangeFlag("WINDTHRUST", 0)
		Stop
	}
}

//////////////////////////////////////////////////////////////////
////////////////// PLAYER WEAPON TRACERS /////////////////////////
//////////////////////////////////////////////////////////////////

//The Player's variants on the tracers, so they have different speeds and damages based on which weapon is being used.

//Muzzle velocity: 1,148-1,312 ft/s (350-400 m/s) - Wikipedia
ACTOR LugerTracer : Tracer
{
	ProjectileKickback 60
	Damage (random(3,17))
	Speed 100
}

//Muzzle velocity: 1,200 ft/s (365 m/s) - Wikipedia
ACTOR WaltherTracer : LugerTracer
{
	Damage (random(6,18))
	Speed 90
}

ACTOR ShotgunTracer : Tracer
{
	Scale .3
	Damage (random(5,15))
	States
	{
		Death:
		Crash:
		TNT1 AAA 0 A_SpawnItemEx("TracerSpark", 0, 0, 0, random(-2,2), random(-2,2), random(-2,2), random(0,359)) //T667 improvements
		TNT1 A 0 A_ChangeFlag("WINDTHRUST", 0)
		PUFF BCD 3 BRIGHT LIGHT(BPUFF1)
		Stop
	}
}

ACTOR AutoShotgunTracer : ShotgunTracer
{
	Obituary "$OBAUTOSH"
}

//Muzzle velocity: 1,312 ft/s (400 m/s) - Wikipedia
ACTOR MP40Tracer : Tracer
{
	Damage (random(9,14))
	Speed 90
}

//Muzzle velocity: 1,001 ft/s (305 m/s) - Wikipedia
ACTOR StenTracer : Tracer
{
	Damage (random(9,14))
}

//Muzzle velocity: 935 ft/s (285 m/s) - Wikipedia
ACTOR ThompsonTracer : Tracer
{
	ProjectileKickback 100
	Damage (random(14,18))
	Speed 120
}

//Muzzle velocity: 2,822 ft/s (860 m/s) - Wikipedia
ACTOR Kar98kTracer : Tracer
{
	ProjectileKickback 100
	Damage (random(40,65))
	Speed 180
	Obituary "$OBMAUSER"
}

//Muzzle velocity: 2,448-2,546 ft/s (746-776 m/s) - Wikipedia
ACTOR G43Tracer : Kar98kTracer
{
	Damage (random(26,55))
	Speed 150
	Obituary "$OBG43"
}

ACTOR ChaingunTracer : Tracer
{
	Damage (random(11,15))
	Speed 90
}

//////////////////////////////////////////////////////////////////
//////////////// SHELLS & CASINGS ////////////////////////////////
//////////////////////////////////////////////////////////////////
ACTOR 9MMCasing
{
	Scale .075
	Projectile
	-NOBLOCKMAP
	-NOGRAVITY
	+BOUNCEONACTORS
	+FORCEXYBILLBOARD
	+WINDTHRUST
	BounceType Doom
	BounceSound "bulletcasing/bounce"
	BounceFactor .5
	WallBounceFactor .2
	Mass 5
	Radius 3
	Height 3
	States
	{
	Spawn:
		CAS1 ABCDEFGH 2 NoDelay A_JumpIf(waterlevel == 3, "AdjustMass") //mxd
		Loop
	AdjustMass: //mxd
		"####" "#" 0 A_SetMass(400)
		goto Swim
	Swim: //mxd. This presumes all cases have the same ammout of frames...
		"####" A 2 A_ScaleVelocity(0.7)
		"####" B 2 A_SpawnItemEx("Bubble", 0, 0, 0, 0, 0, 2, random (0, 360), 0, 128)
		"####" C 2 A_ScaleVelocity(0.7)
		"####" D 2 A_SpawnItemEx("Bubble", 0, 0, 0, 0, 0, 2, random (0, 360), 0, 128)
		"####" E 2 A_ScaleVelocity(0.7)
		"####" F 2 A_SpawnItemEx("Bubble", 0, 0, 0, 0, 0, 2, random (0, 360), 0, 128)
		"####" G 2 A_ScaleVelocity(0.7)
		"####" H 2 A_SpawnItemEx("Bubble", 0, 0, 0, 0, 0, 2, random (0, 360), 0, 128)
		Loop
	Death:
		"####" A 0 A_UnSetSolid
		"####" A 0 A_Jump(256,1,2,3)
		"####" ACG 0 A_Jump(256,"Death1")
		Stop
	Death1:
		"####" "#" 1 A_SetTics(35*CallACS("ShellLifetime")) //try to run tics into seconds for a player-friendly setting
		"####" "#" 1 A_FadeOut(0.1)
		Wait
	}
}

ACTOR RevolverCasing : 9MMCasing{}

ACTOR 45ACPCasing : 9MMCasing
{
	Scale .1
	States
	{
	Spawn:
		CAS3 ABCDEFGH 2 NoDelay A_JumpIf(waterlevel == 3, "AdjustMass") //mxd
		Loop
	}
}

ACTOR ShotgunCasing : 9MMCasing
{
	WallBounceSound "shellcasing/bounce"
	BounceSound "shellcasing/bounce"
	DeathSound "shellcasing/stop"
	Scale .175
	States
	{
	Spawn:
		CAS2 ABCDEFGH 2 NoDelay A_JumpIf(waterlevel == 3, "AdjustMass") //mxd
		Loop
	}
}

ACTOR TokarevCasing : 9MMCasing{}

ACTOR MauserRifleCasing : 9MMCasing
{
	Scale .15
	States
	{
	Spawn:
		CAS4 ABCDEFGH 2 NoDelay A_JumpIf(waterlevel == 3, "AdjustMass") //mxd
		Loop
	}
}

ACTOR EnfieldRifleCasing : MauserRifleCasing{}
ACTOR GarandRifleCasing : MauserRifleCasing{}
ACTOR STGCasing : MauserRifleCasing{}

/////////////////////////////////////////////////////////////
/////////////////// MUTANT PROJECTILES //////////////////////
/////////////////////////////////////////////////////////////

//Nifty new mutants variations for tracers
ACTOR MutantTracer : Tracer
{
	//MissileType MutantTrail
	DamageType MutantPoison

	States
	{
	Spawn:
		TNT1 A 1 BRIGHT A_JumpIfTracerCloser(96,"Whiz")
		TNT1 A 0 A_SpawnItemEx("MutantTrail", 0, 0, 0, velx, vely, velz, 0, SXF_ABSOLUTEVELOCITY) //mxd
		Loop
	}
}

ACTOR MutantTrail: TracerTrail {}

/////////////////////////////////////////////////////////////
/////////////////// ASTROSTEIN PROJECTILES //////////////////
/////////////////////////////////////////////////////////////

//Variant for Enemies
ACTOR AstroTracer : Tracer
{
	+BLOODLESSIMPACT
	DamageType AstroPoison
	DeathType Disintegrate
	States
	{
	Spawn:
		TNT1 A 1 BRIGHT A_JumpIfTracerCloser(96,"Whiz")
		TNT1 A 0 A_SpawnItemEx("AstroTrail", 0, 0, 0, velx, vely, velz, 0, SXF_ABSOLUTEVELOCITY) //mxd
		Loop
	Whiz:
		TNT1 A -1 BRIGHT A_SpawnItemEx("AstroWhizzer")
		Stop
	Death:
	Crash:
		TNT1 AAAAA 0 A_SpawnItemEx("Spark_G1", 0, 0, 0, random(1,2), random(1,2), random(1,2), random(1,360), SXF_CLIENTSIDE)
		TNT1 A 0 A_SpawnItemEx("SparkFlare_G1")
		TNT1 A 8 A_PlaySound("astroricochet")
		Stop
	XDeath:
		TNT1 AAAAA 0 A_SpawnItemEx("Spark_G1", 0, 0, 0, random(1,2), random(1,2), random(1,2), random(1,360), SXF_CLIENTSIDE)
		TNT1 A 0 A_SpawnItemEx("SparkFlare_G1")
		TNT1 A 1 A_PlaySound("astrohitflesh")
		Stop
	}
}

ACTOR EnemyAstroTracer : AstroTracer
{
	Damage (random(2,4))
}

ACTOR AstroWhizzer
{
	States
	{
	Spawn:
		TNT1 A 1 A_PlaySound("astrowhiz")
		Stop
	}
}

ACTOR AstroTrail: TracerTrail{}

//Variant for Players
ACTOR AstroTracerPlayer : AstroTracer
{
	ProjectileKickback 60
	Damage (random(3,17))
	Speed 100
}

/////////////////////////////////////////////////////////////
/////////////////// SKILL SETTINGS //////////////////////////
/////////////////////////////////////////////////////////////

// Enemy tracer variants for Normal mode.
ACTOR EnemyPistolTracer : Tracer
{
	Damage (random(4,8))
}
ACTOR EnemySMGTracer : Tracer
{
	Damage (random(6,7))
}
ACTOR EnemyRifleTracer : Tracer
{
	Damage (random(15,30))
}
ACTOR EnemyStGTracer : Tracer
{
	Damage (random(7,8))
}
ACTOR EnemyChaingunTracer : Tracer
{
	Damage (random(6,8))
}
ACTOR EnemyMutantTracer : MutantTracer //new
{
	Damage (random(2,4))
}

// Enemy tracer variants for Baby mode
ACTOR BabyEnemyPistolTracer : EnemyPistolTracer
{
	Speed 30
}
ACTOR BabyEnemySMGTracer : EnemySMGTracer
{
	Speed 30
}
ACTOR BabyEnemyRifleTracer : EnemyRifleTracer
{
	Speed 30
}
ACTOR BabyEnemyStGTracer : EnemyStGTracer
{
	Speed 30
}
ACTOR BabyEnemyChaingunTracer : EnemyChaingunTracer
{
	Speed 30
}
ACTOR BabyEnemyMutantTracer : EnemyMutantTracer //new
{
	Speed 30
}
ACTOR BabyEnemyAstroTracer : EnemyAstroTracer //new
{
	Speed 30
}

// Enemy tracer variants for Easy mode
ACTOR EasyEnemyPistolTracer : EnemyPistolTracer
{
	Speed 50
}
ACTOR EasyEnemySMGTracer : EnemySMGTracer
{
	Speed 50
}
ACTOR EasyEnemyRifleTracer : EnemyRifleTracer
{
	Speed 50
}
ACTOR EasyEnemyStGTracer : EnemyStGTracer
{
	Speed 50
}
ACTOR EasyEnemyChaingunTracer : EnemyChaingunTracer
{
	Speed 50
}
ACTOR EasyEnemyMutantTracer : EnemyMutantTracer //new
{
	Speed 50
}

ACTOR EasyEnemyAstroTracer : EnemyAstroTracer //new
{
	Speed 50
}

// Enemy tracer variants for Hard and Nightmare mode
ACTOR HardEnemyPistolTracer : EnemyPistolTracer
{
	+MTHRUSPECIES
	Speed 90
}
ACTOR HardEnemySMGTracer : EnemySMGTracer
{
	+MTHRUSPECIES
	Speed 90
}
ACTOR HardEnemyRifleTracer : EnemyRifleTracer
{
	+MTHRUSPECIES
	Speed 90
}
ACTOR HardEnemyStGTracer : EnemyStGTracer
{
	+MTHRUSPECIES
	Speed 90
}
ACTOR HardEnemyChaingunTracer : EnemyChaingunTracer
{
	+MTHRUSPECIES
	Speed 90
}
ACTOR HardEnemyMutantTracer : EnemyMutantTracer //new
{
	+MTHRUSPECIES
	Speed 90
}
ACTOR HardEnemyAstroTracer : EnemyAstroTracer //new
{
	+MTHRUSPECIES
	Speed 90
}