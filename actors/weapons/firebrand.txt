ACTOR Firebrand : NaziWeapon
{
	//$Category Weapons (Wolf3D)
	//$Title (1) Tyrfing
	//$Color 14
	Weapon.SelectionOrder 9999
	Inventory.PickupMessage "$TYRF"
	Inventory.PickupSound "sword/draw"
	Weapon.UpSound "sword/draw"
	-WEAPON.AMMO_CHECKBOTH
	+WEAPON.NOALERT
	+WEAPON.NOAUTOFIRE
	Tag "Blade of Agony"
	States
	{
	Select:
		SRDG A 0 A_Raise
		"####" A 1 A_Raise
		Loop
	Deselect:
		SRDG A 0 A_Lower
		"####" A 1 A_Lower
		Loop
	Ready:
		TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
		SRDG A 1 A_WeaponReady
		Loop
		SRDF A 3 A_WeaponReady
		SRDI BC 3 A_WeaponReady
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
		SRDG B 0 A_JumpIfInventory("SwordSeq",2,"Fire3")
		"####" B 1 A_JumpIfInventory("SwordSeq",1,"Fire2")
		"####" C 1 A_GiveInventory("SwordSeq")
		TNT1 A 4
		SRDG DE 1
		"####" F 1 A_PlaySound("sword/swing")
		"####" GHI 1
		"####" J 1 A_CustomPunch(35,1,0,"SwordPuff",96)
		"####" KLM 1
		TNT1 A 8 A_WeaponReady
		SRDG C 1
		"####" B 1 A_TakeInventory("SwordSeq")
		Goto ready
	Fire2:
		TNT1 A 5
		SRDG N 1 A_GiveInventory("SwordSeq")
		"####" O 1 A_PlaySound("sword/swing")
		"####" PQR 1
		"####" S 1 A_CustomPunch(45,1,0,"SwordPuff",96)
		"####" TUV 1
		TNT1 A 8 A_WeaponReady
		"####" A 0 A_TakeInventory("SwordSeq")
		Goto Ready
	Fire3:
		TNT1 A 4
		SRDG DE 1
		"####" F 1 A_PlaySound("sword/swing")
		"####" GHI 1
		"####" J 1 A_CustomPunch(35,1,0,"SwordPuff",96)
		"####" KL 1
		"####" M 1 A_TakeInventory("SwordSeq",1)
		TNT1 A 8 A_WeaponReady
		SRDG C 1
		"####" B 1 A_TakeInventory("SwordSeq")
		Goto Ready
	SuperFire:
		SRDF B 0 A_JumpIfInventory("SwordSeq",2,"SuperFire3")
		"####" B 1 A_JumpIfInventory("SwordSeq",1,"SuperFire2")
		"####" C 1 A_GiveInventory("SwordSeq")
		TNT1 A 4
		SRDG DE 1
		SRDF F 1 A_PlaySound("flamesword/swing")
		"####" GHI 1
		"####" J 1 A_CustomPunch(80,1,0,"FireSwordPuff",96)
		"####" KLM 1
		TNT1 A 8 A_WeaponReady
		SRDF C 1
		"####" B 1 A_TakeInventory("SwordSeq")
		Goto ready
	SuperFire2:
		TNT1 A 5
		SRDF N 1 A_GiveInventory("SwordSeq")
		"####" O 1 A_PlaySound("flamesword/swing")
		"####" PQR 1
		"####" S 1 A_CustomPunch(100,1,0,"FireSwordPuff",96)
		"####" TUV 1
		TNT1 A 8 A_WeaponReady
		"####" A 0 A_TakeInventory("SwordSeq")
		Goto Ready
	SuperFire3:
		TNT1 A 4
		SRDG DE 1
		SRDF F 1 A_PlaySound("flamesword/swing")
		"####" GHI 1
		"####" J 1 A_CustomPunch(80,1,0,"FireSwordPuff",96)
		"####" KL 1
		"####" M 1 A_TakeInventory("SwordSeq",1)
		TNT1 A 8 A_WeaponReady
		SRDF C 1
		"####" B 1 A_TakeInventory("SwordSeq")
		Goto Ready
	AltFire:
		SRDG A 1 A_JumpIfInventory("PowerWeaponLevel2",1,"Altfire2")
		"####" BC 1
		TNT1 A 7 Thing_Damage(0,25)
		SRDG C 1 A_GiveInventory("PowerWeaponLevel2")
		"####" BA 1
		Goto Ready
	Altfire2:
		SRDF BC 1
		TNT1 A 4
		SRDG DE 1
		SRDF F 1 A_PlaySound("flamesword/swing")
		"####" GH 1
		"####" I 1
		"####" J 1 { A_FireCustomMissile("FirebrandFireball"); Thing_Damage(0,2); }
		"####" KLM 1
		TNT1 A 20 A_WeaponReady
		SRDF C 1
		"####" B 1 A_TakeInventory("SwordSeq")
		Goto Ready
	Spawn:
		SRDP ABCD 4 LIGHT(BOAFLAMW)
		Loop
	}
}

ACTOR FirebrandFireball
{
	Projectile
	Radius 12
	Height 12
	Speed 40
	Damage (40)
	+WINDTHRUST
	RenderStyle "Add"
	DeathSound "world/barrelboom"
	DamageType Fire
	Obituary "$OBTYRF1"
	ProjectileKickback 30
	States
	{
	Spawn:
		CFFX N 0 BRIGHT LIGHT(ITBURNS3)
		CFFX N 0 BRIGHT LIGHT(ITBURNS2)
		CFFX N 1 BRIGHT LIGHT(ITBURNS1) A_SpawnItemEx("FirebrandFireballTrail")
		Loop
	Death:
		CFCF Q 2 BRIGHT A_Scream
		"####" R 2 BRIGHT A_Explode(16,128,0)
		"####" STUVWXYZ 2 BRIGHT
		Stop
	}
}

ACTOR FirebrandFireballTrail
{
	RenderStyle "Add"
	States
	{
	Spawn:
		CFFX NOP 4 BRIGHT LIGHT(ITBURNS1)
		Stop
	}
}

ACTOR SwordPuff
{
	+NOBLOCKMAP
	+NOGRAVITY
	+PUFFONACTORS
	AttackSound "sword/wall"
	SeeSound "sword/hit"
	Obituary "$OBTYRF2"
	ProjectileKickback 10
	States
	{
	Spawn:
	Melee:
		TNT1 A 1
		Stop
	Crash:
		"####" AAAA 0 A_SpawnItemEx("TracerSpark", 0, 0, 0, random(-2,1), random(-2,1), random(-2,1), random(0,359)) //T667 improvements
		"####" A 1 A_AlertMonsters
		Stop
	}
}

ACTOR FireSwordPuff : SwordPuff
{
	DamageType Fire
	ProjectileKickback 30
	SeeSound "flamesword/hit"
}

ACTOR SwordPuffSpark
{
	Health 4
	Radius 3
	Height 6
	Speed .1
	RenderStyle "Add"
	Scale .4
	Mass 0
	BounceType Doom
	+DONTSPLASH
	+FLOORCLIP
	+MISSILE
	States
	{
	States:
		PUFF A 1
		"####" A 5 BRIGHT A_SetTranslucent(.8,1)
		"####" A 6 BRIGHT A_SetTranslucent(.6,1)
		"####" A 8 BRIGHT A_SetTranslucent(.4,1)
		"####" A 10 BRIGHT A_SetTranslucent(.2,1)
		Stop
	}
}

ACTOR SwordSeq : Inventory {Inventory.MaxAmount 2}