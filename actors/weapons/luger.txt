ACTOR Luger9mm : DoomWeapon replaces Pistol 15008
{
//$Category Weapons (Wolf3D)
//$Title (2) Luger
//$Color 14
  Weapon.SelectionOrder 9999
  Weapon.SlotNumber 2
  weapon.kickback 60
  Weapon.AmmoType "LugerAmmo"
  Weapon.AmmoUse1 0
  Weapon.AmmoGive1 0
  weapon.ammotype2 "MPClip"
  weapon.ammouse2 1
  weapon.ammogive2 8 //10
  Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.2
	Weapon.BobRangeY 0.5
	Weapon.BobRangeX 0.7
  Decal "BulletChip"
  Tag "Luger P08"
  attacksound "weapons/Luger"
  Inventory.PickupMessage "You got the Luger!"
  +Ammo_Optional
  	+Alt_Ammo_Optional
	+WEAPON.PRIMARY_USES_BOTH
	+Weapon.NoAlert
	+Weapon.NoAutoFire
  States
  {
  Ready:
		LUGG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		LUGG A 0 A_Lower
		LUGG A 1 A_Lower
		Loop
	Select:
		LUGG A 0 A_Raise
		LUGG A 1 A_Raise
		Loop
	Fire:
		LUGG A 0 A_JumpIfInventory("LugerAmmo",1,1)
		Goto Dryfire
		LUGF A 0 BRIGHT A_GunFlash
		LUGF A 0 A_SetPitch(pitch - 0.2)
		LUGF A 0 A_PlayWeaponSound("weapons/Luger")
		LUGF A 0 A_TakeInventory("LugerAmmo",1)
		LUGF A 1 BRIGHT A_FireCustomMissile("LugerTracer",frandom(-1,1),0)
		LUGG B 1 A_AlertMonsters
		LUGG A 0 A_SetPitch(pitch - 0.2)
		LUGG B 1 Offset(0,36)
		LUGG B 1 Offset(0,41)
		LUGG C 1 Offset(0,35) A_WeaponReady(1)
		LUGG C 1 Offset(0,33) A_WeaponReady(1)
		LUGG C 1 Offset(0,31) A_WeaponReady(1)
		LUGG A 1 Offset(0,32) A_WeaponReady(1)
		Goto Ready
	Dryfire:
		LUGG A 1
	Reload:
		LUGG A 0 A_JumpIfInventory("LugerAmmo",8,2)
		LUGG A 0 A_JumpIfNoAmmo(1)
		Goto ProperReload
		LUGG A 1
		Goto Ready
	ProperReload:
		TNT1 A 0 A_Jump(192,2)
		TNT1 A 0 A_PlaySound("*taunt", CHAN_VOICE)
		LUGG A 1 Offset(0,35) A_PlaySound("reloads/luger", CHAN_ITEM)
		LUGG A 1 Offset(0,38)
		LUGG A 1 Offset(0,44)
		LUGG A 1 Offset(0,52)
		LUGG C 1 Offset(0,62)
		LUGG C 1 Offset(0,72)
		LUGG C 1 Offset(0,82)
		TNT1 A 11
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("MPClip",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("LugerAmmo",1)
		TNT1 A 0 A_JumpIfInventory("LugerAmmo",8,"ReloadFinish")
		TNT1 A 0 A_JumpIfNoAmmo("ReloadFinish")
		Goto ReloadLoop
	ReloadFinish:
		LUGG B 1 Offset(0,82)
		LUGG B 1 Offset(0,72)
		LUGG B 1 Offset(0,62)
		LUGG C 1 Offset(0,52)
		LUGG C 1 Offset(0,44)
		LUGG C 1 Offset(0,38)
		LUGG A 1 Offset(0,35)
		LUGG A 1 Offset(0,32)
		Goto Ready
	Flash:
		TNT1 A 3 A_Light2
		TNT1 A 3 A_Light1
		TNT1 A 0 A_Light0
		Goto LightDone
	Spawn:
		LUGP A -1
		Stop
	}
}

ACTOR LugerAmmo : Ammo
{
	+IGNORESKILL
	Inventory.MaxAmount 8
	Inventory.Icon "WALT01"
}