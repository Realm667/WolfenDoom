Actor KTurretStand : SwitchableDecoration //IMPORTANT: The TurretStand NEEDS to have a unique TID.
{
	//$Category Weapons (Wolf3D)
	//$Title Usable Turret (GIVE ALWAYS A TAG TO IT!)
	//$Color 14
	Radius 8
	Height 32
	+MOVEWITHSECTOR //Added this for arcade sequences if needed - ozy
	+MTHRUSPECIES
	+NOGRAVITY
	+SOLID
	+THRUSPECIES
	+USESPECIAL
	Species "TURR"
	
	Activation	THINGSPEC_ThingTargets| THINGSPEC_ThingActs| THINGSPEC_NoDeathSpecial| THINGSPEC_Switch 
	//note about THINGSPEC_ThingTargets: as soon as it's activated, the player becomes the Master of the turret instead of Target & the Target pointer is cleared.
	
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("KTurretGun",0,0,32,0,0,0,0,SXF_SETTARGET|SXF_ISTRACER) //let's spawn the interactive turret (tweaked from modeldef too)
		MDLA A 1
		Goto Spawn2

	Spawn2:
		MDLA A -1
		Stop

	Active:	//MP compatibility: if the target has a master, it means it's manned by another player, & thus the script below should not be executed.
		TNT1 A 0 A_JumpIf(IsPointerEqual(AAPTR_MASTER,AAPTR_NULL),"StartLoop")
		Goto Spawn2
	
	StartLoop:
		MDLA A 0 ACS_NamedExecute("GunLoop",0)
		Goto Spawn2

	Inactive: //the inactive and active state do the exact same thing! Ugly, but works.
		TNT1 A 0
		Goto Active
	}
}

Actor KTurretGun 
{
	Radius 2
	Height 2
	+NOGRAVITY
	+NOINTERACTION
	+MISSILE //Ensures the player is the owner of turret-spawned projectiles
	+MOVEWITHSECTOR //Added this for arcade sequences if needed - ozy
	+MTHRUSPECIES
	+NOBLOCKMAP
	+SYNCHRONIZED //let's avoid some issues with model orientation vs sounds
	+THRUSPECIES
	Species "TURR"
	
	Threshold 3 //this is Z spawn offset for projectiles. Higher values for taller guns.
	DefThreshold 30 //this is X spawn offset for projectiles. Higher values for longer barrels. - it's not used atm (ozy)
	
	MeleeRange 1.0 //this is accuracy. We use this because it's the only Float variable native to the actor and usable by inventory items. Higher values = more spread.
	Accuracy 8 //Accuracy is instead the delay in tics between shots. Lower values = higher fire rate.
	
	var float user_startangle;
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_GiveInventory("KTurret_ammo",1)
		TNT1 A 0 A_SetUserVar("user_startangle",angle)
		MDLA B 1
		Goto Spawn2

	Spawn2:
		MDLA B 1 A_Jumpif(CountInv("KTurret_Ammo")== 0,"recharge")
		Loop

	Recharge:
		MDLA B 0 A_PlaySound("mp40/fire",CHAN_WEAPON)
		MDLA B 1 A_SetTics(Accuracy) //the duration of this frame controls fire rate.
		MDLA B 0 A_GiveInventory("KTurret_Ammo",1)
		Goto Spawn2
	}
}

ACTOR KTurret_Token : CustomInventory //fires the turret when given to it through acs
{
	States
	{
	Pickup:
		//TNT1 A 0 A_SpawnItemEx("KTurretFlash",cos(pitch)*60.0,0,10.0+sin(-pitch)*60) //muzzle flash - we need to rework this (ozy)
		TNT1 A 0 A_SpawnItemEx("9MMCasing",ScaleX*4,ScaleY*4,ScaleX+ScaleY*8,8,random(-2,2),random(0,4),random(50,80),SXF_NOCHECKPOSITION)
		TNT1 A 0 A_CustomMissile("KTurretTracer",Threshold,0,frandom(-MeleeRange,MeleeRange), CMF_TRACKOWNER | CMF_AIMDIRECTION,-pitch + frandom(-MeleeRange,MeleeRange))
		Stop
	}
}

ACTOR KTurret_Ammo: Inventory //a counter item
{
	Inventory.MaxAmount 1
	-INVBAR
	+UNDROPPABLE
}

ACTOR KHolster : Weapon //dummy weapon that's pretty useful
{
	+WEAPON.CHEATNOTWEAPON
	Tag "Usable Turret" //this will be shown on HUD while "holding" the weapon - Ozy
	States
	{
	Select:
		TNT1 A 0 A_Raise
		Wait
	Deselect:
		TNT1 A 1
		TNT1 A 0 A_Lower
		Wait
	Fire:
		TNT1 A 0 A_AlertMonsters(0,0) //this will need some more tests because of stealth system and awareness-wise settings on BoA (ozy)
	Ready:
		TNT1 A 1 A_WeaponReady
		Loop
	}
}

ACTOR KTurretTracer : Tracer
{
	Radius 2
	Height 2
	Damage (random(10,30))
	Speed 200
	+CANNOTPUSH
	+DONTSPLASH
	+FORCEXYBILLBOARD
	+MISSILE
	+MTHRUSPECIES
	+NOBLOCKMAP
	+NODAMAGETHRUST
	+NOGRAVITY
	+NOTELEPORT
	+THRUSPECIES
	Alpha .75
	Scale .6
	RenderStyle "Add"
	Species "TURR"
	
	States
	{
	Spawn:
		PUFF A 0 NoDelay
		Goto Spawn2
	
	Spawn2: 
		PUFF A 1 BRIGHT
		Loop

	Death:
	Crash:
		TNT1 AAA 0 A_SpawnItemEx("TracerSpark", 0, 0, 0, random(-2,2), random(-2,2), random(-2,2), random(0,359)) //T667 improvements
		TNT1 A 0 A_ChangeFlag("WINDTHRUST", 0)
		PUFF B 3 BRIGHT LIGHT(BPUFF1) A_PlaySound("ricochet")
		PUFF CD 3 BRIGHT LIGHT(BPUFF1)
		Stop
		
	XDeath:
		TNT1 A 0 A_ChangeFlag("WINDTHRUST", 0)
		TNT1 A 1 A_PlaySound("hitflesh")
		Stop
	}
}

/*
Actor KTurretFlash //this is a muzzle flash actor //this must be replaced (ozy)
{
	+FORCEXYBILLBOARD
	+NOBLOCKMAP
	+NOINTERACTION
	Radius 0 
	Height 0
	Renderstyle "Add"
	Scale 0.05
	
	States
	{
	Spawn:
		TNT1 A 0
		TFSH B 1 BRIGHT Light("BlasterFlash1") //this must be replaced (ozy)
		TNT1 A 0 A_SetScale(Scalex+0.05)
		TFSH B 1 BRIGHT Light("BlasterFlash2") //this must be replaced (ozy)
		Stop
	}
}*/