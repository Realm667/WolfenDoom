ACTOR Nebelwerfer : NaziWeapon //replaces Rocketlauncher
{
	//$Category Weapons (Wolf3D)
	//$Title (6) Nebelwerfer
	//$Color 14
	Weapon.SelectionOrder 2500
	Weapon.AmmoUse 1
	Weapon.AmmoGive 10
	Weapon.AmmoType "NebAmmo"
	Weapon.UpSound "nebelwerfer/select"
	Tag "Klein Nebelwerfer"
	Inventory.PickupMessage "$WERFER"
	-WEAPON.AMMO_CHECKBOTH
	-WEAPON.NOALERT
	States
	{
	Ready:
		NEBG A 1 A_WeaponReady
		Loop
	Deselect:
		NEBG A 0 A_Lower
		NEBG A 1 A_Lower
		Loop
	Select:
		NEBG A 0 A_Raise
		NEBG A 1 A_Raise
		Loop
	Fire:
		NEBG A 0 A_GunFlash
		NEBG A 0 A_PlaySound("nebelwerfer/fire",CHAN_WEAPON)
		NEBG A 2 A_FireCustomMissile("NebRocket")
		NEBG BCDEB 1 A_SetPitch(pitch-(0.5*CallACS("RecoilAmount")))
		NEBG A 2 A_ReFire
		Goto Ready
	Flash:
		NEBF A 2 BRIGHT A_Light2
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		NEBL A -1
		Stop
	}
}

ACTOR NebRocket
{
	Scale .5
	Radius 11
	Height 8
	Speed 35
	Damage 8
	Projectile
	+FORCERADIUSDMG
	+RANDOMIZE
	+WINDTHRUST
	DeathSound "nebelwerfer/xplode"
	Decal Scorch
	ProjectileKickback 100
	States
	{
	Spawn:
		MNSS A 0 NoDelay A_PlaySound("nazi/missileengine", CHAN_AUTO, 0.3, true)
	Fly:
		MNSS A 1 BRIGHT LIGHT(NEBLIGHT) A_SpawnItemEx("RocketFlame",random(-1,1),0,random(-1,1))
		Loop
	Death:
		EXP1 A 0 A_SetScale(0.85,0.95)
		EXP1 A 0 A_AlertMonsters
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("TracerSpark", 0, 0, 0, random(-5,5), random(-5,5), random(-5,5), random(0,359)) //T667 improvements
		TNT1 A 0 A_SpawnItemEx("NebNuke",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
		TNT1 A 8 A_SpawnItemEx("GeneralExplosion_Small",56,0,32)
		FRME A 1 BRIGHT LIGHT(NEBEXPLO) A_Explode(64,56)
		FRME BCDEFGHIHJKLMNOPQRS 1 BRIGHT LIGHT(NEBEXPLO)
		Stop
	}
}

ACTOR RocketFlame
{
	Projectile
	+NOCLIP
	Scale 0.15
	Alpha 0.5
	RenderStyle "Add"
	States
	{
	Spawn:
		TNT1 A 1
		XPLO AB 2 BRIGHT LIGHT(NEBEXPLO)
		XPLO C 2 BRIGHT LIGHT(NEBEXPLO) A_SpawnItemEx("RPG8RocketTrail",random(-1,1),0,1)
		Stop
	}
}

ACTOR RPG8RocketTrail : RocketFlame
{
	Scale 0.18
	Alpha 0.6
	States
	{
	Spawn:
		SMOK ABCDEFGHIJKLMNOPQ 1
		Stop
	}
}

ACTOR MutantFlame
{
	Projectile
	+NOCLIP
	Scale 0.15
	Alpha 0.5
	RenderStyle "Add"
	States
	{
	Spawn:
		TNT1 A 1
		XPLO AB 2 BRIGHT LIGHT(MUTNEXPL)
		XPLO C 2 BRIGHT LIGHT(MUTNEXPL) A_SpawnItemEx("MutnRocketTrail",random(-1,1),0,1)
		Stop
	}
}

ACTOR MutnRocketTrail : MutantFlame
{
	Scale 0.18
	Alpha 0.6
	States
	{
	Spawn:
		SMOK ABCDEFGHIJKLMNOPQ 1
		Stop
	}
}

///////////////////////
// Truck Nebelwerfer //
///////////////////////

ACTOR NebelwerferTruck : Nebelwerfer
{
	Weapon.SelectionOrder 2500
	Weapon.AmmoUse 0
	Weapon.AmmoGive 0
	Weapon.AmmoType "NebAmmo"
	Weapon.UpSound "nebelwerfer/select"
	Tag "Truck Turret"
	-WEAPON.NOALERT
	-WEAPON.AMMO_CHECKBOTH
	States
	{
	Ready:
		NEBG A 1 A_WeaponReady
		Loop
	Deselect:
		NEBG A 0 A_Lower
		NEBG A 1 A_Lower
		Loop
	Select:
		NEBG A 0 A_Raise
		NEBG A 1 A_Raise
		Loop
	Fire:
		NEBG A 0 A_GunFlash
		NEBG A 0 A_PlaySound("nebelwerfer/fire",CHAN_WEAPON)
		NEBG A 2 A_FireCustomMissile("NebRocket")
		NEBG BCDEB 1 A_SetPitch(pitch-(0.5*CallACS("RecoilAmount")))
		NEBG A 40 
		NEBG A 2 A_ReFire
		Goto Ready
	Flash:
		NEBF A 2 BRIGHT A_Light2
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		NEBL A -1
		Stop
	}
}