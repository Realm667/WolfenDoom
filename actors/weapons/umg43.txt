ACTOR UMG43 : Weapon 15022
{
//$Category Weapons (Wolf3D)
//$Title (8) UMG43
//$Color 14
	Inventory.PickupMessage "You got the Ubermaschinengewehr 43!"
	Inventory.PickupSound "chaingun/get"
	Tag "UMG43"
	Weapon.SlotNumber 8
	Weapon.AmmoType1 "MPClip"
	Weapon.AmmoUse1 1
	Weapon.AmmoGive1 30
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.2
	Weapon.BobRangeY 0.5
	Weapon.BobRangeX 0.7
	ConversationID 193
	Decal "BulletChip"
	States
	{
	Spawn:
		UMGP A -1
		Stop
	Select:
		UMGG A 0 A_Raise
		UMGG A 1 A_Raise
		Loop
	Deselect:
		UMGG D 0 A_StopSoundEx("SoundSlot5")
		UMGG A 0 A_Lower
		UMGG A 1 A_Lower
		Loop
	Ready:
		UMGG D 0 A_StopSoundEx("SoundSlot5")
		UMGG A 1 A_WeaponReady
		Loop
	Fire:
		UMGG A 0 A_PlaySound("weapons/chaingunstart")
		UMGG A 0 A_PlaySoundEx("weapons/chaingunloop","SoundSlot5",1)
		TNT1 A 0 A_Jump(192,2)
		TNT1 A 0 A_PlaySound("*taunt", CHAN_VOICE)
		UMGG AAAABBBCCD 1
		UMGG D 1 A_Refire
		UMGG D 0 A_PlaySound("weapons/chaingunstop")
		UMGG D 0 A_StopSoundEx("SoundSlot5")
		UMGG DABCDABCDAABBBCCCCDDDDD 1 A_WeaponReady
		Goto Ready
	Hold:
		UMGF A 0 A_GunFlash
		UMGF A 0 Bright A_SetBlend("Orange",0.045,3)
		UMGF A 0 A_SetPitch(pitch - 0.34)
		UMGF A 0 A_PlayWeaponSound("weapons/chaingun")
		UMGF A 1 BRIGHT Offset(0,34) A_FireCustomMissile("ChaingunTracer",frandom(-5.0,5.0),1,0,0,0,frandom(-3.0,3.0))
		UMGF A 0 A_SetPitch(pitch - 0.34)
		UMGG C 1 Offset(0,36) A_FireCustomMissile("ChaingunTracer",frandom(-5,5),1,0,0,0,frandom(-3,3))
		UMGF A 0 A_GunFlash
		UMGF A 0 Bright A_SetBlend("Orange",0.045,3)
		UMGF A 0 A_SetPitch(pitch - 0.34)
		UMGF A 0 A_PlayWeaponSound("weapons/chaingun")
		UMGF B 1 Offset(0,37) BRIGHT A_FireCustomMissile("ChaingunTracer",frandom(-5.0,5.0),1,0,0,0,frandom(-3.0,3.0))
		UMGF A 0 A_SetPitch(pitch - 0.34)
		UMGG C 1 Offset(0,35) A_FireCustomMissile("ChaingunTracer",frandom(-5,5),1,0,0,0,frandom(-3,3))
		UMGG A 1 BRIGHT A_Refire
		UMGG D 0 A_PlaySound("weapons/chaingunstop")
		UMGG D 0 Offset(0,32) A_StopSoundEx("SoundSlot5")
		UMGG DABCDABCDAABBBCCCCDDDDD 1 A_WeaponReady
		Goto Ready
	Flash:
		TNT1 A 2 BRIGHT A_Light2
		TNT1 A 2 BRIGHT A_Light1
		TNT1 A 0 A_Light0
		Stop
	}
}