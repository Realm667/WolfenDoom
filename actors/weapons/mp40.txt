ACTOR MP40Seq : Inventory
{
	Inventory.MaxAmount 1
}

ACTOR MP40 : DoomWeapon replaces chaingun 15009
{
//$Category Weapons (Wolf3D)
//$Title (4) MP-40
//$Color 14
  scale 0.5
  Weapon.SelectionOrder 600
  Weapon.SlotNumber 4
  weapon.kickback 60
  Weapon.AmmoType "MPAmmo"
  Weapon.AmmoUse1 0
  Weapon.AmmoGive1 0
  weapon.ammotype2 "MPClip"
  weapon.ammouse2 1
  weapon.ammogive2 20
  Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.2
	Weapon.BobRangeY 0.5
	Weapon.BobRangeX 0.7
  Decal "BulletChip"
  Tag "MP-40"
  attacksound "weapons/assaultgun"
  Inventory.PickupMessage "You got the MP-40!"
  ConversationID 188
  +Ammo_Optional
  	+Alt_Ammo_Optional
	+WEAPON.PRIMARY_USES_BOTH
	+Weapon.NoAlert
  States
  {
  Ready:
    RIFG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
    Loop
  Deselect:
	RIFG B 0 A_Lower
    RIFG B 1 A_Lower
    Loop
  Select:
	RIFG A 0 A_Raise
    RIFG A 1 A_Raise
    Loop
  Fire:
    RIFF A 0 A_JumpIfInventory("MPAmmo",1,1)
    Goto DryFire
	RIFF A 0 A_JumpIfInventory("MP40Seq",1,"Fire2")
    RIFF A 0 Offset(0,32) A_GunFlash
    RIFF A 0 A_SetPitch(pitch - 0.5)
    RIFF A 0 A_PlayWeaponSound("weapons/mp40")
    RIFF A 0 A_TakeInventory("MPAmmo",1)
	RIFF A 0 A_Jump(250,2)
	RIFF A 0 A_PlaySound("*taunt", CHAN_VOICE)
    RIFF A 1 BRIGHT A_FireCustomMissile("SMGTracer",frandom(-1.4,1.4),0)
    RIFF B 1 Offset(0,38) A_AlertMonsters
    RIFF B 1 Offset(0,44) A_GiveInventory("MP40Seq",1)
    RIFF B 1 Offset(0,40)
    RIFG A 1 Offset(0,36)
    Goto Ready
	Fire2:
		RIFF A 0 Offset(0,32) A_GunFlash
    RIFF A 0 A_SetPitch(pitch - 0.5)
    RIFF A 0 A_PlayWeaponSound("weapons/mp40")
    RIFF A 0 A_TakeInventory("MPAmmo",1)
	RIFF A 0 A_Jump(250,2)
	RIFF A 0 A_PlaySound("*taunt", CHAN_VOICE)
    RIFF A 1 BRIGHT A_FireCustomMissile("SMGTracer",frandom(-1.4,1.4),0)
    RIFF B 1 Offset(0,38) A_AlertMonsters
    RIFF B 1 Offset(0,44) A_TakeInventory("MP40Seq",1)
    RIFF B 1 Offset(0,40)
    Goto Ready
  Dryfire:
    RIFG A 1
	Reload:
		RIFG A 0 A_JumpIfInventory("MPAmmo",32,2)
    RIFG A 0 A_JumpIfNoAmmo(1)
    Goto ProperReload
		RIFG A 1
		Goto Ready
  ProperReload:
	RIFG A 0 A_Jump(128,2)
	RIFG A 0 A_PlaySound("*taunt", CHAN_VOICE)
		RIFG A 1 Offset(0,35) A_PlaySound("reloads/mp40",CHAN_ITEM)
		RIFG A 1 Offset(0,38)
		RIFG A 1 Offset(0,44)
		RIFF B 1 Offset(0,52)
		RIFF B 1 Offset(-1,54)
		RIFF B 1 Offset(-2,56)
		RIFF B 1 Offset(-3,58)
		RIFF B 1 Offset(-4,58)
		RIFF B 1 Offset(-4,57)
		RIFF B 1 Offset(-3,54)
		RIFF B 1 Offset(-3,51)
		RIFF B 1 Offset(-3,53)
		RIFF B 1 Offset(-3,55)
		RIFF B 1 Offset(-3,56)
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("MPClip",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("MPAmmo",1)
		TNT1 A 0 A_JumpIfInventory("MPAmmo",32,"ReloadFinish")
    TNT1 A 0 A_JumpIfNoAmmo("ReloadFinish")
    Goto ReloadLoop
	ReloadFinish:
		RIFF B 1 Offset(-3,57)
		RIFF B 1 Offset(-3,59)
		RIFF B 1 Offset(-3,63)
		RIFF B 1 Offset(-3,67)
		RIFF B 1 Offset(-3,65)
		RIFF B 1 Offset(-3,62)
		RIFF B 1 Offset(-3,58)
		RIFF B 1 Offset(-3,55)
		RIFF B 1 Offset(-2,53)
		RIFF B 1 Offset(-2,49)
		RIFF B 1 Offset(-2,46)
		RIFF B 1 Offset(-2,45)
		RIFF B 1 Offset(-1,44)
		RIFF B 1 Offset(-1,46)
		RIFF B 1 Offset(-1,47)
		RIFF B 1 Offset(0,49) A_WeaponReady(1)
		RIFG A 1 Offset(0,44) A_WeaponReady(1)
		RIFG A 1 Offset(0,38) A_WeaponReady(1)
		RIFG A 1 Offset(0,35) A_WeaponReady(1)
		RIFG A 1 Offset(0,32) A_WeaponReady(1)
		Goto Ready
  Flash:
    TNT1 A 3 A_Light2
    TNT1 A 3 A_Light1
    TNT1 A 0 A_Light0
    Goto LightDone
  Spawn:
    MP40 A -1
    Stop
  }
}

ACTOR MPClip : Ammo replaces ClipBox 15192
{
//$Category Ammo (Wolf3D)
//$Title Ammobox
//$Color 6
	Inventory.Amount 64
	Inventory.MaxAmount 256
	Ammo.BackpackAmount 64
	Ammo.BackpackMaxAmount 480
	Inventory.PickupMessage "Picked up a box of 9mm ammo."
	Inventory.Icon "MCLPA0"
	States
	{
	Spawn:
		MCLP A -1
		Stop
	}
}

ACTOR MPClipSmall : MPClip 15193
{
//$Category Ammo (Wolf3D)
//$Title Ammoclip
//$Color 6
	Inventory.Amount 16
	Inventory.PickupMessage "Picked up a 9mm ammo clip."
	Inventory.Icon "MCLPA0"
	States
	{
	Spawn:
		CLIP A -1
		Stop
	}
}

ACTOR MPAmmo : Ammo
{
	+IGNORESKILL
	Inventory.MaxAmount 32
	Inventory.Icon "MP40A0"
}
