// See ZScript for CoinItem class - Talon1024

ACTOR SingleCoin : CoinItem
{
	//$Category Pickups (Wolf3D)
	//$Title Treasure (Coin, 1pts)
	//$Color 17
	Inventory.Amount 1
	Inventory.PickupMessage "$COIN"
	Inventory.PickupSound "treasure/pickup"
	States
	{
	Spawn:
		ONEC A -1 Light(COINLITE)
		Stop
	}
}

ACTOR BagOfCoins : SingleCoin
{
	//$Title Treasure (Coin Bag, 10pts)
	Inventory.PickupMessage "$COINSBAG"
	Inventory.Amount 10
	States
	{
	Spawn:
		BAGC A -1 Light(BAGCLITE)
		Stop
	}
}

ACTOR TreasureChest : SingleCoin
{
	//$Title Treasure (Chest, 50pts)
	Scale 0.50
	Inventory.PickupMessage "$CHEST"
	Inventory.Amount 50
	States
	{
	Spawn:
		TREA B -1 Light(TRESLITE)
		Stop
	}
}

ACTOR TreasureChest2 : SingleCoin
{
	//$Title Treasure (Large Chest, 100pts)
	Inventory.PickupMessage "$CHESTBIG"
	Inventory.Amount 100
	States
	{
	Spawn:
		TREA C -1 Light(BIGCLITE)
		Stop
	}
}

ACTOR TreasureChest3 : SingleCoin
{
	//$Title Treasure (Chest, 25pts)
	Inventory.PickupMessage "$CHESTSML"
	Inventory.Amount 25
	States
	{
	Spawn:
		TREA E -1 Light(TRESLITE)
		Stop
	}
}

ACTOR TreasureCross : SingleCoin
{
	//$Title Treasure (Cross, 15pts)
	Scale 0.50
	Inventory.PickupMessage "$CROSS"
	Inventory.Amount 15
	States
	{
	Spawn:
		TREA F -1 Light(COINLITE)
		Stop
	}
}

ACTOR Goblet : TreasureCross
{
	//$Title Treasure (Goblet, 15pts)
	Scale 0.50
	Inventory.PickupMessage "$CHALICE"
	States
	{
	Spawn:
		TREA G -1 Light(TRESLITE)
		Stop
	}
}

ACTOR TreasureCrown : SingleCoin
{
	//$Title Treasure (Crown, 20pts)
	Scale 0.50
	Inventory.PickupMessage "$CROWN"
	Inventory.Amount 20
	States
	{
	Spawn:
		TREA D -1 Light(BAGCLITE)
		Stop
	}
}

ACTOR GoldBar : TreasureCrown
{
	//$Title Treasure (Gold Bar, 20pts)
	Inventory.PickupMessage "$GOLDBAR"
	Scale 1.0
	States
	{
	Spawn:
		TREA A -1 Light(TRESLITE)
		Stop
	}
}

ACTOR Naziward : BagOfCoins
{
	//$Title Treasure (Easteregg, Naziward, 10pts)
	Inventory.PickupMessage "$GOLDWRD"
	Scale 0.2
	States
	{
	Spawn:
		NZWR A -1 Light(TRESLITE)
		Stop
	}
}

/////////////Treasure Box //////////
ACTOR ChestKey : Inventory
{
	//$Category Pickups (Wolf3D)
	//$Title Treasure Chest Key
	//$Color 17
	Inventory.Icon "I_SPBKEY"
	Inventory.PickupMessage "$CHESTKEY"
	Inventory.PickupSound "misc/k_pkup"
	Inventory.UseSound "supplychest/open"
	Inventory.MaxAmount 3
	+INVENTORY.INVBAR
	Tag "Chest key"
	States
	{
	Spawn:
		SBKY A -1
		Stop
	}
}

// Numbered script that calls named "SupplyChest" script.
const int SUPPLY_CHEST_PROXY_SCRIPT = 4000;

ACTOR SupplyChest
{
	//$Category Pickups (Wolf3D)
	//$Title Treasure Chest
	//$Color 17
	//$Arg0 "Prize"
	//$Arg0Tooltip "Predefined chest prize:\n  1 = 100 Coins\n  2 = 75 Coins\n  3 = 25 Coins\n  4 = Kar98k\n  5 = 2 Field Kits\n  6 = Grenades"
	//$Arg1 "Script"
	//$Arg1Tooltip "Optional script number to run when chest is unlocked. 0 means no script."
	//$Arg2 "Objective"
	//$Arg2Tooltip "Optional index of objective to clear when chest is unlocked. A value less than 0 means no objective."
	//$Arg2Default -1
	Height 34
	Radius 21
	// Make the chest the activator when its special starts.
	Activation (THINGSPEC_ThingActs | THINGSPEC_ThingTargets | THINGSPEC_NoDeathSpecial)
	+SOLID
	+USESPECIAL
	States
	{
	Spawn:
		// Automatically set our special to call the SupplyChest script.
		SUBX A 0 NoDelay A_SetSpecial(84, SUPPLY_CHEST_PROXY_SCRIPT, args[0], args[1], args[2])
		SUBX A -1
		Stop
	}
}

ACTOR EmptySupplyChest
{
	States
	{
	Spawn:
		// If this chest has a master (ie. it was spawned by a reward chest),
		// it jumps to a state where it remains invisible until the reward
		// chest that spawned it gets picked up.
		SUBX C 0 NoDelay {
			if (!IsPointerEqual(AAPTR_MASTER, AAPTR_NULL))
			{
				return state("WaitForMaster");
			}
			return state("");
		}
		SUBX C -1
		Stop
	WaitForMaster:
		// Stay invisible until the chest that spawned us is picked up.
		TNT1 A 1
		TNT1 A 0 {
			if (IsPointerEqual(AAPTR_MASTER, AAPTR_NULL))
			{
				return state("Spawn");
			}
			return state("");
		}
		Loop
	}
}

ACTOR SupplyChestGeneric : CustomInventory
{
	Height 34
	Radius 21
	Inventory.PickupSound "treasure/pickup"
	+INVENTORY.ALWAYSPICKUP
	States
	{
	Spawn:
		// Spawn an invisible, empty chest with the same TID as this chest.
		// This reimplements the behaviour of Inventory.PickupFlash but allows
		// the spawned actor to have a TID so it can be referenced in scripts.
		SUBX B 0 NoDelay A_SpawnItemEx(	"EmptySupplyChest", // type
						0, 0, 0,            // offset
						0, 0, 0,            // velocity
						0,                  // angle
						SXF_SETMASTER | SXF_NOCHECKPOSITION,
						0,                  // chance (always)
						tid                 // TID
				)
		SUBX B -1
		Stop
	}
}

ACTOR SupplyChest100Coins : SupplyChestGeneric
{
	Inventory.PickupMessage "$100COINS"
	States
	{
	Pickup:
		SUBX B 0 A_GiveInventory("CoinItem", 100)
	}
}

ACTOR SupplyChest75Coins : SupplyChestGeneric
{
	Inventory.PickupMessage "$75COINS"
	States
	{
	Pickup:
		SUBX B 0 A_GiveInventory("CoinItem", 75)
	}
}

ACTOR SupplyChest25Coins : SupplyChestGeneric
{
	Inventory.PickupMessage "$25COINS"
	States
	{
	Pickup:
		SUBX B 0 A_GiveInventory("CoinItem", 25)
	}
}

ACTOR SupplyChestKar98k : SupplyChestGeneric
{
	Inventory.PickupMessage "$KARCHEST"
	States
	{
	Pickup:
		TNT1 A 0 A_GiveInventory("Kar98k",1)
		Stop
	}
}

ACTOR SupplyChestFieldKits : SupplyChestGeneric
{
	Inventory.PickupMessage "$FKITCHEST"
	States
	{
	Pickup:
		TNT1 A 0 A_GiveInventory("FieldKit",2)
		Stop
	}
}

ACTOR SupplyChestGrenades : SupplyChestGeneric
{
	Inventory.PickupMessage "$GRECHEST"
	States
	{
	Pickup:
		TNT1 A 0 A_GiveInventory("GrenadePickup",3)
		Stop
	}
}
