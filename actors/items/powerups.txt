ACTOR NazisBackpack : BackpackItem
{
	//$Category Items (Wolf3D)
	//$Title Backpack (Backpack)
	//$Color 6
	Height 26
	Scale 0.50
	Inventory.PickupMessage "$SSPACK"
	States
	{
	Spawn:
		BPK2 A -1
		Stop
	}
}

ACTOR NaziBerserk : CustomInventory
{
	//$Category Powerups (Wolf3D)
	//$Title Totale Macht (Berserk)
	//$Color 6
	//$Sprite PSTRA0
	Scale 0.50
	+COUNTITEM
	+INVENTORY.INVBAR
	Inventory.Icon "I_BSRK"
	Inventory.MaxAmount 1
	Inventory.PickupSound "misc/p_pkup"
	Inventory.PickupMessage "$SSBERSK"
	Tag "$TAGBERSP"
	States
	{
	Spawn:
		PSTR A -1
		Loop
	Pickup:
		Stop
	Use:
		TNT1 A 0 {
			A_GiveInventory("PowerStrength");
			HealThing(100);
			A_SelectWeapon("KnifeSilent");
			A_GiveInventory("BerserkToken", 1);
			ACS_NamedExecuteAlways("TotaleMacht", 0);
		}
		Stop
	}
}

ACTOR BerserkToken : Inventory {Inventory.MaxAmount 1}

ACTOR ScubaGearGiver : RadSuit
{
	//$Category Powerups (Wolf3D)
	//$Title Scuba Gear ("Underwater" protection)
	//$Color 6
	Height 32
	Inventory.PickupMessage "$SCUBA2"
	Powerup.Duration -60
	Powerup.Color 0,0,150,.125
	DamageFactor "MutantPoison", 0.0
	States
	{
	Spawn:
		SCUB A -1
		Stop
	}
}

ACTOR SpaceSuit : PowerupGiver
{
	//$Category Powerups (Wolf3D)
	//$Title Space Suit ("IceWater" protection)
	//$Color 6
	Inventory.MaxAmount 0
	Inventory.PickupMessage "$SCUBA1"
	Inventory.RespawnTics 1050 //30 seconds
	Inventory.UseSound "pickups/suite"
	Powerup.Color 100,100,100,.20
	Powerup.Duration -150
	Powerup.Type "SpaceSuit"
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.ALWAYSRESPAWN
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.FANCYPICKUPSOUND
	States
	{
	Spawn:
		SSUT A -1
		Stop
	}
}

ACTOR PowerSpaceSuit : PowerProtection {DamageFactor "IceWater", 0.0}

ACTOR C4 : Inventory
{
	//$Category Powerups (Wolf3D)
	//$Title C4 Explosives (pickups)
	//$Color 6
	Inventory.PickupMessage "$C4PACK"
	Scale 0.5
	Inventory.Icon "I_C4"
	Inventory.MaxAmount 10
	+INVENTORY.INVBAR
	Tag "C4"
	States
	{
	Spawn:
		C4__ A -1
		Stop
	}
}

ACTOR MineSweeperActive : Inventory { Inventory.MaxAmount 1 }
ACTOR Power : Inventory
{
	Inventory.Amount 1
	Inventory.MaxAmount 2400 //Near 1 mins maximum - ozy81
}

ACTOR MineSweeper : CustomInventory
{
	//$Category Powerups (Wolf3D)
	//$Title Mine Scanning Powerup (pickups)
	//$Color 6
	Scale 0.5
	Tag "$TAGSWEEP"
	Inventory.Icon MSPUB0
	Inventory.PickupMessage "$SWEEPER"
	Inventory.MaxAmount 1
	+INVENTORY.INVBAR
	+INVENTORY.UNDROPPABLE
	States
	{
	Spawn:
		MSPU A -1
		Stop
	Pickup:
		"####" A 0 {
			if (CountInv("MineSweeper") == 0) {
				A_GiveInventory ("Power", 2400);
				return true;
			} else if (CountInv("MineSweeper") > 0 && CountInv("Power") < 2400) {
				ACS_NamedExecuteAlways("BoA_MineSweeperOff", 0);
				A_GiveInventory ("Power", 2400);
				A_TakeInventory ("MineSweeper", 1); // The player will immediately get another minesweeper.
				return true;
			} else {
				return false;
			}
		}
		Stop
	Use:
		"####" A 0 ACS_NamedExecuteAlways("BoA_MineSweeper",0)
		Fail
	}
}

ACTOR AdrenalineSpeed : PowerupGiver { +INVENTORY.AUTOACTIVATE +INVENTORY.ADDITIVETIME -INVENTORY.INVBAR Inventory.MaxAmount 0 Powerup.Type "PowerDrugs" }
ACTOR PowerDrugs : PowerDoubleFiringSpeed {Powerup.Duration -10}
ACTOR AdrenalineFactor : PowerupGiver { +INVENTORY.AUTOACTIVATE +INVENTORY.ADDITIVETIME -INVENTORY.INVBAR Inventory.MaxAmount 0 Powerup.Type "PowerFactor" }
ACTOR PowerFactor : PowerDamage {Powerup.Duration -10 DamageFactor "Normal", 2.0}

ACTOR AdrenalineKit : CustomInventory
{
	//$Category Powerups (Wolf3D)
	//$Title Adrenaline Kit (DamageFactor & FiringSpeed augmented)
	//$Color 6
	Tag "$TAGADREN"
	Inventory.Icon "ADRNA0"
	Inventory.MaxAmount 3
	Inventory.PickupMessage "$ADREN"
	Inventory.UseSound "misc/p_pkup"
	+COUNTITEM
	+INVENTORY.INVBAR
	States
	{
	Spawn:
		ADRN A -1
		Stop
	Use:
		"####" A 0 {A_GiveInventory("AdrenalineSpeed"); A_GiveInventory("AdrenalineFactor");}
		Stop
	}
}

ACTOR DeployableMine : CustomInventory
{
	//$Category Powerups (Wolf3D)
	//$Title Deployable Mine
	//$Color 6
	Tag "$TAGMINES"
	Inventory.Icon "I_BOAM"
	Inventory.MaxAmount 5
	Inventory.PickupMessage "$DEPMINE"
	+COUNTITEM
	+INVENTORY.INVBAR
	States
	{
	Spawn:
		BOAM A -1
		Stop
	Use:
		"####" A 0 A_SpawnItemEx("PlacedMine",radius+32,0,0)
		Stop
	}
}