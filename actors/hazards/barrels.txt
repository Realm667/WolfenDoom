ACTOR BarrelFrags
{
	Scale .75
	States
	{
	Spawn:
		FRAG ABCDEFGHIJKLM 3
		"####" N 1 
		DeathWait:
		"####" "#" 1 A_SetTics(35*CallACS("DebrisLifetime"))
		"####" "#" 1 A_FadeOut(0.1)
		Wait
	}
}

ACTOR MetalFrags : BarrelFrags {Scale .55 Translation "13:15=3:3","64:79=80:111","236:239=108:111"}
ACTOR GrassFrags : BarrelFrags {Scale .45 Translation "13:15=125:127","64:79=121:125","236:239=124:127"}
ACTOR GrassFrags_Dry : BarrelFrags {Scale .45 Translation "13:15=237:239","64:79=148:151"}
ACTOR GrassFrags_Snowy : BarrelFrags {Gravity 0.125 Scale .45 Translation "13:15=93:95","64:79=80:95","236:239=148:151"}

ACTOR TNTBarrel1 : ExplosiveBarrel
{
	//$Category Hazards (Wolf3D)
	//$Title TNT Barrel (disguise)
	//$Color 3
	Height 34
	DeathSound "world/barrelboom"
	States
	{
	Spawn:
		BARL A -1
		Stop
	Death:
		BARX A 2 BRIGHT A_SpawnItemEx("BarrelFrags")
		"####" A 0 A_SpawnItemEx("GeneralExplosion_Medium")
		"####" B 2 BRIGHT A_Scream
		"####" CDEFG 2 BRIGHT
		"####" H 2 BRIGHT A_Explode
		"####" IJKLM 2 BRIGHT
		"####" M 1050 BRIGHT A_BarrelDestroy
		"####" M 5 A_Respawn
		Wait
	}
}

ACTOR TNTBarrel2 : TNTBarrel1
{
	//$Title TNT Box
	States
	{
	Spawn:
		BARL B -1
		Stop
	}
}

ACTOR TNTBarrel3 : TNTBarrel1
{
	//$Title TNT Barrel
	States
	{
	Spawn:
		BARL C -1
		Stop
	}
}

ACTOR GoodieBarrel1 : TNTBarrel1
{
	//$Title Goodie Barrel (disguise)
	DeathSound "WOODBRK"
	DropItem "12GaugeAmmo" 64
	DropItem "9mmAmmo" 64
	DropItem "Bandages" 48
	DropItem "Dogfood" 64
	DropItem "GrenadePickup" 16
	DropItem "MauserAmmo" 64
	DropItem "Meal" 32 1
	Dropitem "Medikit_Small" 16
	States
	{
	Death:
		TNT1 A 0 A_Scream
		BARL A 2 A_SpawnItemEx("BarrelFrags")
		TNT1 A 0 A_SpawnItemEx("PowerPlantSmokePuff", random(-3,3), random(-3,3), 0, 0, 0, 3)
		TNT1 A 0 A_NoBlocking
		Stop
	}
}