//Straight from wiki - DAMAGE_THING special: (https://zdoom.org/wiki/DamageThing)
//"If amount is 0, then the thing is guaranteed to be killed regardless of any invulnerability (including God and Buddha)"
//So no 200 but 0 as Arg0 - Ozy81
ACTOR Electricity1 : SwitchableDecoration
{
	//$Category Hazards (Wolf3D)
	//$Title Electricity (large)
	//$Color 3
	//$Sprite ELECA0

	//$Arg0 "Harmless"
	//$Arg0Tooltip "Values greater than 0 make this actor harmless"

	Radius 16
	Height 128
	+BUMPSPECIAL
	+RANDOMIZE
	DamageType IceWater
	Obituary "$OBFIZZLE"
	Activation THINGSPEC_MonsterTrigger
	States
	{
	Spawn:
	Active:
		ELEC A 0 A_JumpIf(args[0] > 0, 2)
		"####" A 0 Thing_SetSpecial(0,73,0,22,0)
		"####" A 0 A_SetSolid
	ActiveAnim:
		ELEC A 0 A_Jump(128, 2)
		"####" ABCD 3
		Loop
	Inactive:
		TNT1 A 0 Thing_SetSpecial(0,0,0,0,0)
		"####" A 0 A_UnSetSolid
	OffAnim:
		"####" A 2
		Loop
	}
}

ACTOR Electricity2 : Electricity1
{
	//$Title Electricity (small)
	//$Sprite ELECE0
	Height 64
	States
	{
	ActiveAnim:
		ELEC EFGH 3
		Loop
	}
}

ACTOR Electricity3 : Electricity1
{
	//$Title Electricity (large, horizontal)
	//$Sprite ELECI0
	Height 16
	Radius 64
	States
	{
	ActiveAnim:
		ELEC IJKL 3
		Loop
	}
}

ACTOR Electricity4 : Electricity1
{
	//$Title Electricity (thin, pitch/roll)
	//$Sprite LABZF0
	Height 2
	Radius 56
	+FLATSPRITE
	+NOGRAVITY
	+ROLLSPRITE
	Renderstyle Add
	Alpha 1.0
	Scale 0.2
	States
	{
	ActiveAnim:
		LABZ F 0 A_Jump(256,1,5,9,13,17)
		"####" GHIJKLMNOJNGIHMKOL 2
		Loop
	}
}

ACTOR ElectricOrb1 : Electricity1
{
	//$Title Electric Orb (big)
	//$Sprite LABZA0
	Radius 16
	Height 16
	+NOGRAVITY
	States
	{
	ActiveAnim:
		LABZ BCDE 3
		Loop
	}
}

ACTOR ElectricOrb2 : Electricity1
{
	//$Title Electric Orb (small)
	//$Sprite LABZP0
	Radius 16
	Height 16
	+NOGRAVITY
	States
	{
	ActiveAnim:
		LABZ PQRST 3
		Loop
	}
}