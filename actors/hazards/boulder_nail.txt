ACTOR RollingBoulderGrey
{
	Radius 24
	Height 48
	Speed 10
	Damage 250
	Scale 0.5
	Gravity 1.5
	BounceFactor 0.7
	BounceType Doom
	Obituary "$BOULDER"
	+BOUNCEONFLOORS
	+DONTRIP
	+DROPOFF
	+MISSILE
	+NOBOUNCESOUND
	+NODAMAGETHRUST
	+NOEXTREMEDEATH
	+RIPPER
	+SOLID
	DeathSound "boulder/stop"
	States
	{
	Spawn:
		GBOL A 2 NODELAY A_PlaySound("Boulder/Roll", CHAN_5, 1.0, TRUE)
		"####" BCDA 2 A_Quake(1,2,0,192,"")
		Loop
	Death:
		TNT1 A 1 A_SpawnItem("BoulderStoppedGrey")
		"####" A 0 A_StopSound(CHAN_5)
		"####" A 0 A_PlaySound("Boulder/Stop")
		Stop
	}
}

ACTOR RollingBoulderGreyHugger : RollingBoulderGrey
{
	Gravity 1
	BounceType None
	-BOUNCEONFLOORS
	-DONTRIP
	-NOBOUNCESOUND
	-SOLID
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	+FLOORHUGGER
	+NOBLOCKMAP
}

ACTOR RollingBoulderBrown : RollingBoulderGrey
{
	States
	{
	Spawn:
		BBOL A 2 NODELAY A_PlaySound("Boulder/Roll", CHAN_5, 1.0, TRUE)
		"####" BCDA 2 A_Quake(1,2,0,192,"")
		Loop
	Death:
		TNT1 A 1 A_SpawnItem("BoulderStoppedBrown")
		"####" A 0 A_StopSound(CHAN_5)
		"####" A 0 A_PlaySound("Boulder/Stop")
		Stop
	}
}

ACTOR RollingBoulderBrownHugger : RollingBoulderGreyHugger
{
	States
	{
	Spawn:
		BBOL A 2 NODELAY A_PlaySound("Boulder/Roll", CHAN_5, 1.0, TRUE)
		"####" BCDA 2 A_Quake(1,2,0,192,"")
		Loop
	}
}

ACTOR BoulderStoppedGrey
{
	Radius 24
	Height 48
	Scale 0.5
	+SOLID
	States
	{
	Spawn:
		GBOL A -1
		Stop
	}
}

ACTOR BoulderStoppedBrown : BoulderStoppedGrey
{
	States
	{
	Spawn:
		BBOL A -1
		Stop
	}
}

ACTOR BoulderStartGrey : SwitchableDecoration
{
	//$Category Hazards (Wolf3D)
	//$Title Boulder grey (bounce, inactive)
	//$Color 3
	Radius 24
	Height 48
	Scale 0.5
	Obituary "$BOULDER"
	+SOLID
	States
	{
	Spawn:
	Inactive:
		GBOL A -1
		Stop
	Active:
		TNT1 A 1 A_SpawnItemEx("RollingBoulderGrey",0,0,0,10)
		Stop
	}
}

ACTOR BoulderStartBrown : BoulderStartGrey
{
	//$Title Boulder brown (bounce, inactive)
	States
	{
	Spawn:
	Inactive:
		BBOL A -1
		Stop
	Active:
		TNT1 A 1 A_SpawnItemEx("RollingBoulderBrown",0,0,0,10)
		Stop
	}
}

ACTOR BoulderStartBrownHugger : BoulderStartGrey
{
	//$Title Boulder brown (hugger, inactive)
	States
	{
	Spawn:
	Inactive:
		BBOL A -1
		Stop
	Active:
		TNT1 A 1 A_SpawnItemEx("RollingBoulderBrownHugger",0,0,0,10)
		Stop
	}
}

//////////////////
// Nails in Air //
//////////////////
ACTOR Nail
{
	Projectile
	Radius 4
	Height 8
	Speed 44
	damage 4
	Scale 0.5
	+BLOODSPLATTER
	SeeSound "NailFlight"
	States
	{
	Spawn:
		DART A 1 A_SpawnItemEx("NailBlur")
		Loop
	Crash:
	Death:
		"####" B 0 A_PlaySound("NailHit")
		"####" BCDEFG 2
		Stop
	XDeath:
		"####" B 0 A_PlaySound("NailHitBleed")
		"####" BCDEFG 2
		Stop
	}
}

ACTOR NailBlur
{
	Height 8
	Radius 1
	Scale 0.5
	Speed 0.2
	RenderStyle "Translucent"
	+NOGRAVITY
	+DROPOFF
	States
	{
	Spawn:
		DART A 1 A_FadeOut(0.2)
		Loop
	}
}

ACTOR NailFlame : Nail
{
	States
	{
	Spawn:
		DARF A 1 BRIGHT Light("Chandelier4") A_SpawnItemEx("NailBlur")
		Loop
	Crash:
	Death:
		DART B 0 A_PlaySound("NailHit")
		"####" BCDEFG 2
		Stop
	XDeath:
		"####" B 0 A_PlaySound("NailHitBleed")
		"####" BCDEFG 2
		Stop
	}
}

//External Actors
ACTOR ScorpionNail : Nail
{
	Speed 48
	Scale 0.4
	SeeSound "scorpion/sting1"
	DeathSound "scorpion/sting2"
}

ACTOR PowerSlow : PowerSpeed {Powerup.Color "LightCyan2" Powerup.Duration -3 Speed 0.33}
ACTOR SlowFreeze : PowerupGiver {+INVENTORY.AUTOACTIVATE +INVENTORY.ADDITIVETIME +INVENTORY.NOSCREENBLINK -INVENTORY.INVBAR Powerup.Type "Slow"}

ACTOR IceDart : FastProjectile
{
	Radius 8
	Height 8
	Speed 16
	Scale 1.1
	Projectile
	Damage (random(5,8))
	DamageType "IceWater"
	SeeSound "IceFlight"
	Renderstyle "Add"
	Alpha 0.9
	+BLOODSPLATTER
	+BRIGHT
	+FORCERADIUSDMG
	Decal "PlasmaScorchLower"
	States
	{
	Spawn:
	See:
		ICED A 1 Light("ICEBALL") A_SpawnItemEx("IceTrail")
		Loop
	XDeath:
		"####" B 0 {A_PlaySound("IceHit"); A_RadiusGive("SlowFreeze", 48, RGF_PLAYERS | RGF_CUBE, 1);}
	Death:
		ICEX AAAAAAAA 0 A_SpawnItemEx("Smoke1A", 0, 0, 0, random(10, 30)*0.1, 0, random(0, 10)*0.1, random(0,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
		"####" AAAA 0 A_SpawnItemEx("Smoke2A", 0, 0, 0, random(10, 30)*0.04, 0, random(0, 10)*0.04, random(0,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
		"####" ABCDEFGHIJKLMA 1 {A_SetRenderStyle(0.5,STYLE_Add); A_SetScale(0.5); A_FadeOut(0.1);}
		Stop
	}
}

ACTOR IceTrail
{
	Radius 8
	Height 8
	Renderstyle Add
	Alpha 0.5
	+BRIGHT
	+NOINTERACTION
	States
	{
	Spawn:
		ICET AABBCDEFG 1
		Stop
	}
}