//mxd. Stealth-related stuff. Must be loaded after nazis.txt. Contains frightening ammount of hacks...
ACTOR SneakableSSMP40Guard : SSMP40Guard
{
	//$Category Monsters (Wolf3D)/Sneak-friendly
	//$Title SS Guard (MP40, sneak-friendly)
	//$Color 4
	
	PainChance 256							// Otherwise our Pain states won't always work...
	DropItem ""
	var int user_chasegoal;			// Current PatrolTarget TID is stored here when the guard becomes alerted
	var int user_stealthkilled;	// Hackity hacks...
	var int user_attacked;
	+FRIENDLY										// The guy is friendly. Looks like walking the patrol without immediately aquiring the player as a targer won't work otherwise...
	+NEVERTARGET								// Prevents +FRIENDLY guards from attacking -FRIENDLY guards
	+NOINFIGHTING								// Prevents guards from attacking other guards with same FRIENDLY flag settings
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_SpawnItemEx("SneakableGuardEyesIdle", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER | SXF_NOCHECKPOSITION)
			Goto Super::Spawn
		Missile:
			"####" "#" 0 A_AlertMonsters(512) // Something fishy is definitely going on!
			Goto Super::Missile
		Pain: 
			"####" "#" 0 A_CheckFlag("FRIENDLY", "BecomeAlerted") // Stop being so friendly when damaged (if not already alerted)
			Goto Super::Pain
		Pain.SilentKnifeAttack: // Done using PainType "SilentKnifeAttack" on KnifePuff because I need this to REPLACE Pain state, not be called AFTER it
			"####" "#" 0 A_CheckFlag("FRIENDLY", "StealthKill") // Only unalerted enemies can be stealth-killed
			Goto Super::Pain
		StealthKill:
			"####" "#" 0 A_Log("Stealth kill!")
			"####" "#" 0 A_RemoveChildren(true, RMVF_EVERYTHING) // Despawn children
			"####" "#" 1 A_AlertMonsters(128) 
			"####" "#" 0 ACS_NamedExecuteAlways("AddExtraVisibility", 0, 200, 1) //...And make the player super-visible
			"####" "#" 0 A_SetUserVar("user_stealthkilled", true)
			"####" "#" 0 A_Die
			Stop
		BecomeAlerted: // Become alerted after being attacked
			"####" "#" 0 A_SetUserVar("user_attacked", 1)
			"####" "#" 0 A_PlaySound("axis1/sighted", CHAN_BODY)
		Pain.BecomeAlerted: // Stop being friendly without visual effects when poked by PatrollingGuardEyes
			"####" "#" 0 A_SetUserVar("user_chasegoal", CallACS("GetChaseGoal", user_chasegoal)) // Store current chase goal, if any
			"####" "#" 0 A_ChangeFlag("FRIENDLY", false)
			"####" "#" 0 A_RemoveChildren(true, RMVF_EVERYTHING) // Despawn children
			"####" "#" 0 A_AlertMonsters(512) // Something fishy is going on. Alert everybody around!
			"####" "#" 0 A_SetHealth(health + 1) // Compensate the loss of health
			"####" "#" 0 A_SpawnItemEx("SneakableGuardEyesAlerted", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER | SXF_NOCHECKPOSITION)
			"####" "#" 0 A_JumpIf(user_attacked > 0, "Super::Pain")
			"####" "#" 0 A_Jump(256, "See") //So that inheriting actors go to their own See State
			Goto See
		Pain.BecomeIdle: // Become friendly again and resume patrolling, if possible
			"####" "#" 0 A_ChangeFlag("FRIENDLY", true)
			"####" "#" 0 A_SetUserVar("user_attacked", 0)
			"####" "#" 0 A_SetHealth(health + 1) // Compensate the loss of health
			"####" "#" 0 A_ClearTarget
			"####" "#" 0 ACS_NamedExecute("TryResumePatrolling") 
			Goto Spawn
		Death:
			"####" "#" 0 A_RemoveChildren(true, RMVF_EVERYTHING) // Despawn children
			"####" "#" 0 A_CheckFlag("FRIENDLY", "Super::Death") // Don't decrease AlertedCount when friendly
			"####" "#" 0 ACS_NamedExecute("DecreaseAlertedCount")
			Goto Super::Death
		XDeath: // Same as Death
			"####" "#" 0 A_RemoveChildren(true, RMVF_EVERYTHING)  // Despawn children
			"####" "#" 0 A_CheckFlag("FRIENDLY", "Super::XDeath") // Don't decrease AlertedCount when friendly
			"####" "#" 0 ACS_NamedExecute("DecreaseAlertedCount")
			Goto Super::XDeath
	}
}

ACTOR SneakableSSMP40GuardStatic : SneakableSSMP40Guard
{
	var int user_angle;

	//$Title SS Guard Static (MP40, sneak-friendly, doesn't move)
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_SetUserVar("user_angle", angle)
			Goto Super::Spawn
		See:
			"####" N 1 {
				A_SetAngle(user_angle);
				A_Chase("Missile", "Missile", CHF_DONTMOVE | CHF_NODIRECTIONTURN);
			}
			Loop
	}
}

// This actor follows SneakableGuard around looking for players
ACTOR SneakableGuardEyesIdle
{
	RenderStyle None
	Height 46
	
	+NEVERTARGET
	+NOCLIP
	+NOGRAVITY
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_SetArg(1, Random(490, 630)) // Set quote countdown time to 14 - 18 seconds
			Goto Look
		Look:
			TNT1 A 1 A_Warp(AAPTR_MASTER, 0, 0, Height) // Need a slight delay between A_Looks to prevent infinite loop
			TNT1 A 0 A_LookEx(0, 0.0, 160.0, 256.0, 160.0, "PlayerSeen") // Wider FOV sight check for short-range
			TNT1 A 0 A_LookEx(0, 0.0, 1024.0, 256.0, 65.0, "PlayerSeen")
			TNT1 A 0 A_CountdownArg(1, "PlayIdleSound")
			Loop
		PlayIdleSound:
			TNT1 A 0 A_PlaySound("axis1/idle", CHAN_BODY)
			Goto Spawn
		PlayerSeen:
			TNT1 A 0 A_JumpIf(CallACS("TargetIsHidden"), "IgnorePlayer")
			Goto AlertMaster
		IgnorePlayer:
			TNT1 A 1 A_ClearTarget
			Goto Look
		AlertMaster:
			TNT1 A 0 A_SpawnItemEx("AlertMarker", 0, 0, 64, 0, 0, 0, 0, SXF_TRANSFERPOINTERS)
			TNT1 A 6 A_PlaySound("axis1/sighted", CHAN_BODY)
			TNT1 A 0 A_DamageMaster(1, "BecomeAlerted") // Cause the master to enter Pain.BecomeAlerted state (HAAAACKS!!!!11)
			Stop
	}
}

// This actor follows SneakableGuard around checking if players are no longer seen
ACTOR SneakableGuardEyesAlerted : SneakableGuardEyesIdle
{
	+NEVERTARGET
	States
	{
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecute("IncreaseAlertedCount")
			TNT1 A 1 A_AlertMonsters(512, AMF_TARGETEMITTER) // Something fishy is going on. Alert everybody around!
			Goto ResetCounters
		
		ResetCounters: 
			TNT1 A 0 A_SetArg(0, Random(280, 350)) // Set countdown time to 8 - 10 seconds
			TNT1 A 0 A_SetArg(1, Random(140, 210))   // Set quote countdown time to 4 - 6 seconds 
			Goto Look
			
		Look:
			TNT1 A 1 A_Warp(AAPTR_MASTER, 0, 0, Height) // Need a slight delay between A_Looks to prevent infinite loop
			TNT1 A 0 A_LookEx(0, 0.0, 1024.0, 256.0, 65.0, "PlayerSeen")
			TNT1 A 0 A_CountdownArg(0, "ResetMaster") // Sets master state to Patrol when the countdown reaches zero
			TNT1 A 0 A_CountdownArg(1, "TryPlayLoosingTargetSound")
			Loop
			
		TryPlayLoosingTargetSound:
			TNT1 A 0 A_JumpIf(args[0] > 220, "Look") // Too early!
			TNT1 A 0 A_PlaySound("axis1/target_not_seen", CHAN_BODY)
			Goto ResetCounters + 1 // Reset quote countdown time only
			
		PlayerSeen: // Clear target and reset countdown
			TNT1 A 1 A_ClearTarget
			Goto ResetCounters
			
		ResetMaster:
			TNT1 A 0 A_DamageMaster(1, "BecomeIdle") // Cause the master to enter Pain.BecomeIdle state (HAAAACKS!!!!11)
			TNT1 A 0 ACS_NamedExecute("DecreaseAlertedCount")
			Stop
	}
}

// It's here just for lulz
ACTOR AlertMarker
{
	Scale 2.0
	
	+BRIGHT
	+NOGRAVITY

	States
	{
		Spawn:
			EXCL C 1 NoDelay A_PlaySound("misc/alert", CHAN_BODY)
			Goto Fade
		
		Fade:
			EXCL C 0 A_Warp(AAPTR_MASTER, 0, 0, 64)
			EXCL C 1 A_FadeOut(0.01)
			Loop
	}
}

ACTOR MGTurretStealth : Nazi
{
	//$Category Monsters (Wolf3D)/Sneak-friendly
	//$Title MGTurret (stealth/hidden/nokillcount)
	//$Color 4
	Obituary "$SSMPGURD"
	Health 100
	Speed 0
	Radius 2
	Height 32
	PainChance 255
	PainSound "metal/pain"
	Mass 0xFFFFFFF
	-COUNTKILL
	+ALLOWPAIN
	+INVULNERABLE
	+NEVERTARGET
	+NOBLOOD
	+NOGRAVITY
	RenderStyle "None"

	States
	{
		Spawn:
			GTUR A 0
			"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd
			Goto Look
		Look:
			"####" A 10 A_LookEx(0, 0.0, 1024.0, 256.0, 65.0, "See")
			Loop
		See:
			"####" A 1 A_JumpIf(CallACS("TargetIsHidden"), "IgnorePlayer")
			"####" A 0 A_Chase("", "Missile", CHF_DONTMOVE)
			Loop
		IgnorePlayer:
			"####" A 1 A_ClearTarget
			Goto Look
		Missile: //This 'turret' fires and reloads like an MP40 Guard 
			"####" A 5 {
				A_FaceTarget;
				A_PlaySound("nazi/mp40",CHAN_WEAPON);
			}
			"####" B 4 {
				A_CustomMissile("EnemySMGTracer",24,4,random(-8,8)); 
				A_GiveInventory("SSMP40GuardCounter", 1);
			}
			"####" A 0 A_JumpIfInventory("SSMP40GuardCounter", 40, "Reload")
			"####" C 2 A_MonsterRefire(10, "See")
			Loop
		Reload:
			"####" A 35 {
				A_PlaySoundEx("mp40/reload", "SoundSlot7", 0);
				A_TakeInventory("SSMP40GuardCounter", 4);
			}
			"####" A 0 A_JumpIfInventory("SSMP40GuardCounter", 1, "Reload")
			"####" A 10 A_PlaySoundEx("mp40/reload", "SoundSlot7", 0)
			Goto See
		Pain: //Disable for 2 seconds when shot, then go to reload
			"####" C 70
			Goto Reload
	}
}