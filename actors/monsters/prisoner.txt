ACTOR PrisonerAgent
{
	//$Category Monsters (Wolf3D)/NPCs
	//$Title Cpt. James Ryan (prison, bloody)
	//$Color 4
	Scale 1.2
	Mass 100
	Obituary "$PRISONER"
	Radius 6
	Height 52
	Speed 5
	MaxStepHeight 57 //This is equal to the maximum distance a Player can jump. Modify to allow no jumps and stuff.
	MaxDropOffHeight 16383
	MaxTargetRange 65536
	PainChance 255
	PainSound "Nazi1/Pain"
	DeathSound "Nazi1/Death"
	Monster
	-COUNTKILL
	+BOSSDEATH
	+DONTGIB
	+DONTHARMSPECIES
	+FIXMAPTHINGPOS
	+FLOORCLIP
	+JUMPDOWN
	+LOOKALLAROUND
	+NOBLOCKMONST
	+NODAMAGE
	+NOFEAR
	+NOTAUTOAIMED
	+NOTELEPORT
	+NOTELESTOMP
	+PUSHABLE
	+SLIDESONWALLS
	States
	{
	Spawn:
		TORT A 1 NODELAY A_LookEx(0,0,0,0,360)
		Loop
	See:
		TORT A 1 A_FaceTarget
		TNT1 A 0 A_ChangeFlag("NOPAIN",0)
		TNT1 A 0 A_ChangeFlag("NOCLIP",0)
		TNT1 A 0 A_JumpIfCloser(128,"Stand")
		TORT A 0 A_FaceTarget
		TORT BBB 1 A_Chase
		"####" B 0 A_SpawnItemEx("PlayerStep",0,0,14,0,0,0,0,SXF_NoCheckPosition)
		TNT1 A 0 A_Recoil(-1)
		TNT1 A 0 A_JumpIfCloser(128,"Stand")
		TNT1 A 0 A_CheckSight("Teleporting")
		TORT CCC 1 A_Chase
		TNT1 A 0 A_Recoil(-1)
		TNT1 A 0 A_JumpIfCloser(128,"Stand")
		TNT1 A 0 A_CheckSight("Teleporting")
		TORT DDD 1 A_Chase
		"####" D 0 A_SpawnItemEx("PlayerStep",0,0,14,0,0,0,0,SXF_NoCheckPosition)
		TNT1 A 0 A_Recoil(-1)
		TNT1 A 0 A_JumpIfCloser(128,"Stand")
		TNT1 A 0 A_CheckSight("Teleporting")
		TORT EEE 1 A_Chase
		TNT1 A 0 A_Recoil(-1)
		TNT1 A 0 A_JumpIfCloser(128,"Stand")
		TNT1 A 0 A_CheckSight("Teleporting")
		TORT FFF 1 A_Chase
		"####" F 0 A_SpawnItemEx("PlayerStep",0,0,14,0,0,0,0,SXF_NoCheckPosition)
		TNT1 A 0 A_Recoil(-1)
		TNT1 A 0 A_JumpIfCloser(128,"Stand")
		TNT1 A 0 A_CheckSight("Teleporting")
		Goto See
	Stand:
		TNT1 A 0 A_ClearTarget
		TNT1 A 0 A_Jump(1,"LookAround")
		TORT AA 1 A_LookEx(0,0,0,0,360,"See")
		Goto See
	LookAround:
		TNT1 A 0 A_SetAngle(angle+45)
		TORT EE 4 A_Look
		TNT1 A 0 A_SetAngle(angle+45)
		TORT EE 8 A_Look
		TNT1 A 0 A_SetAngle(angle-45)
		TORT EE 4 A_Look
		TNT1 A 0 A_SetAngle(angle-45)
		TORT EE 4 A_Look
		TORT EEEE 8 A_Look
		TORT E 1 A_LookEx(0,0,0,0,360,"See")
		Goto See
	Pain:
		TORT I 5 A_Pain
		Goto See
	Death:
		TORT J 10 A_NoBlocking
		TORT K 10 A_PlayerScream
		TNT1 A 0 A_Log("Helper down!")
		TNT1 A 0 A_PlaySound("misc/chat2",CHAN_AUTO,1.0,false,ATTN_NONE)
		TORT LM 10
		TORT N 9
		TFOG ABABABABABABABAB 32 BRIGHT //Wait for about 30 seconds.
		TFOG ABCABCABCABCABCA 32 BRIGHT //Every one of these lines is about 15 seconds, so add more or less at your leisure.
		TNT1 A 0 A_PlaySound("misc/chat2",CHAN_AUTO,1.0,false,ATTN_NONE)
		TNT1 A 1 A_Log("Respawning!")
	Teleporting:
		TNT1 A 0 A_ChangeFlag("NOPAIN",1)
		TNT1 A 0 A_ChangeFlag("NOCLIP",1)
		TNT1 A 0 A_Chase
		TNT1 A 1 A_JumpIfCloser(48,"See")
		Goto Teleporting+2
	Idle:
		Stop
	}
}

ACTOR PrisonerAgentTalk
{
	//$Category Conversation Stuff (Wolf3D)/NPCs
	//$Title Cpt. James Ryan (uniform, talking)
	//$Color 5
	Scale 1.2
	Mass 100
	Health 99999999
	Radius 6
	Height 52
	+SOLID
	States
	{
	Spawn:
		FREE A 1 NODELAY A_LookEx(0,0,0,0,360)
		Loop
	}
}

ACTOR PrisonerFree : DirtyDarren
{
	//$Category Monsters (Wolf3D)/NPCs
	//$Title Cpt. James Ryan (uniform, npc)
	//$Color 4
	Obituary "$MARINE"
	Scale 1.3 //mxd
	States
	{
	Spawn:
		FREE A 0
		Goto Look
	Death:
		"####" J 5
		"####" K 5 A_Scream
		"####" L 5 A_NoBlocking
		"####" MN 5
		"####" O -1
		stop
	Pain:
		"####" I 3
		"####" I 3 A_Pain
		"####" I 0 A_Jump(256,"See")
		Stop
	Raise:
		"####" ONMLKJ 5
		Goto See
	}
}

ACTOR HealingParticle
{
	+DONTSPLASH
	+FORCEXYBILLBOARD
	+MISSILE
	+NOBLOCKMAP
	+NOGRAVITY
	Radius 0
	Height 0
	RenderStyle "Add"
	Alpha 0.01
	Scale 0.4
	States
	{
	Spawn:
		HELX A 0 NODELAY A_SetScale(FRandom(0.2, 0.6))
		HELX AAAAAAAAAA 1 A_FadeIn(0.07)
		HELX AAAAAAAAAA 8 A_FadeOut(0.08)
	Death:
		HELX A 1 BRIGHT A_FadeOut(0.1)
		Loop
	}
}

Actor PrisonerMedikitCounter : Inventory {Inventory.MaxAmount 3}

ACTOR PrisonerEnemyBoss : DirtyDarren
{
	//$Category Monsters (Wolf3D)/NPCs
	//$Title Cpt. James Ryan (uniform, boss, hostile)
	//$Color 4
	Tag "Cpt. James Ryan"
	Obituary "$MARINE"
	Scale 1.3 //mxd
	Health 1200
	SeeSound "prisoner/sight"
	DeathSound "prisoner/death"
	PainSound "prisoner/pain"
	Painchance 40
	Mass 800
	Speed 5
	+AVOIDMELEE
	+BOSS
	+LOOKALLAROUND
	+MISSILEEVENMORE
	+MISSILEMORE
	States
	{
	Spawn:
		FREE A 0
		Goto Look
	Heal:
		"####" F 0 A_Playsound("misc/health_pkup")
		"####" F 0 A_GiveInventory("PrisonerMedikitCounter", 1)
		"####" F 0 A_GiveInventory("Health", 500)
		"####" FFFFFFFFFF 2 A_SpawnItemEx("HealingParticle", random(10,-10), random(10,-10), random(16,64), 0, 0, random(1, 2), 0)
	See:
		"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd		
	SeeLoop:
		"####" E 1 A_Chase("","",CHF_FASTCHASE)
		"####" EEE 1 A_Chase("","")
		"####" E 1 A_Chase
		"####" EEE 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" B 0 A_SpawnItemEx("EnemyStep",0,0,14,0,0,0,0,SXF_NoCheckPosition)
		"####" BBB 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" BBB 1 A_Chase("","")
		"####" C 1 A_Chase("","",CHF_FASTCHASE)
		"####" CCC 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" CCC 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" D 0 A_SpawnItemEx("EnemyStep",0,0,14,0,0,0,0,SXF_NoCheckPosition)
		"####" DDD 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" DDD 1 A_Chase("","")
		Loop
	Missile:
		"####" A 0 A_JumpIfInventory("PrisonerMedikitCounter", 3, 2)
		"####" A 0 A_JumpIfHealthLower(600, "Heal")
		"####" A 0 A_Jump(192, "MP40")
	Grenade:
		"####" F 16 A_FaceTarget
		"####" F 10 A_CustomMissile("HandGrenade",40,4,random(-8,8), CMF_OFFSETPITCH, random(0,16))	
		goto See
	MP40:
		"####" FG 5 A_FaceTarget
		"####" H 0 A_PlaySound("nazi/mp40",CHAN_WEAPON)
		"####" H 2 LIGHT(NAZIFIRE) A_CustomMissile("EnemySMGTracer",50,4,random(-8,8))
		"####" H 0 A_GiveInventory("MP40GuardCounter", 1)
		"####" H 0 A_SpawnItemEx("9MMCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" G 2 A_FaceTarget
		"####" H 0 A_PlaySound("nazi/mp40",CHAN_WEAPON)
		"####" H 2 LIGHT(NAZIFIRE) A_CustomMissile("EnemySMGTracer",50,4,random(-8,8))
		"####" H 0 A_GiveInventory("MP40GuardCounter", 1)
		"####" H 0 A_SpawnItemEx("9MMCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" G 2 A_FaceTarget
		"####" H 0 A_PlaySound("nazi/mp40",CHAN_WEAPON)
		"####" H 2 LIGHT(NAZIFIRE) A_CustomMissile("EnemySMGTracer",50,4,random(-8,8))
		"####" H 0 A_GiveInventory("MP40GuardCounter", 1)
		"####" H 0 A_SpawnItemEx("9MMCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" G 2 A_FaceTarget
		"####" H 0 A_PlaySound("nazi/mp40",CHAN_WEAPON)
		"####" H 2 LIGHT(NAZIFIRE) A_CustomMissile("EnemySMGTracer",50,4,random(-8,8))
		"####" H 0 A_GiveInventory("MP40GuardCounter", 1)
		"####" H 0 A_SpawnItemEx("9MMCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" G 2 A_FaceTarget
		"####" H 0 A_PlaySound("nazi/mp40",CHAN_WEAPON)
		"####" H 2 LIGHT(NAZIFIRE) A_CustomMissile("EnemySMGTracer",50,4,random(-8,8))
		"####" H 0 A_GiveInventory("MP40GuardCounter", 1)
		"####" G 2 A_SpawnItemEx("9MMCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" G 0 A_JumpIfInventory("MP40GuardCounter", 32, "Reload")
		Goto See
	Reload:
		"####" F 0 A_ChangeFlag("NoPain", 1)
		"####" F 30 A_PlaySoundEx("mp40/reload", "SoundSlot7", 0)
		"####" F 0 A_TakeInventory("MP40GuardCounter", 4)
		"####" F 0 A_SpawnItemEx("9MMCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" F 0 A_JumpIfInventory("MP40GuardCounter", 1, 1)
		Goto Reload+6
		"####" F 0
		Goto Reload+2
		"####" F 10 A_PlaySoundEx("mp40/reload", "SoundSlot7", 0)
		"####" F 0 A_ChangeFlag("NoPain", 0)
		"####" "#" 0 A_Jump(256, "See") //So that inheriting actors go to their own See State
	Death:
		"####" J 8
		"####" K 8 A_Scream
		"####" L 8 A_NoBlocking
		"####" MN 8
		"####" N -1
		stop
	Pain:
		"####" I 3
		"####" I 3 A_Pain
		"####" I 0 A_Jump(256,"See")
		Stop
	Raise:
		"####" NMLKJ 5
		Goto See
	}
}

ACTOR PrisonerCampMale : PrisonerAgent //this is a placeholder for now
{
	//$Category Monsters (Wolf3D)/NPCs
	//$Title Concentration Camp Prisoner
	//$Color 4
	//Obituary "$PRISONER" //needs a different obituary though
	Health 50
	Speed 1
	-BOSSDEATH
	-NOBLOCKMONST
	-NODAMAGE
	-NOFEAR
	+FRIENDLY
	+FRIGHTENED
	+JUMPDOWN
	+LOOKALLAROUND
	States
	{
	Death:
		TORT J 6 A_NoBlocking
		TORT K 6 A_PlayerScream
		TNT1 A 0 A_Log("Prisoner dead!")
		TNT1 A 0 A_PlaySound("misc/chat2",CHAN_AUTO,1.0,false,ATTN_NONE)
		TORT LM 3
		TORT N -1
		Stop
	Idle:
		Stop
	}
}