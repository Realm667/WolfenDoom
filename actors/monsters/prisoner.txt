ACTOR PrisonerAgentTalk
{
	//$Category Conversation Stuff (Wolf3D)/NPCs
	//$Title Cpt. James Ryan (uniform, talking)
	//$Color 5
	Scale 0.67
	Mass 100
	Health 99999999
	Radius 6
	Height 52
	+SOLID
	States
	{
	Spawn:
		FREE A 1 NODELAY A_LookEx(0,0,0,0,360)
		Loop
	}
}

ACTOR PrisonerFree : DirtyDarren
{
	//$Category Monsters (Wolf3D)/NPCs
	//$Title Cpt. James Ryan (uniform, npc)
	//$Color 4
	Obituary "$MARINE"
	States
	{
	Spawn:
		FREE A 0
		Goto Look
	Death:
		"####" J 5
		"####" K 5 A_Scream
		"####" L 5 A_NoBlocking
		"####" MN 5
		"####" O -1
		Stop
	Pain:
		"####" I 2
		"####" I 1 A_SpawnItemEx("Pain_Overlay", ScaleX+3, 0, Height-8, 0, 0, 0, 0, SXF_NOCHECKPOSITION | SXF_USEBLOODCOLOR)
		"####" I 3 A_Pain
		"####" I 0 A_Jump(256,"See")
		Stop
	Raise:
		"####" ONMLKJ 5
		Goto See
	CapturedDarren:
		FREE O random(105,175)
		"####" P 35
		Loop
	ShootCapturedDarren:
		FREE Q 2 A_PlaySound("nazi/pistol")
		"####" Q 0 A_SpawnItemEx("NashGore_Blood", 2.0, 9.0, 36.0) // Shooting Dirty Douglas
		"####" R 40 A_SpawnItemEx("DyingDarren", 2.0, 10.0, 0.0) // Dropping Dirty Douglas' dead body on the ground.
		"####" A 1
		Goto Spawn
	}
}

ACTOR DyingDarren : DirtyDarren
{
	States
	{
	Spawn:
		DARR N 0 NoDelay A_Die
	}
}

ACTOR HealingParticle
{
	+DONTSPLASH
	+FORCEXYBILLBOARD
	+MISSILE
	+NOBLOCKMAP
	+NOGRAVITY
	Radius 0
	Height 0
	RenderStyle "Add"
	Alpha 0.01
	Scale 0.4
	States
	{
	Spawn:
		HELX A 0 NODELAY A_SetScale(frandom(0.2, 0.6))
		"####" AAAAAAAAAA 1 A_FadeIn(0.07)
		"####" AAAAAAAAAA 8 A_FadeOut(0.08)
	Death:
		"####" A 1 BRIGHT A_FadeOut(0.1)
		Loop
	}
}

ACTOR PrisonerMedikitCounter : Inventory {Inventory.MaxAmount 3}

ACTOR PrisonerEnemyBoss : DirtyDarren
{
	//$Category Monsters (Wolf3D)/NPCs
	//$Title Cpt. James Ryan (uniform, boss, hostile)
	//$Color 4
	Tag "Cpt. James Ryan"
	Obituary "$MARINE"
	Health 1200
	SeeSound "prisoner/sight"
	DeathSound "prisoner/death"
	PainSound "prisoner/pain"
	Painchance 40
	Mass 800
	Speed 5
	+AVOIDMELEE
	+BOSS
	+LOOKALLAROUND
	+MISSILEEVENMORE
	+MISSILEMORE
	States
	{
	Spawn:
		FREE A 0
		Goto Look
	Heal:
		"####" F 0 A_Playsound("misc/health_pkup")
		"####" F 0 A_GiveInventory("PrisonerMedikitCounter", 1)
		"####" F 0 A_GiveInventory("Health", 500)
		"####" FFFFFFFFFF 2 A_SpawnItemEx("HealingParticle", random(10,-10), random(10,-10), random(16,64), 0, 0, random(1, 2), 0)
	See:
		"####" "#" 0 A_SetUserVar("user_incombat", TRUE) //mxd
	SeeLoop:
		"####" E 1 A_Chase("","",CHF_FASTCHASE)
		"####" EEE 1 A_Chase("","")
		"####" E 1 A_Chase
		"####" EEE 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" B 0 A_SpawnItemEx("EnemyStep",0,0,14,0,0,0,0,SXF_NOCHECKPOSITION)
		"####" BBB 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" BBB 1 A_Chase("","")
		"####" C 1 A_Chase("","",CHF_FASTCHASE)
		"####" CCC 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" CCC 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" D 0 A_SpawnItemEx("EnemyStep",0,0,14,0,0,0,0,SXF_NOCHECKPOSITION)
		"####" DDD 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" DDD 1 A_Chase("","")
		Loop
	Missile:
		"####" A 0 A_JumpIfInventory("PrisonerMedikitCounter", 3, 2)
		"####" A 0 A_JumpIfHealthLower(600, "Heal")
		"####" A 0 A_Jump(192, "MP40")
	Grenade:
		"####" F 16 A_FaceTarget
		"####" F 10 A_SpawnProjectile("HandGrenade",40,4,random(-8,8), CMF_OFFSETPITCH|CMF_BADPITCH, random(0,16))
		Goto See
	MP40:
		"####" FG 5 A_FaceTarget
		"####" H 0 A_PlaySound("nazi/mp40", CHAN_WEAPON)
		"####" H 2 LIGHT(NAZIFIRE) A_SpawnProjectile("EnemySMGTracer",50,4,random(-8,8))
		"####" H 0 A_GiveInventory("MP40GuardCounter", 1)
		"####" H 0 A_SpawnItemEx("9MMCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" G 2 A_FaceTarget
		"####" H 0 A_PlaySound("nazi/mp40", CHAN_WEAPON)
		"####" H 2 LIGHT(NAZIFIRE) A_SpawnProjectile("EnemySMGTracer",50,4,random(-8,8))
		"####" H 0 A_GiveInventory("MP40GuardCounter", 1)
		"####" H 0 A_SpawnItemEx("9MMCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" G 2 A_FaceTarget
		"####" H 0 A_PlaySound("nazi/mp40", CHAN_WEAPON)
		"####" H 2 LIGHT(NAZIFIRE) A_SpawnProjectile("EnemySMGTracer",50,4,random(-8,8))
		"####" H 0 A_GiveInventory("MP40GuardCounter", 1)
		"####" H 0 A_SpawnItemEx("9MMCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" G 2 A_FaceTarget
		"####" H 0 A_PlaySound("nazi/mp40", CHAN_WEAPON)
		"####" H 2 LIGHT(NAZIFIRE) A_SpawnProjectile("EnemySMGTracer",50,4,random(-8,8))
		"####" H 0 A_GiveInventory("MP40GuardCounter", 1)
		"####" H 0 A_SpawnItemEx("9MMCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" G 2 A_FaceTarget
		"####" H 0 A_PlaySound("nazi/mp40", CHAN_WEAPON)
		"####" H 2 LIGHT(NAZIFIRE) A_SpawnProjectile("EnemySMGTracer",50,4,random(-8,8))
		"####" H 0 A_GiveInventory("MP40GuardCounter", 1)
		"####" G 2 A_SpawnItemEx("9MMCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" G 0 A_JumpIfInventory("MP40GuardCounter", 32, "Reload")
		Goto See
	Reload:
		"####" F 0 A_ChangeFlag("NoPain", 1)
		"####" F 30 A_PlaySound("mp40/reload", CHAN_WEAPON)
		"####" F 0 A_TakeInventory("MP40GuardCounter", 4)
		"####" F 0 A_SpawnItemEx("9MMCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" F 0 A_JumpIfInventory("MP40GuardCounter", 1, 1)
		Goto Reload+6
		"####" F 0
		Goto Reload+2
		"####" F 10 A_PlaySound("mp40/reload", CHAN_WEAPON)
		"####" F 0 A_ChangeFlag("NoPain", 0)
		"####" "#" 0 A_Jump(256, "See") //So that inheriting actors go to their own See State
	Death:
		"####" J 8
		"####" K 8 A_Scream
		"####" L 8 A_NoBlocking
		"####" MN 8
		"####" N -1
		Stop
	Pain:
		"####" I 2
		"####" I 1 A_SpawnItemEx("Pain_Overlay", ScaleX+3, 0, Height-8, 0, 0, 0, 0, SXF_NOCHECKPOSITION | SXF_USEBLOODCOLOR)
		"####" I 3 A_Pain
		"####" I 0 A_Jump(256,"See")
		Stop
	Raise:
		"####" NMLKJ 5
		Goto See
	}
}

ACTOR PrisonerCampMale : PrisonerAgent //this is a placeholder for now - still not used on 30/04/17 - ozy81
{
	//$Category Concentration Camp (Wolf3D)
	//$Title Concentration Camp Prisoner
	//$Color 4
	//Obituary "$PRISONER" //needs a different obituary though
	Health 50
	Speed 1
	-BOSSDEATH
	-NOBLOCKMONST
	-NODAMAGE
	-NOFEAR
	+FRIENDLY
	+FRIGHTENED
	+JUMPDOWN
	+LOOKALLAROUND
	States
	{
	Death:
		TORT J 6 A_NoBlocking
		"####" K 6 A_PlayerScream
		TNT1 A 0 A_Log("Prisoner dead!")
		"####" A 0 A_PlaySound("misc/chat2", CHAN_AUTO, 1.0, FALSE, ATTN_NONE)
		TORT LM 3
		"####" N -1
		Stop
	Idle:
		Stop
	}
}