ACTOR Scorpion
{
	//$Category Monsters (Wolf3D)/Critters
	//$Title Scorpion (normal)
	//$Color 4
	Radius 10
	Height 12
	Scale 0.6
	Mass 20
	Speed 6
	Health 20
	MeleeRange 16
	MaxStepHeight 16
	MaxDropOffHeight 128
	Damage 1 //jumpattacks
	DeathSound "rat/death"
	Obituary "$SCORPSML"
	BloodType "ScorpionBlood"
	BloodColor "DarkOrange1"
	Monster
	-COUNTKILL
	+DONTMORPH
	+FLOORCLIP
	+LOOKALLAROUND
	+NOINFIGHTING
	+NOTRIGGER
	Species Nazi
	MaxTargetRange 112
	States
	{
	Spawn:
		SCRP A 2
	See:
		SCRP AAAA 1 A_Chase
		SCRP A 0 A_Jump(16,"Procrastinate")
		SCRP BBBB 1 A_Chase
		SCRP A 0 A_Jump(16,"Procrastinate")
		SCRP A 0 A_Jump(11,"TryJump")
		Loop
	Melee:
		TNT1 A 0 A_FaceTarget
		SCRP AB 6 A_CustomMeleeAttack(2*random(1,8),"scorpion/attacks","scorpion/attacks")
		Goto See
	Missile:
		SCRP AB 4 A_FaceTarget
		SCRP A 8 A_SkullAttack
		SCRP B 5 A_Gravity
		Goto See
	TryJump:
		TNT1 A 0 A_CheckFloor("Jump") //don't jump if mid-air
		Goto See
	Jump:
		TNT1 A 0 ThrustThing(angle*256/360,Random(4,6),0,0)
		SCRP C 15 ThrustThingZ(0,Random(30,40),0,1)
		Goto See
	Procrastinate:
		SCRP "#" 35
		SCRP "#" 0 A_Jump(128,"Procrastinate")
		Goto See
	Death:
		TNT1 A 0 A_ScreamAndUnblock
		TNT1 AAAAAAAA 0 A_SpawnItemEx("ScorpionChunk",0,0,4,Random(-2,2),Random(-2,2),Random(5,10),Random(0,256),0,100)
		Stop
	}
}

ACTOR ScorpionBig
{
	//$Category Monsters (Wolf3D)/Critters
	//$Title Scorpion (big)
	//$Color 4
	Radius 32
	Height 48
	Scale 1.0
	Mass 100
	Speed 4
	Health 200
	MeleeRange 32
	MaxStepHeight 32
	MaxDropOffHeight 256
	PainChance 235
	Damage 2 //jumpattacks
	ActiveSound "scorpion/see"
	SeeSound "scorpion/see"
	PainSound "scorpion/pains"
	DeathSound "scorpion/death"
	Obituary "$SCORPBIG"
	Species Nazi
	Monster
	-COUNTKILL
	+DONTMORPH
	+FLOORCLIP
	+LOOKALLAROUND
	+NOINFIGHTING
	+NOTRIGGER
	States
	{
	Spawn:
		SCR2 A 2
	See:
		SCR2 A 1 A_Chase
		SCR2 AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		SCR2 A 1 A_Chase
		SCR2 AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		SCR2 B 1 A_Chase
		SCR2 BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		SCR2 B 1 A_Chase
		SCR2 BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		SCR2 A 0 A_Jump(11,"TryJump")
		Loop
	Melee:
		TNT1 A 0 A_FaceTarget
		SCR2 AB 6 A_CustomMeleeAttack(3*random(1,8),"scorpion/attacks","scorpion/attacks")
		Goto See
	Missile:
		SCR2 A 0 A_Jump(256,"Spit","Sting","Reach")
	Spit:
		SCR2 AB 4 A_FaceTarget
		SCR2 B 8 A_CustomMissile("ScorpionSpit",8,0,0,2,3)
		Goto See
	Sting:
		SCR2 AB 4 A_FaceTarget
		SCR2 BBBB 4 A_CustomMissile("ScorpionNail",24,0,0)
		Goto See
	Reach:
		SCR2 A 0 A_Jumpifcloser(224,2)
		Goto See
		SCR2 AB 6 A_FaceTarget
		SCR2 A 6 A_SkullAttack
		SCR2 B 6 A_Gravity
		Goto See
	TryJump:
		TNT1 A 0 A_CheckFloor("Jump") //don't jump if mid-air
		Goto See
	Jump:
		TNT1 A 0 ThrustThing(angle*256/360,Random(4,6),0,0)
		TNT1 A 0 ThrustThingZ(0,Random(30,40),0,1)
		SCR2 C 15 A_PlaySound("scorpion/fall",CHAN_AUTO,1.5)
		Goto See
	Pain:
		SCR2 A 2 A_Pain
		Goto See
	Death:
		TNT1 A 0 A_ScreamAndUnblock
		TNT1 AAAAAAAA 0 A_SpawnItemEx("BigScorpionChunk",0,0,4,Random(-2,2),Random(-2,2),Random(5,10),Random(0,256),0,100)
		Stop
	}
}

ACTOR ScorpionSpit
{
	Scale 0.5
	Radius 13
	Height 8
	Speed 28
	VSpeed 20
	Damage 6
	Projectile
	-NOGRAVITY
	Gravity 0.4
	DamageType PoisonCloud
	PoisonDamage 20
	SeeSound "scorpion/spit"
	States
	{
	Spawn:
		SPIT A 0 NoDelay A_SetScale(ScaleX + frandom(-0.05,0.05),ScaleY + frandom(-0.05,0.05))
		Goto See
	See:
		SPIT A 3 A_SpawnItemEx("NashGore_FlyingBlood",0,0,0,0,0,0,0,128)
		Loop
	}
}

ACTOR ScorpionChunk
{
	Radius 8
	Height 8
	Scale 0.6
	Mass 50
	BounceType "Doom"
	BounceCount 1
	Gravity 0.6
	DeathSound "gore/splat"
	Projectile
	-NOGRAVITY
	+BOUNCEAUTOOFF
	+CANBOUNCEWATER
	+DONTSPLASH
	+FORCEXYBILLBOARD
	+MOVEWITHSECTOR
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetScale(ScaleX + frandom(-0.1,0.1),ScaleY + frandom(-0.1,0.1))
		TNT1 A 0 A_Jump(256,1,2,3,4,5,6,7)
		SCRG ABCDEFG 0 A_Jump(256,"Chunk")
		Stop
	Chunk:
		SCRG "#" 6 A_SpawnItemEx("ScorpionFlyingBlood",Random(-1,1),Random(-1,1))
		Loop
	Death:
		SCRG "#" 0 A_SpawnItemEx("ScorpionBlood")
		SCRG "#" 250 A_CheckSight("Vanish")
		Wait
	Vanish:
		TNT1 A 0
		Stop
	}
}

ACTOR BigScorpionChunk : ScorpionChunk
{
	Scale 1.1
	Mass 60
	Gravity 0.8
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetScale(ScaleX + frandom(-0.1,0.1),ScaleY + frandom(-0.1,0.1))
		TNT1 A 0 A_Jump(256,1,2,3,4,5,6,7)
		SC2G ABCDEFG 0 A_Jump(256,"Chunk")
		Stop
	Chunk:
		SC2G "#" 6 A_SpawnItemEx("NashGore_FlyingBlood",Random(-1,1),Random(-1,1))
		Loop
	Death:
		SC2G "#" 0 A_SpawnItemEx("NashGore_Blood")
		SC2G "#" 250 A_CheckSight("Vanish")
		Wait
	}
}