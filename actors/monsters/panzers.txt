//OzyTanks defs here...
ACTOR TankSmoke : PuffSmoke //connections with GuardSoul smoke
{
	Radius 1
	Height 1
	Scale 1.7
	Speed 2
	Alpha 0.0001
	Projectile
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	-DROPOFF
	+DONTSPLASH
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2)
		GRM2 A 0 A_Jump(256,"End")
		GRM1 A 0 A_Jump(256,"End")
	End:
		"####" AAAAAAAAAA 1 A_FadeIn(0.05)
		"####" AAAAAAAAAAAAAAAA 2 A_FadeOut(0.025)
		Stop
	}
}

//Ka-Boom base code from Russian Overkill by PillowBlaster - Tweaked by Ozymandias81
ACTOR KaBoom
{
	Scale 2
	RenderStyle "Add"
	Projectile
	+BRIGHT
	+NOINTERACTION
	States
	{
	Spawn:
		BFE1 ABCDE 2
		"####" FGHI 2 A_Explode(75,128)
		"####" JKL 2
		Stop
	}
}

ACTOR KaBoomer
{
	+NOGRAVITY
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 AA 1 A_SpawnItemEx("KaBoom",0,0,0,random(-4,4),random(-4,4),random(-2,6),random(0,359),SXF_TRANSFERTRANSLATION|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION,40)
		Stop
	}
}

ACTOR Nuke
{
	+NOBLOCKMAP
	+NOGRAVITY
	+NOINTERACTION
	Radius 0
	Height 0
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_PlaySound("panzer/explode", CHAN_AUTO, 1.0, FALSE, ATTN_NORM)
		"####" A 0 Radius_Quake(7,30,0,96,0)
		"####" A 0 A_SpawnItemEx("TankBoom",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		"####" A 0 A_SpawnItemEx("TankFlare")
		"####" A 25 A_SpawnItemEx("NukeFloor")
		"####" AA 0 A_SpawnItemEx("NukeSmokeFloor")
		"####" A 0 A_SpawnItemEx("TankPillar",0,0,0,0,0,2)
		"####" A 45 A_SpawnItemEx("TankSmokePillar",0,0,0,0,0,2)
		"####" A 0 A_SpawnItemEx("TankMushroom",0,0,56)
		"####" AA 0 A_SpawnItemEx("TankSmokeMushroom",0,0,56)
		Stop
	}
}

ACTOR TankBoom
{
	+FORCERADIUSDMG
	+NOBLOCKMAP
	+NOGRAVITY
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAA 2 A_Explode(10,128,0)
		Stop
	}
}

ACTOR TankFire : Nuke
{
	RenderStyle "Add"
	+BRIGHT
	+DONTSPLASH
	+EXPLODEONWATER
	+NOINTERACTION
	Scale 0.5
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3,4)
		FLA1 A 2 A_FadeOut(0.03)
		Wait
		FLA2 A 2 A_FadeOut(0.02)
		Wait
		FLA3 A 2 A_FadeOut(0.03)
		Wait
		FLA4 A 2 A_FadeOut(0.02)
		Wait
	}
}

ACTOR TankFlare : TankFire
{
	Scale 0.8
	+NOINTERACTION
	States
	{
	Spawn:
		FLAR B 1 A_FadeOut(0.011)
		Wait
	}
}

ACTOR NukeSmoke : Nuke
{
	Scale 1.7
	+DONTSPLASH
	+EXPLODEONWATER
	Alpha 0.5
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3,4)
		NSMK ABCD 0 A_Jump(256,"End")
	End:
		NSMK "#" 2 A_FadeOut(0.008)
		Wait
	}
}

ACTOR NukeFloor : Nuke
{
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("TankFire",0,0,0,0.1 * random(0,30),0.1 * random(0,30),0,random(0,359),128)
		Stop
	}
}

ACTOR NukeSmokeFloor : Nuke
{
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("NukeSmoke",0,0,0,0.1 * random(0,30),0.1 * random(0,30),0,random(0,359),128)
		Stop
	}
}

ACTOR TankPillar : Nuke
{
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("TankFire",0,0,0,0.1 * random(0,10),0.1 * random(0,10),random(0,-3),random(0,359),128)
		"####" A 0 A_SpawnItemEx("TankSmokeRing")
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("TankFire",0,0,0,0.1 * random(0,10),0.1 * random(0,10),random(0,-3),random(0,359),128)
		Stop
	}
}

ACTOR TankSmokePillar : Nuke
{
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("NukeSmoke",0,0,0,0.1 * random(0,10),0.1 * random(0,10),0.1 * random(0,-40),random(0,359),128)
		Stop
	}
}

ACTOR TankMushroom : Nuke
{
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("TankFire",0,0,0,0.1 * random(0,25),0.01 * random(0,25),0.1 * random(-10,10),random(0,359),128)
		Stop
	}
}

ACTOR TankSmokeMushroom : Nuke
{
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("NukeSmoke",0,0,0,0.1 * random(0,25),0.1 * random(0,25),0.1 * random(-10,10),random(0,359),128)
		Stop
	}
}

ACTOR TankSmokeRing : Nuke
{
	var int user_theta;
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItemEx("NukeSmoke",0,0,0,1,0,0,user_theta)
		UWMR A 0 A_SetUserVar("user_theta",user_theta+20)
		"####" A 0 A_JumpIf(user_theta==360,1)
		Loop
		TNT1 A 0
		Stop
	}
}

//Nebelwerfers
ACTOR NebBoom : TankBoom
{
	-FORCERADIUSDMG
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAA 2 A_Explode(5,32,0)
		Stop
	}
}

ACTOR NebFire : TankFire
{
	Scale 0.15
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3,4)
		FLA1 A 1 A_FadeOut(0.04)
		Wait
		FLA2 A 1 A_FadeOut(0.02)
		Wait
		FLA3 A 1 A_FadeOut(0.04)
		Wait
		FLA4 A 1 A_FadeOut(0.02)
		Wait
	}
}

ACTOR NebSmoke : NukeSmoke
{
	Scale 0.35
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3,4)
		NSMK ABCD 0 A_Jump(256,"End")
	End:
		NSMK "#" 1 A_FadeOut(0.008)
		Wait
	}
}

ACTOR NebFlare : TankFlare {Scale 0.25}

ACTOR NebFloor : NukeFloor
{
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAAAAA 1 A_SpawnItemEx("NebFire",0,0,0,0.1 * random(0,12),0.1 * random(0,12),0.1 * random(-3,3),random(0,359),128)
		Stop
	}
}

ACTOR NebSmokeFloor : NukeSmokeFloor
{
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAAAAA 1 A_SpawnItemEx("NebSmoke",0,0,0,0.1 * random(0,15),0.1 * random(0,15),0,random(0,359),128)
		Stop
	}
}

ACTOR NebSmokePillar : TankSmokePillar
{
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAAAAA 1 A_SpawnItemEx("NebSmoke",0,0,0,0.1 * random(0,5),0.1 * random(0,5),0.1 * random(0,-20),random(0,359),128)
		Stop
	}
}

ACTOR NebSmokeMushroom : TankSmokeMushroom
{
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAAAAA 1 A_SpawnItemEx("NebSmoke",0,0,0,0.1 * random(0,15),0.1 * random(0,15),0.1 * random(-5,5),random(0,359),128)
		Stop
	}
}

ACTOR NebNuke : Nuke
{
	States
	{
	Spawn:
		TNT1 A 0 Radius_Quake(3,8,0,24,0)
		"####" A 0 A_SpawnItemEx("NebBoom",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		"####" A 0 A_SpawnItemEx("NebFlare")
		"####" A 3 A_SpawnItemEx("NebFloor")
		"####" A 0 A_SpawnItemEx("NebSmokeFloor")
		"####" A 6 A_SpawnItemEx("NebSmokePillar",0,0,0,0,0,2)
		"####" A 0 A_SpawnItemEx("NebSmokeMushroom",0,0,0)
		Stop
	}
}

ACTOR PanzerNuke : Nuke //for panzerschreck rockets
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_PlaySound("panzer/explode", CHAN_AUTO, 1.0, FALSE, ATTN_NORM)
		"####" A 0 Radius_Quake(7,30,0,96,0)
		"####" A 0 A_SpawnItemEx("TankBoom",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		"####" A 0 A_SpawnItemEx("TankFlare")
		"####" A 6 A_SpawnItemEx("NukeFloor")
		"####" AA 0 A_SpawnItemEx("NukeSmokeFloor")
		"####" A 11 A_SpawnItemEx("TankSmokePillar",0,0,0,0,0,2)
		"####" AA 0 A_SpawnItemEx("TankSmokeMushroom",0,0,56)
		Stop
	}
}

ACTOR KaZomBoom : KaBoom //Zombie KaBoom
{
	States
	{
	Spawn:
		BFE1 ABCDE 2
		"####" F 0 A_Explode(random(45,65),144,TRUE,128)
		"####" FGHIJKL 2
		Stop
	}
}

ACTOR ZombieNuke : Nuke //for zombiekazi
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_PlaySound("panzer/explode", CHAN_AUTO, 1.0, FALSE, ATTN_NORM)
		"####" A 0 Radius_Quake(7,30,0,96,0)
		"####" A 0 A_SpawnItemEx("TankFlare")
		"####" A 1 A_SpawnItemEx("NukeFloor")
		"####" A 1 A_SpawnItemEx("KazomBoom")
		"####" AA 0 A_SpawnItemEx("NukeSmokeFloor")
		"####" A 1 A_SpawnItemEx("TankSmokePillar",0,0,0,0,0,2)
		Stop
	}
}

//UFOs
ACTOR UFONuke : Nuke
{
	States
	{
	Spawn:
		TNT1 A 0 A_PlaySound("panzer/explode", CHAN_ITEM, 1.0, FALSE, ATTN_NORM)
		"####" A 0 A_SpawnItemEx("UFOFlare")
		"####" A 25 A_SpawnItemEx("UFONukeFloor")
		"####" A 0 A_SpawnItemEx("UFONukeSmokeFloor")
		"####" A 0 A_SpawnItemEx("UFOPillar",0,0,-8,0,0,2)
		"####" A 45 A_SpawnItemEx("UFOSmokePillar",0,0,-8,0,0,2)
		"####" A 0 A_SpawnItemEx("UFOMushroom",0,0,-8)
		"####" A 0 A_SpawnItemEx("UFOSmokeMushroom",0,0,-8)
		Stop
	}
}

ACTOR UFOFire : TankFire
{
	Scale 4.0
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3,4)
		FLA1 A 2 A_FadeOut(0.01)
		Wait
		FLA2 A 2 A_FadeOut(0.02)
		Wait
		FLA3 A 2 A_FadeOut(0.01)
		Wait
		FLA4 A 2 A_FadeOut(0.02)
		Wait
	}
}

ACTOR UFOFlare : TankFlare {Scale 1.8}

ACTOR UFONukeSmoke : NukeSmoke
{
	Scale 3.7
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3,4)
		NSMK ABCD 0 A_Jump(256,"End")
	End:
		NSMK "#" 2 A_FadeOut(0.001)
		Wait
	}
}

ACTOR UFONukeFloor : Nuke
{
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("UFOFire",0,0,0,-0.1 * random(0,30),-0.1 * random(0,30),0,random(0,359),128)
		Stop
	}
}

ACTOR UFONukeSmokeFloor : Nuke
{
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("UFONukeSmoke",0,0,0,-0.1 * random(0,30),-0.1 * random(0,30),0,random(0,359),128)
		Stop
	}
}

ACTOR UFOPillar : Nuke
{
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("UFOFire",0,0,0,-0.1 * random(0,10),-0.1 * random(0,10),random(0,-3),random(0,359),128)
		"####" A 0 A_SpawnItemEx("UFOSmokeRing")
		"####" AAAAAAAAAAAA 1 A_SpawnItemEx("UFOFire",0,0,0,-0.1 * random(0,10),-0.1 * random(0,10),random(0,-3),random(0,359),128)
		Stop
	}
}

ACTOR UFOSmokePillar : Nuke
{
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("UFONukeSmoke",0,0,0,-0.1 * random(0,10),-0.1 * random(0,10),-0.1 * random(0,-40),random(0,359),128)
		Stop
	}
}

ACTOR UFOMushroom : Nuke
{
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("UFOFire",0,0,0,-0.1 * random(0,25),-0.01 * random(0,25),-0.1 * random(-10,10),random(0,359),128)
		Stop
	}
}

ACTOR UFOSmokeMushroom : Nuke
{
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("UFONukeSmoke",0,0,0,-0.1 * random(0,25),-0.1 * random(0,25),-0.1 * random(-10,10),random(0,359),128)
		Stop
	}
}

ACTOR UFOSmokeRing : Nuke
{
	var int user_theta;
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItemEx("UFONukeSmoke",0,0,0,1,0,0,user_theta)
		UWMR A 0 A_SetUserVar("user_theta",user_theta+20)
		"####" A 0 A_JumpIf(user_theta==360,1)
		Loop
		TNT1 A 0
		Stop
	}
}

//Tanks
ACTOR SSTank1
{
	//$Category Monsters (Wolf3D)/Tanks
	//$Title Tank 1 (3500 HP, Panther Panzer IV, Turret + Missile)
	//$Color 4
	Health 3500
	Radius 100
	Height 96
	Mass 0x7ffffff
	Speed 2.66666667
	MinMissileChance 160
	Tag "Panzer IV F"
	+BOSS
	+BOSSDEATH
	+DONTMORPH
	+FLOORCLIP
	+MISSILEMORE
	+NOBLOODDECALS
	+NOINFIGHTING	//ozy81
	+PAINLESS
	+SLIDESONWALLS	//ozy81 - avoid incorrect clipping
	+SOLID
	+SYNCHRONIZED	//ozy81 - added for 3dmodels synch
	Monster
	Obituary "$TANKS"
	Species Nazi
	MaxStepHeight 32
	BloodType "TankSpark"
	States
	{
	Spawn:
		MDLA A 10 A_Look
		Loop
	See:
		"####" A 1
		"####" A 0 A_JumpIfTargetInLOS(1) // only move if we have a target
		Loop
		"####" A 0 A_PlaySound("TKMOVE", CHAN_7, 1.0, TRUE)
		"####" A 0 
		"####" A 0 A_FaceTarget(1.0)
		"####" A 0 A_ChangeVelocity(Cos(angle) * speed, Sin(angle) * speed, velz, CVF_REPLACE)
//only attack if our target is within 5 degrees if the top part of the tank model was separate, this behavior could potentially be removed.
		"####" A 0 A_JumpIfTargetInLOS(1, 5)
		Loop
		"####" A 0 A_Chase("Missile", "Missile", CHF_DONTTURN|CHF_NODIRECTIONTURN|CHF_DONTMOVE)
		Goto See
	Missile:
		"####" A 10
		"####" A 0 A_Jump(128, "Turret")
		"####" A 0 A_Jump(96, "Missile2")
		"####" BCD 8
		"####" E 8 A_SpawnProjectile("PanzerRocket", ScaleX * 76, 0, angle, CMF_ABSOLUTEANGLE|CMF_BADPITCH)
		"####" FG 8
		"####" A 0 A_JumpIfHealthLower(1, "See", AAPTR_TARGET)
		"####" A 0 A_JumpIfTargetInLOS("Missile", 1) // attack again if we're perfectly aimed
		Goto See
	Melee:
	Turret:
		MDLA A 0 A_PlaySound("weapons/chaingun", CHAN_WEAPON)
		"####" EEEE 1 LIGHT("SUPAFIRE") A_SpawnProjectile("EnemyChaingunTracer", ScaleX * 48, ScaleY * 26, random(-4, 4))
		"####" A 0 A_JumpIfHealthLower(1, "See", AAPTR_TARGET)
		MDLA A 2 A_JumpIfTargetInLOS("Turret", 45)
		Goto See
	Missile2:
		MDLA BCD 2
		"####" E 2 A_SpawnProjectile("PanzerRocket", ScaleX * 76, 0, angle, CMF_ABSOLUTEANGLE|CMF_BADPITCH)
		"####" FG 2
		"####" A 20
		"####" BCD 4
		"####" E 4 A_SpawnProjectile("PanzerRocket", ScaleX * 76, 0, angle, CMF_ABSOLUTEANGLE|CMF_BADPITCH)
		"####" FG 4
		"####" A 20
		"####" A 0 A_JumpIfHealthLower(1, "See", AAPTR_TARGET)
		"####" A 0 A_JumpIfTargetInLOS("Missile2", 1) // attack again if we're perfectly aimed
		Goto See
	Death:
		TNT1 A 0 A_StopSound(CHAN_7)
		"####" A 0 A_PlaySound("weapons/explode", CHAN_AUTO, 1.0, FALSE, ATTN_IDLE)
		"####" A 0 A_Scream
		"####" A 0 A_NoBlocking
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Debris_Tank", random(88, 112), random(88, 112), random(128, 144), random(1, 3), random(1, 3), random(1, 3), random(0, 360), SXF_CLIENTSIDE)
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Debris_GlassShard_Large", random(88, 112), random(88, 112), random(128, 144), random(1, 3), random(1, 3), random(1, 3), random(0, 360), SXF_CLIENTSIDE)
		"####" A 0 A_SpawnItemEx("Nuke", 0, 0, 5, 0, 0, 0, 0, SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
		"####" A 1 A_SpawnItemEx("KaBoomer", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS)
		"####" A 1 A_SpawnItemEx("GeneralExplosion_Large", 56, 0, 32)
		"####" A 20 A_SpawnItemEx("DestroyedTank1")
		Stop
	}
}

ACTOR DestroyedTank1
{
	//$Category Gore (Wolf3D)
	//$Title Tank 1 (Panther Panzer IV, destroyed)
	//$Color 4
	Radius 88
	Height 96
	Mass 0x7ffffff
	+FLOORCLIP
	+INVULNERABLE
	+NOBLOOD
	+NOBLOODDECALS
	+NODAMAGE
	+NOTAUTOAIMED
	+SHOOTABLE
	+SOLID
	BloodType "TankSpark"
	States
	{
	Spawn:
		MDLA A 12 A_SpawnProjectile("TankSmoke",58,0,random(0,360),2,random(70,130))
		Loop
	}
}

ACTOR SSTank2 : SSTank1
{
	//$Title Tank 2 (4000 HP, KFZ 251 HalfTrack, Turret + Missile)
	Height 88
	Health 4000
	Speed 2.33333333
	Tag "KFZ 251 HalfTrack"
	States
	{
	Spawn:
		MDLA A 10 A_Look
		Loop
	Missile:
		"####" A 10
		"####" A 0 A_Jump(128, "Turret")
		"####" BCD 8
		"####" E 8 A_SpawnProjectile("PanzerRocket", ScaleX * 88, 0, angle, CMF_ABSOLUTEANGLE|CMF_BADPITCH)
		"####" FG 8
		"####" A 0 A_JumpIfHealthLower(1, "See", AAPTR_TARGET)
		"####" A 0 A_JumpIfTargetInLOS("Missile", 1) // attack again if we're perfectly aimed
		Goto See
	Melee:
	Turret:
		MDLA A 0 A_PlaySound("weapons/chaingun", CHAN_WEAPON)
		"####" EEEE 1 LIGHT("SUPAFIRE") A_SpawnProjectile("EnemyChaingunTracer", ScaleX * 70, ScaleY * -16, random(-4, 4))
		"####" A 0 A_JumpIfHealthLower(1, "See", AAPTR_TARGET)
		MDLA A 2 A_JumpIfTargetInLOS("Turret", 45)
		Goto See
	Death:
		TNT1 A 0 A_StopSound(CHAN_7)
		"####" A 0 A_PlaySound("weapons/explode", CHAN_AUTO, 1.0, FALSE, ATTN_IDLE)
		"####" A 0 A_Scream
		"####" A 0 A_NoBlocking
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Debris_Tank2", random(88, 96), random(88, 96), random(96, 128), random(1, 3), random(1, 3), random(1, 3), random(0, 360), SXF_CLIENTSIDE)
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Debris_GlassShard_Large", random(88, 96), random(88, 96), random(96, 128), random(1, 3), random(1, 3), random(1, 3), random(0, 360), SXF_CLIENTSIDE)
		"####" A 0 A_SpawnItemEx("Nuke", 0, 0, 5, 0, 0, 0, 0, SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
		"####" A 1 A_SpawnItemEx("KaBoomer", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS)
		"####" A 1 A_SpawnItemEx("GeneralExplosion_Large", 56, 0, 32)
		"####" A 20 A_SpawnItemEx("DestroyedTank2")
		Stop
	}
}

ACTOR DestroyedTank2 : DestroyedTank1
{
	//$Title Tank 2 (KFZ 251 HalfTrack, destroyed)
	Height 88
}

ACTOR SSTank3 : SSTank1
{
	//$Title Tank 3 (3000 HP, Tiger I Panzer, Missile only)
	Health 3000
	Height 88
	Speed 2.55555556
	Tag "Tiger I Panzer"
	States
	{
	Spawn:
		MDLA A 10 A_Look
		Loop
	Missile:
		MDLA A 10
		"####" A 0 //A_Jump(128, "Turret")
		"####" BCD 8
		"####" E 8 A_SpawnProjectile("PanzerRocket", ScaleX * 65, ScaleY * 4, angle, CMF_ABSOLUTEANGLE|CMF_BADPITCH)
		"####" FG 8
		"####" A 0 A_JumpIfHealthLower(1, "See", AAPTR_TARGET)
		"####" A 0 A_JumpIfTargetInLOS("Missile", 1) // attack again if we're perfectly aimed
		Goto See
	Melee:
	Turret:
		MDLA A 0 A_PlaySound("weapons/chaingun", CHAN_WEAPON)
		"####" EEEE 1 LIGHT("SUPAFIRE") A_SpawnProjectile("EnemyChaingunTracer", ScaleX * 48, ScaleY * 24, random(-4, 4))
		"####" A 0 A_JumpIfHealthLower(1, "See", AAPTR_TARGET)
		MDLA A 2 A_JumpIfTargetInLOS("Turret", 45)
		Goto See
	Death:
		TNT1 A 0 A_StopSound(CHAN_7)
		"####" A 0 A_PlaySound("weapons/explode", CHAN_AUTO, 1.0, FALSE, ATTN_IDLE)
		"####" A 0 A_Scream
		"####" A 0 A_NoBlocking
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Debris_Tank2", random(88, 96), random(88, 96), random(88, 112), random(1, 3), random(1, 3), random(1, 3), random(0, 360), SXF_CLIENTSIDE)
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Debris_GlassShard_Large", random(88, 96), random(88, 96), random(96, 128), random(1, 3), random(1, 3), random(1, 3), random(0, 360), SXF_CLIENTSIDE)
		"####" A 0 A_SpawnItemEx("Nuke", 0, 0, 5, 0, 0, 0, 0, SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
		"####" A 1 A_SpawnItemEx("KaBoomer", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS)
		"####" A 1 A_SpawnItemEx("GeneralExplosion_Large", 56, 0, 32)
		"####" A 20 A_SpawnItemEx("DestroyedTank3")
		Stop
	}
}

ACTOR DestroyedTank3 : DestroyedTank1
{
	//$Title Tank 3 (Tiger I Panzer, destroyed)
	Height 88
}

ACTOR SSTank4 : SSTank1
{
	//$Title Tank 4 (1000 HP, Light Panzer KFZ 222, Turret only)
	Health 1000
	Radius 80
	Height 80
	Speed 2.55555556
	Tag "Light Panzer KFZ 222"
	States
	{
	Spawn:
		MDLA A 10 A_Look
		Loop
	Missile:
		"####" B 0 A_PlaySound("weapons/chaingun", CHAN_WEAPON)
		"####" BBBB 1 LIGHT("SUPAFIRE") A_SpawnProjectile("EnemyChaingunTracer", ScaleX * 80, ScaleY * 0, random(-4, 4))
		"####" A 0 A_JumpIfHealthLower(1, "See", AAPTR_TARGET)
		"####" A 2 A_JumpIfTargetInLOS("Missile", 45)
		Goto See
	Death:
		TNT1 A 0 A_StopSound(CHAN_7)
		"####" A 0 A_PlaySound("weapons/explode", CHAN_AUTO, 1.0, FALSE, ATTN_NORM)
		"####" A 0 A_Scream
		"####" A 0 A_NoBlocking
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Debris_Tank2", random(88, 96), random(88, 96), random(88, 112), random(1, 3), random(1, 3), random(1, 3), random(0, 360), SXF_CLIENTSIDE)
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Debris_GlassShard_Large", random(88, 96), random(88, 96), random(96, 128), random(1, 3), random(1, 3), random(1, 3), random(0, 360), SXF_CLIENTSIDE)
		"####" A 0 A_SpawnItemEx("Nuke", 0, 0, 5, 0, 0, 0, 0, SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
		"####" A 1 A_SpawnItemEx("KaBoomer", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS)
		"####" A 1 A_SpawnItemEx("GeneralExplosion_Large", 56, 0, 32)
		"####" A 20 A_SpawnItemEx("DestroyedTank4")
		Stop
	}
} 

ACTOR DestroyedTank4 : DestroyedTank1
{
	//$Title Tank 4 (Light Panzer KFZ 222, destroyed)
	Height 80
}