//OzyTanks defs here...
ACTOR TankSmoke : PuffSmoke //connections with GuardSoul smoke
{
	Radius 1
	Height 1
	Scale 1.7
	Speed 2
	Alpha 0.0001
	Projectile
	-DROPOFF
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	+DONTSPLASH
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2)
		GRM2 A 0 A_Jump(256,"End")
		GRM1 A 0 A_Jump(256,"End")
	End:
		"####" AAAAAAAAAA 1 A_FadeIn(0.05)
		"####" AAAAAAAAAAAAAAAA 2 A_FadeOut(0.025)
		stop
	}
}

//Ka-Boom from Russian Overkill by PillowBlaster - Tweaked by Ozymandias81
ACTOR KaBoom
{
	Scale 2
	RenderStyle "Add"
	Projectile
	+NOINTERACTION
	States
	{
	Spawn:
		BFE1 ABCDE 2 BRIGHT
		"####" FGHI 2 BRIGHT A_Explode(75,128)
		"####" JKL 2 BRIGHT
		Stop
	}
}

ACTOR KaBoomer
{
	+NOGRAVITY
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 AA 1 A_SpawnItemEx("KaBoom",0,0,0,random(-4,4),random(-4,4),random(-2,6),random(0,359),SXF_TRANSFERTRANSLATION|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION,40)
		Stop
	}
}

ACTOR Nuke
{
	+NOBLOCKMAP
	+NOGRAVITY
	+NOINTERACTION
	Radius 0
	Height 0
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_PlaySound("panzer/explode",CHAN_VOICE,1,0,ATTN_NONE)
		"####" A 0 Radius_Quake(7,30,0,96,0)
		"####" A 0 A_SpawnItemEx("TankBoom",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		"####" A 0 A_SpawnItemEx("TankFlare")
		"####" A 25 A_SpawnItemEx("NukeFloor")
		"####" AA 0 A_SpawnItemEx("NukeSmokeFloor")
		"####" A 0 A_SpawnItemEx("TankPillar",0,0,0,0,0,2)
		"####" A 45 A_SpawnItemEx("TankSmokePillar",0,0,0,0,0,2)
		"####" A 0 A_SpawnItemEx("TankMushroom",0,0,56)
		"####" AA 0 A_SpawnItemEx("TankSmokeMushroom",0,0,56)
		Stop
	}
}

ACTOR TankBoom
{
	+NOBLOCKMAP
	+NOGRAVITY
	+FORCERADIUSDMG
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAA 2 A_Explode(10,128,0)
		Stop
	}
}

ACTOR NebBoom : TankBoom
{
	-FORCERADIUSDMG
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAA 2 A_Explode(5,32,0)
		Stop
	}
}

ACTOR TankFire : Nuke
{
	RenderStyle "Add"
	+NOINTERACTION
	+DONTSPLASH
	+EXPLODEONWATER
	Scale 0.5
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3,4)
		FLA1 A 2 BRIGHT A_FadeOut(0.03)
		Wait
		FLA2 A 2 BRIGHT A_FadeOut(0.02)
		Wait
		FLA3 A 2 BRIGHT A_FadeOut(0.03)
		Wait
		FLA4 A 2 BRIGHT A_FadeOut(0.02)
		Wait
	}
}

ACTOR NebFire : TankFire
{
	Scale 0.15
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3,4)
		FLA1 A 1 BRIGHT A_FadeOut(0.02)
		Wait
		FLA2 A 1 BRIGHT A_FadeOut(0.01)
		Wait
		FLA3 A 1 BRIGHT A_FadeOut(0.02)
		Wait
		FLA4 A 1 BRIGHT A_FadeOut(0.01)
		Wait
	}
}

ACTOR NukeSmoke : Nuke
{
	Scale 1.7
	+NOINTERACTION
	+DONTSPLASH
	+EXPLODEONWATER
	Alpha 0.5
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3,4)
		NSMK ABCD 0 A_Jump(256,"End")
	End:
		NSMK "#" 2 A_FadeOut(0.008)
		Wait
	}
}

ACTOR NebSmoke : NukeSmoke
{
	Scale 0.35
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3,4)
		NSMK ABCD 0 A_Jump(256,"End")
	End:
		NSMK "#" 1 A_FadeOut(0.004)
		Wait
	}
}

ACTOR TankFlare : TankFire
{
	Scale 0.8
	+NOINTERACTION
	States
	{
	Spawn:
		FLAR B 1 BRIGHT A_FadeOut(0.011)
		Wait
	}
}

ACTOR NebFlare : TankFlare {Scale 0.25}

ACTOR NukeFloor : Nuke
{
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("TankFire",0,0,0,0.1 * Random(0,30),0.1 * Random(0,30),0,Random(0,359),128)
		Stop
	}
}

ACTOR NebFloor : NukeFloor
{
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAAAAA 1 A_SpawnItemEx("NebFire",0,0,0,0.1 * Random(0,15),0.1 * Random(0,15),0,Random(0,359),128)
		Stop
	}
}

ACTOR NukeSmokeFloor : Nuke
{
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("NukeSmoke",0,0,0,0.1 * Random(0,30),0.1 * Random(0,30),0,Random(0,359),128)
		Stop
	}
}

ACTOR NebSmokeFloor : NukeSmokeFloor
{
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAAAAA 1 A_SpawnItemEx("NebSmoke",0,0,0,0.1 * Random(0,15),0.1 * Random(0,15),0,Random(0,359),128)
		Stop
	}
}

ACTOR TankPillar : Nuke
{
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("TankFire",0,0,0,0.1 * Random(0,10),0.1 * Random(0,10),Random(0,-3),Random(0,359),128)
		"####" A 0 A_SpawnItemEx("TankSmokeRing")
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("TankFire",0,0,0,0.1 * Random(0,10),0.1 * Random(0,10),Random(0,-3),Random(0,359),128)
		Stop
	}
}

ACTOR TankSmokePillar : Nuke
{
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("NukeSmoke",0,0,0,0.1 * Random(0,10),0.1 * Random(0,10),0.1 * Random(0,-40),Random(0,359),128)
		Stop
	}
}

ACTOR NebSmokePillar : TankSmokePillar
{
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAAAAA 1 A_SpawnItemEx("NebSmoke",0,0,0,0.1 * Random(0,5),0.1 * Random(0,5),0.1 * Random(0,-20),Random(0,359),128)
		Stop
	}
}

ACTOR TankMushroom : Nuke
{
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("TankFire",0,0,0,0.1 * Random(0,25),0.01 * Random(0,25),0.1 * Random(-10,10),Random(0,359),128)
		Stop
	}
}

ACTOR TankSmokeMushroom : Nuke
{
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("NukeSmoke",0,0,0,0.1 * Random(0,25),0.1 * Random(0,25),0.1 * Random(-10,10),Random(0,359),128)
		Stop
	}
}

ACTOR NebSmokeMushroom : TankSmokeMushroom
{
	States
	{
	Spawn:
		TNT1 A 0
		"####" AAAAAAAA 1 A_SpawnItemEx("NebSmoke",0,0,0,0.1 * Random(0,15),0.1 * Random(0,15),0.1 * Random(-5,5),Random(0,359),128)
		Stop
	}
}

ACTOR TankSmokeRing : Nuke
{
	var int user_theta;
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItemEx("NukeSmoke",0,0,0,1,0,0,user_theta)
		UWMR A 0 A_SetUserVar("user_theta",user_theta+20)
		"####" A 0 A_JumpIf(user_theta==360,1)
		Loop
		TNT1 A 0
		Stop
	}
}

ACTOR PanzerNuke : Nuke //for panzerschreck rockets
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_PlaySound("panzer/explode",CHAN_VOICE,1,0,ATTN_NONE)
		"####" A 0 Radius_Quake(7,30,0,96,0)
		"####" A 0 A_SpawnItemEx("TankBoom",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		"####" A 0 A_SpawnItemEx("TankFlare")
		"####" A 6 A_SpawnItemEx("NukeFloor")
		"####" AA 0 A_SpawnItemEx("NukeSmokeFloor")
		"####" A 11 A_SpawnItemEx("TankSmokePillar",0,0,0,0,0,2)
		"####" AA 0 A_SpawnItemEx("TankSmokeMushroom",0,0,56)
		Stop
	}
}

ACTOR NebNuke : Nuke //for nebelwerfer rockets
{
	States
	{
	Spawn:
		TNT1 A 0 Radius_Quake(3,8,0,24,0)
		"####" A 0 A_SpawnItemEx("NebBoom",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		"####" A 0 A_SpawnItemEx("NebFlare")
		"####" A 3 A_SpawnItemEx("NebFloor")
		"####" A 0 A_SpawnItemEx("NebSmokeFloor")
		"####" A 6 A_SpawnItemEx("NebSmokePillar",0,0,0,0,0,2)
		"####" A 0 A_SpawnItemEx("NebSmokeMushroom",0,0,56)
		Stop
	}
}

ACTOR SSTank1
{
	//$Category Monsters (Wolf3D)
	//$Title Tank 1 (3500 HP - Panther Panzer IV)
	//$Color 4
	Health 3500
	Radius 100
	Height 96
	Mass 0x7ffffff
	Speed 2.66666667
	MinMissileChance 160
	Tag "Panther Panzer IV"
	+SLIDESONWALLS //ozy81 - avoid incorrect clipping
	+NOBLOODDECALS
	+NOINFIGHTING //ozy81
	+SYNCHRONIZED
	+MISSILEMORE
	+NORADIUSDMG
	+FLOORCLIP
	+DONTMORPH
	+BOSSDEATH
	+PAINLESS
	+SOLID
	+BOSS
	Monster
	Obituary "$TANKS"
	Species Nazi
	MaxStepHeight 32
	BloodType "TankSpark"
	States
	{
	Spawn:
		CYBR A 10 A_Look
		Loop
	See:
		"####" A 0 A_PlaySound("TKMOVE",7,1,1)
		"####" A 1 A_Chase
		Loop
	Missile:
		"####" A 10 A_FaceTarget
		"####" A 0 A_Jump(128,"Turret")
		"####" A 0 A_Jump(96,"Missile2")
		"####" BCD 8
		"####" E 8 A_CustomMissile("PanzerRocket",ScaleX*76)
		"####" FG 8
		Goto See
	Turret:
		CYBR A 0 A_FaceTarget
		BOS2 E 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		"####" EEEE 1 LIGHT("SUPAFIRE") A_CustomMissile("EnemyChaingunTracer",ScaleX*48,ScaleY*26,random(-4,4))
		CYBR A 2 A_SentinelRefire
		Loop
	Missile2:
		CYBR BCD 2
		"####" E 2 A_CustomMissile("PanzerRocket",78)
		"####" FG 2
		"####" A 20 A_FaceTarget
		"####" BCD 4
		"####" E 4 A_CustomMissile("PanzerRocket",78)
		"####" FG 4
		"####" A 20 A_FaceTarget
		Goto See
	Death:
		TNT1 A 0 A_StopSound(7)
		"####" A 0 A_PlaySound("weapons/explode")
		"####" A 0 A_Scream
		"####" A 0 A_NoBlocking
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx ("Debris_Tank", random(64,112), random(64,112), random(128,144), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx ("Debris_GlassShard_Large", random(64,112), random(64,112), random(128,144), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" A 0 A_SpawnItemEx("Nuke",0,0,5,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
		"####" A 1 A_SpawnItemEx("KaBoomer",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		"####" A 1 A_SpawnItemEx("GeneralExplosion_Large",56,0,32)
		"####" A 20 A_SpawnItemEx("DestroyedTank1")
		Stop
	}
}

ACTOR DestroyedTank1
{
	//$Category Gore (Wolf3D)
	//$Title Tank 1 (Panther Panzer IV, destroyed)
	//$Color 4
	Radius 100
	Height 96
	Mass 0x7ffffff
	+NOBLOODDECALS
	+NOTAUTOAIMED
	+INVULNERABLE
	+SHOOTABLE
	+FLOORCLIP
	+NODAMAGE
	+NOBLOOD
	+SOLID
	BloodType "TankSpark"
	States
	{
	Spawn:
		CYBR A 12 A_CustomMissile("TankSmoke",58,0,random(0,360),2,random(70,130))
		Loop
	}
}

ACTOR SSTank2 : SSTank1
{
	//$Title Tank 2 (4000 HP - KFZ 251 HalfTrack)
	Height 88
	Health 4000
	Speed 2.33333333
	Tag "KFZ 251 HalfTrack"
	States
	{
	Spawn:
		CYBR A 10 A_Look
		Loop
	Missile:
		"####" A 10 A_FaceTarget
		"####" A 0 A_Jump(128,"Turret")
		"####" BCD 8
		BOS2 E 8 A_CustomMissile("PanzerRocket",96)
		"####" FG 8
		Goto See
	Turret:
		CYBR A 0 A_FaceTarget
		BOS2 E 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		"####" EEEE 1 LIGHT("SUPAFIRE") A_CustomMissile("EnemyChaingunTracer",60,12,random(-4,4))
		CYBR A 2 A_SentinelRefire
		Loop
	Death:
		TNT1 A 0 A_StopSound(7)
		"####" A 0 A_PlaySound("weapons/explode")
		"####" A 0 A_Scream
		"####" A 0 A_NoBlocking
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx ("Debris_Tank2", random(48,96), random(48,96), random(96,128), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx ("Debris_GlassShard_Large", random(48,96), random(48,96), random(96,128), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" A 0 A_SpawnItemEx("Nuke",0,0,5,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
		"####" A 1 A_SpawnItemEx("KaBoomer",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		"####" A 1 A_SpawnItemEx("GeneralExplosion_Large",56,0,32)
		"####" A 20 A_SpawnItemEx("DestroyedTank2")
		Stop
	}
}

ACTOR DestroyedTank2 : DestroyedTank1
{
	//$Title Tank 2 (KFZ 251 HalfTrack, destroyed)
	Height 88
}

ACTOR SSTank3 : SSTank1
{
	//$Title Tank 3 (3000 HP - Tiger I Panzer)
	Health 3000
	Height 88
	Speed 2.55555556
	Tag "Tiger I Panzer"
	States
	{
	Spawn:
		CYBR A 10 A_Look
		Loop
	Missile:
		CYBR A 10 A_FaceTarget
		"####" A 0 A_Jump(128,"Turret")
		"####" BCD 8
		"####" E 8 A_CustomMissile("PanzerRocket",ScaleX*65,ScaleY*4)
		"####" FG 8
		Goto See
	Turret:
		CYBR A 0 A_FaceTarget
		BOS2 E 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		"####" EEEE 1 LIGHT("SUPAFIRE") A_CustomMissile("EnemyChaingunTracer",ScaleX*48,ScaleY*24,random(-4,4))
		CYBR A 2 A_SentinelRefire
		Loop
	Death:
		TNT1 A 0 A_StopSound(7)
		"####" A 0 A_PlaySound("weapons/explode")
		"####" A 0 A_Scream
		"####" A 0 A_NoBlocking
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx ("Debris_Tank2", random(48,96), random(48,96), random(88,112), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx ("Debris_GlassShard_Large", random(48,96), random(48,96), random(96,128), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" A 0 A_SpawnItemEx("Nuke",0,0,5,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
		"####" A 1 A_SpawnItemEx("KaBoomer",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		"####" A 1 A_SpawnItemEx("GeneralExplosion_Large",56,0,32)
		"####" A 20 A_SpawnItemEx("DestroyedTank3")
		Stop
	}
}

ACTOR DestroyedTank3 : DestroyedTank1
{
	//$Title Tank 3 (Tiger I Panzer, destroyed)
	Height 88
}

ACTOR SSTank4 : SSTank1
{
	//$Title Tank 4 (1000 HP - Light Panzer KFZ 222)
	Health 1000
	Radius 80
	Height 80
	Speed 2.55555556
	Tag "Light Panzer KFZ 222"
	States
	{
	Spawn:
		CYBR A 10 A_Look
		Loop
	Missile:
		CYBR A 0 A_FaceTarget
		"####" B 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		"####" BBBB 1 LIGHT("SUPAFIRE") A_CustomMissile("EnemyChaingunTracer",ScaleX*80,ScaleY*0,random(-4,4))
		"####" A 2 A_SentinelRefire
		Loop
	Death:
		TNT1 A 0 A_StopSound(7)
		"####" A 0 A_PlaySound("weapons/explode")
		"####" A 0 A_Scream
		"####" A 0 A_NoBlocking
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx ("Debris_Tank2", random(48,96), random(48,96), random(88,112), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx ("Debris_GlassShard_Large", random(48,96), random(48,96), random(96,128), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" A 0 A_SpawnItemEx("Nuke",0,0,5,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
		"####" A 1 A_SpawnItemEx("KaBoomer",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		"####" A 1 A_SpawnItemEx("GeneralExplosion_Large",56,0,32)
		"####" A 20 A_SpawnItemEx("DestroyedTank4")
		Stop
	}
}

ACTOR DestroyedTank4 : DestroyedTank1
{
	//$Title Tank 4 (Light Panzer KFZ 222, destroyed)
	Height 80
}