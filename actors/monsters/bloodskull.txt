Actor BloodSkull 7892
{
//$Category Monsters (Wolf3D)
//$Title Bloodskull
//$Color 4
Health 150
Radius 16
Height 56
Mass 9999
Speed 8
PainChance 50
Monster
+FLOAT
+NOGRAVITY
+DONTFALL
+NEVERRESPAWN
SeeSound "BSkull/See"
PainSound "BSkull/Pain"
DeathSound "BSkull/Death"
ActiveSound " "
Obituary "%o got spitted by a blood skull."
States
{
Spawn:
	SWBS AA 4 A_Look
	TNT1 A 0 A_SpawnItemEx("SkullBloodDrip",random(-2,2),random(-2,2),0,0,0,0,0,0)
	Loop
See:
	SWBS AA 4 A_Chase
	TNT1 A 0 A_SpawnItemEx("SkullBloodDrip",random(0,4),random(-2,2),0,0,0,0,0,0)
	Loop
Missile:
	SWBS A 2 A_FaceTarget
	SWBS B 2 A_CustomMissile("BloodSpit",12,0,0,0,0)
	SWBS BA 2
	TNT1 A 0 A_SpawnItemEx("SkullBloodDrip",random(-2,2),random(-2,2),0,0,0,0,0,0)
	Goto See
Pain:
	SWBS A 2 A_Pain
	Goto See
Death:
	SWBS C 6 A_NoBlocking
	SWBS D 5 Bright A_Scream
	TNT1 A 0 A_SetTranslucent(0.8,0)
	SWBS EFGH 5 Bright
	Stop
}
}

Actor SkullBloodDrip
{
Radius 1
Height 4
+MISSILE
+TOUCHY
+DONTSPLASH
Damage 0
Scale 0.4
Gravity 0.7
ReactionTime 10
States
{
Spawn:
	BDR2 A 5 A_CountDown
	Loop
Death:
	TNT1 A 1
	Stop
}
}

Actor BloodSpit
{
Radius 6
Height 6
Speed 17
Damage 3
Scale 0.5
SeeSound "Blood/Spit"
DeathSound "Blood/Impact"
PROJECTILE
Decal BloodSplat
States
{
Spawn:
	BSHT AB 2 A_CustomMissile("BloodSpitTrail",0,0,180)
	Loop
Death:
	BSHT C 6 
	BSHT D 6 
	Stop
}
}

Actor BloodSpitTrail
{
Radius 1
Height 1
Speed 0
PROJECTILE
Renderstyle Translucent
Alpha 0.8
Scale 0.3
+NOCLIP
states
{
Spawn:
	BSHT CD 4 
	Stop
}
}



































