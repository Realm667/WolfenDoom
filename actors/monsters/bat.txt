ACTOR BatFamiliar
{
	//$Category Monsters (Wolf3D)/Critters
	//$Title Bat
	//$Color 4
	Health 10
	Radius 12
	Height 24
	FloatSpeed 2.33333333
	Speed 3.33333334
	PainChance 200
	MONSTER
	-MTHRUSPECIES
	-THRUSPECIES
	+FLOAT
	+NOGRAVITY
	+NOINFIGHTING
	+THRUGHOST
	Species "Player"
	SeeSound "batfam/idle"
	PainSound "batfam/pain"
	DeathSound "batfam/death"
	ActiveSound "batfam/idle"
	HitObituary "$BAT"
	States
	{
	Spawn:
		BFAM ABCB 3 A_Look
		Loop
	See:
		BFAM A 1 A_Chase
		"####" AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" B 1 A_Chase
		"####" BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" C 1 A_Chase
		"####" CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" D 1 A_Chase
		"####" DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Melee:
		BFAM A 3 A_FaceTarget
		"####" B 3 A_CustomMeleeAttack(random(1,8)*1,"batfam/idle")
		"####" CB 3 A_FaceTarget
		Goto See
	Pain:
		BFAM A 2
		"####" A 2 A_Pain
		Goto See
	Death:
		BFAM D 5 A_ScreamAndUnblock
		"####" EFD 5 {A_ChangeFlag("MTHRUSPECIES", TRUE); A_ChangeFlag("THRUSPECIES", TRUE);}
		Goto Death+1
	Crash:
		BFAM G 6 A_PlaySound("batfam/crash")
		"####" H 7
		"####" I 8
		"####" J -1 {A_ChangeFlag("MTHRUSPECIES", TRUE); A_ChangeFlag("THRUSPECIES", TRUE);}
		Stop
	}
}

ACTOR RatFamiliar
{
	//$Category Monsters (Wolf3D)/Critters
	//$Title Rat (attacking)
	//$Color 4
	Health 10
	Radius 12
	Height 24
	Speed 3.66666667
	PainChance 200
	MONSTER
	-FLOAT
	-NOGRAVITY
	+NOINFIGHTING
	SeeSound "rat/squeek"
	PainSound "batfam/pain"
	DeathSound "rat/death"
	ActiveSound "rat/active"
	HitObituary "$RAT"
	States
	{
	Spawn:
		PAIN A 3 A_Look
		Loop
	See:
		PAIN A 1 A_Chase
		"####" AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" B 1 A_Chase
		"####" BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Melee:
		PAIN A 3 A_FaceTarget
		"####" B 3 A_CustomMeleeAttack(random(1,4)*1,"batfam/idle")
		"####" BB 3 A_FaceTarget
		Goto See
	Pain:
		PAIN A 2
		"####" A 2 A_Pain
		Goto See
	Death:
		PAIN H 5 A_ScreamAndUnblock
		"####" IJKL 5
		"####" M -1
		Stop
	}
}