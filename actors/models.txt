//Base stuff
ACTOR ModelBase
{
	Height 56
	+FLOORCLIP
	+DONTSPLASH
	+NOGRAVITY
	States
	{
	Spawn:
		MDLA A -1
		Stop
	}
}

ACTOR 3dObject : ModelBase
{
	+NOBLOCKMAP
	+SOLID
}

ACTOR 3dObstacle : 3dObject
{
	Mass 0x7fffffff
	Health -1
	-FLOORCLIP
	-NOBLOCKMAP
	+CANPASS
	+NOBLOOD
	+NOBLOODDECALS
	+NODAMAGE
	+NOTAUTOAIMED
	+SHOOTABLE
}

ACTOR 3dGlass : ModelBase
{
	RenderStyle "Shaded"
	StencilColor "CC CC CC"
}

//some smart bridges
ACTOR 3dBridge256 : InvisibleBridge
{Radius 256 Height 256}

ACTOR 3dBridge192 : InvisibleBridge
{Radius 192 Height 192}

ACTOR 3dBridge176 : InvisibleBridge
{Radius 176 Height 176}

ACTOR 3dBridge144 : InvisibleBridge
{Radius 144 Height 144}

ACTOR 3dBridge128 : InvisibleBridge
{Radius 128 Height 128}

ACTOR 3dBridge96 : InvisibleBridge
{Radius 96 Height 96}

ACTOR 3dBridge64 : InvisibleBridge
{Radius 64 Height 64}

ACTOR 3dBridge56 : InvisibleBridge
{Radius 56 Height 56}

ACTOR 3dBridge48 : InvisibleBridge
{Radius 48 Height 48}

ACTOR 3dBridge32 : InvisibleBridge
{Radius 32 Height 32}

ACTOR 3dBridge24 : InvisibleBridge
{Radius 24 Height 24}

ACTOR 3dBridge16 : InvisibleBridge
{Radius 16 Height 16}

ACTOR 3dBridge8 : InvisibleBridge
{Radius 8 Height 24}

//Models
ACTOR SkyboxMoon
{
	//$Category Props (Wolf3D)/Astrostein
	//$Title Moon (skybox)
	//$Color 3
	Radius 16
	Height 16
	+NOGRAVITY
	States
	{
	Spawn:
		MDLA A -1 NoDelay {
							A_SpawnItemEx("SkyboxPlanetShade", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPITCH | SXF_TRANSFERROLL | SXF_TRANSFERSCALE | SXF_SETMASTER);
							A_SpawnItemEx("SkyboxMoonLights", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPITCH | SXF_TRANSFERROLL | SXF_TRANSFERSCALE | SXF_SETMASTER);
						}
		Stop
	}
}

ACTOR SkyboxEarth : SkyboxMoon
{
	//$Title Earth (skybox)
	States
	{
	Spawn:
		MDLA A -1 NoDelay {
							A_SpawnItemEx("SkyboxPlanetWeather", 0, 0, 0, 0, 0, 0, 180, SXF_TRANSFERPITCH | SXF_TRANSFERROLL | SXF_TRANSFERSCALE | SXF_SETMASTER);
							A_SpawnItemEx("SkyboxPlanetShade", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPITCH | SXF_TRANSFERROLL | SXF_TRANSFERSCALE | SXF_SETMASTER);
							A_SpawnItemEx("SkyboxPlanetLights", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPITCH | SXF_TRANSFERROLL | SXF_TRANSFERSCALE | SXF_SETMASTER);
						}
		Stop
	}
}

ACTOR SkyboxPlanetShade : 3dGlass //shades of darkness - ozy81, adjusted by T667
{
	RenderStyle "Shaded"
	StencilColor "00 00 00"
	States
	{
	Spawn:
		MDLA A 1 A_Warp(AAPTR_MASTER,0,0,0,0,WARPF_NOCHECKPOSITION | WARPF_INTERPOLATE | WARPF_COPYVELOCITY)
		Loop
	}
}

ACTOR SkyboxPlanetWeather : SkyboxPlanetShade //adjusted by T667
{
	RenderStyle "Shaded"
	StencilColor "aaaaaa"
}

ACTOR SkyboxPlanetLights : SkyboxPlanetShade //adjusted by T667
{
	RenderStyle "Add"
	Alpha 0.5
}

ACTOR SkyboxMoonLights : SkyboxPlanetShade //adjusted by T667
{
	RenderStyle "Add"
	Alpha 0.8
}

ACTOR TestCrate : 3dObstacle
{
	//$Category Props (Wolf3D)/Labs
	//$Title Test Lab Crate
	//$Color 3
	DistanceCheck "scenelod"
	Radius 24
	Height 64
	-SOLID
	-NOGRAVITY
	-NOTELEPORT
	States
	{
	Spawn:
		MDLA A -1 NoDelay A_SpawnItemEx("CrateGlass", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPITCH | SXF_TRANSFERROLL | SXF_TRANSFERSCALE)
		Stop
	}
}

ACTOR TestCrate2 : TestCrate
{
	//$Title Test Lab Crate (with random subject)
	DistanceCheck "scenelod"
	States
	{
	Spawn:
		MDLA A -1 NoDelay {
											A_SpawnItemEx("CrateGlass", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPITCH | SXF_TRANSFERROLL | SXF_TRANSFERSCALE | SXF_SETMASTER);
											A_SpawnItemEx("MutantSubjectWarped", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPITCH | SXF_TRANSFERROLL | SXF_TRANSFERSCALE | SXF_SETMASTER);}
		Stop
	}
}

ACTOR CrateGlass : 3dGlass 
{
	Radius 24
	Height 64
	DistanceCheck "scenelod"
	-SOLID
	-NOGRAVITY
	-NOTELEPORT
	Alpha 0.5
}

ACTOR CeilingFan : 3dObstacle
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Ceiling Fan
	//$Color 3
	DistanceCheck "scenelod"
	Radius 32
	Height 0
	+NOGRAVITY
	+SPAWNCEILING
	Scale 0.85
	
	States
	{
	Spawn:
		MDLA A -1 NoDelay A_SpawnItemEx("WoodenBlades", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPITCH | SXF_TRANSFERROLL | SXF_TRANSFERSCALE)
		Stop
	}
}

ACTOR WoodenBlades : ModelBase
{
	DistanceCheck "scenelod"
	Radius 32
	Height 0
	+SPAWNCEILING
	Scale 0.85
}

ACTOR ModelBeachHedgeHog : 3dObstacle
{
	//$Category Models (Wolf3D)/Obstacles
	//$Title Beach Hedgehog
	//$Color 3
	DistanceCheck "scenelod"
	Radius 32
}

ACTOR ModelCampBed : 3dObstacle
{
	//$Category Models (Wolf3D)/Furniture
	//$Title Camp Bed (double)
	//$Color 3
	DistanceCheck "scenelod"
	Radius 24
}

ACTOR ModelCampBed2 : 3dObstacle
{
	//$Category Models (Wolf3D)/Furniture
	//$Title Camp Bed (single)
	//$Color 3
	DistanceCheck "scenelod"
	Radius 24
	Height 24
}

ACTOR ModelIncaTrain : ModelBase
{
	//$Category Models (Wolf3D)/Vehicles
	//$Title Train
	//$Color 3
	Radius 4
	Height 4
	DistanceCheck "scenelod"
}

ACTOR ModelRailBoxcar : ModelIncaTrain
{
	//$Title Train (boxcar)
	DistanceCheck "scenelod"
}

ACTOR TrainWheels : ModelIncaTrain
{
	//$Category Models (Wolf3D)/Vehicles
	//$Title Train Wheels
	//$Color 3
	DistanceCheck "scenelod"
	Radius 4
	Height 4
}

ACTOR ModelRailBoxcar2 : ModelIncaTrain
{
	//$Title Train (boxcar,kz)
	DistanceCheck "scenelod"
}

ACTOR ModelRailBoxcar3 : ModelIncaTrain
{
	//$Title Train (future)
	DistanceCheck "scenelod"
}

ACTOR ModelRailBoxcar3_Projectile : ModelRailBoxcar3
{
	SeeSound "subway/active"
	SpawnID 212
	Radius 48
	Height 48
	Projectile
	Speed 1
	Damage 1000
	+RIPPER
}

ACTOR ModelRailTanker : ModelIncaTrain
{
	//$Title Train (tanker)
	DistanceCheck "scenelod"
}

ACTOR ModelSandBagCurve : ModelBase
{
	//$Category Models (Wolf3D)/Obstacles
	//$Title Sandbag (curve)
	//$Color 3
	DistanceCheck "scenelod"
	Radius 12
	Height 16
	States
	{
	Spawn:
		MDLA A 0 NoDelay A_SpawnItemEx("3dBridge16",0,-16,0)
		MDLA A -1 A_SpawnItemEx("3dBridge16",0,16,0)
		Stop
	}
}

ACTOR ModelSandBagStraight : 3dObstacle
{
	//$Category Models (Wolf3D)/Obstacles
	//$Title Sandbag (straight)
	//$Color 3
	DistanceCheck "scenelod"
	Radius 16
	Height 16
}

ACTOR WaterTower001 : ModelBase
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Watertower
	//$Color 3
	DistanceCheck "scenelod"
}

ACTOR Windmill : 3dObstacle
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Windmill
	//$Color 3
	DistanceCheck "scenelod"

	Radius 64

	States
	{
	Spawn:
		MDLA A 0 NoDelay A_PlaySound("misc/windmill", CHAN_AUTO, 0.7, true)
		MDLA A -1 A_SpawnItemEx("WindmillWheel", 24, 0, scaleY*476) //don't scale the model more than 1.2 on GZDB
		Stop
	}
}

ACTOR WindmillWheel : ModelBase
{
	DistanceCheck "scenelod"
	States
	{
	Spawn:
		MDLA A 1 A_SetRoll(roll + 0.3)
		Loop
	}
}

ACTOR Tent : ModelBase
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Tent
	//$Color 3
	DistanceCheck "scenelod"
}

ACTOR BomberVTOL3 : ModelBase
{
	//$Category Models (Wolf3D)/Planes
	//$Title Bomber VTOL-3
	//$Color 3
}

ACTOR Submarine : ModelBase
{
	//$Category Models (Wolf3D)/Ships
	//$Title Submarine
	//$Color 3
	
	//$Arg0 "Engine Sound"
	//$Arg0Type 11
	//$Arg0Enum noyes
	
	-SOLID
	Radius 64
	Height 64
	DistanceCheck "scenelod"

	States
	{
	Spawn:
		MDLA A 0 NoDelay A_JumpIf(args[0] == 1, "PlayEngineSound")
		Goto Super::Spawn

	PlayEngineSound:
		MDLA A 0 A_PlaySound("uboat/engine", CHAN_AUTO, 1.0, true, 0.5)
		Goto Super::Spawn
	}
}

ACTOR SubmarineIcy : Submarine
{
	//$Title Submarine (congealed)
	DistanceCheck "scenelod"
	-SOLID
	+NOGRAVITY
	Radius 64
	Height 64
}

ACTOR Vidcam : 3dObstacle
{
	//$Category Models (Wolf3D)/Intermap
	//$Title Video Cam
	//$Color 3
	DistanceCheck "scenelod"
	Height 64
}

ACTOR 88mmFlak : ModelBase
{
	//$Category Models (Wolf3D)/Mission
	//$Title 88mm Flak
	//$Color 3
	Radius 64
	Height 80
}

ACTOR 88mmFlakDestroyed : ModelBase
{
	//$Category Models (Wolf3D)/Mission
	//$Title 88mm Flak (destroyed)
	//$Color 3
	Radius 64
	Height 80
}

ACTOR Nebelwerfer3D
{
	//$Category Models (Wolf3D)/Mission
	//$Title Nebelwerfer
	//$Color 3
	Tag "Nebelwerfer"
	Radius 32
	Height 56
	Health 1000
	Mass 0x7fffffff
	+CANPASS
	+DONTSPLASH
	+FLOORCLIP
	+NOBLOODDECALS
	+SHOOTABLE
	+SOLID
	BloodType "TankSpark"
	States
	{
	Spawn:
		MDLA A -1
		Stop
	Death:
		TNT1 A 0 A_PlaySound("weapons/explode", CHAN_AUTO, 1.0, FALSE, ATTN_NORM)
		"####" A 0 A_Scream
		"####" A 0 A_NoBlocking
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx ("Debris_Tank", random(64,112), random(64,112), random(128,144), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" A 0 A_SpawnItemEx("Nuke",0,0,5,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
		"####" A 1 A_SpawnItemEx("KaBoomer",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		TNT1 A 5 A_SpawnItemEx("Nebelwerfer3D_Destroyed")
		Stop
	}
}

ACTOR Nebelwerfer3D_Destroyed
{
	Radius 32
	Height 56
	Health -1
	Mass 0x7fffffff
	+CANPASS
	+DONTSPLASH
	+FLOORCLIP
	+NOBLOODDECALS
	+SHOOTABLE
	+SOLID
	BloodType "TankSpark"
	States
	{
	Spawn:
		MDLA B 8 A_CustomMissile("DarkSmoke2",32,0,random(0,360),2,random(70,130))
		Loop
	}
}

ACTOR Turret3D : ModelBase
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Turret
	//$Color 3
	DistanceCheck "scenelod"
	Radius 4
	Height 4
}

ACTOR Wire3D : 3dObstacle
{
	//$Category Models (Wolf3D)/Obstacles
	//$Title Wire
	//$Color 3
	DistanceCheck "scenelod"
	Height 32
}

ACTOR Bunker3D : ModelBase
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Bunker
	//$Color 3
	DistanceCheck "scenelod"
	Radius 4
	Height 4
}

ACTOR FlakGun3D : 3dObstacle
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Flakgun
	//$Color 3
	DistanceCheck "scenelod"
	Radius 40
	Height 64
}

ACTOR TankPanzer3 : ModelBase
{
	//$Category Models (Wolf3D)/Vehicles
	//$Title Tank (Panzer III)
	//$Color 3
	Radius 4
	Height 64
	DistanceCheck "scenelod"
}

ACTOR Cruiser : ModelBase
{
	//$Category Models (Wolf3D)/Ships
	//$Title Battleship
	//$Color 3
	DistanceCheck "scenelod"
	Radius 4
	Height 4
}

ACTOR FlakVierling3D : SwitchableDecoration
{
	//$Category Models (Wolf3D)/Mission
	//$Title Flak Vierling
	//$Color 3
	
	//$Arg0 "Type"
	//$Arg0Tooltip "Pickup the desired type\nSolid: 0\nNon-Solid: 1"
	
	Radius 80
	Height 88
	Mass 0x7fffffff
	Health -1
	-FLOORCLIP
	-NOBLOCKMAP
	+CANPASS
	+DONTSPLASH
	+NOBLOOD
	+NOBLOODDECALS
	+NODAMAGE
	+NOGRAVITY
	+NOTAUTOAIMED
	+SHOOTABLE
	+SOLID
	States
	{
	Active:
		MDLA A 0 A_ChangeFlag("DORMANT", false) //update manually on GZDB
	Spawn:
		MDLA A -1 NoDelay A_JumpIf(Args[0]==1, "NonSolid")
		Stop
	Inactive:
		MDLA B 0 A_ChangeFlag("DORMANT", true) //update manually on GZDB
		MDLA B 0 A_PlaySound("weapons/explode", CHAN_AUTO, 1.0, FALSE, ATTN_NORM)
		MDLA B 0 A_Scream
		MDLA B 0 A_NoBlocking
		MDLA BBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBB 0 A_SpawnItemEx ("Debris_Tank", random(64,112), random(64,112), random(128,144), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		MDLA B 1 A_SpawnItemEx("Nuke",0,0,5,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
		Destroyed:
			MDLA B 8 A_CustomMissile("DarkSmoke2",32,0,random(0,360),2,random(70,130))
			Loop
	NonSolid: //Needed for Intromap - ozy81
		MDLA A 0 A_UnsetSolid
		MDLA A 0 {A_ChangeFlag("NOGRAVITY",false);A_ChangeFlag("CANPASS",false);A_ChangeFlag("FLOORCLIP",true);A_ChangeFlag("ACTLIKEBRIDGE",false);} //oh god.. - ozy81
		"####" A -1 A_SetInvulnerable
		Stop
	}
}

ACTOR Luftschiff : 3dObstacle
{
	//$Category Models (Wolf3D)/Ships
	//$Title Luftschiff LZ130
	//$Color 3
	DistanceCheck "scenelod"
	-SOLID
	+NOGRAVITY
	Radius 32
	Height 32
}

//Return to Castle Wolfenstein models
ACTOR RTCWGenerator : 3dObstacle
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Generator
	//$Color 3
	DistanceCheck "scenelod"
	Radius 16
	Height 32
}

ACTOR RTCWChalice : 3dObstacle
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Holy Graal
	//$Color 3
	DistanceCheck "scenelod"
	Radius 8
	Height 32
}

ACTOR RTCWClock : 3dObstacle //fixed by mxd
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Clock
	//$Color 3
	DistanceCheck "scenelod"

	Radius 12
	Height 112

	+SYNCHRONIZED

	States
	{
	Spawn:
		MDLA A 0 NoDelay A_SpawnItemEx("RTCWClockPendulum", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPITCH | SXF_TRANSFERROLL | SXF_TRANSFERSCALE)
		Goto TickLoop

	TickLoop: // Tried adding those sound effects to RTCWClockPendulum, found out that it horribly messes model interpolation...
		MDLA A 44 A_PlaySound("oldclock/tick")
		MDLA A 0 A_Jump(1, "Chime")
		Loop

	Chime:
		MDLA A 278 A_PlaySound("oldclock/chime", CHAN_ITEM)
		Goto TickLoop
	}
}

ACTOR RTCWClockPendulum : 3dObstacle //mxd
{
	DistanceCheck "scenelod"
	+SYNCHRONIZED

	States
	{
	Spawn:
		MDLA ABCDEFGHIJKLKJIHGFEDCB 4
		Loop
	}
}

ACTOR RTCWCar : 3dObstacle
{
	//$Category Models (Wolf3D)/Vehicles
	//$Title Staff Car
	//$Color 3

	Radius 32
	Height 32
	DistanceCheck "scenelod"
	States
	{
	Spawn:
		MDLA A -1 NoDelay A_SpawnItemEx("RTCWCarGlass", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPITCH | SXF_TRANSFERROLL | SXF_TRANSFERSCALE)
		Stop
	}
}

ACTOR RTCWCarGlass : 3dGlass
{
	DistanceCheck "scenelod"
}

ACTOR FlagWave : ModelBase
{
	//$Category Models (Wolf3D)/Flags
	//$Title Waving Flag (Swastika)
	//$Color 3
	DistanceCheck "scenelod"
	Radius 1
	Height 1
	Mass 5
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(64,12)
		TNT1 A 0 A_Jump(84,24)
		TNT1 A 0 A_Jump(104,36)
		TNT1 A 0 A_Jump(124,48)
		MDLA ABCDEFGHIJKLMNOPQRSTU 1
		MDLA VWXYZ 1
		MDLB ABCDEFGHIJKLMNOPQRSTUV 1
		MDLB WXYZ 1
		MDLC ABCDEFGHIJKLMNOPQRSTU 1
		MDLC VWXYZ 1
		MDLD ABCDEFGHIJKLMNOPQR 1
		Goto Spawn+4
	}
}

ACTOR FlagWave_SS : FlagWave
{
	//$Title Waving Flag (SS)
	DistanceCheck "scenelod"
}

ACTOR FlagWave_War : FlagWave
{
	//$Title Waving Flag (War Times)
	DistanceCheck "scenelod"
}

ACTOR FlagWide_Flutter : ModelBase
{
	//$Category Models (Wolf3D)/Flags
	//$Title Waving Flag, Wide (Swastika)
	//$Color 3
	DistanceCheck "scenelod"
	Radius 1
	Height 1
	Mass 5
	+NOINTERACTION
	States
	{
	Spawn:
		MDLA ABCDEFGHIJKLMNOP 8
		Loop
	}
}

ACTOR FlagWide_Hang : FlagWide_Flutter
{
	//$Title Hanging Flag, Wide (Swastika)
	DistanceCheck "scenelod"
	States
	{
	Spawn:
		MDLA A -1
		Stop
	}
}

ACTOR FlagShort_Flutter : FlagWide_Flutter
{
	//$Title Waving Flag, Short (Swastika)
	DistanceCheck "scenelod"
}

ACTOR FlagShort_Hang : FlagWide_Flutter
{
	//$Title Hanging Flag, Short (Swastika)
	DistanceCheck "scenelod"
	States
	{
	Spawn:
		MDLA A -1
		Stop
	}
}

ACTOR Typewriter : 3dObstacle
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Typewriter
	//$Color 3
	DistanceCheck "scenelod"
	Radius 16
	Height 16
}

ACTOR RadioModern : 3dObstacle
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Radio (modern)
	//$Color 3
	DistanceCheck "scenelod"
	Radius 16
	Height 20
}

ACTOR RTCWParlor : SwitchableDecoration
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Parlor Radio (interactive)
	//$Color 3
	DistanceCheck "scenelod"
	Radius 8
	Height 8
	Mass 0x7fffffff
	Health -1
	-FLOORCLIP
	-NOBLOCKMAP
	+CANPASS
	+DONTSPLASH
	+NOBLOOD
	+NOBLOODDECALS
	+NODAMAGE
	+NOTAUTOAIMED
	+SHOOTABLE
	+SOLID
	+SYNCHRONIZED
	+USESPECIAL
	Activation THINGSPEC_Switch
	States
	{
	Active:
		MDLA A 0 A_PlaySound("radios/loops", CHAN_ITEM)
	Spawn:
	  MDLA A -1
		Stop
	Inactive:
		MDLA A 0 A_StopSound(CHAN_ITEM)
		Goto Spawn
	}
}

ACTOR RTCWPhone : 3dObstacle
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Phone
	//$Color 3
	DistanceCheck "scenelod"
	Radius 8
	Height 8
}

ACTOR RTCWGramo : SwitchableDecoration
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Gramophone (interactive)
	//$Color 3
	DistanceCheck "scenelod"
	Radius 16
	Height 32
	Mass 0x7fffffff
	Health -1
	-FLOORCLIP
	-NOBLOCKMAP
	+CANPASS
	+DONTSPLASH
	+NOBLOOD
	+NOBLOODDECALS
	+NODAMAGE
	+NOTAUTOAIMED
	+SHOOTABLE
	+SOLID
	+SYNCHRONIZED
	+USESPECIAL
	Activation THINGSPEC_Switch
	States
	{
	Active:
		MDLA A 0 A_PlaySound("WALKURE", CHAN_ITEM)
	Spawn:
	  MDLA A -1
		Stop
	Inactive:
		MDLA A 0 A_StopSound(CHAN_ITEM)
		Goto Spawn
	}
}

ACTOR EnigmaMachine : 3dObstacle
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Enigma Machine
	//$Color 3
	DistanceCheck "scenelod"
	Radius 16
	Height 8
}

ACTOR RTCWBench : 3dObstacle
{
	//$Category Models (Wolf3D)/Furniture
	//$Title Wooden Bench
	//$Color 3
	DistanceCheck "scenelod"
	Radius 16
	Height 8
}

ACTOR 3dBook1 : 3dObstacle
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Book (front)
	//$Color 3
	DistanceCheck "scenelod"
	Radius 16
	Height 8
}

ACTOR 3dBook2 : 3dBook1
{
	//$Title Book (back)
	DistanceCheck "scenelod"
}

ACTOR RTCWShield : 3dObstacle
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Shield with Swords
	//$Color 3
	DistanceCheck "scenelod"
	Radius 8
	Height 32
	+NOGRAVITY
}

ACTOR RTCWParachute1 : ModelBase
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Parachute (Ragged One)
	//$Color 3
	DistanceCheck "scenelod"
}

ACTOR RTCWParachute2 : ModelBase
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Parachute (Open One)
	//$Color 3
	DistanceCheck "scenelod"
}

ACTOR RTCW_V2_Complete : ModelBase
{
	//$Category Models (Wolf3D)/Scenery
	//$Title V2 (Complete)
	//$Color 3
	DistanceCheck "scenelod"
	Radius 46
	Height 192
	States
	{
	Spawn:
		MDLA A -1
		Stop
	}
}

ACTOR RTCW_V2_NoRamp : RTCW_V2_Complete
{
	//$Title V2 (Without Ramp)
	DistanceCheck "scenelod"
}

ACTOR RTCW_V2_Rocket : RTCW_V2_Complete
{
	//$Title V2 (Rocket Only)
	DistanceCheck "scenelod"
}

//MaxED stuff
ACTOR Trample : BlastEffect //ozy81
{
	Radius 56
	Height 56
	DamageType Trample
	+NOGRAVITY
	States
	{
	Spawn:
		TNT1 A 0 
	Death:
		"####" # 20 A_Explode(5, Radius, 0, false, Radius) //100dmg
		Stop
	}
}

ACTOR ModelGMCTruck : SwitchableDecoration //ozy81
{
	//$Category Models (Wolf3D)/Vehicles
	//$Title Truck (US, MoH)
	//$Color 3
	Mass 0x7fffffff
	Health -1
	-FLOORCLIP
	-NOBLOCKMAP
	+CANPASS
	+DONTSPLASH
	+NOBLOOD
	+NOBLOODDECALS
	+NODAMAGE
	+NOTAUTOAIMED
	+SHOOTABLE
	States
	{
	Active:
		MDLA A 0 A_ChangeFlag("DORMANT", false) //mxd. DORMANT flag must be updated manually
		TrampleIsOn:
		MDLA A 1 A_SpawnItemEx("Trample", ScaleX*128, 0, Height-16, 0, 0, 0, 0, SXF_TRANSFERPITCH | SXF_TRANSFERROLL | SXF_TRANSFERSCALE)
		Loop
	Spawn:
	  MDLA A -1
		Stop
	Inactive:
		MDLA A -1 A_ChangeFlag("DORMANT", true) //mxd. DORMANT flag must be updated manually
		Stop
	}
}

ACTOR ModelOpelTruck : 3dObstacle
{
	//$Category Models (Wolf3D)/Vehicles
	//$Title Opel Blitz Truck (Afrika Korps or Wehrmacht)
	//$Color 3
	
	//$Arg0 "Type"
	//$Arg0Tooltip "Pickup the desired type\nAfrika: 0\nWehrmacht: 1"
	Radius 24
	Height 24
	+NOGRAVITY
	DistanceCheck "scenelod"
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		TNT1 A 0 A_JumpIf(Args[0]==1, "Wehrmacht")
		Random: //fall through if set at 0
		MDLA A -1 A_SpawnItemEx("ModelOpelTruckWindows", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPITCH | SXF_TRANSFERROLL | SXF_TRANSFERSCALE)
		Stop
		Wehrmacht:
		MDLA B -1 A_SpawnItemEx("ModelOpelTruckWindows", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPITCH | SXF_TRANSFERROLL | SXF_TRANSFERSCALE)
		Stop
	}
}

ACTOR ModelOpelTruckWindows : ModelBase
{
	DistanceCheck "scenelod"
	RenderStyle "Shaded"
	StencilColor "CC CC CC"
}

ACTOR WallLight1 : 3dObstacle
{
	//$Category Light Sources (Wolf3D)/Static
	//$Title Wall Light (off, static)
	//$Color 11
	Radius 16
	Height 16
	+NOGRAVITY
	-SOLID
}

ACTOR WallLight1Lit : WallLight1
{
	//$Title Wall Light (on, static)
	States
	{
	Spawn:
		MDLA A 0 NoDelay A_SpawnItemEx("VolumetricLight_ConeDown", ScaleX*16, 0, 24, 0, 0, 0, 0, SXF_ISTRACER) //for Inactive states, use A_KillTracer to remove the Volumetric light again
		MDLA A -1 LIGHT(OZYWALLT)
		Stop
	}
}

ACTOR WallLight2 : 3dObstacle
{
	//$Category Light Sources (Wolf3D)/Static
	//$Title Wall Lantern (off, static)
	//$Color 11
	Radius 14
	Height 48
	-SOLID
	+NOGRAVITY
}

ACTOR WallLight2Lit : WallLight2
{
	//$Title Wall Lantern (on, static)
	States
	{
	Spawn:
		MDLA A -1 LIGHT(OZYWALLT)
		Stop
	}
}

ACTOR SimpleLight1 : SwitchableDecoration
{
	//$Category Light Sources (Wolf3D)
	//$Title Simple Light (off, Set DORMANT & activate it via ACS)
	//$Color 11
	Radius 8
	Height 12
	Mass 0x7fffffff
	Health -1
	-FLOORCLIP
	-NOBLOCKMAP
	+DONTSPLASH
	+NOBLOOD
	+NOBLOODDECALS
	+NODAMAGE
	+NOGRAVITY
	+NOTAUTOAIMED
	+SHOOTABLE
	States
	{
	Active:
		MDLA A 0 A_ChangeFlag("DORMANT", false)
	Spawn:
	  MDLA A 0 NoDelay
	Activate:
		MDLA A -1
		Loop
	Inactive:
		MDLA A 0 A_SpawnItemEx("VolumetricLight_ConeDown", 0, 0, ScaleX+ScaleY*24, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE)
	InactiveLights:
		MDLA A 0 A_ChangeFlag("DORMANT", true)
		MDLA A 0 A_RemoveChildren(TRUE, RMVF_MISC)
		MDLA B -1 LIGHT(OZYSIMLT)
		Stop	
	}
}

ACTOR SimpleLight1Lit : SimpleLight1
{
	//$Title Simple Light (on, Set DORMANT & activate it via ACS)
	States
	{
	Active:
		MDLA A 0 A_ChangeFlag("DORMANT", false)
	Spawn:
	  MDLA A 0 NoDelay
	ActiveLights:
		MDLA A 0 A_SpawnItemEx("VolumetricLight_ConeDown", 0, 0, ScaleX+ScaleY*24, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE)
	Activate:
		MDLA A 1 LIGHT(OZYSIMLT)
		Loop
	Inactive:
		MDLA A 0 A_ChangeFlag("DORMANT", true)
		MDLA A 0 A_RemoveChildren(TRUE, RMVF_MISC)
		MDLA B -1
		Stop	
	}
}

ACTOR IntermapSkylight : 3dObstacle
{
	//$Category Models (Wolf3D)/Intermap
	//$Title INTERMAP Skylight
	//$Color 3

	Radius 64
	Height 16

	+NOGRAVITY
}

ACTOR LighthouseBeam : IntermapSkylight
{
	//$Title Lighthouse Beam

	RenderStyle "Add"

	+BRIGHT

	States
	{
		Spawn:
			MDLA A -1
			Stop
	}
}

ACTOR FuelDrum : 3dObstacle
{
	//$Category Models (Wolf3D)/Obstacles
	//$Title Fuel Drum
	//$Color 3

	Radius 16
	Height 42
}

ACTOR FuelDrumAstro //Uses JK2 3dModel
{
	//$Category Models (Wolf3D)/Obstacles
	//$Title Fuel Drum (Astrostein, pushable)
	//$Color 3

	-NOGRAVITY 			//or it will move towards the void without falling
	+CANPASS
	+MOVEWITHSECTOR	//I add this because we may need it for some "arcade" sequences -ozy
	+NOBLOOD
	+NODAMAGE
	+PUSHABLE
	+SHOOTABLE
	+SLIDESONWALLS	//let's prevent some weird behaviors, like being stuck in a map because the crate can't get out from an angle -ozy
	+SOLID

	Radius 24
	Height 56
	Mass 15000
	
	States
	{
	Spawn:
		MDLA A 1
		Goto Look
	
	Look:
		MDLA A 8 A_LookEx(LOF_NOSOUNDCHECK, 0, 64, 0, 360) 
		Loop
	
	See:
		MDLA A 0 A_JumpIf((velx != 0 || vely != 0), "Moving")
		Goto Spawn
	
	Moving:
		MDLA A 35 A_PlaySound("astrodrum_moving", CHAN_AUTO, 0.65)
		Goto Spawn
	}
}

ACTOR MovableCrate1 : FuelDrumAstro
{
	//$Title Movable Crate (paper, 64)

	Radius 32
	Height 64
	Mass 9000
	
	States
	{
	Moving:
		MDLA A 35 A_PlaySound("papercrate_moving", CHAN_AUTO, 0.65)
		Goto Spawn
	}	
}

ACTOR MovableCrate2 : MovableCrate1 
{
	//$Title Movable Crate (paper, 32)

	Radius 16
	Height 32
	Mass 4500
}

//Easter eggs
ACTOR Tie_Fighter : 3dObstacle //ozy81
{
	//$Category Models (Wolf3D)/Astrostein
	//$Title Tie Fighter (Easteregg)
	//$Color 3
	Radius 48
	Height 192
}

//DoomJuan Models, added & fixed a bit by Ozymandias81
ACTOR 3dEagle : 3dObstacle
{
	//$Category Models (Wolf3D)/Scenery
	//$Title SS Eagle
	//$Color 3
	DistanceCheck "scenelod"
	-SOLID
	-CANPASS
	+NOBLOCKMAP
}

ACTOR Lion_Left : 3dObstacle
{
	//$Category Models (Wolf3D)/Statues
	//$Title Lion Statue (Left)
	//$Color 3
	DistanceCheck "scenelod"
	Radius 48
	Height 192
}

ACTOR Lion_Right : Lion_Left
{
	//$Title Lion Statue (Right)
	DistanceCheck "scenelod"
}

ACTOR NaziBannerTallFlutter512 : FlagWave
{
	//$Title Nazi Banner Tall, Flutter (512)
	DistanceCheck "scenelod"
	States
	{
	Spawn:
		MDLA ABCDEFGHIJKLMNOP 8
		Loop
	}
}

ACTOR NaziBannerTallHang512 : FlagWave
{
	//$Title Nazi Banner Tall, Not Moving (512)
	DistanceCheck "scenelod"
	States
	{
	Spawn:
		MDLA A -1
		Stop
	}
}

ACTOR NaziBannerTallFlutter256 : NaziBannerTallFlutter512
{
	//$Title Nazi Banner Tall, Flutter (256)
	DistanceCheck "scenelod"
}

ACTOR NaziBannerTallHang256 : NaziBannerTallHang512
{
	//$Title Nazi Banner Tall, Not Moving (256)
	DistanceCheck "scenelod"
}

ACTOR TrestleTable : 3dObstacle
{
	//$Category Models (Wolf3D)/Furniture
	//$Title Trestle Table
	//$Color 3
	DistanceCheck "scenelod"
	Radius 40
	Height 34
}

//HERETIC II, HEXEN II and JEDI KNIGHT 3 MODELS - From Gore - Ozymandias81
ACTOR Caesar : 3dObstacle
{
	//$Category Models (Wolf3D)/Statues
	//$Title Caesar's Statue
	//$Color 3
	
	//$Arg0 "Type"
	//$Arg0Tooltip "Pickup the desired type\nSolid: 0\nNon-Solid: 1"
	
	DistanceCheck "scenelod"
	Radius 32
	Height 192
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		TNT1 A 0 A_JumpIf(Args[0]==1, "NonSolid")
	Solid:
		MDLA A -1
		Stop
	NonSolid:
		MDLA A 1 A_UnSetSolid
		Wait
	}
}

ACTOR King : Caesar
{
	//$Category Models (Wolf3D)/Statues
	//$Title King's Statue
	//$Color 3
	DistanceCheck "scenelod"
	Radius 48
}

ACTOR MineCartF : 3dObstacle
{
	//$Category Models (Wolf3D)/Scenery
	//$Title "Mine Cart, Full"
	//$Color 3
	DistanceCheck "scenelod"
	Radius 34
	Height 24
	+NOGRAVITY
}

ACTOR MineCartE : MineCartF
{
	//$Title "Mine Cart, Empty"
	DistanceCheck "scenelod"
}

ACTOR MineCartEMoving : MineCartF
{
	//$Title "Mine Cart, Empty (for movement sequence)"
	Activation THINGSPEC_MonsterTrigger
	+BUMPSPECIAL
}

ACTOR MineCartW : MineCartF
{
	//$Title "Mine Cart, Wrecked"
	DistanceCheck "scenelod"
}

ACTOR 3dMinerals : 3dObstacle
{
	//$Category Models (Wolf3D)/Scenery
	//$Title "Minerals"
	//$Color 3
	DistanceCheck "scenelod"
	Radius 16
	Height 16
	+NOGRAVITY
	States
	{
	Spawn:
		MDLA A -1 LIGHT(MINERALLITE)
		Stop
	}
}

//Sitters models
ACTOR Cable1 : ModelBase
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Cable A
	//$Color 3
	DistanceCheck "scenelod"
	Height 0
	+SPAWNCEILING
}

ACTOR Cable2: Cable1
{
	//$Title Cable B
	DistanceCheck "scenelod"
}

ACTOR Cable3: Cable1
{
	//$Title Cable C
	DistanceCheck "scenelod"
}

ACTOR Cable4: Cable1
{
	//$Title Cable D
	DistanceCheck "scenelod"
}

ACTOR Cable5: Cable1
{
	//$Title Cable E
	DistanceCheck "scenelod"
}

ACTOR 3dTrolley : 3dObstacle
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Trolley
	//$Color 3
	DistanceCheck "scenelod"
	Radius 16
	Height 48
	+NOGRAVITY
}

ACTOR 3dCannon : Caesar
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Cannon
	//$Color 3
	DistanceCheck "scenelod"
	Radius 16
	Height 48
	+NOGRAVITY
}

ACTOR 3dCannonballs: 3dCannon
{
	//$Title Cannonballs
	DistanceCheck "scenelod"
}

ACTOR 3dGuillotine : Caesar
{
	//$Category Models (Wolf3D)/Occult
	//$Title Guillotine
	//$Color 3
	DistanceCheck "scenelod"
	Radius 16
	Height 48
	+NOGRAVITY
}

ACTOR 3dHeadCrusher : 3dGuillotine
{
	//$Title Head Crusher
	DistanceCheck "scenelod"
}

ACTOR 3dJudasCrusher: 3dGuillotine
{
	//$Title Judas Crusher
	DistanceCheck "scenelod"
}

ACTOR 3dPillory : 3dGuillotine
{
	//$Title Pillory
	DistanceCheck "scenelod"
}

ACTOR 3dRack: 3dGuillotine
{
	//$Title Rack
	DistanceCheck "scenelod"
}

ACTOR 3dSplitter: 3dGuillotine
{
	//$Title Splitter
	DistanceCheck "scenelod"
}

ACTOR 3dSkeleton_Sit : Caesar
{
	//$Category Models (Wolf3D)/Occult
	//$Title Skeleton (Sit)
	//$Color 3
	DistanceCheck "scenelod"
	Radius 16
	Height 24
	+NOGRAVITY
}

//S.T.A.L.K.E.R. models
ACTOR 3dPrometheus_L : Caesar
{
	//$Category Models (Wolf3D)/Statues
	//$Title Prometheus Statue (Left Side)
	//$Color 3
	DistanceCheck "scenelod"
	Radius 32
	Height 96
}

ACTOR 3dPrometheus_R: 3dPrometheus_L
{
	//$Title Prometheus Statue (Right Side)
	DistanceCheck "scenelod"
}

//TF3DM / 3dRegenerator Models
ACTOR Female_StatueA : Caesar
{
	//$Title Female Statue (without leaves)
	DistanceCheck "scenelod"
	Radius 24
	Height 72
}

ACTOR Female_StatueB : Female_StatueA
{
	//$Title Female Statue (with leaves)
}

ACTOR MercedesLimo : 3dObstacle
{
	//$Category Models (Wolf3D)/Vehicles
	//$Title Mercedes Nazi Limo
	//$Color 3
	Radius 32
	Height 32
	DistanceCheck "scenelod"
	States
	{
	Spawn:
		MDLA A -1 NoDelay A_SpawnItemEx("LimoGlass", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPITCH | SXF_TRANSFERROLL | SXF_TRANSFERSCALE)
		Stop
	}
}

ACTOR LimoGlass : 3dGlass
{
	DistanceCheck "scenelod"
}

ACTOR Kubelwagen : 3dObstacle
{
	//$Category Models (Wolf3D)/Vehicles
	//$Title Kubelwagen
	//$Color 3
	Radius 32
	Height 32
	DistanceCheck "scenelod"
	States
	{
	Spawn:
		MDLA A -1 NoDelay A_SpawnItemEx("KubelGlass", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPITCH | SXF_TRANSFERROLL | SXF_TRANSFERSCALE)
		Stop
	}
}

ACTOR KubelGlass : 3dGlass
{
	DistanceCheck "scenelod"
}

ACTOR OldCar_Spawner : 3dObstacle
{
	//$Category Models (Wolf3D)/Vehicles
	//$Title Old Car Spawner
	//$Color 3
	
	//$Arg0 "Color"
	//$Arg0Tooltip "Pickup the desired color\nRandom: 0\nOlive: 1\nRed: 2\nCyan: 3"
	
	Height 64
	+NOGRAVITY
	DistanceCheck "scenelod"
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		TNT1 A 0 A_JumpIf(Args[0]==1, "Olive")
		TNT1 A 0 A_JumpIf(Args[0]==2, "Red")
		TNT1 A 0 A_JumpIf(Args[0]==2, "Cyan")
		Random: //fall through if set at 0
		MDLA A -1 A_Jump(128,1)
		MDLA B -1 A_Jump(128,1)
		MDLA C -1 
		Stop
		Olive:
		MDLA A -1
		Stop
		Red:
		MDLA B -1
		Stop
		Cyan:
		MDLA C -1
		Stop
	}
}

ACTOR Tractor : 3dObstacle
{
	//$Category Models (Wolf3D)/Vehicles
	//$Title Tractor
	//$Color 3
	Radius 32
	Height 32
	DistanceCheck "scenelod"
}

ACTOR 3dPiano : 3dObstacle
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Piano
	//$Color 3
	DistanceCheck "scenelod"
	Radius 56
	Height 48
}

ACTOR Bunk_Bed1 : 3dObstacle
{
	//$Category Models (Wolf3D)/Furniture
	//$Title Bunk Bed (single)
	//$Color 3
	DistanceCheck "scenelod"
	Radius 24
	Height 24
}

ACTOR Bunk_Bed2 : Bunk_Bed1
{
	//$Title Bunk Bed (double)
	DistanceCheck "scenelod"
	Height 72
}

//CODs Stuff - Ozy81
ACTOR COD_Gascan : 3dObstacle
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Gascan
	//$Color 3
	DistanceCheck "scenelod"
	Radius 8
	Height 32
}

ACTOR COD_FemaleBust : 3dObstacle
{
	//$Category Models (Wolf3D)/Statues
	//$Title Female Bust (silver or bronze)
	//$Color 3
	DistanceCheck "scenelod"
	
	//$Arg0 "Material"
	//$Arg0Tooltip "Choose the desired material\nSilver: 0\nBronze: 1"
	
	Radius 8
	Height 24
	+NOGRAVITY
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		TNT1 A 0 A_JumpIf(Args[0]==1, "Bronze")
		Silver: //fall through if set at 0
		MDLA A -1
		Stop
		Bronze:
		MDLA B -1
		Stop
	}
}

ACTOR COD_EagleBust : COD_FemaleBust
{
	//$Title Eagle Bust (silver or bronze)
	DistanceCheck "scenelod"
}

ACTOR COD_HitlerBust : COD_FemaleBust
{
	//$Title Hitler's Bust (bronze or stone)
	DistanceCheck "scenelod"
	
	//$Arg0 "Material"
	//$Arg0Tooltip "Choose the desired material\nBronze: 0\nStone: 1"
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		TNT1 A 0 A_JumpIf(Args[0]==1, "Stone")
		Bronze: //fall through if set at 0
		MDLA A -1
		Stop
		Stone:
		MDLA B -1
		Stop
	}
}

ACTOR COD_Tiger1 : ModelBase
{
	//$Category Models (Wolf3D)/Vehicles
	//$Title Tank (Tiger I)
	//$Color 3
	Radius 4
	Height 64
	DistanceCheck "scenelod"
}

ACTOR COD_RadioSimple : SwitchableDecoration
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Radio (interactive, destroyable, simple)
	//$Color 3
	DistanceCheck "scenelod"
	Radius 16
	Height 16
	Mass 0x7fffffff
	Health 25
	-FLOORCLIP
	-NOBLOCKMAP
	+CANPASS
	+DONTSPLASH
	+NOBLOOD
	+NOBLOODDECALS
	+NOTAUTOAIMED
	+SHOOTABLE
	+SOLID
	+SYNCHRONIZED
	+USESPECIAL
	Activation THINGSPEC_Switch
	States
	{
	Active:
		MDLA A 0 A_PlaySound("SPEECH1", CHAN_ITEM)
	Spawn:
		MDLA A -1
		Stop
	Inactive:
		MDLA A 0 A_StopSound(CHAN_ITEM)
		Goto Spawn
	Death:
		MDLA B 0 A_StopSound(CHAN_ITEM)
		"####" B 0 A_PlaySound("METALBRK", frandom (0.5,0.8))
		"####" B 0 A_PlaySound("tesla/loop",6,1,1)
		"####" B 0 A_ChangeFlag("USESPECIAL", false)
		"####" B 0 A_UnSetSolid
		"####" B 0 A_SpawnItemEx("KD_HL2SmokeGenerator", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_TRANSFERSCALE)
		"####" BBBBBB 3 A_SpawnItemEx("Spark_B1", ScaleX*24, frandom(-8.0,8.0), ScaleY*8, 0, frandom(-1.0,1.0), frandom(-1.0,1.0), random(0,360), SXF_CLIENTSIDE)
		"####" B 0 A_StopSound(6)
		"####" BBBBBB 3 A_SpawnItemEx("Spark_B1", ScaleX*24, frandom(-8.0,8.0), ScaleY*8, 0, frandom(-1.0,1.0), frandom(-1.0,1.0), random(0,360), SXF_CLIENTSIDE)
		"####" B -1
		Stop
	}
}

ACTOR COD_Radio_Tall : 3dObstacle
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Radio, Tall
	//$Color 3
	DistanceCheck "scenelod"
	Radius 8
	Height 32
	+FIXMAPTHINGPOS
	+NOGRAVITY
}

ACTOR COD_Radio_Tall_D : COD_Radio_Tall
{
	//$Title Radio, Tall (Blown)
	DistanceCheck "scenelod"
}

ACTOR COD_SSHorch : 3dObstacle
{
	//$Category Models (Wolf3D)/Vehicles
	//$Title Truck, Horch-1A (Nazi)
	//$Color 3

	Radius 12
	Height 52
	DistanceCheck "scenelod"
	States
	{
		Spawn:
		MDLA A 0 NoDelay A_SpawnItemEx("COD_SSHorch_Glass", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPITCH | SXF_TRANSFERROLL | SXF_TRANSFERSCALE)
		MDLA A 0 A_SpawnItemEx("InvisibleBridge32",-48,0,20)
		MDLA A 0 A_SpawnItemEx("InvisibleBridge32",80,0,36)
		MDLA A -1 A_SpawnItemEx("InvisibleBridge32",88,0,16)
		Stop
	}
}

ACTOR COD_SSHorch_Glass : 3dGlass
{
	DistanceCheck "scenelod"
}

ACTOR COD_USJeep : 3dObstacle
{
	//$Category Models (Wolf3D)/Vehicles
	//$Title Jeep (US)
	//$Color 3
	
	//$Arg0 "Shape"
	//$Arg0Tooltip "Change the variant\nDamaged: 1"

	Radius 12
	Height 52
	DistanceCheck "scenelod"
	States
	{
		Spawn:
		TNT1 A 0 NoDelay
		TNT1 A 0 A_JumpIf(Args[0]==1, "Damaged")
		Normal:
		MDLA A 0 A_SpawnItemEx("COD_USJeep_Glass", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPITCH | SXF_TRANSFERROLL | SXF_TRANSFERSCALE)
		MDLA A 0 A_SpawnItemEx("InvisibleBridge32",-48,0,20)
		MDLA A 0 A_SpawnItemEx("InvisibleBridge32",80,0,36)
		MDLA A -1 A_SpawnItemEx("InvisibleBridge32",88,0,16)
		Stop
		Damaged:
		MDLA B 0 A_UnsetSolid
		DmgLoop:
		MDLA B 8 A_CustomMissile("DarkSmoke2",32,0,random(0,360),2,random(70,130))
		Loop
	}
}

ACTOR COD_USJeep_Glass : 3dGlass
{
	DistanceCheck "scenelod"
}

ACTOR CODBike : 3dObstacle
{
	//$Category Models (Wolf3D)/Vehicles
	//$Title Old Bike
	//$Color 3
	
	//$Arg0 "Shape"
	//$Arg0Tooltip "Pickup the desired shape\nRandom: 0\nClean: 1\nBroken: 2"
	
	Radius 16
	Height 32
	+NOGRAVITY
	DistanceCheck "scenelod"
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		TNT1 A 0 A_JumpIf(Args[0]==1, "Clean")
		TNT1 A 0 A_JumpIf(Args[0]==2, "Broken")
		Random: //fall through if set at 0
		MDLA A -1 A_Jump(128,1)
		MDLA B -1 A_UnsetSolid
		Stop
		Clean:
		MDLA A -1
		Stop
		Broken:
		MDLA B -1 A_UnsetSolid
		Stop
	}
}

ACTOR CODOmahaBoat : FlakVierling3D
{
	//$Category Models (Wolf3D)/Ships
	//$Title Landing Boat
	//$Color 3
	States
	{
	Active:
		MDLA A 0 A_ChangeFlag("DORMANT", false) //update manually on GZDB
	Spawn:
		MDLA A -1
		Stop
	Inactive:
		MDLA B 0 A_ChangeFlag("DORMANT", true) //update manually on GZDB
		MDLA B 0 A_PlaySound("weapons/explode", CHAN_AUTO, 1.0, FALSE, ATTN_NORM)
		MDLA B 0 A_Scream
		MDLA B 0 A_NoBlocking
		MDLA BBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBB 0 A_SpawnItemEx ("Debris_Tank", random(64,112), random(64,112), random(128,144), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		MDLA B 1 A_SpawnItemEx("Nuke",0,0,5,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
		Destroyed:
			MDLA B 8 A_CustomMissile("DarkSmoke2",32,0,random(0,360),2,random(70,130))
			Loop
	}
}

ACTOR CODStuka : ModelBase
{
	//$Category Models (Wolf3D)/Planes
	//$Title Stuka Airplane (Static)
	//$Color 3
	
	States
	{
	Spawn:
		MDLA A -1 NoDelay A_SpawnItemEx("CODStuka_Glass", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPITCH | SXF_TRANSFERROLL | SXF_TRANSFERSCALE)
		Stop
	}
}

ACTOR CODStuka_Glass : 3dGlass {}

ACTOR CODStuka_Flight : ModelBase
{
	Projectile
	Speed 1
	Height 1
	Radius 1
}

ACTOR CODStuka_Flight_Fog : CODStuka_Flight {}

ACTOR TMPlane : CODStuka
{
	//$Category Misc (Wolf3D)/Titlemap
	//$Title Titlemap Stuka
}

ACTOR CODSideCar : 3dObstacle
{
	//$Category Models (Wolf3D)/Vehicles
	//$Title SideCar
	//$Color 3
	Radius 56
	Height 48
	DistanceCheck "scenelod"
}

ACTOR CODCivCarD_Spawner : 3dObstacle
{
	//$Category Models (Wolf3D)/Vehicles
	//$Title Civilian Car Spawner (Destroyed)
	//$Color 3
	
	//$Arg0 "Color"
	//$Arg0Tooltip "Pickup the desired color\nRandom: 0\nTan: 1\nRed: 2\nBlue: 3"
	Height 64
	+NOGRAVITY
	DistanceCheck "scenelod"
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		TNT1 A 0 A_JumpIf(Args[0]==1, "Tan")
		TNT1 A 0 A_JumpIf(Args[0]==2, "Red")
		TNT1 A 0 A_JumpIf(Args[0]==2, "Blue")
		Random: //fall through if set at 0
		MDLA A -1 A_Jump(128,1)
		MDLA B -1 A_Jump(128,1)
		MDLA C -1 
		Stop
		Tan:
		MDLA A -1
		Stop
		Red:
		MDLA B -1
		Stop
		Blue:
		MDLA C -1
		Stop
	}
}

ACTOR CODScheinwerfer : SwitchableDecoration
{
	//$Category Models (Wolf3D)/Vehicles
	//$Title Scheinwerfer (Set DORMANT & activate it via ACS)
	//$Color 3
	Radius 64
	Height 80
	Mass 0x7fffffff
	Health -1
	-FLOORCLIP
	-NOBLOCKMAP
	+CANPASS
	+DONTSPLASH
	+NOBLOOD
	+NOBLOODDECALS
	+NODAMAGE
	+NOGRAVITY
	+NOTAUTOAIMED
	+SHOOTABLE
	+SOLID
	BloodType "TankSpark"
	States
	{
	Active:
		MDLA A 0 A_ChangeFlag("DORMANT", false) //update manually on GZDB
	Spawn:
		MDLA A -1 LIGHT(SPOTLITE) A_SpawnItemEx("Flare3d", ScaleX*28, 0, ScaleY*92, 0, 0, 0, 0, SXF_TRANSFERPITCH | SXF_TRANSFERROLL | SXF_TRANSFERSCALE)
		Stop
	Inactive:
		MDLA B -1 A_ChangeFlag("DORMANT", true) //update manually on GZDB
		Stop	
	}
}

ACTOR CODNeoLights1 : SwitchableDecoration
{
	//$Category Light Sources (Wolf3D)
	//$Title Neoclassical Wall Light (Set DORMANT & activate it via ACS)
	//$Color 11
	Radius 8
	Height 24
	Mass 0x7fffffff
	Health -1
	-FLOORCLIP
	-NOBLOCKMAP
	+DONTSPLASH
	+NOBLOOD
	+NOBLOODDECALS
	+NODAMAGE
	+NOGRAVITY
	+NOTAUTOAIMED
	+SHOOTABLE
	States
	{
	Active:
		MDLA A 0 A_ChangeFlag("DORMANT", false) //update manually on GZDB
	Spawn:
		MDLA A -1 LIGHT(OZYWALLT)
		Stop
	Inactive:
		MDLA B -1 A_ChangeFlag("DORMANT", true) //update manually on GZDB
		Stop	
	}
}

ACTOR CODSignal_Light : 3dObstacle
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Train Signal Light (off/red/yel/green)
	//$Color 3
	DistanceCheck "scenelod"
	
	//$Arg0 "Light"
	//$Arg0Tooltip "Pickup the desired light\nOff: 0\nGreen: 1\nYellow: 2\nRed: 3"
	Radius 24
	Height 128
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		TNT1 A 0 A_JumpIf(Args[0]==1, "Green")
		TNT1 A 0 A_JumpIf(Args[0]==2, "Yellow")
		TNT1 A 0 A_JumpIf(Args[0]==3, "Red")
		Off: //fall through if set at 0
		MDLA A -1 
		Stop
		Green:
		MDLA B -1 LIGHT("SIGNLGRN")
		Stop
		Yellow:
		MDLA C -1 LIGHT("SIGNLYEL")
		Stop
		Red:
		MDLA D -1 LIGHT("SIGNLRED")
		Stop
	}
}

ACTOR CODRailway_Switch : SwitchableDecoration
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Train Railway Switch (Set DORMANT & activate it via ACS)
	//$Color 3
	DistanceCheck "scenelod"
	Radius 16
	Height 144
	Mass 0x7fffffff
	Health -1
	-FLOORCLIP
	-NOBLOCKMAP
	+CANPASS
	+DONTSPLASH
	+NOBLOOD
	+NOBLOODDECALS
	+NODAMAGE
	+NOGRAVITY
	+NOTAUTOAIMED
	+SHOOTABLE
	+SOLID
	States
	{
	Active:
		MDLA A 0 A_ChangeFlag("DORMANT", false) //update manually on GZDB
	Spawn:
		MDLA A -1
		Stop
	Inactive:
		MDLA B -1 A_ChangeFlag("DORMANT", true) //update manually on GZDB
		Stop	
	}
}

//Archibase.net Stuff
ACTOR CanyonRockSpawner : 3dObstacle //ozymandias81
{
	//$Category Models (Wolf3D)/Rocks
	//$Title Canyon Rock Spawner
	//$Color 3
	DistanceCheck "scenelod"
	
	//$Arg0 "Shape"
	//$Arg0Tooltip "Pickup the desired shape\nRandom: 0\nLarge: 1\nMedium: 2\nSmall: 3"
	Radius 24
	Height 24
	-CANPASS
	+MOVEWITHSECTOR
	+NOGRAVITY
	+RELATIVETOFLOOR
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		TNT1 A 0 A_JumpIf(Args[0]==1, "Large")
		TNT1 A 0 A_JumpIf(Args[0]==2, "Medium")
		TNT1 A 0 A_JumpIf(Args[0]==3, "Small")
		Random: //fall through if set at 0
		MDLA A -1 A_Jump(128,1)
		MDLA B -1 A_Jump(128,1)
		MDLA C -1 
		Stop
		Large:
		MDLA A -1
		Stop
		Medium:
		MDLA B -1
		Stop
		Small:
		MDLA C -1
		Stop
	}
}

ACTOR DryRockSpawner : CanyonRockSpawner
{
	//$Title Dry Rock Spawner
	DistanceCheck "scenelod"
}

ACTOR SnowyRockSpawner : CanyonRockSpawner
{
	//$Title Snowy Rock Spawner
	DistanceCheck "scenelod"
}

ACTOR MuddyRockSpawner : CanyonRockSpawner
{
	//$Title Muddy Rock Spawner
	DistanceCheck "scenelod"
}

ACTOR LunarRockSpawner : CanyonRockSpawner
{
	//$Title Lunar Rock Spawner (Astrostein)
	DistanceCheck "scenelod"
}

ACTOR SandRockSpawner : CanyonRockSpawner
{
	//$Title Sand Rock Spawner (Tunis)
	DistanceCheck "scenelod"
}

//NOLF1 Stuff
ACTOR RooftopAntennae : 3dObstacle
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Rooftop Antennae
	//$Color 3
	DistanceCheck "scenelod"
	Radius 16
	Height 64
}

//Mohofoz Stuff
ACTOR 3dSpeakers_1 : 3dObstacle
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Tannoy Speakers (Single)
	//$Color 3
	DistanceCheck "scenelod"
	
	Radius 8
	Height 8
	-CANPASS
}

//Hypnagogia Stuff
ACTOR NaziGlobe : Caesar
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Hitler's Globe
	//$Color 3
	DistanceCheck "scenelod"
	Radius 24
	Height 48
}

ACTOR 3dSpeakers_2 : 3dObstacle
{
	//$Category Models (Wolf3D)/Scenery
	//$Title Tannoy Speakers (Double)
	//$Color 3
	DistanceCheck "scenelod"
	
	Radius 32
	Height 96
}

ACTOR 3dSpeakers_4 : 3dSpeakers_2
{
	//$Title Tannoy Speakers (Four)
	DistanceCheck "scenelod"
}

//AFADoomer Stuff
ACTOR AstroShuttle : ModelBase
{
	//$Category Models (Wolf3D)/Astrostein
	//$Title Galileo's Shuttle
	//$Color 3
	DistanceCheck "scenelod"
	Height 32
	Radius 64
}

ACTOR Spotlight : ModelBase
{
	//$Category Hazards (Wolf3D)
	//$Title Spotlight
	//$Color 3

	//$Arg0 "Left Swing Amount"
	//$Arg0Tooltip "Radius of turn to the left in degrees\nDefault: 0"
	//$Arg1 "Right Swing Amount"
	//$Arg1Tooltip "Radius of turn to the right in degrees\nDefault: 0"

	var int user_minAngle;
	var int user_maxAngle;
	var int user_startAngle;
	var int user_startPitch;
	var int user_position;
	var int user_sightRange;
	var int user_holdTime;
	var int user_direction;
	var int user_FOV;

	+NOGRAVITY

	States
	{
	Spawn:
		MDLA A Random(1, 35)
		"####" # 0 {
			if (pitch == 0 && args[0] == 0 && args[1] == 0) {
				A_SetPitch(90);
				A_SetArg(0, 45);
				A_SetArg(1, 45);
			}
		}
		"####" # 0 {
				A_SetUserVar("user_minAngle", -args[1]);
				A_SetUserVar("user_maxAngle", args[0]);
				A_SetUserVar("user_startAngle", angle);
				A_SetUserVar("user_startPitch", pitch);
				A_SetUserVar("user_position", 0);
				A_SetUserVar("user_sightRange", 1024);
				A_SetUserVar("user_FOV", 60);
				A_SpawnItemEx("SpotlightFlare", 5.0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER | SXF_NOCHECKPOSITION | SXF_TRANSFERPITCH);
				A_SpawnItemEx("SpotlightBeam", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER | SXF_NOCHECKPOSITION | SXF_TRANSFERPITCH);
		}
		Goto Pan
	Pan:
		"####" # 0 {
			A_SetAngle(user_startAngle, SPF_INTERPOLATE);
			A_SetPitch(user_startPitch, SPF_INTERPOLATE);
			A_SetUserVar("user_direction", RandomPick(-1, 1));
			A_SetUserVar("user_position", 0);
			A_ClearTarget;
		}
		Pan.Hold:
			"####" # 0 {
				A_SetUserVar("user_holdTime", Random(0, 35));
				A_SetUserVar("user_direction", -user_direction);
			}
			Pan.HoldLoop:
				"####" # 1 {
					A_SetPitch(-pitch + 90);
					A_CustomRailgun(0, 0, "none", "none", RGF_SILENT | RGF_NOPIERCING, 0, 0, "SpotlightGlow", 0, 0, user_sightRange, 1);
					A_SetPitch(-pitch + 90);
					A_SetUserVar("user_holdTime", user_holdTime + 1);
					return A_JumpIf(user_holdTime >= 70, "Pan.Loop");
				}
				"####" # 0 A_JumpIfTargetInLOS("See", user_FOV, 0, user_sightRange)
				Loop
		Pan.Loop:
			"####" # 1 {
				A_SetPitch(-pitch + 90);
				A_CustomRailgun(0, 0, "none", "none", RGF_SILENT | RGF_NOPIERCING, 0, 0, "SpotlightGlow", 0, 0, user_sightRange, 1);
				A_SetPitch(-pitch + 90);
			}
			"####" # 0 A_JumpIf(args[0] == 0 && args[1] == 0, "Pan.Loop")
			"####" # 0 {
				A_SetUserVar("user_position", user_position + user_direction);
				A_SetAngle(angle + user_direction);
				A_LookEx(LOF_NOSOUNDCHECK | LOF_NOSEESOUND | LOF_NOJUMP, 0, user_sightRange, 0, user_FOV, "");
			}
			"####" # 0 A_JumpIfTargetInLOS("See", user_FOV, 0, user_sightRange)
			"####" # 0 A_JumpIf(user_position >= user_maxAngle || user_position <= user_minAngle, "Pan.Hold")
			Loop
	See:
		"####" # 0 {
			A_AlertMonsters(1280);
			A_FaceTarget(1); //, -120.0);
		}
		"####" # 1 {
			A_SetPitch(-pitch + 90);
			A_CustomRailgun(0, 0, "none", "none", RGF_SILENT | RGF_NOPIERCING, 0, 0, "SpotlightGlow", 0, 0, user_sightRange, 1);
			A_SetPitch(-pitch + 90);
		}
		"####" # 0 A_JumpIfTargetInLOS("See", user_FOV, JLOSF_DEADNOJUMP, user_sightRange)
		"####" # 70
		Goto Pan
	}
}

ACTOR SpotlightGlow
{
	Height 16
	Radius 16

	+ALWAYSPUFF
	+BLOODLESSIMPACT
	+INVISIBLE
	+NOBLOCKMAP
	+NOGRAVITY
	+PAINLESS
	+PUFFONACTORS

	States
	{
	Spawn:
		UNKN A 0
		"####" # 25 Light("SpotLight")
		"####" # 0 A_LookEx(LOF_NOSOUNDCHECK | LOF_NOSEESOUND, 0, 272.0, 0, 360.0, "Illuminate")
		Stop
	Illuminate:
		"####" # 0 ACS_NamedExecuteAlways("AddExtraVisibility", 0, 100)
		Stop
	XDeath:
	Crash:
	Melee:
		TNT1 A 0 A_Jump(256, "Spawn")
	}
}

ACTOR SpotlightFlare : Flare1
{
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetScale(2.0)
	Active:
		FLAR A 1 BRIGHT
		TNT1 A 0 A_Warp(AAPTR_MASTER, cos(pitch) * 5.0, 0, sin(pitch) * 5.0, 0, WARPF_COPYPITCH)
		Loop
	Inactive:
		TNT1 A -1
		Loop
	}
}

ACTOR SpotlightBeam : ModelBase
{
	RenderStyle "Add"
	Alpha 0.4
	+BRIGHT
	+NOBLOCKMAP
	+NOGRAVITY
	States
	{
	Spawn:
		MDLA A 1
		TNT1 A 0 A_Warp(AAPTR_MASTER, 0, 0, 0, 0, WARPF_COPYPITCH)
		Loop
	}
}

//Special Stuff
ACTOR SlopeMe1 : 3dObstacle
{
	//$Category Models (Wolf3D)/Special
	//$Title Slopeable Glass (clear)
	//$Color 3
	Radius 16
	Height 4
	+NOGRAVITY
}

ACTOR SlopeMe2 : SlopeMe1
{
	//$Title Slopeable Glass (checker)
}

ACTOR Caesar_Museum
{
	//$Category Models (Wolf3D)/Statues/Special
	//$Title Caesar's Statue (without hitbox)
	//$Color 7
	DistanceCheck "scenelod"
	
	Radius 4
	Height 4
	+MOVEWITHSECTOR
	+NOBLOCKMAP
	+NOGRAVITY
	-SOLID
	
	States
	{
	Spawn:
		MDLA A -1
		Stop
	}
}

ACTOR King_Museum : Caesar_Museum
{
	//$Title King's Statue (without hitbox)
	DistanceCheck "scenelod"
}

ACTOR PromL_Museum : Caesar_Museum
{
	//$Title Prometheus Statue (left, without hitbox)
	DistanceCheck "scenelod"
}

ACTOR PromR_Museum : Caesar_Museum
{
	//$Title Prometheus Statue (right, without hitbox)
	DistanceCheck "scenelod"
}

ACTOR LionL_Museum : Caesar_Museum
{
	//$Title Lion's Statue (left, without hitbox)
	DistanceCheck "scenelod"
}

ACTOR LionR_Museum : Caesar_Museum
{
	//$Title Lion's Statue (right, without hitbox)
	DistanceCheck "scenelod"
}