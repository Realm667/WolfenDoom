//first argument - number of rats. This ranges from 1 to 5.
//second argument - radius to spawn rats from centre

ACTOR RatSpawner
{
	//$Category Critters (Wolf3D)
	//$Title Rats (arg1number1-5 arg2radius)
	//$Color 0

	//$Arg0 "Number"
	//$Arg0Tooltip "Number of rats, from 1-5"	
	//$Arg1 "Radius"
	//$Arg1Tooltip "Radius in map units"

	Radius 2
	Height 2
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(Args[0]==5,5)
		"####" A 0 A_JumpIf(Args[0]==4,5)
		"####" A 0 A_JumpIf(Args[0]==3,5)
		"####" A 0 A_JumpIf(Args[0]==2,5)
		"####" A 0 A_JumpIf(Args[0]==1,5)
		"####" AAAAA 0 A_SpawnItemEx ("ScurryRat", random (-Args[1], Args[1]), 0, 0, 0, 0, 0, random (0, 360), 0)
		"####" A 1
		Stop
	}
}

ACTOR ScurryRat
{
	Radius 8
	Height 8
	Mass 50
	Speed 8
	Scale 0.5
	Health 1
	SeeSound "rat/squeek"
	ActiveSound "rat/active"
	DeathSound "rat/death"
	+AMBUSH
	+FLOORCLIP
	+FRIGHTENED
	+LOOKALLAROUND
	+NEVERRESPAWN
	+STANDSTILL
	+TOUCHY
	+VULNERABLE
	States
	{
	Spawn:
		RATS A 10 A_Look
		Loop
	See:
		PAIN A 1 A_Chase
		"####" A 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" A 0 A_CheckSight ("Vanish")
		"####" A 1 A_Chase
		"####" A 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" A 0 A_CheckSight ("Vanish")
		"####" B 1 A_Chase
		"####" B 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" A 0 A_CheckSight ("Vanish")
		"####" B 1 A_Chase
		"####" B 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" A 0 A_CheckSight ("Vanish")
		"####" B 1 A_Chase
		"####" B 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" A 0 A_CheckSight ("Vanish")
		"####" B 1 A_Chase
		"####" B 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" A 0 A_CheckSight ("Vanish")
		"####" B 1 A_Chase
		"####" B 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" A 0 A_CheckSight ("Vanish")
		"####" B 1 A_Chase
		"####" B 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" A 0 A_CheckSight ("Vanish")
		Loop
	Vanish:
		TNT1 A 1
		Stop
	Death:
		PAIN H 3 A_ScreamAndUnblock
		"####" IJKL 3
		"####" M -1
		Stop
	}
}