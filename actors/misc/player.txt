//Player
ACTOR Stamina : Inventory {Inventory.MaxAmount 100}

ACTOR DisguiseToken : Inventory { Inventory.MaxAmount 1}
ACTOR ScientistUniformToken : DisguiseToken
{
	Inventory.Icon "SCUNC0"
	Tag "Lab coat"
}
ACTOR SSBJUniformToken : DisguiseToken
{
	Inventory.Icon "SSUFC0"
	Tag "SS Uniform"
}

ACTOR BaseLineSpawner
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItemEx("BaseLine", random(16, -16), random(16, -16), random(0, 8), 0, 0, random(1,3), 0, 129, 0)
		"####" A 0 A_FadeOut(0.02)
		Loop
	}
}

ACTOR EnemyStep
{
	Radius 32
	Height 55
	Gravity 10
	-DONTSPLASH
	+CORPSE
	+NOCLIP
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_CheckRange(1024,"Unsighted") //feel free to change this value, 768 would be sufficient imho - ozy81
		TNT1 A 3
		Stop
	Crash:
		"####" A 1
		Stop
	Unsighted: //let's avoid flooding sound channels - ozy81
		TNT1 A 0 A_ChangeFlag("DONTSPLASH", TRUE)
		Goto Spawn+1
	}
}

ACTOR PlayerStep
{
	Speed 0
	Gravity 10
	+CORPSE
	+LOOKALLAROUND
	States
	{
	Spawn:
		TNT1 A 1 NoDelay A_Look
		Loop
	See:
		"####" A 0 A_AlertMonsters(CallACS("ApplyNoiseLevelMultiplier", 256))
		"####" A 3
		Goto See + 1
	Crash:
		"####" A 1
		Stop
	}
}