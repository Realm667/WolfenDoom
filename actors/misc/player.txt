//Player
ACTOR Stamina : Inventory {Inventory.MaxAmount 100}

ACTOR BoAPlayer : PlayerPawn
{
	Species "Player"
	+DONTBLAST //avoid kick's blast effect
	+THRUSPECIES
	Player.InvulnerabilityMode "reflective"
	Player.StartItem "KnifeSilent"
	Player.StartItem "TrenchShotgunLoaded",8
	Player.StartItem "Browning5Loaded",4
	Player.StartItem "MP40Loaded",32
	Player.StartItem "G43Loaded",10
	Player.StartItem "Walther9mmLoaded",8
	Player.StartItem "Luger9mmLoaded",8
	Player.StartItem "StenLoaded",32
	Player.StartItem "Kar98kLoaded",5
	Player.StartItem "PyrolightLoaded",25
	Player.StartItem "PanzerschreckLoaded"
	Player.StartItem "AstroShotgunLoaded",8
	Player.StartItem "AstroRocketLauncherLoaded",5
	Player.StartItem "AstroChaingunLoaded",30
	Player.StartItem "GrenadePickup"
	Player.StartItem "Stamina", 100
	Player.ViewHeight 56
	Player.AttackZOffset 24
	Player.DisplayName "William Blazkowicz"
	Player.SideMove 1.0,0.5
	Player.ForwardMove 1.0,0.5
	Player.WeaponSlot 1,KnifeSilent,Shovel,Firebrand,AstrosteinMelee
	Player.WeaponSlot 2,Luger9mm,Walther9mm
	Player.WeaponSlot 3,TrenchShotgun,Browning5,AstroShotgun
	Player.WeaponSlot 4,MP40,Sten,AstroChaingun
	Player.WeaponSlot 5,Kar98k,G43,UMG43
	Player.WeaponSlot 6,Pyrolight,Nebelwerfer,AstroRocketlauncher
	Player.WeaponSlot 7,Panzerschreck
	Player.DamageScreenColor "ff ff ff", 1, "IceWater"
	Player.DamageScreenColor "64 00 C8", 1, "MutantPoison"
	Scale 1.2
	States
	{
	Spawn:
		PLAY A -1
		Stop
	See:
		"####" ABCD 7
		Loop
	Pain:
		"####" G 0 ACS_NamedExecuteWithResult("PlayerFlinch", 0)
		"####" G 3 A_Pain
		Goto Spawn
	Missile:
		"####" E 12
		Goto Spawn
	Melee:
		"####" F 6 BRIGHT
		Goto Missile
	Death:
		"####" H 5 A_PlayerScream
		"####" I 8
		"####" I 1 A_PlaySound("death/bjfall")
		"####" J 5 A_NoBlocking
		"####" K 5
		"####" L -1
		Stop
	Death.Fire:
		BURN A 5 BRIGHT LIGHT(ITBURNS1)
		"####" BC 5 BRIGHT LIGHT(ITBURNS1)
		"####" D 5 BRIGHT LIGHT(ITBURNS1) A_PlaySound("death/burning")
		"####" E 5 BRIGHT LIGHT(ITBURNS1)
		"####" FGHIJ 5 BRIGHT LIGHT(ITBURNS2)
		"####" KLMN 5 BRIGHT LIGHT(ITBURNS3)
		"####" O 5 BRIGHT LIGHT(ITBURNS3) A_NoBlocking
		"####" PQR 5 BRIGHT LIGHT(ITBURNS2)
		"####" STU 5 BRIGHT LIGHT(ITBURNS1)
		"####" V -1
		Stop
	Disintegrate:
		PLAY G 0 A_Playsound("astrostein/guard_death")
		"####" G 5 A_PlayerScream
		"####" G 0 A_NoBlocking
		"####" G 0 A_SpawnItemEx("BaseLineSpawner", random(16, -16), random(16, -16), random(0, 8), 0, 0, random(1,3), 0, 129, 0)
		"####" GGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 1 A_FadeOut(0.02,0)
		TNT1 A -1 A_SetTranslucent(1)
		Stop
	}
}

ACTOR BaseLineSpawner
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItemEx("BaseLine", random(16, -16), random(16, -16), random(0, 8), 0, 0, random(1,3), 0, 129, 0)
		"####" A 0 A_FadeOut(0.02)
		Loop
	}
}

ACTOR EnemyStep
{
	Radius 32
	Height 55
	Gravity 10
	+CORPSE
	+NOCLIP
	States
	{
	Spawn:
		TNT1 A 3
		Stop
	Crash:
		"####" A 1
		Stop
	}
}

ACTOR PlayerStep
{
	Speed 0
	Gravity 10
	+CORPSE
	+LOOKALLAROUND
	States
	{
	Spawn:
		TNT1 A 1 NoDelay A_Look
		Loop
	See:
		"####" A 0 A_AlertMonsters(CallACS("ApplyNoiseLevelMultiplier", 256))
		"####" A 3
		Goto See + 1
	Crash:
		"####" A 1
		Stop
	}
}

DamageType IceWater
{
	NoArmor
	ReplaceFactor
}

DamageType MutantPoison
{
	NoArmor
	ReplaceFactor
}

DamageType Trample
{
	NoArmor
}