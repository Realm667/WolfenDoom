////////////////////////////////////////////////////
// ITEMS & POWERUPS ////////////////////////////////
////////////////////////////////////////////////////
ACTOR CKShikadiSoda : SingleCoin
{
	//$Category Misc (Wolf3D)/Commander Keen
	//$Title Pickup (Shikadi Soda, 1pts)
	//$Color 17
	Inventory.PickupMessage "$CKSODA"
	Inventory.PickupSound "ckeen/pickup"
	States
	{
	Spawn:
		CKPU AB 15 
		Loop
	Pickup:
		"####" C 5 A_GiveInventory("CoinItem",1)
		Stop
	}
}

ACTOR CKShikkersCandyBar : SingleCoin
{
	//$Category Misc (Wolf3D)/Commander Keen
	//$Title Pickup (Shikkers Candy Bar, 5pts)
	//$Color 17
	Inventory.PickupMessage "$CKCANDY"
	Inventory.PickupSound "ckeen/pickup"
	States
	{
	Spawn:
		CKPU DE 15 
		Loop
	Pickup:
		"####" F 5 A_GiveInventory("CoinItem",2)
		Stop
	}
}

ACTOR CKDoughnut : SingleCoin
{
	//$Category Misc (Wolf3D)/Commander Keen
	//$Title Pickup (Dougnut, 20pts)
	//$Color 17
	Inventory.PickupMessage "$CKDNUT"
	Inventory.PickupSound "ckeen/pickup"
	States
	{
	Spawn:
		CKPU GH 15 
		Loop
	Pickup:
		"####" I 5 A_GiveInventory("CoinItem",5)
		Stop
	}
}

ACTOR CKIceCreamCone : SingleCoin
{
	//$Category Misc (Wolf3D)/Commander Keen
	//$Title Pickup (IceCreamCone, 50pts)
	//$Color 17
	Inventory.PickupMessage "$CKCONE"
	Inventory.PickupSound "ckeen/pickup"
	States
	{
	Spawn:
		CKPU JK 15 
		Loop
	Pickup:
		"####" L 5 A_GiveInventory("CoinItem",10)
		Stop
	}
}

ACTOR CKRaindrop : Health
{
	//$Category Misc (Wolf3D)/Commander Keen
	//$Title Health (Raindrop, +1)
	//$Color 17
	+COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	Inventory.Amount 1
	Inventory.MaxAmount 200
	Inventory.PickupMessage "$CKDROP"
	Inventory.PickupSound "ckeen/raindrop"
	States
	{
	Spawn:
		CKDP ABCB 6
		Loop
	Pickup:
		"####" DEF 3
		Stop
	}
}

ACTOR CKOneUp : Health
{
	//$Category Misc (Wolf3D)/Commander Keen
	//$Title Health (Lifewater Flask, +100)
	//$Color 17
	+COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.AUTOACTIVATE
	Inventory.Amount 100
	Inventory.MaxAmount 200
	Inventory.PickupMessage "$CKONEUP"
	Inventory.PickupSound "ckeen/life"
	States
	{
	Spawn:
		CKPU MN 15 BRIGHT
		Loop
	Pickup:
		"####" O 50
	Death:
		"####" O 50
		Stop
	}
}

//////////////////////////////////////////////////
// WEAPONS ///////////////////////////////////////
//////////////////////////////////////////////////

ACTOR CKStunner : NaziWeapon
{
	//$Category Weapons (Wolf3D)
	//$Title (2) Blaster
	//$Color 14
	Weapon.SelectionOrder 9998
	Weapon.AmmoType "CKStunnerAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 5
	Weapon.UpSound "ckeen/flag"
	Tag "Neural Stunner"
	Inventory.PickupMessage "$NEUST"
	Inventory.Pickupsound "ckeen/gun"
	+WEAPON.NOAUTOFIRE
	States
	{
	Ready:
		CKBL Q 1 A_WeaponReady
		Loop
	Deselect:
		CKBL Q 0 A_Lower
		CKBL Q 1 A_Lower
		Loop
	Select:
		CKBL Q 0 A_Raise
		CKBL Q 1 A_Raise
		Loop
	Fire:
		CKBL R 8 
		CKBL S 10 A_FireCustomMissile ("CKStunnerBall")
		CKBL T 0 A_ReFire
		Goto Ready
	Spawn:
		CKBL A 5
		CKBL B 5
		Loop
	}
}

ACTOR CKStunnerBall
{
  Radius 11
  Height 8
  Speed 20
  Damage 20
  Projectile
  +RANDOMIZE
  SeeSound "ckeen/shoot"
  DeathSound "ckeen/hit"
  Obituary "$OB_CKBLAST" 
  States
  {
  Spawn:
    CKBL UV 5 Bright
    Loop
  Death:
    CKBL W 4 Bright
    CKBL X 4 Bright
	CKBL Y 4 Bright
	CKBL Z 4 Bright
    Stop
  }
}

ACTOR CKStunnerAmmo : Ammo
{
	Tag "Energy Cell"
	+IGNORESKILL
	Inventory.MaxAmount 5
	Inventory.Icon CKNE01
}

//////////////////////////////////////////////////
// KEYS & KEYPOTS ////////////////////////////////
//////////////////////////////////////////////////
ACTOR CKBlueKey : PuzzleItem
{
	//$Category Misc (Wolf3D)/Commander Keen
	//$Title Gem, blue (Blue Card)
	//$Color 13
	puzzleitem.number 101
	Inventory.PickupMessage "$CKBGEM"
	Inventory.Icon "CKKEYS1"
	Inventory.PickupSound "ckeen/gem"
	Species "BoABlueKey"
	+BRIGHT
	+INVENTORY.INVBAR
	States
	{
	Spawn:
		CKKY CD 5
		Loop
	Pickup:
		"####" G 5
		Stop
	}
}

ACTOR CKYellowKey : PuzzleItem
{
	//$Category Misc (Wolf3D)/Commander Keen
	//$Title Gem, yellow (Yellow Card)
	//$Color 13
	puzzleitem.number 102
	Inventory.PickupMessage "$CKYGEM"
	Inventory.Icon "CKKEYS0"
	Inventory.PickupSound "ckeen/gem"
	Species "BoAYellowKey"
	+BRIGHT	
	+INVENTORY.INVBAR
	States
	{
	Spawn:
		CKKY EF 5
		Loop
	Pickup:
		"####" G 5
		Stop
	}
}

ACTOR CKRedKey : PuzzleItem
{
	//$Category Misc (Wolf3D)/Commander Keen
	//$Title Gem, red (Red Card)
	//$Color 13
	puzzleitem.number 103
	Inventory.PickupMessage "$CKRGEM"
	Inventory.Icon "CKKEYS2"
	Inventory.PickupSound "ckeen/gem"
	Species "BoARedKey"
	+BRIGHT
	+INVENTORY.INVBAR
	States
	{
	Spawn:
		CKKY AB 5
		Loop
	Pickup:
		"####" G 5
		Stop
	}
}

ACTOR CKRedKeyPod : SwitchableDecoration
{
	//$Category Misc (Wolf3D)/Commander Keen
	//$Title Gempod, red
	//$Color 13
	Radius 16
	Height 32
	Mass 999999999
	+SOLID
	+USESPECIAL
	States
	{
	Spawn:
	Inactive:
		CKKS B -1
		Stop
	Active:
		TNT1 A 0 A_Playsound("ckeen/gemuse")
		CKKS A -1 BRIGHT
		Stop
	}
}

ACTOR CKBlueKeyPod : CKRedKeyPod
{
	//$Category Misc (Wolf3D)/Commander Keen
	//$Title Gempod, blue
	//$Color 13
	States
	{
	Spawn:
	Inactive:
		CKKS D -1
		Stop
	Active:
		TNT1 A 0 A_Playsound("ckeen/gemuse")
		CKKS C -1 BRIGHT
		Stop
	}
}

ACTOR CKYellowKeyPod : CKRedKeyPod
{
	//$Category Misc (Wolf3D)/Commander Keen
	//$Title Gempod, yellow
	//$Color 13
	States
	{
	Spawn:
	Inactive:
		CKKS F -1
		Stop
	Active:
		TNT1 A 0 A_Playsound("ckeen/gemuse")
		CKKS E -1 BRIGHT
		Stop
	}
}

/////////////////////////////////////////////
// MONSTERS /////////////////////////////////
/////////////////////////////////////////////
ACTOR CKConfusion
{
	Radius 1
	Height 1
	Scale 2.0
	-SOLID
	States
	{
	Spawn:
		CKCF ABC 10
		Loop
	}
}

ACTOR CKBaseEnemy
{
	Scale 2.0
	Monster
	PainSound "ckeen/kill"
	DeathSound "ckeen/kill"
	+BUMPSPECIAL
}

ACTOR CKArachnut : CKBaseEnemy
{
	//$Category Misc (Wolf3D)/Commander Keen
	//$Title Arachnut
	//$Color 4
	Health 999
	PainChance 128
	Speed 10
	Radius 30
	Height 56
	Mass 400
	+FLOORCLIP
	+LOOKALLAROUND
	Obituary "$CK_ARACH"
	States
	{
	Spawn:
		CKAC A 10 A_Look
		Loop
	See:
		"####" A 0 Thing_SetSpecial(0, 73, 15, 22, 0)
		"####" AABBCCDD 2 Fast A_Chase
		Loop
	Melee:
		"####" A 0 Fast A_FaceTarget
		"####" A 1 Fast A_CustomMeleeAttack(10,"","","Melee",TRUE)
		Goto See
	Pain:
		"####" E 0 Thing_SetSpecial(0, 0, 0, 0, 0)
		"####" E 350 Fast
		"####" AEAE 15
		Goto See
	Death:
		"####" E 0 A_ChangeVelocity(0, 0, 5, CVF_RELATIVE)
		"####" E 0 A_SpawnItemEx("CKConfusion", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
		"####" E -1
		Stop
	}
}

ACTOR CKMadMushroom : CKBaseEnemy
{
	//$Category Misc (Wolf3D)/Commander Keen
	//$Title Mad Mushroom
	//$Color 4
	Health 999
	Painchance 0
	Speed 0
	Radius 30
	Height 56
	Mass 9999999
	Gravity 0.7
	Obituary "$CK_MUSH"
	States
	{
	Spawn:
	See: 
		CKMR A 0 NoDelay Thing_SetSpecial(0, 73, 15, 22, 0)
		CKMR A 1 A_CheckFloor("Jump")
		Goto See
	Jump:
		TNT1 A 0 A_PlaySound("ckeen/madmushroombounce")
		CKMR B 12 ThrustThingZ(0, 32, 0, 1)
		"####" A 12
		TNT1 A 0 A_PlaySound("ckeen/madmushroombounce")
		CKMR B 12 ThrustThingZ(0, 32, 0, 1)
		"####" A 12
		TNT1 A 0 A_PlaySound("ckeen/madmushroombounce")
		CKMR B 20 ThrustThingZ(0, 64, 0, 1)
		"####" A 20
		Loop	
	}
}

ACTOR CKPoisonSlug : CKBaseEnemy
{
	//$Category Misc (Wolf3D)/Commander Keen
	//$Title Poison Slug
	//$Color 4
	Health 40
	PainChance 128
	Speed 10
	Radius 30
	Height 56
	Mass 400
	+FLOORCLIP
	+LOOKALLAROUND
	Obituary "$CK_SLUG"
	States
	{
	Spawn:
		CKPS A 10 A_Look
		Loop
	See:
		"####" A 0 Thing_SetSpecial(0, 73, 15, 22, 0)
		"####" AABB 4 A_Wander
		"####" A 0 A_Jump(48, "Pee")
		Loop
	Melee:
		"####" A 0 Fast A_FaceTarget
		"####" A 1 Fast A_CustomMeleeAttack(10,"","","Melee",TRUE)
		Goto See
	Pee:
		TNT1 A 0 A_PlaySound("ckeen/slugpoo")
		CKPS C 5 
		"####" C 30 A_SpawnItemEx("CKPoisonSlugPoison", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
		Goto See
	Death:
		TNT1 A 0 A_ChangeVelocity(0, 0, 5, CVF_RELATIVE)
		TNT1 A 0 A_Jump(128, 2)
		"####" E 1
		Goto DeathFloorCheck
		"####" F 1
	DeathFloorCheck:
		"####" "#" 1 A_CheckFloor("DeathConfusion")
		Loop
	DeathConfusion:
		"####" "#" 0 A_SpawnItemEx("CKConfusion", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
		"####" "#" -1
		Stop
	}
}

ACTOR CKPoisonSlugPoison : CKBaseEnemy
{
	Radius 48
	Height 2
	Scale 3.0
	+FLATSPRITE
	States
	{
	Spawn:
		TNT1 A 0 NoDelay Thing_SetSpecial(0, 73, 15, 22, 0)
		TNT1 A 0 A_SetAngle(random(0,359))
		CKSL Z 210
		"####" Y 70
		Stop 
	}
}

ACTOR CKWallShooter : SwitchableDecoration
{
	//$Category Misc (Wolf3D)/Commander Keen
	//$Title Wall Shooter (3 types)
	//$Color 4
	
	//$Arg0 "Position"
	//$Arg0Type 11
	//$Arg0Enum { 0 = "Ceiling"; 1 = "Floor"; 2 = "Wall"; }
	
	Health 9999999
	Radius 8
	Height 16
	Mass 40000
	Obituary "$CK_WALSH"
	+WALLSPRITE
	+NOGRAVITY
	-SOLID
	States
	{
	Spawn:
	Active:
		TNT1 A 0 NoDelay A_JumpIf(Args[0] == 0,"APos_Ceiling")
		TNT1 A 0 A_JumpIf(Args[0] == 1,"APos_Floor")
		TNT1 A 0 A_JumpIf(Args[0] > 1,"APos_Wall")
		APos_Ceiling:
		CKWS C 70 A_SpawnItemEx("CKWallShooterArrow_Down", 0, 0, -10, 0, 0, -10, 0, SXF_NOCHECKPOSITION)
		Loop
		APos_Floor:
		CKWS A 70 A_SpawnItemEx("CKWallShooterArrow_Up", 0, 0, 0, 0, 0, 10, 0, SXF_NOCHECKPOSITION)
		Loop
		APos_Wall:
		CKWS B 70 A_SpawnItemEx("CKWallShooterArrow_Horizontal", 0, 0, 2, 10, 0, 0, 90, SXF_NOCHECKPOSITION)		
		Loop
	Inactive:
		TNT1 A 0 A_JumpIf(Args[0] == 0,"IPos_Ceiling")
		TNT1 A 0 A_JumpIf(Args[0] == 1,"IPos_Floor")
		TNT1 A 0 A_JumpIf(Args[0] > 1,"IPos_Wall")
		IPos_Ceiling:
		CKWS C 1
		Loop
		IPos_Floor:
		CKWS A 1
		Loop
		IPos_Wall:
		CKWS B 1
		Loop
	}
}

ACTOR CKWallShooterArrow_Down
{
	Radius 4
	Height 32
	Damage 16
	Speed 16
	Seesound "ckeen/klick"
	Deathsound "ckeen/klick"
	PROJECTILE
	States
	{
	Spawn:
		CKAR CD 8 
		Loop
	Death:
		TNT1 A 0
		Stop
	}
}

ACTOR CKWallShooterArrow_Up : CKWallShooterArrow_Down 
{
	States
	{
	Spawn:
		CKAR AB 8 
		Loop
	}
}

ACTOR CKWallShooterArrow_Horizontal : CKWallShooterArrow_Down 
{
	Radius 8
	Height 8
	+WALLSPRITE
	+NOGRAVITY
	States
	{
	Spawn:
		CKAR E 0 NoDelay A_SetAngle(angle+90)
		CKAR EF 8 
		Goto Spawn+1
	}
}

ACTOR CKOracle : CKBaseEnemy
{
	//$Category Misc (Wolf3D)/Commander Keen
	//$Title Oracle of Wisdom
	//$Color 4
	Health 40
	PainChance 0
	Speed 7
	Radius 16
	Height 64
	Mass 400
	+FLOORCLIP
	+LOOKALLAROUND
	Obituary "$CK_ORACLE"
	States
	{
	Spawn:
		CKCC A 10 A_Look
		Loop
	See:
		"####" AABB 8 A_Wander
		"####" A 0 A_Jump(48, "Stand")
		Loop
	Stand:
		"####" C 70
		Goto See
	}
}

ACTOR CKBounder : CKBaseEnemy
{
	//$Category Misc (Wolf3D)/Commander Keen
	//$Title Bounder
	//$Color 4
	Health 50
	Painchance 0
	Speed 0
	Radius 30
	Height 30
	Mass 30
	Gravity 0.7
	Obituary "$CK_BOUND"
	States
	{
	Spawn:
	See: 
		CKBB A 1 A_CheckFloor("Jump")
		Loop
	Jump:
		"####" A 0 A_Jump(64, "JumpDirection")
		"####" A 0 A_PlaySound("ckeen/madmushroombounce")
		"####" B 12 ThrustThingZ(0, 50, 0, 1)
		"####" A 12
		Goto See
	JumpDirection:
		CKBB A 0 A_PlaySound("ckeen/madmushroombounce")
		"####" B 0 ThrustThingZ(0, 50, 0, 1)
		"####" B 12 ThrustThing(random(0,255), 8, 0, 0)
		"####" A 12
		Goto See
	Death:
		"####" C 0 A_PlaySound("ckeen/madmushroombounce")
		"####" C 0 ThrustThingZ(0, 70, 0, 1)
		"####" C 12 ThrustThing(random(0,255), 15, 0, 0)
		"####" C 1 A_CheckFloor("DeathConfusion")
		Goto Death+3
	DeathConfusion:
		"####" C 0 A_SpawnItemEx("CKConfusion", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
		"####" C -1
		Stop
	}
}