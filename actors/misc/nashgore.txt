//const int NASHGORE_BLOODSPOT_DURATION = 1050; //not needed anymore because of cvars - Ozy81
const int NASHGORE_BLOODFLAGS1 = SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY;
const int NASHGORE_BLOODFLAGS2 = SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEVELOCITY | SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION;
const int NASHGORE_BLOODFLAGS3 = SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE;

// Base blood
Actor NashGore_BloodBase
{
	Game Doom
	+CORPSE
	+DONTSPLASH
	+FORCEXYBILLBOARD
	+NOTELEPORT
	+NOBLOCKMONST
	+NOBLOCKMAP
	-SOLID
}

// Emulated regular Doom blood
Actor NashGore_DoomBlood : Blood
{
	+DONTSPLASH
	+FORCEXYBILLBOARD
}

// added for BoA
Actor NashGore_RavenBlood : BloodSplatter
{
	+DONTSPLASH
	+FORCEXYBILLBOARD
}

// Modify the internal Blood actor to spawn flying blood
Actor NashGore_Blood : Blood replaces Blood
{
	-RANDOMIZE
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_SpawnItem("NashGore_DoomBlood", 0, 0, 0, 1)
		// fall through
		SpawnDone:
			TNT1 A 0 A_SpawnItemEx("NashGore_FlyingBlood", 0, 0, 0, FRandom(0.1, 6.0) * RandomPick(-1, 1), FRandom(0.1, 6.0) * RandomPick(-1, 1), FRandom(0.0, 6.0), 0, NASHGORE_BLOODFLAGS1, 64)
			TNT1 AA 0 A_SpawnItemEx("NashGore_SplatMaker", 0, 0, 0, FRandom(-5, 5), FRandom(-5, 5), FRandom(0, 5), Random(0, 360), NASHGORE_BLOODFLAGS2)
			TNT1 AA 4 A_SpawnItemEx("NashGore_BloodSplasher", 0, 0, 0, FRandom(-2.0, 2.0), FRandom(-2.0, 2.0), FRandom(1.0, 2.0), Random(0, 360), NASHGORE_BLOODFLAGS1, 128)
			Stop
	}
}

// added for BoA
Actor NashGore_BloodSplatter : NashGore_Blood replaces BloodSplatter
{
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_SpawnItem("NashGore_RavenBlood", 0, 0, 0, 1)
			TNT1 A 0 A_Jump(256, "SpawnDone")
			Stop
	}
}

// Blood actor that flies outwards and leaves trails behind
Actor NashGore_FlyingBlood : NashGore_BloodBase
{
	Radius 8
	Height 2
	Gravity 0.25
	States
	{
		Spawn:
		FlyingNormal:
			TNT1 A 2 A_SpawnItem("NashGore_FlyingBloodTrail", 0, 0, 0, 1)
			TNT1 A 0 A_Jump(12, "RandomlyDestroy")
			TNT1 A 0 A_Jump(27, "FlyingDecel")
			Loop
		FlyingDecel:
			TNT1 A 2 A_SpawnItem("NashGore_FlyingBloodTrail", 0, 0, 0, 1)
			TNT1 A 0 A_ChangeVelocity(velx * 0.8, vely * 0.8, velz, CVF_REPLACE)
			Loop
		RandomlyDestroy:
			TNT1 A 0
			Stop
		Crash:
			TNT1 A 0 A_PlaySound("bloodsplat")
			TNT1 A 0 A_SpawnItem("NashGore_BloodSplash", 0, 0, 0, 1)
		BloodSplasher:
			TNT1 A 0 A_SpawnItemEx("NashGore_BloodSpot", 0, 0, 0, 0, 0, 0, FRandom(0, 360), 7, 128)
			TNT1 AAAAA 1 A_SpawnItemEx("NashGore_BloodSplasher", 0, 0, 0, FRandom(-2.0, 2.0), FRandom(-2.0, 2.0), FRandom(1.0, 2.0), Random(0, 360), NASHGORE_BLOODFLAGS1, 150)
			Stop
	}
}

// Blood trails spawned by FlyingBlood
Actor NashGore_FlyingBloodTrail : NashGore_BloodBase
{
	Radius 1
	Height 1
	Gravity 0.15
	Scale 0.55
	States
	{
		Spawn:
			BTRL ABCD 4
			Loop
		Crash:
			TNT1 A 0
			Stop
	}
}

// Blood splashers, generates Random splashes
Actor NashGore_BloodSplasher : NashGore_BloodBase
{
	Radius 1
	Height 1
	Gravity 0.20
	States
	{
		Spawn:
			TNT1 A 1
			Loop
		Crash:
			TNT1 A 0 A_SpawnItem("NashGore_BloodSplash", 0, 0, 0, 1)
			TNT1 A 0 A_SpawnItemEx("NashGore_BloodSpot", 0, 0, 0, 0, 0, 0, Random(0, 360), NASHGORE_BLOODFLAGS3, 200)
			Stop
	}
}

// Blood spots that temporarily stay on the floor
Actor NashGore_BloodSpot : NashGore_BloodBase
{
	Radius 4
	Height 2
	Alpha 0.85
	-NOBLOCKMAP
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SpawnItem("NashGore_BloodSplash", 0, 0, 0, 1)
			TNT1 A 0 A_Jump(256, "BloodSpot1", "BloodSpot2", "BloodSpot3", "BloodSpot4")
		BloodSpot1:
			BSPT A 0 A_SetTics(CallACS("BloodLifetime")) //A_SetTics(NASHGORE_BLOODSPOT_DURATION)
			"####" "#" 0 A_Jump(256, "FadeOut")
		BloodSpot2:
			BSPT B 0 A_SetTics(CallACS("BloodLifetime")) //A_SetTics(NASHGORE_BLOODSPOT_DURATION)
			"####" "#" 0 A_Jump(256, "FadeOut")
		BloodSpot3:
			BSPT C 0 A_SetTics(CallACS("BloodLifetime")) //A_SetTics(NASHGORE_BLOODSPOT_DURATION)
			"####" "#" 0 A_Jump(256, "FadeOut")
		BloodSpot4:
			BSPT D 0 A_SetTics(CallACS("BloodLifetime")) //A_SetTics(NASHGORE_BLOODSPOT_DURATION)
			"####" "#" 0 A_Jump(256, "FadeOut")
		FadeOut:
			"####" "#" 2 A_FadeOut(0.005)
			Loop
	}
}

// Blood splashes
Actor NashGore_BloodSplash : NashGore_BloodBase
{
	Radius 1
	Height 2
	Scale 0.5
	-NOBLOCKMAP
	States
	{
		Spawn:
			BSPL ABCDE 5
			Stop
	}
}

// Blood splat decal maker
// TO DO: This only generates red blood decals PLEASE FIX
// http://forum.zdoom.org/viewtopic.php?f=15&t=46146&p=774415#p774415
Actor NashGore_SplatMaker
{
	Radius 4
	Height 8
	PROJECTILE
	States
	{
		Spawn:
			TNT1 A 3
		Death:
			TNT1 A 1
			Stop
	}
}

// BOA Translations
ACTOR ScorpionBlood : NashGore_Blood
{
	Translation "16:47=208:223","168:191=210:224"
}

ACTOR ScorpionFlyingBlood : NashGore_FlyingBlood
{
	Translation "16:47=208:223","168:191=210:224"
}

ACTOR MutantBlood : NashGore_Blood
{
	Translation "16:47=[205,0,215]:[40,0,96]","168:191=[205,0,215]:[40,0,96]"
}

ACTOR MutantFlyingBlood : NashGore_FlyingBlood
{
	Translation "16:47=[205,0,215]:[40,0,96]","168:191=[205,0,215]:[40,0,96]"
}