class InventoryClearHandler : EventHandler
{
	override void PlayerEntered(PlayerEvent e)
	{
		// Give the player the camera tilt handling item (modified version of Nash's Tilt++ v1.3)
		players[e.PlayerNumber].mo.A_GiveInventory("BoATilt", 1);
		players[e.PlayerNumber].mo.A_GiveInventory("BoAVisibility", 1);
		players[e.PlayerNumber].mo.A_GiveInventory("BoAUnderwater", 1);

		// Force reset of certain shader effects
		players[e.PlayerNumber].mo.A_SetInventory("OldVideoShaderControl", 1);
		players[e.PlayerNumber].mo.A_SetInventory("BlurShaderControl", 1);
		players[e.PlayerNumber].mo.A_SetInventory("HeatShaderControl", 1);

		players[e.PlayerNumber].mo.A_GiveInventory("NullWeapon", 1);

		// Ed the Bat's updated weapon stripping
		if(level.levelnum!=99)
			return;
		let me=PlayerPawn(players[e.PlayerNumber].mo);
		DropItem drop=me.GetDropItems();
		
		for(int i=0;i<AllActorClasses.Size();i++)
		{
			let type=AllActorClasses[i];
			// First, remove all weapons, except those with the UNDROPPABLE flag
			if(type is "Weapon")
			{
				let weptype=(class<weapon>)(type);
				let wepitem=weapon(me.FindInventory(weptype));
				if(wepitem!=null&&!wepitem.bUNDROPPABLE)
					me.A_TakeInventory(name(weptype));
			}
			// Remove all ammo, except that with the UNDROPPABLE flag
			if(type is "Ammo")
			{
				let ammotype=(class<ammo>)(type);
				let ammoitem=ammo(me.FindInventory(ammotype));
				if(ammoitem!=null&&!ammoitem.bUNDROPPABLE)
					me.A_TakeInventory(name(ammotype));
			}
			// Remove Commander Keen items, or else player will have them after
			// playing one of the Commander Keen secret missions.
			if(type is "CKPogoStick" || type is "CKLedgeGrab")
			{
				let invclass = (class<Inventory>)(type);
				me.TakeInventory(invclass, 0); // 0 will clear the item from the inventory
			}
		}
		//If the player has any weapons in StartItem, set them here
		//They're not supposed to come with ammo, so clear that after this
		if(drop!=null)
		{
			for(DropItem di=drop;di!=null;di=di.Next)
			{
				if(di.Name=='None')
					continue;
				let weptype=(class<weapon>)(di.Name);
				if(weptype!=null)
					me.A_SetInventory(di.Name,di.Amount);
			}
		}
		//If the player has any ammo in StartItem, set it here
		if(drop!=null)
		{
			for(DropItem di=drop;di!=null;di=di.Next)
			{
				if(di.Name=='None')
					continue;
				let ammotype=(class<ammo>)(di.Name);
				if(ammotype!=null)
					me.A_SetInventory(di.Name,di.Amount);
			}
		}
	}
}