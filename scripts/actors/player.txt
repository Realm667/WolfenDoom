/*

  New Functions:
	GetLineTarget()
	- Saves reference to whatever actor is under the player's crosshair to the CrossHairTarget variable
	- Used by Status Bar code to draw boss health bars

	GetClosestForcedHealthBar()
	- Finds the closest Nazi class descendant with the user_DrawHealthBar variable set to true and saves
	  a reference to that actor to the ForcedHealthBar variable
	- Used by Status Bar code to draw always-present health bar (e.g., Nebelwerfer in Paris)

	DoTokenChecks()
	- Parent function where various inventory checks should be performed

	CheckSpriteToken(token name, standing sprite, [crouching sprite])
	- Handles sprite changing for Disguise Tokens:

		CheckSpriteToken("ScientistUniformToken", "PLSC", "PLYS")

	  This checks the player's inventory for the "ScientistUniformToken" token, and, if found, sets the
	  base sprite to PLSC if the player is standing and PLYS if the player is crouching.  Also cancels
	  out the sprite squishing if you specify a crouch sprite
	- If you don't specify a crouching sprite, the standing sprite is used and default squishing applies

	SpawnEffect(class<PlayerEffect>)
	RemoveEffect(class<PlayerEffect>)
	- Used to spawn/remove an effect actor that follows the player around (e.g., glow for FireBrand)
	- See PlayerEffect class information below
	- Checks should be performed in DoTokenChecks:

		// If current weapon is BFG9000, spawn swirlysparkles that follow the player
		if (player && player.ReadyWeapon is "BFG9000") { SpawnEffect("SwirlySparkles"); }
		else { RemoveEffect("SwirlySparkles"); }

  New Base Classes:
	PlayerEffect
	- Base class for player effects, to be spawned/removed with SpawnEffect/RemoveEffect as shown above
	- Literally just a simple actor that constantly warps to the position of its master (the player)
	- Developer note: These actors have their statnum set to STAT_DEFAULT - 4
	  (used to limit ThinkerIterator performance hit)

  Useful variables:
	flinchfactor
	- Acts as multiplier for flinch amount; range from 0 to 255
	- Set via ACS like this: 
		SetUserVariable(playerTID, "flinchfactor", 128);

*/

class BoAPlayer : PlayerPawn
{
	double user_crouch;
	double user_ScaleY;
	double leveltilt, oldtilt, leveltiltangle;
	Actor CrosshairTarget;
	Actor ForcedHealthBar;
	Actor DragTarget;
	SpriteID baseSprite;
	Weapon LastWeapon;
	Line UseTarget;
	int crosshair;
	String crosshairstring;
	Actor climbing;
	int interactiontimeout;
	int flinchfactor;

	Default
	{
		+THRUSPECIES // Needed for swimming through ActorBlockers
		Species "Player";
		Player.MaxHealth 100; // Needed for improved vitality serum
		Player.InvulnerabilityMode "reflective";
		Player.StartItem "AstroChaingunLoaded",30;
		Player.StartItem "AstroRocketLauncherLoaded",5;
		Player.StartItem "AstroShotgunLoaded",8;
		Player.StartItem "Browning5Loaded",4;
		Player.StartItem "G43Loaded",10;
		Player.StartItem "Kar98kLoaded",5;
		Player.StartItem "Luger9mmLoaded",8;
		Player.StartItem "MP40Loaded",32;
		Player.StartItem "PanzerschreckLoaded";
		Player.StartItem "PyrolightLoaded",25;
		Player.StartItem "StenLoaded",32;
		Player.StartItem "TeslaLoaded",50;
		Player.StartItem "TrenchShotgunLoaded",8;
		Player.StartItem "Walther9mmLoaded",8;
		Player.StartItem "Stamina", 100;
		Player.StartItem "NullWeapon", 1;
		Player.StartItem "KnifeSilent";
		Player.ViewHeight 56;
		Player.AttackZOffset 24;
		Player.DisplayName "William Blazkowicz";
		Player.CrouchSprite "PLYC";
		Player.SideMove 1.0,0.5;
		Player.ForwardMove 1.0,0.5;
		Player.ViewBob 0.44;
		Player.WeaponSlot 1, "KnifeSilent", "Shovel", "Firebrand", "AstrosteinMelee"; //, "FakeID";
		Player.WeaponSlot 2, "Luger9mm", "Walther9mm", "AstroLuger";
		Player.WeaponSlot 3, "TrenchShotgun", "Browning5", "AstroShotgun";
		Player.WeaponSlot 4, "MP40", "Sten", "AstroChaingun";
		Player.WeaponSlot 5, "Kar98k", "G43", "UMG43";
		Player.WeaponSlot 6, "Pyrolight", "Nebelwerfer", "AstroRocketlauncher";
		Player.WeaponSlot 7, "Panzerschreck";
		Player.WeaponSlot 8, "TeslaCannon";
		Player.WeaponSlot 0, "NullWeapon";
		Player.DamageScreenColor "ff ff ff", 1, "IceWater";
		Player.DamageScreenColor "64 00 C8", 1, "MutantPoison";
		Player.DamageScreenColor "00 5A 40", 1, "UndeadPoison";
		Player.DamageScreenColor "00 5A 40", 1, "UndeadPoisonAmbience";
		DamageFactor "Creepy", 0.0;
		DamageFactor "FriendlyFrag", 0.5;
		Scale 0.65;
	}

	States
	{
	Spawn:
		PLAY A 0 DoTokenChecks(); // Must run the check once here because it can miss a frame somehow otherwise.  All other states are taken care of in the Tick function.
	SpawnLoop:
		"####" A 1;
		Loop;
	See:
		PLAY ABCD 8 {   // Urgh.  So that player menu preview will show up properly...
			// The menu code looks for the See state frames and duration, but doesn't process code - It also doesn't handle "####" frames.
			// Since this return is processed before the frame is rendered, the actor will jump to the proper See state in-game without
			// screwing up the handling for disguises or ever showing the wrong sprites.
			return ResolveState("See.InGame");
		}
		Loop;
	See.InGame:
		"####" AAAAAAAABBBBBBBBCCCCCCCCDDDDDDDD 1;
		"####" # 0 A_Jump(256, "SpawnLoop");
	Pain.Numbness:
		"####" G 0 {
			A_GiveInventory("CurseNumbness", Random(25, 50));
			A_GiveInventory("BlurShaderControl", Random(25, 50));
		}
		"####" # 0 A_Jump(256, "Pain");
	Pain.Electric:
		"####" G 0 {
			A_SetBlend("White", FRandom(3.25, 7.25), Random(1, 4));
			A_GiveInventory("BlurShaderControl", Random(5, 10));
		}
	Pain:
		"####" G 0 ACS_NamedExecuteWithResult("PlayerFlinch", flinchfactor);
		"####" G 4 A_Pain();
		"####" # 0 A_Jump(256, "SpawnLoop");
	Missile:
		"####" E 12;
		"####" # 0 A_Jump(256, "SpawnLoop");
	Melee:
		"####" F 6;
		"####" # 0 A_Jump(256, "Missile");
	Disintegrate: //here in order to transfer properly frames if needed - ozy81
		"####" G 0 A_Playsound("astrostein/guard_death");
		"####" G 5 A_PlayerScream();
		"####" G 0 A_NoBlocking;
		"####" G 0 A_SpawnItemEx("BaseLineSpawner", random(16, -16), random(16, -16), random(0, 8), 0, 0, random(1,3), 0, 129, 0);
		"####" GGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 1 A_FadeOut(0.02,0);
		TNT1 A -1 A_SetTranslucent(1);
		Stop;
	Death:
		"####" H 5 A_PlayerScream();
		"####" I 8;
		"####" I 1 A_PlaySound("death/bjfall");
		"####" J 5 A_NoBlocking;
		"####" K 5;
		"####" LM 2;
		"####" N -1;
		Stop;
	Death.Fire:
		"####" # 0 {sprite = GetSpriteIndex(Random() < 128 ? "BURN" : "NRUB");}
		"####" A 5 Bright Light("ITBURNS1") { A_Wander(); }
		"####" BC 5 Bright Light("ITBURNS1") { A_Wander(); A_SpawnItemEx("FloatingCinder", random(-8,8), random(-8,8), random(0,32), 1, 0, random (1, 3), random (0, 360), SXF_TRANSFERPITCH | SXF_CLIENTSIDE, 160); A_SpawnItemEx("BodySmoke", random(-3,3), random(-3,3), random(0,56), 0, 0, frandom(0.2,1.0)); }
		"####" D 5 Bright Light("ITBURNS1") { A_Wander(); A_PlaySound("death/burning"); }
		"####" E 5 Bright Light("ITBURNS1") { A_Wander(); A_SpawnItemEx("FloatingCinder", random(-8,8), random(-8,8), random(0,32), 1, 0, random (1, 3), random (0, 360), SXF_TRANSFERPITCH | SXF_CLIENTSIDE, 160); A_SpawnItemEx("BodySmoke", random(-3,3), random(-3,3), random(0,56), 0, 0, frandom(0.2,1.0)); }
		"####" FABCD 5 Bright Light("ITBURNS2") { A_Wander(); A_SpawnItemEx("FloatingCinder", random(-8,8), random(-8,8), random(0,32), 1, 0, random (1, 3), random (0, 360), SXF_TRANSFERPITCH | SXF_CLIENTSIDE, 160); A_SpawnItemEx("BodySmoke", random(-3,3), random(-3,3), random(0,56), 0, 0, frandom(0.2,1.0)); }
		"####" EFAG 5 Bright Light("ITBURNS3") A_Wander();
		"####" H 5 Bright Light("ITBURNS3") A_Wander();
		"####" IJK 5 Bright Light("ITBURNS2") { A_SpawnItemEx("FloatingCinder", random(-8,8), random(-8,8), random(0,16), 1, 0, random (1, 3), random (0, 360), SXF_TRANSFERPITCH | SXF_CLIENTSIDE, 160); A_SpawnItemEx("BodySmoke", random(-3,3), random(-3,3), random(0,56), 0, 0, frandom(0.2,1.0)); }
		"####" LMN 5 Bright Light("ITBURNS1") { A_SpawnItemEx("FloatingCinder", random(-8,8), random(-8,8), random(0,8), 1, 0, random (1, 3), frandom (0, 360), SXF_TRANSFERPITCH | SXF_CLIENTSIDE, 160); A_SpawnItemEx("BodySmoke", random(-3,3), random(-3,3), random(0,56), 0, 0, frandom(0.2,1.0)); }
	Death.Fire.Smoke:
		"####" O 0 A_Jump(32,"Death.Fire.End");
		"####" O 2 Light("ITBURNS4") A_SpawnItemEx("BodySmoke", random(-3,3), random(-3,3), 0, 0, 0, frandom(0.2,1.0));
		"####" O 4 Light("ITBURNS5") A_SpawnItemEx("BodySmoke", random(-3,3), random(-3,3), 0, 0, 0, frandom(0.2,1.0));
		"####" O 3 Light("ITBURNS6") A_SpawnItemEx("BodySmoke", random(-3,3), random(-3,3), 0, 0, 0, frandom(0.2,1.0));
		"####" O 5 Light("ITBURNS5") A_SpawnItemEx("BodySmoke", random(-3,3), random(-3,3), 0, 0, 0, frandom(0.2,1.0));
		Loop;
	Death.Fire.End:
		"####" O -1;
		Stop;
	Death.Electric:
		"####" # 0 {sprite = GetSpriteIndex(Random() < 128 ? "FIZZ" : "ZZIF");}
		"####" # 0 A_RadiusGive("BlurShaderControl", 192, RGF_PLAYERS | RGF_GIVESELF, 80);
		"####" A 5 Bright Light("TPortNormal");
		"####" BA 5 Bright Light("TPortNormal") A_SpawnItemEx("BodySmoke", random(-3,3), random(-3,3), random(0,56), 0, 0, frandom(0.2,1.0));
		"####" B 5 Bright Light("TPortNormal") { A_PlaySound("death/burning"); A_SpawnItemEx("BodySmoke", random(-3,3), random(-3,3), random(0,56), 0, 0, frandom(0.2,1.0)); }
		"####" AABAB 5 Bright Light("TPortNormal") A_SpawnItemEx("BodySmoke", random(-3,3), random(-3,3), random(0,56), 0, 0, frandom(0.2,1.0));
		"####" C 7 Light("TPortNormal");
		"####" DE 6 Light("TPortNormal") A_SpawnItemEx("BodySmoke", random(-3,3), random(-3,3), random(0,56), 0, 0, frandom(0.2,1.0));
		"####" FG 5 A_SpawnItemEx("BodySmoke", random(-3,3), random(-3,3), random(0,56), 0, 0, frandom(0.2,1.0));
	Death.Electric.Smoke:
		"####" H 0 A_Jump(32,"Death.Electric.End");
		"####" H 2 A_SpawnItemEx("BodySmoke", random(-3,3), random(-3,3), 0, 0, 0, frandom(0.2,1.0));
		"####" H 4 A_SpawnItemEx("BodySmoke", random(-3,3), random(-3,3), 0, 0, 0, frandom(0.2,1.0));
		"####" H 3 A_SpawnItemEx("BodySmoke", random(-3,3), random(-3,3), 0, 0, 0, frandom(0.2,1.0));
		"####" H 5 A_SpawnItemEx("BodySmoke", random(-3,3), random(-3,3), 0, 0, 0, frandom(0.2,1.0));
		Loop;
	Death.Electric.End:
		"####" H -1;
		Stop;
	SpriteList:
		// Any sprite not used above that is switched to by a token check must be listed here or the game will not find it!
		PLSC A 0;
		PLYS A 0;
		PLCP A 0; //camp
		PLYP A 0;
		PLSD A 0; //SS
		PLSS A 0;
		PLRR A 0; //Ryan
		PLYR A 0;
		KEEN A 0;
		Stop;
	SpriteLookups:
		BURN A 0;
		NRUB A 0;
		FIZZ A 0;
		ZZIF A 0;
	}

	override void PostBeginPlay()
	{
		flinchfactor = 255; // Start out with full flinch amount.

		Super.PostBeginPlay();
	}

	override void Tick()
	{
		if (player && !player.morphtics)
		{
			if (Level.time % (35 * 5) == 0) { ForcedHealthBar = GetClosestForcedHealthBar(); } // Only run this check occasionally

			CrosshairTarget = GetLineTarget();

			DoGravity();

			DoTokenChecks();

			DoInteractions();
		}

		Super.Tick();
	}

	override void TickPSprites()
	{
		let player = self.player;
		let pspr = player.psprites;
		while (pspr)
		{
			// Destroy the psprite if it's from a weapon that isn't currently selected by the player
			// or if it's from an inventory item that the player no longer owns. 
			if ((pspr.Caller == null ||
				(pspr.Caller is "Inventory" && Inventory(pspr.Caller).Owner != pspr.Owner.mo) ||
				(pspr.Caller is "Weapon" && pspr.Caller != pspr.Owner.ReadyWeapon)))
			{
				pspr.Destroy();
			}
			else
			{
				pspr.Tick();
			}

			pspr = pspr.Next;
		}

		if ((health > 0) || (player.ReadyWeapon != null && !player.ReadyWeapon.bNoDeathInput))
		{
			if (player.ReadyWeapon == null)
			{
				if (player.PendingWeapon != WP_NOCHANGE)
					player.mo.BringUpWeapon();
			}
			else
			{
				CheckWeaponChange();
				if (player.WeaponState & (WF_WEAPONREADY | WF_WEAPONREADYALT) && !FindInventory("HQ_Checker", true))
				{
					CheckWeaponFire();
				}
				// Check custom buttons
				CheckWeaponButtons();
			}
		}
	}

	override void MovePlayer ()
	{
		let player = self.player;
		UserCmd cmd = player.cmd;

		// [RH] 180-degree turn overrides all other yaws
		if (player.turnticks)
		{
			player.turnticks--;
			Angle += (180. / TURN180_TICKS);
		}
		else
		{
			if (DragTarget) { cmd.yaw = int(cmd.yaw * 0.15); }
			Angle += cmd.yaw * (360./65536.);
		}

		player.onground = (pos.z <= floorz) || bOnMobj || bMBFBouncer || (player.cheats & CF_NOCLIP2);

		// killough 10/98:
		//
		// We must apply thrust to the player and bobbing separately, to avoid
		// anomalies. The thrust applied to bobbing is always the same strength on
		// ice, because the player still "works just as hard" to move, while the
		// thrust applied to the movement varies with 'movefactor'.

		if (cmd.forwardmove | cmd.sidemove)
		{
			double forwardmove, sidemove;
			double bobfactor;
			double friction, movefactor;
			double fm, sm;

			[friction, movefactor] = GetFriction();
			bobfactor = friction < ORIG_FRICTION ? movefactor : ORIG_FRICTION_FACTOR;

			if (!player.onground && !bNoGravity && !waterlevel && !leveltilt && !climbing)
			{
				// [RH] allow very limited movement if not on ground.
				movefactor *= level.aircontrol;
				bobfactor *= level.aircontrol;
			}

			fm = cmd.forwardmove;
			sm = cmd.sidemove;
			[fm, sm] = TweakSpeeds (fm, sm);
			fm *= Speed / 256;
			sm *= Speed / 256;

			// When crouching, speed and bobbing have to be reduced
			if (CanCrouch() && player.crouchfactor != 1)
			{
				fm *= player.crouchfactor;
				sm *= player.crouchfactor;
				bobfactor *= player.crouchfactor;
			}

			forwardmove = fm * movefactor * (35 / TICRATE);
			sidemove = sm * movefactor * (35 / TICRATE);

			if (forwardmove)
			{
				if (leveltilt <= 30) { Bob(Angle, cmd.forwardmove * bobfactor / 256., true); }
				if (leveltilt && !climbing)
				{
					vel.x += forwardmove * cos(angle) * cos(leveltilt) * speed * 1.5;
					vel.y += forwardmove * sin(angle) * speed;
					vel.z -= forwardmove * cos(angle - leveltiltangle) * sin(leveltilt) * speed * 1.5;
				}
				else
				{
					ForwardThrust(forwardmove, Angle);
				}
			}
			if (sidemove)
			{
				let a = Angle - 90;
				if (leveltilt <= 30) { Bob(a, cmd.sidemove * bobfactor / 256., false); }

				if (leveltilt && !climbing)
				{
					vel.x += sidemove * sin(angle) * cos(leveltilt) * speed * 1.75;
					vel.y += -sidemove * cos(angle) * speed * 1.75;
					vel.z -= sidemove * sin(angle - leveltiltangle) * sin(leveltilt) * speed * 1.75;
				}
				else
				{
					Thrust(sidemove, a);
				}
			}

			if (!(player.cheats & CF_PREDICTING) && (forwardmove != 0 || sidemove != 0))
			{
				PlayRunning ();
			}

			if (player.cheats & CF_REVERTPLEASE)
			{
				player.cheats &= ~CF_REVERTPLEASE;
				player.camera = player.mo;
			}
		}
	}

	override void CheckCheats()
	{
		let player = self.player;
		// No-clip cheat
		if ((player.cheats & (CF_NOCLIP | CF_NOCLIP2)) == CF_NOCLIP2)
		{ // No noclip2 without noclip
			player.cheats &= ~CF_NOCLIP2;
		}
		bNoClip = (player.cheats & (CF_NOCLIP | CF_NOCLIP2) || Default.bNoClip);
		if (player.cheats & CF_NOCLIP2)
		{
			bNoGravity = true;
		}
		else if (!climbing && !bFly && !Default.bNoGravity) // Added 'climbing' check
		{
			bNoGravity = false;
		}
	}

	override void PlayerThink()
	{
		let player = self.player;
		UserCmd cmd = player.cmd;
		
		CheckFOV();

		if (player.inventorytics)
		{
			player.inventorytics--;
		}
		CheckCheats();

		if (bJustAttacked)
		{ // Chainsaw/Gauntlets attack auto forward motion
			cmd.yaw = 0;
			cmd.forwardmove = 0xc800/2;
			cmd.sidemove = 0;
			bJustAttacked = false;
		}

		bool totallyfrozen = CheckFrozen();

		// Handle crouching
		CheckCrouch(totallyfrozen);
		CheckMusicChange();

		if (player.playerstate == PST_DEAD)
		{
			DeathThink ();
			return;
		}

		if (player.jumpTics != 0)
		{
			player.jumpTics--;
			if (player.onground && player.jumpTics < -18)
			{
				player.jumpTics = 0;
			}
		}

		if (player.morphTics && !(player.cheats & CF_PREDICTING)) { MorphPlayerThink (); }

		// Crouching moves down while climbing
		if (climbing && cmd.buttons & BT_CROUCH) { vel.z -= 0.95; }

		CheckPitch();
		HandleMovement();

		// Only recalculate the view position if you're not climbing or if you are climbing at speed
		// Keeps the float bob effect from being visible to the player
		if (!climbing || abs(vel.length()) > 1.0) { CalcHeight (); }

		// Bobbing while on the ladder is caused by P_ZMovement in p_mobj.cpp and can't be altered (currently lines 3028-3035).  
		// This flag tricks the player actor into only bobbing a tiny bit, but is an awful hack that depends on a quirk in the checks in the internal code.
		// NOCLIP2 gets unset almost immediately after the check is made here, so never actually takes effect.
		// Since this is just cosmetic (you only see it in chasecam or multiplayer), it may be best to comment it out, just in case internal code changes in the future.
//		if (climbing) { player.cheats |= CF_NOCLIP2; }

		if (!(player.cheats & CF_PREDICTING))
		{
			CheckEnvironment();
			CheckUse();
			CheckUndoMorph();
			// Cycle psprites.
			// Note that after this point the PlayerPawn may have changed due to getting unmorphed so 'self' is no longer safe to use.
			player.mo.TickPSprites();
			// Other Counters
			if (player.damagecount)	player.damagecount--;
			if (player.bonuscount) player.bonuscount--;

			if (player.hazardcount)
			{
				player.hazardcount--;
				if (!(level.time % player.hazardinterval) && player.hazardcount > 16*TICRATE)
					player.mo.DamageMobj (NULL, NULL, 5, player.hazardtype);
			}
			player.mo.CheckPoison();
			player.mo.CheckDegeneration();
			player.mo.CheckAirSupply();
		}
	}

	override void CheckJump()
	{
		let player = self.player;
		// [RH] check for jump
		if (player.cmd.buttons & BT_JUMP)
		{
			if (player.crouchoffset != 0)
			{
				// Jumping while crouching will force an un-crouch but not jump
				player.crouching = 1;
			}
			else if (waterlevel >= 2)
			{
				Vel.Z = 4 * Speed;
			}
			else if (bNoGravity && !leveltilt && !climbing)
			{
				Vel.Z = 3.;
			}
			else if (climbing && !player.onground && level.IsJumpingAllowed())
			{
				if (abs(deltaangle(angle, AngleTo(climbing))) > 60)
				{
					if (player.cmd.forwardmove > 0)
					{
						Thrust(Speed / 2, angle);
						Vel.Z = 1.0;
					}
				}
				else
				{
					Vel.Z = 3.;
				}
			}
			else if (level.IsJumpingAllowed() && (player.onground || leveltilt != 0) && player.jumpTics == 0)
			{
				double jumpvelz = JumpZ * 35 / TICRATE;
				double jumpfac = 0;

				// [BC] If the player has the high jump power, double his jump velocity.
				// (actually, pick the best factors from all active items.)
				for (let p = Inv; p != null; p = p.Inv)
				{
					let pp = PowerHighJump(p);
					if (pp)
					{
						double f = pp.Strength;
						if (f > jumpfac) jumpfac = f;
					}
				}
				if (jumpfac > 0) jumpvelz *= jumpfac;

				Vel.X += jumpvelz * sin(leveltilt) * cos(leveltiltangle) * level.aircontrol;
				Vel.Y += jumpvelz * sin(leveltilt) * sin(leveltiltangle) * level.aircontrol;
				Vel.Z += jumpvelz * cos(leveltilt);
				bOnMobj = false;
				player.jumpTics = -1;
				if (!(player.cheats & CF_PREDICTING)) A_PlaySound("*jump", CHAN_BODY);
			}
		}
	}

	override void CheckUndoMorph()
	{
		if (player) { Super.CheckUndoMorph(); }
	}

	Actor GetLineTarget()
	{
		CrosshairTracer actortrace;
		actortrace = new("CrosshairTracer");

		actortrace.skipactor = self;
		Vector3 tracedir = (cos(angle) * cos(pitch), sin(angle) * cos(pitch), -sin(pitch));
		actortrace.Trace(self.pos + (0, 0, player.viewheight), self.CurSector, tracedir, 1024, 0);

		return actortrace.Results.HitActor;
	}

	Actor GetClosestForcedHealthBar()
	{
		ThinkerIterator Finder = ThinkerIterator.Create("Base", Thinker.STAT_DEFAULT - 3);
		Base it;
		Actor mo;

		while ( it = Base(Finder.Next()) )
		{
			if (!it.user_DrawHealthBar == True) { continue; } // Only process actors with the AlwaysDrawHealthBar flag set
			if (
				it.health <= 0 ||
				!it.bShootable ||
				it.bDormant
			) { continue; }
			if (mo && Distance3D(it) > Distance3D(mo)) { continue; } // Only draw health bar for the closest one

			mo = it;
		}

		return mo;
	}

	// Helper function for sprite index lookups to avoid hard engine crash
	SpriteID LookupSprite(Name spritecheck)
	{
		SpriteID temp = GetSpriteIndex(spritecheck);
		if (temp > -1) { return temp; }

		console.printf("\cgERROR: \cjThe sprite name index for '\cf%s\cj' could not be found.  Did you forget to add it to one of the actor's states?", spritecheck);
		return sprite;
	}

	// Function to do the actual token checking and sprite setting
	bool CheckSpriteToken(String token, Name standsprite, Name crouchsprite = "")
	{
		user_crouch = GetCrouchFactor(AAPTR_DEFAULT);

		DisguiseToken token = DisguiseToken(FindInventory(Name(token)));

		if (token && token.Amount > 0)
		{
			if (!(crouchsprite == "") && user_crouch < 0.75)
			{
				sprite = LookupSprite(crouchsprite);
				Scale.Y = user_ScaleY / user_crouch; // Negate crouch sprite scaling
			}
			else
			{
				sprite = LookupSprite(standsprite);
				Scale.Y = user_ScaleY;
			}

			if (token.viewheight > -1)
			{
				viewheight = token.viewheight;
				attackzoffset = Default.attackzoffset * token.viewheight / Default.viewheight;
			}
			if (token.playerscale > 0 && scale != Default.scale * token.playerscale)
			{
				scale = Default.scale * token.playerscale;
				A_SetSize(Default.radius * scale.x / Default.scale.x, Default.height * scale.y / Default.scale.y);
			}
			if (token.viewbob > -1) { viewbob = token.viewbob; }
			if (token.forwardmove1 > -1 && token.forwardmove2 > -1) { forwardmove1 = token.forwardmove1; forwardmove2 = token.forwardmove2; }
			if (token.sidemove1 > -1 && token.sidemove2 > -1) { sidemove1 = token.sidemove1; sidemove2 = token.sidemove2; }

			return true;
		}

		viewheight = Default.viewheight;
		attackzoffset = Default.attackzoffset;
		if (scale != Default.scale)
		{
			scale = Default.scale;
			A_SetSize(Default.radius, Default.height);
		}
		viewbob = Default.viewbob;
		forwardmove1 = Default.forwardmove1;
		forwardmove2 = Default.forwardmove2;
		sidemove1 = Default.sidemove1;
		sidemove2 = Default.sidemove2;

		return false;
	}

	void DoTokenChecks()
	{
		if (health <= 0) { return; } // Don't change sprites if the player is dead

		if (user_ScaleY == 0) { user_ScaleY = Scale.Y; }
		if (!baseSprite) { baseSprite = sprite; }

		// Firebrand effect handling
		if (player && player.ReadyWeapon is "FireBrand" && CountInv("PowerWeaponLevel2") > 0) { SpawnEffect("FireBrandEffect"); }
		else { RemoveEffect("FireBrandEffect"); }
		
		// Lantern effect handling
		LanternPickup lantern = LanternPickup(FindInventory("LanternPickup"));

		if (player && lantern && lantern.active) { SpawnEffect("LanternEffect"); }
		else { RemoveEffect("LanternEffect"); }

		// New uniform or other sprite-changing tokens should be set up here.  See CheckSpriteToken function above.
		//  These should be listed in order of precedence - The ones at the top will take priority.
		//  All new sprites need to be added to the 'SpriteList' state above or the engine won't be able to use them.
		if (
			CheckSpriteToken("CCBJUniformToken", "PLCP", "PLYP") ||
			CheckSpriteToken("ScientistUniformToken", "PLSC", "PLYS") ||
			CheckSpriteToken("SSBJUniformToken", "PLSS", "PLSD") ||
			CheckSpriteToken("RyanToken", "PLRR", "PLYR") ||
			CheckSpriteToken("KeenToken", "KEEN", "KEEN")
		) { return; }

		// If no tokens found, restore the base sprite and original scale
		sprite = baseSprite;
		Scale.Y = user_ScaleY;
	}

	// Function to spawn no more than a single copy of an effect actor as a child of the player
	void SpawnEffect(class<PlayerEffect> effect)
	{
		ThinkerIterator it = ThinkerIterator.Create("PlayerEffect", Thinker.STAT_DEFAULT - 4);
		PlayerEffect mo;

		while (mo = PlayerEffect(it.Next()))
		{
			if (mo.master && mo.master != self) { continue; } // Only look at effects that belong to this player actor
			if (mo is effect) { return; } // The effect already exists, so abort so we don't spawn it again
		}

		// If it wasn't already there, spawn this effect
		Actor newEffect = Spawn(effect);
		if (newEffect) { newEffect.master = self; }
	}

	// Function to remove any spawned copy of an effect that is a child of the player
	void RemoveEffect(class<PlayerEffect> effect)
	{
		ThinkerIterator it = ThinkerIterator.Create("PlayerEffect", Thinker.STAT_DEFAULT - 4);
		PlayerEffect mo;

		while (mo = PlayerEffect(it.Next()))
		{
			if (mo.master && mo.master != self) { continue; } // Only look at effects that belong to this player actor
			if (mo is effect) { mo.SetStateLabel('null'); } // Remove the effect, if found
		}
	}

	// Function to handle leveltilt-induced "gravity" effects.
	void DoGravity()
	{
		// If the player isn't using NOCLIP and they are at the "tipping point" for their mass, make them slide to the new "down"
		if (abs(leveltilt) >= min(mass, 1000) / 1000.0 * 45 && !(player.cheats & (CF_NOCLIP | CF_NOCLIP2)) && !climbing)
		{
			bNoGravity = leveltilt != 0;

			double grav = level.gravity * CurSector.gravity * Gravity * 0.00125;

			vel.z -= grav * cos(leveltilt);
			vel.x -= grav * sin(leveltilt) * cos(leveltiltangle);
			vel.y -= grav * sin(leveltilt) * sin(leveltiltangle);

			if (leveltilt != oldtilt)
			{
				if (leveltilt == 0)
				{
					A_SetSize(Default.Radius, Default.Height);
				}
				else
				{
					double abstilt = abs(leveltilt);
					double newradius = Default.radius * cos(abstilt) + Default.height / 2 * sin(abstilt);
					double newheight = Default.radius * sin(abstilt) + Default.height / 2 * cos(abstilt);

					if (height != newheight || radius != newradius) { A_SetSize(newradius, newheight, true); }
				}

				oldtilt = leveltilt;
			}
		}
	}

	void DoInteractions()
	{
		bool dodragging = false; // Set true to enable dragging of corpses and pushable actors (crouch and use)

		FLineTraceData trace;
		LineTrace(angle, UseRange, pitch, TRF_THRUACTORS, player.viewheight, 0.0, 0.0, trace);
		Line AimLine = trace.HitLine;
		TextureID AimTexture = trace.HitTexture;

		interactiontimeout = max(interactiontimeout - 1, 0);

		if (AimTexture)
		{
			// Handle texture-specific "generic" activations - really need to figure out a 
			// good way to make this an event handler or something, so it's not tied only
			// to the BoAPlayer player class.
			if (player.buttons & BT_USE && !(player.oldbuttons & BT_USE)) // If the player just pressed use
			{
				// See what texture we are aiming at
				String texname = TexMan.GetName(AimTexture);

				if (texname.IndexOf(".") >= 0) // If it's a long filename
				{
					// Strip the texture name down to only the filename (basically the old-style texture name)
					int start = texname.RightIndexOf("/") + 1;
					texname = texname.Mid(start, texname.RightIndexOf(".") - start);
				}

				// Convert to all upper-case for comparison
				texname.ToUpper();

				if (texname == "CABN_T02" && !interactiontimeout) // If it's the storage shelf with medkits...
				{
					if (Health < MaxHealth) // ...and we're not at full health...
					{
						GiveInventory("BandagesPowerup", 1); // Heal by 10 and play the bandage tearing sound
						A_Playsound("pickup/bandage", CHAN_VOICE);
						interactiontimeout = 350; // Set use delay of 10 seconds before re-use
					}
				}
				else if (texname == "VENT_M01") // If it's a breakable vent...
				{
					if (trace.HitType == FLineTraceData.TRACE_HitWall)
					{
						AimLine.Activate(self, trace.LineSide, SPAC_Impact); // Pretend it was shot
					}
				}
				// This can be expanded to include custom interaction sounds for any texture present in 
				// the game (e.g., Ion Maiden-style 'locked door' rattles).  This does *not* override
				// line specials that are present in the map...  If a texture with an action set here
				// is also given a line special to be executed on player use, both actions will be run.
				else if ( // I think this is most of the 'unusable door' textures?
					texname == "DOOR_B09" ||
					texname == "DOOR_B89" ||
					texname == "DOOR_F00_X" ||
					texname == "DOOR_G11" ||
					texname == "DOOR_G12" ||
					texname == "DOOR_G20" ||
					texname == "DOOR_G21" ||
					texname == "DOOR_G30" ||
					texname == "DOOR_M00" ||
					texname == "DOOR_M02" ||
					texname == "DOOR_M02D" ||
					texname == "DOOR_SET1" ||
					texname == "DOOR_SET2" ||
					texname == "DOOR_W40" ||
					texname == "DOOR_L02" ||
					texname == "DOOR_B52" ||
					texname == "MIDG_M89"
				)
				{
					A_Playsound("picklock/locked", CHAN_VOICE, 0.25);
				}
			}
		}

		Actor AimActor;
		if (dodragging)
		{
			FLineTraceData actortrace;
			LineTrace(angle, UseRange, pitch, TRF_ALLACTORS, player.viewheight, 0.0, 0.0, actortrace); // Corpse dragging needs to trace without TRF_THRUACTORS flag
			AimActor = actortrace.HitActor;
		}

		if (AimLine && !crosshair)// || Level.GetUDMFInt(Level.UDMF_Line, AimLine.Index(), nm"user_forcecrosshair"))
		{
			// Only show custom crosshair if the line is running a script, is a locked door or puzzle item use line, or has a UDMF lock number
			//  Otherwise - Why?  It's not an activation line, so what's the point of showing a hint?
			if ( 	
				AimLine.special == 13 || // Door_LockedRaise
				AimLine.special == 80 || // ACS_Execute 
				(AimLine.special >= 83 && AimLine.special <= 85) || // ACS_LockedExecute, ACS_ExecuteWithResult, ACS_LockedExecuteDoor
				Aimline.special == 129 || // UsePuzzleItem
				AimLine.special == 226 || // ACS_ExecuteAlways
				Aimline.locknumber
			)
			{
				crosshair = AimLine.GetUDMFInt("user_crosshair"); // Use the line's user_crosshair property if it has a value
				crosshairstring = AimLine.GetUDMFString("user_crosshair"); // Or try looking for a class name as a string value
			}
			else
			{
				crosshair = 0;
				crosshairstring = "";
			}
		}

		if (AimLine && !AimLine.special) { AimLine = null; }

		// If line is set to block hitscans only, do some special handling
		if (AimLine && AimLine.flags & Line.ML_BLOCKHITSCAN && !(AimLine.flags & Line.ML_BLOCKEVERYTHING) && !(AimLine.flags & Line.ML_WRAP_MIDTEX))
		{
			if (AimLine.flags & Line.ML_3DMIDTEX) // If it's a 3D midtex, figure out if we hit the texture or not...
			{
				let side = AimLine.sidedef[trace.LineSide];

				if (side)
				{
					TextureID tex = side.GetTexture(trace.LinePart);

					if (tex)
					{
						Vector2 texsize = TexMan.GetScaledSize(tex);
						double scaley = side.GetTextureYScale(trace.LinePart);
						double sizey = texsize.y / scaley;
						double offsety = side.GetTextureYOffset(trace.LinePart) / scaley;

						double topz, bottomz;

						// Adjust things if lower unpegged is set...
						if (AimLine.flags & Line.ML_DONTPEGBOTTOM) { topz = sizey; }
						else { topz = trace.HitSector.ceilingplane.D - trace.HitSector.floorplane.D; }

						topz += offsety;
						bottomz = topz - sizey;

						double hitz = trace.HitLocation.z + trace.HitSector.floorplane.D;

						if (topz < hitz || bottomz > hitz) // You're not actually looking at the midtex!
						{
							AimLine = null; // Clear the line pointer
						}
					}
					else // No texture
					{
						AimLine = null; // Clear the line pointer
					}
				}
			}
		}

		// Handle player holding down use button while looking at a repeatable, script-executing line that's within use range
		if (AimLine) // If we are actually looking at a line within range
		{
			if (
				( // And the line is running a script
					AimLine.special == 80 || 
					(AimLine.special >= 83 && AimLine.special <= 85) || 
					AimLine.special == 226
				) &&
				( // And is flagged as repeatable and you're pressing use
					AimLine.flags & Line.ML_REPEAT_SPECIAL && 
					player.cmd.buttons & BT_USE
				) &&
				( // And we have the right UDMF-defined key if one was set on the line!
					!Aimline.locknumber || 
					CheckKeys(Aimline.locknumber, false, true)
				)
			)
			{
				player.usedown = false; // Pretend that the use button wasn't actually being held so that the use action will repeat as long as use is held

				if (!LastWeapon && crosshair >= 80 && crosshair <= 90) // Only lower weapon when there's a status display
				{
					Weapon lowered = Weapon(FindInventory("NullWeapon"));

					if (player.ReadyWeapon != lowered) { LastWeapon = player.ReadyWeapon; }
					player.PendingWeapon = lowered;
				}
			}
		}
		// Draggable corpse handling (also lets you pull pushable actors)
		//  Player must crouch within use range of a draggable actor and hold use, then move to drag the actor along
		else if (dodragging && (AimActor || DragTarget) && player.usedown && player.cmd.buttons & BT_CROUCH)
		{
			if (DragTarget)
			{
				DragTarget.Warp(self, Clamp(Distance2D(DragTarget), radius + DragTarget.radius + speed, UseRange), flags:WARPF_INTERPOLATE | WARPF_NOCHECKPOSITION);

				// Closed hand grab icon
				crosshair = 97;
			}
			else
			{
				if (
					AimActor && 
					Distance2D(AimActor) < UseRange && 
					(
						IsCorpse(AimActor) ||
						AimActor.bPushable
					)
				)
				{
					if (!LastWeapon)
					{
						Weapon lowered = Weapon(FindInventory("NullWeapon"));

						if (player.ReadyWeapon != lowered) { LastWeapon = player.ReadyWeapon; }
						player.PendingWeapon = lowered;
					}
					DragTarget = AimActor;
					Speed = Default.Speed * 0.05;
				}
			}
		}
		else
		{
			if (LastWeapon)
			{
				player.PendingWeapon = LastWeapon;
				LastWeapon = null;
			}

			crosshair = 0;
			crosshairstring = "";

			if (dodragging)
			{
				DragTarget = null;
				Speed = Default.Speed;

				if (
					player.cmd.buttons & BT_CROUCH &&
					AimActor && 
					Distance2D(AimActor) < UseRange + AimActor.Radius && 
					(
						IsCorpse(AimActor) ||
						(!AimActor.bIsMonster && AimActor.bPushable)
					)
				)
				{
					// Open hand grab icon
					crosshair = 98;
				}
			}
		}

		// Blank out the actual crosshair if a custom one is being used - 0 reverts to the player or weapon's default
		if (player.ReadyWeapon) { player.ReadyWeapon.Crosshair = (crosshair > 0 || crosshairstring) ? 99 : 0; }
	}

	bool IsCorpse(Actor mo)
	{
		if (
			!mo.bIsMonster ||
			mo.health > 0 ||
			mo.bDormant
		) { return false; }

		return true;
	}

	static void FireWeapon(Actor mo, bool alt = 0, bool onuse = 1)
	{
		if (mo.player && mo.player.ReadyWeapon && mo.player.playerstate == PST_LIVE)
		{
			let psp = mo.player.GetPSprite(PSP_WEAPON);

			State newState = alt ? mo.player.ReadyWeapon.FindState('AltFire') : mo.player.ReadyWeapon.FindState('Fire');

			if (newState) { psp.SetState(newState); }
		}
	}
}

// Base class for simple visual effects that will always follow the player until removed
class PlayerEffect : Actor
{
	Default
	{
		+NOINTERACTION
	}

	States
	{
		Spawn:
			// This state should be replaced in child classes
			UNKN A 1;
			Loop;
	}

	override void BeginPlay()
	{
		ChangeStatNum(Thinker.STAT_DEFAULT - 4);
	}

	override void Tick()
	{
		// If a master is assigned, always move to its location
		if (master) { A_Warp(AAPTR_MASTER, flags:WARPF_COPYPITCH | WARPF_COPYINTERPOLATION | WARPF_NOCHECKPOSITION); }
		Super.Tick();
	}
}

class FireBrandEffect : PlayerEffect
{
	States
	{
		Spawn:
			TNT1 A 4 NODELAY Light("ITBURNSOC1");
			TNT1 A 4 Light("ITBURNSOC2");
		SpawnLoop:
			TNT1 A 4 Light("ITBURNSOC3") A_SetTics(Random(1, 8));
			Loop;
	}
}

class LanternEffect : PlayerEffect
{
	States
	{
		Spawn:
			TNT1 A 4 NODELAY Light("LANT01");
			TNT1 A 4 Light("LANT02");
		SpawnLoop:
			TNT1 A 4 Light("LANT03") A_SetTics(Random(1, 3));
			Loop;
	}
}

Class CrosshairTracer : LineTracer
{
	Actor skipactor;

	override ETraceStatus TraceCallback() 
	{
		if (Results.HitType == TRACE_HitActor)
		{
			if (
				Results.HitActor != skipactor && // Skip the player
				(!Results.HitActor.master || Results.HitActor.master != skipactor) && // And any children
				!Results.HitActor.bNoInteraction && // And it's actually interactive...
				(Results.HitActor.bSolid || Results.HitActor.bShootable) // And only return shootable actors
			) { return TRACE_Continue; } // Fall through, but remember the actor that you hit

			return TRACE_Skip;
		}
		else if (Results.HitType == TRACE_HitFloor || Results.HitType == TRACE_HitCeiling)
		{
			return TRACE_Stop;
		}
		else if (Results.HitType == TRACE_HitWall)
		{
			if (Results.HitLine.flags & (Line.ML_BLOCKING | Line.ML_BLOCKEVERYTHING)) { return TRACE_Stop; }
			if (Results.HitTexture)
			{
				if (Results.Tier != TIER_Middle || Results.HitLine.flags & Line.ML_3DMIDTEX) // 3D Midtex check still isn't perfect...
				{
					return TRACE_Stop;
				}
				return TRACE_Skip;
			}
			return TRACE_Skip;
		}

		return TRACE_Stop;
	}
}