/*

  ActorSpawner
    These are spawn point actors that spawn actors when active, and maintain a set
    number of those actors alive (1 by default).  Actors will not be spawned if a player
    can see the spawn point or if a player is within a defined minimum spawn distance
    from the spawner (default 512 units).

    By default, the spawner will spawn a single guard.  Once that guard is killed, the
    spawner will spawn another guard, and so on.

    Spawned actors can be given a TID and can be set to navigate to a specific
    PatrolPoint TID after spawn as well.

  Custom UDMF Properties
    arg0str
      Sets the class of actor to be spawned (default "Guard")

    user_tid
      Sets the TID that the spawned enemies are given.  By default they have no TID.

    user_goal
      Sets the goal/patrolpoint TID that the spawned enemies will walk to once they return to
      being idle.  By default, they go back to their spawn point and stand still.

    user_maxactors
      Sets how many enemies from this spawner to maintain alive at once (default 1) - use negative numbers to set total number spawned instead of number to keep alive

    user_minspawndistance
      Distance from player inside of which the spawner will stop spawning (default 512)

*/
class ActorSpawner : SwitchableDecoration
{
	Array<Actor> Spawns;
	Class<Actor> user_spawntype;

	// TID to give the spawned enemy actor (default no TID)
	int user_tid;

	// TID of goal that the enemy should walk to after spawning (default no goal)
	int user_goal;

	// How many enemies from this spawner to maintain alive at once (default 1)
	int user_maxactors;

	// How many enemies from this spawner to spawn in total (defaults to infinite)
	int user_maxspawns;

	// Distance from player inside of which the spawner will stop spawning (default 512)
	int user_minspawndistance;

	Default
	{
		//$Arg0 "Thing ID to spawn"
		//$Arg0Str "Actor class to spawn"
		//$Arg0ToolTip "What actor to spawn.  Numeric values set Thing ID, and string values set spawn class."
		//$Arg1 "Maximum number alive"
		//$Arg1ToolTip "Determines the number of actors to keep alive at once (e.g., '5' will spawn a new actor every time you kill one, keeping 5 alive at all times)."
		//$Arg2 "TID to give actor"
		//$Arg2ToolTip "TID that will be given to the spawned actor(s).  Default is 0 (no TID)."
		//$Arg3 "Minimum player distance"
		//$Arg3ToolTip "Distance that the player must be away from this actor in order for spawns to take place.  A value of '0' defaults to 512."
		//$Arg3Default 512
		//$Arg4 "Maximum number to spawn, total"
		//$Arg4ToolTip "Determines the number of actors to spawn in total, regardless of how many are killed. Zero means unlimited."
		//$Sprite EXCLC0
	}

	States
	{
		Spawn:
			TNT1 A 1;
			Loop;
		Active:
			"####" "#" 35 A_SetTics(Random[Spawn](1, 7) * 5);
			"####" "#" 0 { return ResolveState("Active.Spawn"); }
		Active.Spawn:
			"####" "#" 1 A_DoSpawns();
		Inactive:
			"####" "#" 35;
			Loop;
	}

	override void PostBeginPlay()
	{
		Super.PostBeginPlay();

		user_spawntype = GetSpawnableType(args[0]);
		user_maxactors = args[1];
		user_tid = args[2];
		user_minspawndistance = args[3];
		user_maxspawns = args[4];

		if (!user_spawntype) { user_spawntype = "Guard"; }
		if (user_maxactors == 0) { user_maxactors = 1; }
		if (user_minspawndistance == 0) { user_minspawndistance = 512; }

		if (bDormant) { SetStateLabel("Inactive"); }
	}

	bool InPlayerSightOrRange(int range = 512)
	{
		for (int p = 0; p < MAXPLAYERS; p++) // Iterate through all of the players and see if any can see the spawn point
		{
			Actor mo = players[p].mo;

			if (mo) {
				LookExParams look;
				look.fov = mo.player.fov * 1.4; // Plus a little extra to account for wider screen ratios...

				if (mo.IsVisible(self, false, look)) { return true; }
				if (Distance3d(mo) <= range) { return true; }
			}
		}

		return false;
	}

	int CountSpawns(Class<Actor> spawntype = null, bool countdead = false)
	{
		int spawncount = 0;
		if (!spawntype) { spawntype = user_spawntype; }

		for (int i = 0; i < Spawns.Size(); i++) {
			Actor mo = Spawns[i];

			if (mo) {
				if (!mo.bShootable || mo.health <= 0) {
					if (countdead) { spawncount++; }
					//Spawns.Delete(i);
					//Spawns.ShrinkToFit();
				} else if (mo.GetClass() == spawntype) {
					spawncount++;
				}
			}
		}

		return spawncount;
	}

	int TotalSpawns(Class<Actor> spawntype = null, bool countdead = false)
	{
		if (!spawntype) { spawntype = user_spawntype; }

		if (!tid) { return CountSpawns(spawntype, countdead); }

		int total = 0;

		ActorIterator it = ActorIterator.Create(tid, "ActorSpawner");
		Actor spot;

		while (spot = it.Next())
		{
			total += ActorSpawner(spot).CountSpawns(spawntype, countdead);
		}

		return total;
	}

	virtual state A_DoSpawns()
	{
		int spawncount = TotalSpawns();
		int totalspawncount = TotalSpawns(countdead:true);

		if (spawncount < user_maxactors && (totalspawncount < user_maxspawns || user_maxspawns == 0)) {
			//Spawn enemies if not visible to player
			if (!InPlayerSightorRange(user_minspawndistance)) {
				Actor spawned = Spawn(user_spawntype, pos);
				if (spawned)
				{
					Spawns.Push(spawned);
					spawned.master = self;
					spawned.target = target;
					spawned.angle = angle;
					spawned.ChangeTID(user_tid);
					if (user_goal > 0)
					{
						let it = ActorIterator.Create(user_goal, "PatrolPoint");
						Actor goal;
						if (it) { goal = it.Next(); }
						if (goal) { spawned.goal = goal; }
	
						if (Nazi(spawned))
						{
							if (Nazi(spawned).user_sneakable) { Nazi(spawned).BecomeAlerted(); }
							Nazi(spawned).activationgoal = goal;
						}	

						spawned.SetStateLabel("See");
					}
				}
			}
		}

		if (bDormant) { return ResolveState("Inactive"); } // Cut out if the spawner is now dormant
		return ResolveState("Active");
	}
}

struct Wave
{
	Class<Actor> enemy;
	int count;
	int maxspawns;
	int round;
}

class WaveSpawner : ActorSpawner
{
	Wave Waves[32]; // Can't use dynamic arrays with structs, unfortunately...  If we need more waves, this number can be increased
	int waveindex;
	int index;
	int maxindex;
	int round;
	bool user_lightburns;

	Property BrightLightKills:user_lightburns;

	Default
	{
		//$Arg0 "Unused"
		//$Arg1 "Unused"
		//$Arg4 "Unused"
	}

	States
	{
		Active:
			"####" "#" 0 { round++; } // Increment the round when activated
		Active.Spawn:
			"####" "#" 35 A_SetTics(Random[Spawn](1, 21) * 5);
			"####" "#" 0 A_DoSpawns();
		Inactive:
			"####" "#" 0 {
				Spawns.Clear(); // Clear the enemy spawn queue when deactivated.  This way, when re-activated, everything starts over again
				Spawns.ShrinkToFit();
			}
		InactiveLoop:
			"####" "#" 35;
			Loop;
	}

	static void AddWaveComponent(int tid, String actorclass, int count = 1, int maxspawns = 0, int round = 0)
	{
		if (tid)
		{
			ActorIterator it = ActorIterator.Create(tid, "WaveSpawner");
			WaveSpawner mo;

			while (mo = WaveSpawner(it.Next()))
			{
				mo.NewWaveComponent(actorclass, count, maxspawns, round);
			}
		}
	}

	void NewWaveComponent(String actorclass, int count = 1, int maxspawns = 0, int round = 0)
	{
		Waves[waveindex].enemy = actorclass;
		Waves[waveindex].count = count;
		Waves[waveindex].maxspawns = maxspawns;
		Waves[waveindex].round = round;

		waveindex++;
	}

	bool ValidSpawn(int index)
	{
		if (!Waves[index].enemy) { return false; }
		if (Waves[index].round > round) { return false; }
		if (TotalSpawns(Waves[index].enemy) >= Waves[index].count) { return false; }
		if (
			Waves[index].maxspawns != 0 &&
			TotalSpawns(Waves[index].enemy, true) >= Waves[index].maxspawns
		) { return false; }

		return true;
	}

	double GetLightLevel(Actor mo = null)
	{
		if (!mo) { mo = self; }

		Color light = mo.CurSector.ColorMap.LightColor;

		// Sector light level minus the average (inverted) RGB light level minus fog depth
		return mo.CurSector.lightlevel - (255 - (light.r + light.g + light.b) / 3) - 255 * (mo.CurSector.ColorMap.FogDensity / 510);
	}

	override state A_DoSpawns()
	{
		if (user_lightburns) // Chapter 3 Secret Level
		{
			double lightlevel = GetLightLevel();

			if (ceilingpic == skyflatnum && lightlevel > 80) { return ResolveState("Active.Spawn"); } // Don't spawn until it's actually dark
			else if (lightlevel > 128) { return ResolveState("Active.Spawn"); } // But be more forgiving in our definition of 'dark' if you're not under sky
		}

		bool looped = false;

		index = Random(0, waveindex - 1);

		While (!ValidSpawn(index))
		{
			index++;

			if (index > 31)
			{
				index = 0;
				if (looped) { return ResolveState("Active.Spawn"); }
				looped = true;
			}
		}

		user_spawntype = Waves[index].enemy;
		user_maxactors = Waves[index].count;
		user_maxspawns = Waves[index].maxspawns;

		int spawncount = TotalSpawns();
		int totalspawncount = TotalSpawns(countdead:true);

		if (spawncount < user_maxactors && (totalspawncount < user_maxspawns || user_maxspawns == 0)) {
			//Spawn enemies if not visible to player
			if (!InPlayerSightorRange(user_minspawndistance)) {
				Actor spawned = Spawn(user_spawntype, pos);
				if (spawned)
				{
					Spawns.Push(spawned);

					spawned.master = self;
					spawned.target = target;
					spawned.angle = angle;

					spawned.ChangeTID(user_tid);

					if (Nazi(spawned)) { Nazi(spawned).dodgetimeout = 350; }
					if (Base(spawned))
					{
						Base(spawned).lightthreshold = 130;
						Base(spawned).user_lightburns = true;
					}

					if (user_goal > 0)
					{
						let it = ActorIterator.Create(user_goal, "PatrolPoint");
						Actor goal;
						if (it) { goal = it.Next(); }
						if (goal) { spawned.goal = goal; }
	
						if (Nazi(spawned) && !(spawned is "ZombieStandard"))
						{
							if (Nazi(spawned).user_sneakable) { Nazi(spawned).BecomeAlerted(); }
							Nazi(spawned).activationgoal = goal;
						}	

						spawned.SetStateLabel("See");
					}
				}
			}
		}

		if (bDormant) { return ResolveState("Inactive"); } // Cut out if the spawner is now dormant
		return ResolveState("Active.Spawn");
	}

	override void Tick()
	{
		if (user_lightburns) // Chapter 3 Secret Level
		{
			for (int i = 0; i < Spawns.Size(); i++)
			{
				if (!Spawns[i])
				{
 					Spawns.Delete(i);
					Spawns.ShrinkToFit();
				}
			}
		}

		Super.Tick();
	}
}

// Base barrel actor that allows you to specify an item as args[0] in-editor that will always be dropped when the barrel/object is destroyed.
class BarrelSpawner : ExplosiveBarrel
{
	Class<Actor> user_spawntype; 
	Actor drop;

	Default
	{
		//$Arg0 "Thing ID to spawn"
		//$Arg0Str "Actor class to spawn"
		//$Arg0ToolTip "What actor to spawn.  Numeric values set Thing ID, and string values set spawn class.  \nDefaults to nothing spawned, beyond any defined DropItems."
	}

	override void PostBeginPlay()
	{
		// Setting a special on the actor overrides the use of args[0] to specify the drop item
		if (!special && !user_spawntype)
		{
			user_spawntype = GetSpawnableType(args[0]);
		}

		Super.PostBeginPlay();
	}

	override int DamageMobj(Actor inflictor, Actor source, int damage, Name mod, int flags, double angle)
	{
		int damageamt = Super.DamageMobj(inflictor, source, damage, mod, flags, angle);

		if (health <= 0)
		{
			if (user_spawntype && !drop)
			{
				drop = Spawn(user_spawntype, pos);

				if (drop)
				{
					drop.SetOrigin(pos + (0, 0, 10), false);
					drop.angle = angle;
					drop.VelFromAngle(5, inflictor.AngleTo(self));
					drop.vel.z = 1;
					drop.vel += vel;
					drop.bNoGravity = false;
					drop.ClearCounters();
				}
			}
			else
			{
				A_NoBlocking(); // Unset blocking and drop the actor's defined DropItems
			}
		}

		return damageamt;
	}
}

// Generic actor that can be inherited from to give an actor editor-configurable item drop
// Used by Egyptian vases
class DestructionSpawner : Actor
{
	Class<Actor> user_spawntype; 
	Actor drop;

	Default
	{
		//$Arg0 "Thing ID to spawn"
		//$Arg0Str "Actor class to spawn"
		//$Arg0ToolTip "What actor to spawn.  Numeric values set Thing ID, and string values set spawn class.  \nDefaults to nothing spawned, beyond any defined DropItems."
	}

	override void PostBeginPlay()
	{
		// Setting a special on the actor overrides the use of args[0] to specify the drop item
		if (!special && !user_spawntype)
		{
			user_spawntype = GetSpawnableType(args[0]);
		}

		Super.PostBeginPlay();
	}

	override int DamageMobj(Actor inflictor, Actor source, int damage, Name mod, int flags, double angle)
	{
		int damageamt = Super.DamageMobj(inflictor, source, damage, mod, flags, angle);

		if (health <= 0)
		{
			if (user_spawntype && !drop)
			{
				drop = Spawn(user_spawntype, pos);

				if (drop)
				{
					drop.SetOrigin(pos + (0, 0, 10), false);
					drop.angle = angle;
					drop.VelFromAngle(5, inflictor.AngleTo(self));
					drop.vel.z = 1;
					drop.vel += vel;
					drop.bNoGravity = false;
					drop.ClearCounters();
				}
			}
			else
			{
				A_NoBlocking(); // Unset blocking and drop the actor's defined DropItems
			}
		}

		return damageamt;
	}
}