/*

  Base class for BoA Weapons

  Custom Properties
	NaziWeapon.Stealth
	- Boolean property that allows the weapon to be carried while in disguise 
	  without breaking the disguise.  See Shovel and NullWeapon for example.
	- Firing the weapon can still alert enemies and cause the disguise to fail.

*/
class NaziWeapon : Weapon
{
	int allowdisguised; // Set to 1 or True for normal behavior (can carry, but alerts if you fire).  Set to 2 to never alert 
	int notargettimeout;
	int lowertimeout;
	float lowerstart, lowerspeed;

	Property Stealth:allowdisguised; // Weapons tagged as stealth can be carried while in disguise without breaking the disguise unless they are fired

	Default
	{
		+WEAPON.AMMO_CHECKBOTH
		+WEAPON.NOALERT
		Weapon.BobStyle "InverseSmooth";
		Weapon.BobSpeed 2.2;
		Weapon.BobRangeY 1.0;
		Weapon.BobRangeX 1.25;
		NaziWeapon.Stealth False;
	}

	States
	{
		Dryfire:
			"####" "#" 1 A_JumpIf(CallACS("Autoreload")==1,"Reload");
			"####" "#" 1 Offset(0,35) A_PlaySound("weapon/dryfire", CHAN_WEAPON);
			"####" "#" 9 Offset(0,34);
			"####" "#" 0 A_Jump(256,"Ready");
			Stop;
	}

	override void PostBeginPlay()
	{
		// Allow the drop amount of the weapon to set the amount of primary ammunition that is in a pickup
		// Default drop amount value is 1, and is interpreted as normal drop amount handling...
		if (Amount > 1)
		{
			// Take skill ammo drop amounts into account when calculating amount of ammo to give
			float dropammofactor = G_SkillPropertyFloat(SKILLP_DropAmmoFactor);
			if (dropammofactor == -1) { dropammofactor = 0.5; }

			// Default to affecting AmmoType1, with fallback to AmmoType2 if the
			//  weapon does not give any of AmmoType1 (Nazis weapons use AmmoType1
			//  as the current clip's amount, and give only to AmmoType2 normally.
			if (AmmoGive1 > 0) { AmmoGive1 = int(Amount * dropammofactor); }
			else if (AmmoGive2 > 0) { AmmoGive2 = int(Amount * dropammofactor); }

			Amount = 1;
		}

		if (skill >= 2) { bNoAutoAim = true; } // Force autoaim off if we're on normal skill or higher

		Super.PostBeginPlay();
	}

	override void Tick()
	{
		if (owner && owner.player)
		{
			if (owner.player.ReadyWeapon == self && owner.FindInventory("HQ_Checker", true))
			{
				lowertimeout++;

				let psp = owner.player.GetPSprite(PSP_WEAPON);
				let downstate = GetDownState();

				if (psp.CurState.InStateSequence(GetUpState()) || lowertimeout < 35) { return; }
				else if (!psp.CurState.InStateSequence(downstate))
				{
					owner.player.SetPsprite(PSP_WEAPON, downstate);
					lowerstart = psp.y;
				}
				else
				{
					if (!lowerspeed) { lowerspeed = psp.y - lowerstart; }

					if (psp.y >= WEAPONBOTTOM - lowerspeed)
					{
						owner.player.ReadyWeapon = null;
						owner.player.PendingWeapon = WP_NOCHANGE;
						lowertimeout = 0;
					}
				}
			}

			if (allowdisguised)
			{
				let disguise = DisguiseToken(owner.FindInventory("DisguiseToken", true));

				if (owner.player.ReadyWeapon == self && disguise && disguise.NoTarget && !owner.FindInventory("DisguiseFailToken", true))
				{
					let psp = owner.player.GetPSprite(PSP_WEAPON);

					if ( // If in ready state, up state, or down state, turn notarget on unless enemies are already alerted
						allowdisguised > 1 ||
						psp.CurState == GetReadyState() ||
						psp.CurState == GetUpState() ||
						psp.CurState == GetDownState()
					) 
					{
						Inventory vis = owner.FindInventory("BoAVisibility");

						if (!vis || !BoAVisibility(vis).alertedcount) // If no enemies are already alerted, turn on notarget
						{
							notargettimeout = level.time + 2;
						}
					}
					else
					{
						notargettimeout = level.time;
					}
				}

				if (notargettimeout)
				{
					if (notargettimeout <= level.time) // Turn off notarget and reset speed
					{
						if (
							owner.player.ReadyWeapon == self ||
							(
								!NaziWeapon(owner.player.ReadyWeapon) ||
								!NaziWeapon(owner.player.ReadyWeapon).allowdisguised)
							)
						{
							owner.player.cheats &= ~CF_NOTARGET;
							owner.speed = owner.Default.speed;
						}
						notargettimeout = 0;
					}
					else // Turn on notarget and slow down your movement
					{
						owner.player.cheats |= CF_NOTARGET;
						owner.speed = owner.Default.speed * 0.85;
					}
				}
			}
		}

		Super.Tick();
	}

	action void A_SpawnLightning(Class<LightningBeam> beam = "LightningBeam")
	{
		bool spawned;
		Actor b;

		let p = player.mo;

		b = Spawn(beam, pos + (RotateVector((max(4, cos(p.pitch) * p.radius), 0), angle), player.viewheight * 0.8));
		if (b)
		{
			b.master = p;
			b.pitch = p.pitch;
			b.angle = p.angle;
		}
	}
}

class NullWeapon : NaziWeapon
{
	Default
	{
		+INVENTORY.UNDROPPABLE
		+WEAPON.NOALERT
		Weapon.SelectionOrder 99999; // Make this absolutely last in weapon priority
		Tag "$TAGCLOAK";
		NaziWeapon.Stealth True;
	}

	States
	{
		Select:
			TNT1 A 1 A_Raise;
			Loop;
		Deselect:
			TNT1 A 1 A_Lower;
			Loop;
		Fire:
		Ready:
			TNT1 A 1 A_WeaponReady(WRF_NOFIRE);
			Loop;
	}
}

// The astrostein weapons have altfires that should use primary (loaded) ammo
class NaziAstroWeapon : NaziWeapon {
	Default
	{
		//$Category Astrostein (BoA)/Weapons
		//$Color 14
		Scale 0.50;
		+WEAPON.CHEATNOTWEAPON
	}

	// DryFire state definition removed because it was the same as the DryFire state for NaziWeapon

	// Astrostein weapons use primary ammo for both fire modes.
	override bool CheckAmmo (int firemode, bool autoSwitch, bool requireAmmo, int ammocount) {
		// True if player has loaded ammo, or ammo in the stockpile.
		// For the shocker, or if player has infinite ammo powerup.
		if (bAmmo_Optional || Owner.FindInventory("PowerInfiniteAmmo", true)) return true;
		// Make sure player has either loaded ammo, or ammo in the stockpile.
		if (AmmoType1 && AmmoType2 && !Owner.FindInventory(AmmoType1) && !Owner.FindInventory(AmmoType2)) return false;
		return Super.CheckAmmo(firemode, autoSwitch, requireAmmo, ammocount);
	}

	override bool DepleteAmmo (bool altfire, bool checkEnough, int ammouse) {
		if (AmmoUse1 == 0 || Owner.FindInventory("PowerInfiniteAmmo", true)) return true;

		bool requireAmmo = AmmoUse1 > 0 || AmmoUse2 > 0 || !bAmmo_Optional;

		if (checkEnough) {
			if (CheckAmmo(PrimaryFire, true, requireAmmo, ammouse)) {
				if (AmmoType1) {
					Ammo magAmmo = Ammo(Owner.FindInventory(AmmoType1));
					if (magAmmo) {
						magAmmo.amount -= AmmoUse1;
						return true;
					}
				}
			}
		}
		return false;
	}
}

class Cannon75mm : NaziWeapon // Weapon used when morphed into a tank. 
{
	int cannontimeout;

	Default
	{
		Weapon.SelectionOrder 1;
	}

	States
	{
		Ready:
			TNT1 A 1 A_TankRefire();
			Loop;
		Select:
			TNT1 A 1 A_Raise;
			Loop;
		Deselect:
			TNT1 A 1 A_Lower;
			Loop;
		Fire:
			TNT1 A 2 DoCannon();
			TNT1 A 0 A_TankRefire();
			Goto Ready;
		AltFire:
			TNT1 A 6 DoMachineGun();
			TNT1 A 20 A_TankRefire();
			Goto Ready;
		Spawn:
			UNKN A -1;
			Loop;
	}

	action void A_TankReFire(statelabel flash = null)
	{
		let player = self.player;

		if (!player) { return; }

		if ((player.cmd.buttons & BT_ATTACK) && player.health > 0 && !player.refire) 
		{
			player.refire++;
			player.mo.FireWeapon(ResolveState(flash));
		}
		else if ((player.cmd.buttons & BT_ALTATTACK) && player.health > 0)
		{
			if (player.mo is "ShermanPlayer") { ShermanPlayer(player.mo).altrefire++; }
			else { player.refire++; }
			player.mo.FireWeaponAlt(ResolveState(flash));
		}
		else
		{
			if (player.mo is "ShermanPlayer") { ShermanPlayer(player.mo).altrefire = 0; }
			else { player.refire = 0; }
		}
	}

	override void Tick()
	{
		Super.Tick();

		if (cannontimeout > 0) { cannontimeout--; }
		else if (owner && owner.player && !(owner.player.cmd.buttons & BT_ATTACK)) { owner.player.refire = 0; }
	}

	action void DoCannon()
	{
		Actor origin = self;
		if (origin && ShermanPlayer(origin) && ShermanPlayer(origin).turret && US_ShermanTurret(ShermanPlayer(origin).turret).gunspot) { origin = US_ShermanTurret(ShermanPlayer(origin).turret).gunspot; }

		double angledelta = deltaangle(angle, origin.angle);
		double pitchdelta = deltaangle(pitch, origin.pitch);

		A_PlaySound("Panzer/fire", CHAN_WEAPON);
		A_GunFlash();
		Actor mo = A_FireProjectile("TankRocket", angledelta, False, 0, 0, FPF_NOAUTOAIM, pitchdelta);
		if (mo && origin != self)
		{
			mo.SetOrigin(origin.pos, false); // Move the rocket so that it flies from the model's gun
		}

		invoker.cannontimeout = 40;
	}

	action void DoMachineGun()
	{
		Actor origin = self;
		if (origin && ShermanPlayer(origin) && ShermanPlayer(origin).treads) { origin = ShermanPlayer(origin).treads; }

		// Nullify angle and pitch so the weapon always fires straight ahead
		double angledelta = deltaangle(angle, origin.angle);
		double pitchdelta = deltaangle(pitch, origin.pitch);

		A_PlaySound("mauser/fire", CHAN_WEAPON, FRandom(0.6, 0.8));
		Actor mo = A_FireProjectile("Kar98kTracer", angledelta + Random(-2, 2), False, 20, 16, 0, pitchdelta + Random(-2, 2));
		if (mo)
		{
			mo.bMThruSpecies = true;
		}
	}
}

class TankRocket : Actor
{
	Default
	{
		Radius 5;
		Height 6;
		Speed 70;
		Projectile;
		-NOTELEPORT
		+WINDTHRUST
		+THRUGHOST
		Damage (500);
		DamageType "Rocket";
		Decal "Scorch";
		Obituary "$OBPANZA";
		ProjectileKickback 25000;
	}

	States
	{
		Spawn:
			TNT1 A 2;
		SpawnLoop:
			MNSS A 1 Bright Light("BOAFLMW2") A_PlaySound("panzer/fly", CHAN_VOICE, 1.0, TRUE);
			MNSS A 1 Bright Light("BOAFLMW2") A_SpawnItemEx("RocketFlame", random(-1,1), 0, random(-1,1));
			Wait;
		Death:
			EXP1 A 0 A_SetScale(0.75, 0.75);
			EXP1 A 0 A_StopSound(CHAN_VOICE);
			EXP1 A 0 A_PlaySound("panzer/explode", CHAN_VOICE, 1.0, FALSE, ATTN_NORM);
			EXP1 A 0 A_Explode(0, 192, TRUE, 0, 320);
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("TracerSpark_Longlive", 0, 0, 0, random(-5,5), random(-5,5), random(-5,5), random(0,359)); //T667 improvements
			TNT1 A 0 A_SpawnItemEx("PanzerNuke", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION);
			TNT1 A 8 A_SpawnItemEx("GeneralExplosion_Large", 56, 0, 32);
			EXP1 A 2 Bright Light("BOAFLMW2") A_Quake(9, 15, 0, 1024, "");
			EXP1 BCDEFGHIJKLMN 2 Bright Light("BOAFLMW2");
			Stop;
	}

	// Handling so that rockets won't hit actors that have the rocket's origin actor set as their master (or inherit from one that does)
	override bool CanCollideWith(Actor other, bool passive)
	{
		if (target)
		{
			Actor roottarget = target;
			Actor rootmaster = other;
			while (roottarget.master) { roottarget = roottarget.master; }
			while (rootmaster.master) { rootmaster = rootmaster.master; }

			if (roottarget && roottarget == rootmaster) { return false; }
		}
		return true;
	}
}

class TurretSwivel : ActorPositionable
{
	Default
	{
		Height 2;
		Radius 2;
		+NOBLOCKMAP;
		+NOGRAVITY;
		+NOINTERACTION
		+THRUSPECIES
		Species "Turret";
	}

	States
	{
		Spawn:
			 MDLA A -1;
		Stop;
	}
}

class TurretGun : ActorPositionable
{
	Actor swivel, light, light2, light3;
	TurretOverheatSmokeSpawner smokefx;
	int shotcount;
	int maxshotcount;
	int cooldowntime, cooldowntimeout, heatlevel, heatthreshold;
	string ammoGraphic;

	Property MaxShotCount:maxshotcount;
	Property TemperatureThreshold:heatthreshold;
	Property CoolDownTime:cooldowntime;
	Property AmmoGraphic:ammoGraphic;

	Default
	{
		Radius 2;
		Height 2;
		+MOVEWITHSECTOR
		+NOBLOCKMAP
		+NOGRAVITY
		+NOINTERACTION
		+THRUSPECIES
		Species "Turret";
		TurretGun.MaxShotCount 100; // How many rounds to fire before reloading
		TurretGun.TemperatureThreshold 200; // How many tics of continuous fire before cooldown (roughly 50 degrees per 100 rounds)
		TurretGun.CoolDownTime 140; // How many tics you must wait before firing again after hitting max temperature (does NOT guarantee full cooldown!)
		TurretGun.AmmoGraphic "WALT01";
	}

	States
	{
		Spawn:
			MDLA A 1;
			Loop;
		Reload:
			MDLA A 5 A_PlaySound("weapon/dryfire", CHAN_WEAPON);
			MDLA A 35 DoReload();
			MDLA A 5 A_PlaySound("mp40/reload", CHAN_WEAPON);
			MDLA A 5;
			MDLA A 0 { if (cooldowntimeout > 0) { SetStateLabel("Cooldown"); } }
			Goto Spawn;
		Cooldown:
			MDLA A 0 DoCoolDown();
			Goto Spawn;
		Fire:
			MDLA ABCD 2 DoFire();
			Goto Spawn;
	}

	override void PostBeginPlay()
	{
		while (!swivel) { swivel = Spawn("TurretSwivel", pos); }
		swivel.scale = scale;
		swivel.master = master;

		while (!light) { light = Spawn("AlphaLight", pos + (RotateVector((28 * scale.x, 0), angle), 8 * scale.y)); }
		light.scale = scale;
		light.master = self;
		DynamicLight(light).bAdditive = true;
		AlphaLight(light).clr = 0xFF2000;

		while (!light2) { light2 = Spawn("AlphaLight", pos + (RotateVector((28 * scale.x, 0), angle), 8 * scale.y)); }
		light2.scale = scale;
		light2.master = self;
		AlphaLight(light2).clr = 0xFF6600;

		while (!light3) { light3 = Spawn("AlphaLight", pos + (RotateVector((28 * scale.x, 0), angle), 8 * scale.y)); }
		light3.scale = scale;
		light3.master = self;
		AlphaLight(light3).clr = 0xFFAA77;

		Super.PostBeginPlay();
	}

	override void Tick()
	{
		if (heatlevel && level.time && level.time % 4 == 0) { heatlevel--; }

		if (swivel) { swivel.angle = angle; }

		double heatamt = double(heatlevel) / heatthreshold;
		
		if (cooldowntimeout > 0) cooldowntimeout -= 1;

		if (light) { light.alpha = clamp(heatamt * 1.25, 0, 1.0); }
		if (light2) { light2.alpha = clamp((heatamt - 0.75) * 3, 0, 1.0); }
		if (light3) { light3.alpha = clamp((heatamt - 0.85) * 5.7, 0, 1.0); }

		RotateWithMaster(ROT_MATCHROLL);

		Actor.Tick();
	}

	void DoFire()
	{
		A_PlaySound("mp40/fire", CHAN_WEAPON);

		A_SpawnItemEx("9MMCasing", 4 * scale.x, 4 * scale.x, 8 * scale.y, 8, Random(-2,2), Random(0,4), Random(50,80), SXF_NOCHECKPOSITION);
		A_SpawnItemEx("TurrSmokeSpawner", 10 * scale.x, 0, 8 * scale.x, 0, 0, 0, 0, SXF_NOCHECKPOSITION | SXF_TRANSFERROLL | SXF_TRANSFERPITCH);

		bool temp;
		Actor origin;
		[temp, origin] = A_SpawnItemEx("KTFlare", 34 * scale.x, 0, 8 * scale.x, 0, 0, 0, 0, SXF_NOCHECKPOSITION | SXF_TRANSFERPITCH | SXF_TRANSFERROLL | SXF_SETMASTER);

		if (!origin) { origin = self; }
		// Spawn the tracer from the flare so that the tracer model isn't visible sticking through the turret gun body
		origin.A_SpawnProjectile("KTurretTracer", 8 * scale.y, 0, angle + FRandom(-1.0, 1.0), CMF_AIMDIRECTION | CMF_ABSOLUTEANGLE, pitch + FRandom(-1.0, 1.0));

		shotcount++;
		heatlevel++;

		if (maxshotcount && shotcount >= maxshotcount)
		{
			SetStateLabel("Reload");
		}

		if (heatthreshold && heatlevel > heatthreshold)
		{
			cooldowntimeout = cooldowntime;
			SetStateLabel("Cooldown");
		}
	}

	void DoReload()
	{
		shotcount = 0;
		A_PlaySound("mauser/insert", CHAN_WEAPON);
	}

	void DoCoolDown()
	{
		A_SetTics(cooldowntimeout);

		if (cooldowntimeout > 24)
		{
			A_PlaySound("STEAM_BURST", CHAN_WEAPON);
		}

		if (!smokefx)
		{
			smokefx = TurretOverheatSmokeSpawner(Spawn("TurretOverheatSmokeSpawner", Pos, NO_REPLACE));
		}

		smokefx.master = self;
		smokefx.cooldowntime = cooldowntimeout;
	}

	static void Overheat(Actor activator, int goToCooldownState = 0)
	{
		TurretGun gun;

		// Find the gun
		FTranslatedLineTarget gtarg;
		activator.AimLineAttack(activator.Angle, 512.0, gtarg, flags: ALF_CHECK3D | ALF_CHECKNONSHOOTABLE);

		if (gtarg.linetarget)
		{
			if (gtarg.linetarget is "TurretStand")
			{
				TurretStand stand = TurretStand(gtarg.linetarget);
				gun = stand.gun;
			}
			else if (gtarg.linetarget is "TurretGun")
			{
				gun = TurretGun(gtarg.linetarget);
			}
			else
			{
				if (gtarg.linetarget)
				{
					Console.Printf("Found a %s", gtarg.linetarget.GetClassName());
				}
				return;
			}

			gun.heatlevel = gun.heatthreshold;
		}

		if (gun && goToCooldownState)
		{
			gun.cooldowntimeout = gun.cooldowntime;
			gun.SetStateLabel("Cooldown");
		}
	}
}

class TurretOverheatSmokeSpawner : GunSmokeSpawner
{
	int cooldowntime;

	Default
	{
		GunSmokeSpawner.SmokeClass "TurrCoolSmoke";
	}

	States
	{
		Spawn:
			TNT1 A 2
			{
				A_SpawnSmoke(zVelFactor: 2.0);
				if (cooldowntime <= 0)
				{
					Destroy();
				}
			}
			Loop;
	}

	override void Tick()
	{
		if (master)
		{
			Angle = master.Angle;
			Pitch = master.Pitch;
			SetOrigin(GetMuzzlePos(), false);
		}
		if (cooldowntime)
		{
			cooldowntime -= 1;
		}
		Super.Tick();
	}
	
	vector3 GetMuzzlePos()
	{
		vector3 mPos = master.Pos;

		vector3 offsetVector = (34.2, .57, 9.12);
		double offsetLen = offsetVector.Length();

		// A_SpawnSmoke offsets X by 24
		offsetVector.X -= 24;

		offsetVector.XY = RotateVector(offsetVector.XY, master.Angle);
		offsetVector.Z -= sin(master.Pitch) * offsetLen;


		return mPos + offsetVector;
	}
}

// "Proxy" weapon that copies the ammo counts from the turret gun so that they can be displayed in the status bar.
class TurretWeapon : NullWeapon
{
	TurretGun gun;

	Default
	{
		Tag "$TAGTURET";
		Weapon.AmmoType1 "TurretBulletAmmo";
		Weapon.AmmoType2 "TurretHeatAmmo";
		NaziWeapon.Stealth False;
	}

	override void DoEffect()
	{
		static const string heatlevelicons[] = {"HEAT01", "HEAT02", "HEAT03", "HEAT04", "HEAT05", "HEAT06"};
		static const int heatlevels[] =        {0, 20, 40, 60, 80, 100};

		if (gun)
		{
			Ammo1.MaxAmount = gun.maxshotcount;
			Ammo1.Amount = gun.maxshotcount - gun.shotcount;
			Ammo1.Icon = TexMan.CheckForTexture(gun.ammoGraphic, TexMan.Type_MiscPatch);

			Ammo2.Amount = int((double(gun.heatlevel) / gun.heatthreshold) * 100);
			int i = 0;
			for (;i < 6; i++)
			{
				if (Ammo2.Amount <= heatlevels[i]) break;
			}
			Ammo2.Icon = TexMan.CheckForTexture(heatlevelicons[i], TexMan.Type_MiscPatch);
		}
	}
}

class TurretStand : Actor
{
	Actor shooter;
	TurretGun gun;
	Weapon playerlastweapon;
	transient FLineTraceData crosshairtrace;
	double pangle, ppitch;
	bool freezeonreload;
	TurretWeapon twep;

	Property FreezeOnReload:freezeonreload;

	Default
	{
		//$Category Weapons (BoA)
		//$Title Usable Turret (ZScript)
		//$Color 14
		Radius 8;
		Height 32;
		+MOVEWITHSECTOR
		+MTHRUSPECIES
		+SOLID
		+THRUSPECIES
		Species "Turret";
		TurretStand.FreezeOnReload False;
	}

	States
	{
		Spawn:
			MDLA A -1;
	}

	override bool Used(Actor user)
	{
		if (shooter)
		{
			if (shooter == user)
			{
				return LeaveTurret();
			}

			return false;
		}

		double delta = deltaangle(angle, AngleTo(user));

		if (user.player && abs(delta) > 135) // Player must be within +/-45 degrees of directly behind the gun
		{
			shooter = user;
			shooter.DamageFactor = 0.25; // Player takes quarter damage while using the turret gun
			if (gun)
			{
				gun.target = user;
				user.angle = gun.angle;
			}
			return true;
		}

		return false;
	}

	override void PostBeginPlay()
	{
		vector3 gunPos = Pos;
		gunPos.Z += 26;
		while (!gun) { gun = TurretGun(Spawn("TurretGun", gunPos)); }

		gun.master = self;
		gun.target = self;
		gun.scale = scale;
		gun.Angle = Angle;

		Super.PostBeginPlay();
	}

	override void Tick()
	{
		VehicleBase.SetPitchRoll(self, 0);

		if (shooter && shooter.player && gun)
		{
			if (
				shooter.player.cmd.buttons & BT_CROUCH ||
				shooter.player.cmd.buttons & BT_JUMP ||
				shooter.player.cmd.forwardmove ||
				shooter.player.cmd.sidemove
			)
			{
				if (!LeaveTurret())
				{
					DoInteractions();
				}
			}
			else
			{
				DoInteractions();
			}
		}

		Super.Tick();
	}

	void DoInteractions()
	{
		shooter.SetOrigin(pos + RotateVector((-(shooter.radius + radius) * 1.5, 0), gun.angle), true); // Keep the shooter at the firing position

		if (!twep)
		{
			twep = TurretWeapon(shooter.GiveInventoryType("TurretWeapon"));

			if (!twep.gun || twep.gun != gun)
				twep.gun = gun;
		}

		if (shooter.player.ReadyWeapon != twep)
		{
			playerlastweapon = shooter.player.ReadyWeapon;
			shooter.player.PendingWeapon = twep;
		}

		shooter.player.viewheight = PlayerPawn(shooter).viewheight * 3 / 4; 

		gun.pitch = clamp(shooter.pitch, -55, 55);
		gun.angle = clamp(shooter.angle, angle - 70, angle + 70);

		shooter.angle = gun.angle;

		shooter.player.cheats |= CF_FROZEN;

		if (CanLeaveTurret())
		{
			shooter.player.cheats ^= CF_FROZEN;
		}

		if (shooter.player.cmd.buttons & BT_ATTACK)
		{
			if (GunIsIdle())
			{
				gun.SetStateLabel("Fire");
			}
		}
		else if (shooter.player.cmd.buttons & BT_RELOAD)
		{
			if (!GunIsReloading() && gun.shotcount > 0)
			{
				gun.SetStateLabel("Reload");
			}
		}
	}

	virtual bool GunIsIdle()
	{
		return gun.InStateSequence(gun.CurState, gun.SpawnState);
	}

	virtual bool GunIsCooling()
	{
		State CooldownState = gun.ResolveState("Cooldown");
		return gun.InStateSequence(gun.CurState, CooldownState);
	}

	virtual bool GunIsReloading()
	{
		State ReloadState = gun.ResolveState("Reload");
		return gun.InStateSequence(gun.CurState, ReloadState);
	}

	virtual bool CanLeaveTurret()
	{
		return !GunIsReloading() || GunIsCooling();
	}

	virtual bool LeaveTurret()
	{
		if (!CanLeaveTurret())
		{
			// Lock player while reloading
			return false;
		}

		shooter.player.PendingWeapon = playerlastweapon;

		shooter.player.viewheight = PlayerPawn(shooter).viewheight;
		shooter.DamageFactor = shooter.Default.DamageFactor;

		Inventory twepInv = shooter.FindInventory("TurretWeapon");

		if (twepInv)
		{
			shooter.TakeInventory("TurretWeapon", twepInv.Amount);
		}

		shooter = null;
		return true;
	}
}

class AlphaLight : DynamicLight
{
	Color clr;
	double maxradius;
	Vector3 spawnoffset;

	Property LightColor:clr;
	Property LightRadius:maxradius;

	Default
	{
		DynamicLight.Type "Point";
		AlphaLight.LightColor 0xFFFFFF;
		AlphaLight.LightRadius 16;
	}

	override void BeginPlay ()
	{
		alpha = 0;

		args[LIGHT_RED] = clr.r;
		args[LIGHT_GREEN] = clr.g;
		args[LIGHT_BLUE] = clr.b;
		args[LIGHT_INTENSITY] = int(maxradius); //int(maxradius * scale.y * alpha);

		Super.BeginPlay();
	}

	override void PostBeginPlay()
	{
		if (master)
		{
			spawnoffset = pos - master.pos;

			Vector2 temp = RotateVector((spawnoffset.x, spawnoffset.y), -master.angle);
			spawnoffset = (temp.x, temp.y, spawnoffset.z);
		}

		Super.PostBeginPlay();
	}

	override void Tick()
	{
		args[LIGHT_RED] = int(clr.r * alpha);
		args[LIGHT_GREEN] = int(clr.g * alpha);
		args[LIGHT_BLUE] = int(clr.b * alpha);
		args[LIGHT_INTENSITY] = int(maxradius); //int(maxradius * scale.y * alpha);

		if (master && spawnoffset != (0, 0, 0)) { Rotate(); }

		Super.Tick();
	}

	void Rotate()
	{
		Vector2 temp = RotateVector((spawnoffset.y, spawnoffset.z), master.roll);
		Vector3 offset = (spawnoffset.x, temp.x, temp.y);

		temp = RotateVector((offset.x, offset.z), 360 - master.pitch);
		offset = (temp.x, offset.y, temp.y);

		temp = RotateVector((offset.x, offset.y), master.angle);
		offset = (temp.x, temp.y, offset.z);

		offset.x *= master.scale.x;
		offset.y *= master.scale.x;
		offset.z *= master.scale.y;

		SetOrigin(master.pos + offset, true);
	}
}

class GunSmoke : Actor
{
	Default
	{
		Radius 1;
		Height 1;
		RenderStyle "Translucent";
		Alpha 0.2;
		Scale 0.01;
		+NOCLIP
		+NOGRAVITY
		+NOBLOCKMAP
		+DONTSPLASH
		+CLIENTSIDEONLY
		+FORCEXYBILLBOARD
		+ROLLSPRITE
	}

	States
	{
		Spawn:
			SMSP A 1 A_FadeIn();
			SMSP A 1 A_FadeIn();
		Next:
			SMSP A 1 A_SetRoll(roll+1, SPF_INTERPOLATE);
			SMSP A 0 A_FadeOut(0.04);
			SMSP A 0 A_SetScale(Scale.X+0.001, Scale.Y+0.001);
			Loop;
	}

	override void PostBeginPlay()
	{
		if (GetClass() == "GunSmoke") { Super.PostBeginPlay(); }

		double newScaleX = Scale.X; // set scale relatively
		double newScaleY = Scale.Y;

		class<Actor> type = GetClass();
		do
		{
			type = (class<Actor>)(type.GetParentClass());
			newScaleX *= (GetDefaultByType(type)).Scale.X;
			newScaleY *= (GetDefaultByType(type)).Scale.Y;
		} while (type != "GunSmoke");

		Scale.X = newScaleX;
		Scale.Y = newScaleY;
		
		Super.PostBeginPlay();
	}
}

class GunSmokeSpawner : Actor
{
	class<Actor> smokeClass;

	Property SmokeClass:smokeClass;

	Default
	{
		Radius 1;
		Height 1;
		Speed 0;
		+NOINTERACTION

		GunSmokeSpawner.SmokeClass "PistolSmoke";
	}

	States
	{
		Spawn:
			TNT1 A 0 NODELAY A_SpawnSmoke();
			Stop;
	}

	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		CVar smokeSwitch = CVar.FindCVar("smokeswitch");
		int smoke = smokeSwitch.GetInt();
		if (smoke == 0)
		{
			Destroy();
			return;
		}
	}

	Actor A_SpawnSmoke(double xPosFactor = 1.0, double xVelFactor = 1.0, double zVelFactor = 1.0)
	{
		bool sp;
		Actor ac;
		[sp, ac] = A_SpawnItemEx(smokeClass,24 * xPosFactor,0,0,frandom(0.1,0.2) * xVelFactor,0,frandom(0.2,0.6) * zVelFactor);
		ac.Roll = frandom(0.0, 360.0);
		return ac;
	}
}

class Firebrand : NaziWeapon
{
	Default
	{
		//$Category Weapons (BoA)
		//$Title (1) Tyrfing
		//$Color 14
		Scale 0.5;
		Weapon.SelectionOrder 9999;
		Inventory.PickupMessage "$TYRF";
		Inventory.PickupSound "sword/draw";
		Weapon.UpSound "sword/draw";
		-WEAPON.AMMO_CHECKBOTH
		+WEAPON.NOALERT
		+WEAPON.NOAUTOFIRE
		Tag "$TAGBLAOA";
	}
	States
	{
	Select:
		SRDG A 0 A_Raise;
		"####" A 1 A_Raise;
		Loop;
	Deselect:
		SRDG A 0 A_StopSound(CHAN_WEAPON);
		SRDG A 0 A_Lower;
		"####" A 1 A_Lower;
		Loop;
	Ready:
		SRDG A 0 A_StopSound(CHAN_WEAPON);
		TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SoundLoop");
		SRDG A 1 A_WeaponReady;
		Loop;
	SoundLoop:
		SRDF A 0 A_PlaySound("SFX/FireLoop2", CHAN_WEAPON, 0.7, TRUE, ATTN_STATIC);
		Goto PowerLoop;
	PowerLoop:
		SRDF A 3 A_WeaponReady;
		SRDI BC 3 A_WeaponReady;
		TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"PowerLoop");
		Goto Ready;
	Fire:
		TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire");
		SRDG B 0 A_JumpIfInventory("SwordSeq",2,"Fire3");
		"####" B 1 A_JumpIfInventory("SwordSeq",1,"Fire2");
		"####" C 1 A_GiveInventory("SwordSeq");
		TNT1 A 4;
		SRDG DE 1;
		"####" F 1 A_PlaySound("sword/swing");
		"####" GHI 1;
		"####" J 1 A_CustomPunch(35,1,0,"SwordPuff",96);
		"####" KLM 1;
		TNT1 A 8 A_WeaponReady;
		SRDG C 1;
		"####" B 1 A_TakeInventory("SwordSeq");
		Goto ready;
	Fire2:
		TNT1 A 5;
		SRDG N 1 A_GiveInventory("SwordSeq");
		"####" O 1 A_PlaySound("sword/swing");
		"####" PQR 1;
		"####" S 1 A_CustomPunch(45,1,0,"SwordPuff",96);
		"####" TUV 1;
		TNT1 A 8 A_WeaponReady;
		"####" A 0 A_TakeInventory("SwordSeq");
		Goto Ready;
	Fire3:
		TNT1 A 4;
		SRDG DE 1;
		"####" F 1 A_PlaySound("sword/swing");
		"####" GHI 1;
		"####" J 1 A_CustomPunch(35,1,0,"SwordPuff",96);
		"####" KL 1;
		"####" M 1 A_TakeInventory("SwordSeq",1);
		TNT1 A 8 A_WeaponReady;
		SRDG C 1;
		"####" B 1 A_TakeInventory("SwordSeq");
		Goto Ready;
	SuperFire: //we can heal ourselves of 25 max from 1 to 25 hp when we do melee attack with powered sword - ozy81
		SRDF B 0 A_JumpIfInventory("SwordSeq",2,"SuperFire3");
		"####" B 1 A_JumpIfInventory("SwordSeq",1,"SuperFire2");
		"####" C 1 A_GiveInventory("SwordSeq");
		TNT1 A 4;
		SRDG DE 1;
		SRDF F 1 A_PlaySound("flamesword/swing");
		"####" GHI 1;
		"####" J 1 A_CustomPunch(80,1,0,"FireSwordPuff",96,1,25);
		"####" KLM 1;
		TNT1 A 8 A_WeaponReady;
		SRDF C 1;
		"####" B 1 A_TakeInventory("SwordSeq");
		Goto ready;
	SuperFire2: //we can heal ourselves of 25 max from 1 to 25 hp when we do melee attack with powered sword - ozy81
		TNT1 A 5;
		SRDF N 1 A_GiveInventory("SwordSeq");
		"####" O 1 A_PlaySound("flamesword/swing");
		"####" PQR 1;
		"####" S 1 A_CustomPunch(100,1,0,"FireSwordPuff",96,1,25);
		"####" TUV 1;
		TNT1 A 8 A_WeaponReady;
		"####" A 0 A_TakeInventory("SwordSeq");
		Goto Ready;
	SuperFire3: //we can heal ourselves of 25 max from 1 to 25 hp when we do melee attack with powered sword - ozy81
		TNT1 A 4;
		SRDG DE 1;
		SRDF F 1 A_PlaySound("flamesword/swing");
		"####" GHI 1;
		"####" J 1 A_CustomPunch(80,1,0,"FireSwordPuff",96,1,25);
		"####" KL 1;
		"####" M 1 A_TakeInventory("SwordSeq",1);
		TNT1 A 8 A_WeaponReady;
		SRDF C 1;
		"####" B 1 A_TakeInventory("SwordSeq");
		Goto Ready;
	AltFire:
		SRDG A 1 A_JumpIfInventory("PowerWeaponLevel2",1,"Altfire2");
		"####" BC 1;
		TNT1 A 7 {
			Inventory soulAmmo = FindInventory("Soul");
			if (soulAmmo) // 1 or more souls
			{
				A_TakeInventory("Soul", 1, TIF_NOTAKEINFINITE);
				A_SetBlend("64 ff 96", .5, 15);
			}
			else
			{
				Thing_Damage(0,25);
				A_SetBlend("ff 00 00", .5, 15);
			}
		}
		SRDG C 1 A_GiveInventory("PowerWeaponLevel2");
		"####" BA 1;
		Goto Ready;
	Altfire2:
		SRDF BC 1;
		TNT1 A 4;
		SRDG DE 1;
		SRDF F 1 A_PlaySound("flamesword/swing");
		"####" GH 1;
		"####" I 1;
		"####" J 1 {
			A_FireProjectile("FirebrandFireball");
			Inventory soulAmmo = FindInventory("Soul");
			if (soulAmmo)
			{
				if (soulAmmo.Amount > 1)
				{
					int soulTakeCount = min(5, CountInv("Soul"));
					int i;
					A_TakeInventory("Soul", soulTakeCount, TIF_NOTAKEINFINITE);
					for (i = 1; i < soulTakeCount; i++)
					{ // Already fired 1 projectile
						A_FireProjectile("FirebrandFireball", frandom(-2.0, 2.0), false, 0, 0, 0, frandom(-2.0, 2.0));
					}
				}
				else
				{
					A_TakeInventory("Soul", 1, TIF_NOTAKEINFINITE);
				}
			}
			/*
			else // No souls
			{
				Thing_Damage(0,2);
			}
			*/
		}
		"####" KLM 1;
		TNT1 A 20 A_WeaponReady;
		SRDF C 1;
		"####" B 1 A_TakeInventory("SwordSeq");
		Goto Ready;
	Spawn:
		SRDP ABCD 4 LIGHT("ITBURNSOC1");
		Loop;
	}
}